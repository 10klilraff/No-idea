;簡易ブラックジャック
;スキルとか使わない完全に運ゲーです, 時間停止でホールカード覗いたりできてもいいかもね
;勝率はルールが本家と違うんでどうなんでしょう……。テストプレイではゲーム内で195分やって-250とかでした（雑魚）
;カードの追加が常に１回しかできないのでダブルのデメリットがないのはプレイヤーに有利な気がする
;P_HAND   ×広域変数化、プレイヤーの手札, 7まであるけど現状3までしか使いません
;D_HAND   ×広域変数化、ディーラーの手札, 上に同じく
;DECK     ×広域変数化、52枚のカードをランダムに並べてます, でも使うのは上から6枚だけ, 下のカードを持ってきたりとイカサマに応用できるかも

;カジノ作者（morph）による改造
;・HITの可能回数を増やした
;・カード関連変数の広域変数化＆処理を独立させて汎用化
;　（判定関連を別関数に任せられるのでこの方が便利、ポーカー実装されたし）
;・カウンティングを可能に、プレイヤースキルが影響するよ！
;・仕様上はジョーカーも混ぜられるようになったけど処理側で対応してない（計算めんどいのよ）
@CASINO_BLACKJACK
#DIM P_BET        ;賭け金
#DIM ORI_BET      ;最初の賭け金, ダブルを選択するとP_BETが2倍になり、続行すると次のゲームでも2倍になったままなので保存用です
#DIM INS          ;インシュアランスの保険金, 賭けてると再度インシュアランスできない
#DIM LPCOUNT      ;ループカウント
#DIM TOTAL, 3     ;
                  ;TOTAL:1  賭け金合計, カモは最後にまとめて精算しました
                  ;TOTAL:2  配当合計
#DIM P_WIN        ;プレイヤーの勝利判定用
#DIM CONST DL = 0 ;定数、ディーラーのカードプレイヤー番号
#DIM CONST PL = 1 ;定数、プレイヤーのカードプレイヤー番号
;参加プレイヤーはふたり
NUM_CARD_PL = 2

PRINTFORML 
IF MONEY:2 <= 0
	PRINTFORML Fairy Maid:
	PRINTFORML 　「Dear customer, we apologize but at this casino we only use a special currency called “Charisma”.」
	PRINTFORMW 　「We apologize for the inconvenience, but please purchase some Charisma first.」
	RETURN
ELSE
	PRINTFORML Fairy Maid:
	PRINTFORML 　「Welcome, how can I help you?」
	PRINTFORML 　「This is the blackjack corner.」
	PRINTFORML 　「Tricks and abilities mean nothing here, this is a game requires skills with cards.」
	PRINTFORMW 　「First of all, please announce your wager.」
	PRINTFORML 
ENDIF
CALL CASINO_INPUTLOOP("カード賭け金")
P_BET = RESULT
ORI_BET = RESULT
VARSET TOTAL
IF P_BET
	CALL CASINO_MONEY("カリスマ", -P_BET)
	TOTAL:1 += P_BET
	CALL PLAY_CARDS("SHUFFLE")
	PRINTFORML 
	PRINTFORMW The Fairy Maid took out a brand new deck and shuffled it.
ENDIF

WHILE P_BET
	IF NEW_CARD > 44
		CALL PLAY_CARDS("SHUFFLE")
		PRINTFORML 
		PRINTFORML Fairy Maid
		PRINTFORMW 　「We're running low on cards, so we'll shuffle them.」
	ENDIF
	TIME += 5
	;手札をリセットして2枚ずつ引きます,INSもリセット
	CALLF CARD_PLAYER("CLEAR_BJ_ALL")
	CALL PLAY_CARDS("RESET_HAND")
	CALL PLAY_CARDS("HIT", DL)
	CALL PLAY_CARDS("HIT", PL)
	CALL PLAY_CARDS("HIT", DL)
	CALL PLAY_CARDS("HIT", PL)
	CALLF CARD_PLAYER("RENEW_BJ_POINT_ALL")
	INS = 0
	P_WIN = 0
	;初期盤面, お互い手札は２枚, ホールカード１枚
	DO
		CALL CASINO_DISP("BJ_CLOSE")
		SIF NEW_CARD < NUM_CARD
			PRINTL [1] - Hit
		PRINTL [2] - Stand
		SIF NEW_CARD < NUM_CARD && NUM_HAND:PL == 2 && MONEY:2 >= P_BET
			PRINTL [3] - Double
		IF NUM_HAND:PL == 2 && CARD_CHECK("RANK", CARD_HAND:DL:0) == 1
			IF CARD_PLAYER("BJ_BJ", PL)
				PRINTL [4] - Even Money
			ELSEIF !INS && MONEY:2 >= P_BET / 2
				PRINTL [4] - Insurance
			ENDIF
		ENDIF
		PRINTL [0] - Surrender
		INPUT
		DRAWLINEFORM ━
		SELECTCASE RESULT
		CASE 1
		;ヒット枚数を無制限に変更
			SIF NEW_CARD >= NUM_CARD
				GOTO SURRENDER
			PRINTFORML Fairy Maid
			PRINTFORMW 　「Your card, please.」
			CALL PLAY_CARDS("HIT_BJ", PL)
			SIF CARD_PLAYER("GET_BJ_POINT", PL) >= 21
				BREAK
			CONTINUE
		CASE 2
		;スタンド, 即勝負します
		CASE 3
		;ダブル, 賭け金を２倍にしてカード１枚追加, 賭け金を用意できるときのみ
			SIF MONEY:2 < P_BET || NUM_HAND:PL > 2 || NEW_CARD >= NUM_CARD
				GOTO SURRENDER
			PRINTFORML Fairy Maid
			PRINTFORML 　「Understood.」
			PRINTFORML 　「The wager will be {P_BET * 2} Charisma.」
			PRINTFORMW 　「Your card, please.」
			CALL PLAY_CARDS("HIT_BJ", PL)
			TOTAL:1 += P_BET
			CALL CASINO_MONEY("カリスマ", -P_BET)
			P_BET = P_BET * 2
		CASE 4
			IF NUM_HAND:PL == 2 && CARD_CHECK("RANK", CARD_HAND:DL:0) == 1
				IF CARD_PLAYER("GET_BJ_BJ", PL)
				;イーブンマネー, 引き分けが怖いので２倍の配当で我慢する, 気になるのでホールカードを見せてくれる
				;表のカードがＡであなたの手札が２１点の時のみ
					PRINTFORML Fairy Maid
					PRINTFORML 　「Understood.」
					PRINTFORML 　「Here's your payout of {P_BET * 2} Charisma.」
					PRINTFORMW 　「Please take it.」
					TOTAL:2 += P_BET * 2
					CALL CASINO_MONEY("カリスマ", P_BET * 2)
					PRINTFORMW 　「Now to reveal the hole card...」
					CALL CARD_DISP("CARD", CARD_HAND:DL:1)
					PRINTW
					IF CARD_PLAYER("GET_BJ_BJ", DL)
						PRINTFORMW 　「Fufufu, it appears it was a wise decision on your part.」
					ELSE
						PRINTFORMW 　「Oh my, that's too bad.」
					ENDIF
					GOTO CHECK_NEXTGAME
				ELSEIF !INS && MONEY:2 >= P_BET / 2
				;インシュアランス, ブラックジャックが怖いので保険金を追加BETする
				;表のカードがＡでINSをしておらず保険金分のカリスマが用意できるときのみ
				;その後再度選択肢を選ぶ
					PRINTFORML Fairy Maid
					PRINTFORML 　「Understood.」
					PRINTFORMW 　「We have added half of your Charisma wager ({P_BET / 2}) as insurance money.」
					INS = P_BET / 2
					TOTAL:1 += P_BET / 2
					CALL CASINO_MONEY("カリスマ", -P_BET / 2)
					CONTINUE
				ELSE
					GOTO SURRENDER
				ENDIF
			ELSE
				GOTO SURRENDER
			ENDIF
		CASEELSE
		;手入力したりするとサレンダー, ゲームを降りて半額返してもらう
			$SURRENDER
			PRINTFORML Fairy Maid
			PRINTFORML 　「Oh, you are folding?」
			PRINTFORMW 　「In that case, please accept the payout of {P_BET / 2} Charisma.」
			TOTAL:2 += P_BET / 2
			CALL CASINO_MONEY("カリスマ", P_BET / 2)
			GOTO CHECK_NEXTGAME
		ENDSELECT
		BREAK
	LOOP 1
	
	IF NUM_HAND:PL > 2
		CALL CASINO_DISP("BJ_CLOSE")
		IF CARD_PLAYER("GET_BJ_BUST", PL)
			PRINTW 
			PRINTFORML Fairy Maid
			PRINTFORML 　「Looks like you have gone over 21.」
			GOTO YOULOSE
		ENDIF
	ENDIF

	;ホールカードを公開して盤面を見る
	PRINTW 
	PRINTFORML Fairy Maid
	PRINTFORMW 　「Now, I'll reveal the cards.」
	PRINTFORML 
	WHILE CARD_PLAYER("GET_BJ_POINT", DL) < 17
		IF NEW_CARD >= 52
			PRINTFORML Fairy Maid
			PRINTFORMW 　「Looks like we're out of cards.」
			BREAK
		ENDIF
		CALL CASINO_DISP("BJ_OPEN")
		PRINTFORML Fairy Maid
		PRINTFORMW 　「I have less than 17 points, so I'll add a card.」
		CALL PLAY_CARDS("HIT_BJ", DL)
	WEND

	CALL CASINO_DISP("BJ_OPEN")
	;結果の判定
	IF CARD_PLAYER("GET_BJ_BJ", DL)
	;ディーラーのBJ
		PRINTFORML Fairy Maid
		PRINTFORML 　「Blackjack!」
		IF INS
		;INSしてたらセーフ
			PRINTFORMW 　「Please accept the payout of {P_BET + INS} Charisma since you bet on insurance.」
			TOTAL:2 += P_BET + INS
			CALL CASINO_MONEY("カリスマ", P_BET + INS)
		ELSEIF CARD_PLAYER("GET_BJ_BJ", PL)
			PRINTFORML 　「Looks like we have a draw.」
			PRINTFORMW 　「Please accept your bet of {P_BET} Charisma back.」
			TOTAL:2 += P_BET
			CALL CASINO_MONEY("カリスマ", P_BET)
		ELSEIF CARD_PLAYER("GET_BJ_POINT", PL) == 21
		;２１でもBJには勝てない
			PRINTFORML 　「It's your loss.」
			PRINTFORMW 　「Casino will now take your entire wager.」
		ELSE
			GOTO YOULOSE
		ENDIF
	ELSEIF CARD_PLAYER("GET_BJ_BUST", DL)
	;ディーラーBUST（ここまで来た時点であなたはBUSTしていない）
		PRINTFORML Fairy Maid
		PRINTFORML 　「Casino has gone over 21, so you win!」
		P_WIN = 1
	ELSE
	;普通の結果
		PRINTFORML Fairy Maid
		IF CARD_PLAYER("GET_BJ_BJ", PL)
		;21POINTだがPL側のみBJが成立した
			PRINTFORML 　「You have a Blackjack, so you win!」
			P_WIN = 1
		ELSEIF CARD_PLAYER("GET_BJ_POINT", PL) > CARD_PLAYER("GET_BJ_POINT", DL)
			PRINTFORML 　「Congratulations, you win!」
			P_WIN = 1
		ELSEIF CARD_PLAYER("GET_BJ_POINT", PL) == CARD_PLAYER("GET_BJ_POINT", DL)
		;その他の引き分け
			PRINTFORML 　「Looks like we have a draw.」
			PRINTFORMW 　「Please accept your bet of {P_BET} Charisma back.」
			TOTAL:2 += P_BET
			CALL CASINO_MONEY("カリスマ", P_BET)
		ELSE
		;負けはここ
			$YOULOSE
			PRINTFORML 　「I'm sorry, but you lose.」
			PRINTFORMW 　「Casino will now take your entire wager.」
		ENDIF
	ENDIF

	IF P_WIN
		IF CARD_PLAYER("GET_BJ_BJ", PL)
			PRINTFORML 　「Since you have reached a Blackjack, your payout will be {P_BET * 5 / 2} Charisma.」
			PRINTFORMW 　「Please take it!」
			TOTAL:2 += P_BET * 5 / 2
			CALL CASINO_MONEY("カリスマ", P_BET * 5 / 2)
		ELSE
			PRINTFORML 　「Your payout will be {P_BET * 2}.」
			PRINTFORMW 　「Please take it!」
			TOTAL:2 += P_BET * 2
			CALL CASINO_MONEY("カリスマ", P_BET * 2)
		ENDIF
	ENDIF
	
	;ゲームの続行判定
	$CHECK_NEXTGAME
	IF NEMUKE() >= 700
		P_BET = 0
	ELSEIF MONEY:2 <= 0
		P_BET = 0
	ELSEIF 500000 - FLAG:カリスマ流通 - P_BET * 25 / 10 < 10000
	;ここだけBREAK
		BREAK
	ELSE
		PRINTFORML 　「Would you like to play again?」
		PRINTFORML 
		CALL CASINO_INPUTLOOP("カード続行")
		SELECTCASE RESULT
		CASE 0
			P_BET = 0
		CASE 1
			IF MONEY:2 < P_BET
				PRINTFORML Fairy Maid
				PRINTFORML 　「You don't seem to have enough Charisma on hand...」
				PRINTFORML 　「You'll have to wager a different amount.」
				PRINTFORML 
				CALL CASINO_INPUTLOOP("カード賭け金")
				P_BET = RESULT
				ORI_BET = RESULT
				SIF RESULT
					PRINTFORML 
			ELSE
				P_BET = ORI_BET
			ENDIF
		CASE 2
			PRINTFORML Fairy Maid
			PRINTFORML 　「Please make your wager.」
			PRINTFORML 
			CALL CASINO_INPUTLOOP("カード賭け金")
			P_BET = RESULT
			ORI_BET = RESULT
			SIF RESULT
				PRINTFORML 
		ENDSELECT
	ENDIF

	IF P_BET
		TOTAL:1 += P_BET
		CALL CASINO_MONEY("カリスマ", -P_BET)
		PRINTFORML Fairy Maid
		PRINTFORML 　「Yes, the bet of {P_BET} Charisma has been accepted.」
		PRINTFORMW 　「Thank you for your continued support.」
	ENDIF
WEND

PRINTFORML 
IF NEMUKE() >= 700
	PRINTFORML Fairy Maid
	PRINTFORML 　「Er... Customer?」
	PRINTFORML 　「Today's been a long day.」
	PRINTFORML 　「We'll have to stop playing for now.」
	PRINTFORMW 　「We look forward to seeing you some other time.」
ELSEIF MONEY:2 <= 0
	PRINTFORML Fairy Maid
	PRINTFORML 　「Unfortunately, it looks like you've run out of Charisma...」
	PRINTFORMW 　「We look forward to seeing you some other time.」
ELSEIF P_BET
;払い戻しに店が耐えられない時
	PRINTFORML Fairy Maid
	PRINTFORML 　「I'm sorry, but it looks like we're about to close.」
	PRINTFORMW 　「We look forward to seeing you some other time.」
ELSE
	PRINTFORML Fairy Maid
	PRINTFORML 　「Certainly.」
	PRINTFORMW 　「We look forward to your continued patronage.」
ENDIF
CALLF CARD_PLAYER("CLEAR_BJ_ALL")

IF TOTAL:1
	CALL CASINO_MONEY("カモ", TOTAL:1)
	CALL CASINO_DISP("RESULT", TOTAL:1, TOTAL:2)
ENDIF
VARSET TOTAL
