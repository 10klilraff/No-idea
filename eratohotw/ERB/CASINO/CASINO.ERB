;紅魔カジノ
;とりあえず格好だけ作ったが景品がないのはどうしたもんか
;為替市場もここにいれてます
;各ゲームコーナーで保有カリスマを確認できないのはカリスマを搾取するための罠である

@CASINO_ENTRANCE
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM EXIT
EXIT = 0
CALL CASINO_CHECK_EVENT("ENTRANCE")
PRINTFORML 
PRINTFORML ～Scarlet Devil Casino～
PRINTFORML 　Current holdings:　%金額表示(MONEY), 13, RIGHT%
PRINTFORML 　Current Charisma:　%三桁区切(MONEY:2), 13, RIGHT%
PRINTFORML 
;SIF ITEM:紅魔カジノVIP会員証
;	PRINTFORML [6] - VIPルーム
;PRINTFORML [5] - 景品交換
IF FUNC_CASINO("CLOSE_CASINO")
	PRINTFORML [-] - Slots (Closed)
	PRINTFORML [-] - Poker (Closed)
	PRINTFORML [-] - Blackjack (Closed)
	PRINTFORML [-] - Cards (Closed)
	PRINTFORML [-] - Dice (Closed)
	PRINTFORML [-] - Quiz (Closed)
ELSE
	PRINTFORML [6] - Slots
	PRINTFORML [5] - Poker
	PRINTFORML [4] - Blackjack
	PRINTFORML [3] - Cards
	PRINTFORML [2] - Dice
	PRINTFORML [1] - Quiz
ENDIF
PRINTFORML 
PRINTFORML [0] - Leave
PRINTFORML [9] - Explanation
SIF FLAG:会員ランク
	PRINTFORML [8] - Charisma broker
DO
	INPUT
	;休業中はゲーム系は閉まってる
	SIF FUNC_CASINO("CLOSE_CASINO") && INRANGE(RESULT, 1, 6)
		GOTO RETRY
	SELECTCASE RESULT
	CASE 9
		CALL CASINO_CHECK_EVENT("HELP")
	CASE 8
	SIF FLAG:会員ランク
		CALL CASINO_TRADE
	CASE 0
		;IF MONEY:2 >= TFLAG:カジノ入場時カリスマ + 100
		;	PRINTFORML 「カリスマを現金に交換しますか？」
		;	PRINTFORML 「今ですと手数料がお得ですよ」
		;ENDIF
		EXIT = 1
	CASE 6
		CALL CASINO_SLOT
	CASE 5
		CALL CASINO_POKER
	CASE 4
		CALL CASINO_BLACKJACK
	CASE 3
		CALL CASINO_CARD
	CASE 2
		CALL CASINO_DICE
	CASE 1
		CALL CASINO_QUIZ
	CASEELSE
		$RETRY
		REUSELASTLINE Invalid input
		CONTINUE
	ENDSELECT
	SIF NEMUKE() >= 700
		EXIT = 1
	BREAK
LOOP 1
SIF !EXIT
	RESTART
CALL CASINO_CHECK_EVENT("EXIT")



;カジノ系イベントの独自関数化
;説明パネルもここに突っ込んだ
;可読性の確保のためにマジックナンバーを無理矢理排除した結果珍妙な構文になりますた
@CASINO_CHECK_EVENT(TYPE)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS TYPE
#DIMS 台詞
#DIMS 表情
#DIM COUNT_ENTER ;本日入店した回数
#DIM COUNT_EXIT  ;本日店を出た回数
表情 = 笑顔
SELECTCASE TYPE
CASE "ENTRANCE"
	;紅魔館メンバーに対してそっけない反応ってどうなのよって思った
	;金銭的に盛りすぎなのは承知だが
	;ロールプレイ的にはこうなる気がする
	;なりきり時の呼び名をちょっと改変
	IF !FLAG:カジノ訪問日
	;初回
		PRINTFORML 
		;Custom code, translated csv callnames
		SELECTCASE CSVCALLNAME(FLAG:なりきり)
		CASE "Remilia", "Flandre"
			{
			台詞 = 「%\@ CSVCALLNAME(FLAG:なりきり) == "Remilia" ? Ojou # Imouto \@%-sama!」
					/「We appreciate Your patronage.」
					/「Here, take this gift of 250 Charisma.」
					/「Please enjoy yourself.」
			}
			MONEY:2 += 250
		CASE "Patchouli"
			{
			台詞 = 「Welcome %CALLNAME:MASTER%-sama.」
					/「Yes, it has been a tremendous success.」
					/「Please accept this gift of 200 Charisma from Ojousama.」
					/「Please enjoy yourself.」
			}
			MONEY:2 += 200
		CASE "Sakuya"
			{
			台詞 = 「Welcome %CALLNAME:MASTER%-san.」
					/「Here to inspect? Everyone is trying their hardest.」
					/「Please accept this gift of 200 Charisma from Ojousama.」
			}
			MONEY:2 += 200
		CASE "Meiling", "Koakuma"
			台詞 = 「Welcome %CALLNAME:MASTER%-san.」
			IF GET_DAY() == "日" || GET_DAY() == "土"
				台詞 = %台詞%/「Do you have the day off? Don't do this too often.」
			ELSE
				台詞 = %台詞%/「Did work tire you out today? Make sure to relax.」
			ENDIF
			台詞 = %台詞%/「Please accept this gift of 150 Charisma from Ojousama.」
			表情 = 通常
			MONEY:2 += 150
		CASE "Marisa"
		;図書館がらみで出禁くらっててもおかしくないよね
			{
			台詞 = 「Oh, it's %CALLNAME:MASTER%-san.」
					/「Here, take these 100 Charisma.」
					/「It's a gift we give to all new customers first time they show up.」
					/「Nevertheless, you are not welcomed by Ojousama.」
					/「Please be on your best behavior in the casino and enjoy yourself.」
			}
			MONEY:2 += 100
			表情 = 通常
		CASE "Player"
			IF MAIN_MAP == 3
				{
				台詞 = 「Ara, You are the new helper, are you not?」
						/「There is no need for you to clean here as we have our own dedicated cleaning staff.」
						/「Or perhaps you came to visit?」
						/「Accept this gift of 120 Charisma from Ojousama, spend it carefully.」
				}
				MONEY:2 += 120
			ELSE
				{
				台詞 = 「Welcome to the Scarlet Devil Mansion Casino. Is this the customer's first time here?」
						/「Being a first time customer, Ojousama presents 100 Charisma in commemoration.」
				}
				MONEY:2 += 100
			ENDIF
		CASEELSE
			{
			台詞 = 「Welcome to the Scarlet Devil Casino. Is this the customer's first time here?」
					/「Being a first time customer, Ojousama presents 100 Charisma in commemoration.」
			}
			MONEY:2 += 100
		ENDSELECT
		SIF !顔絵付きメッセージ
			PRINTFORML Fairy Maid
		CALL SPTALK, 0, @"%表情%", 17, @"%台詞%", 1
		CM_LIMIT = 100
		COUNT_EXIT = 0
		COUNT_ENTER = 1
	ELSEIF DAY - FLAG:カジノ訪問日
	;1日1回だけ
		PRINTFORML 
		SELECTCASE DAY - FLAG:カジノ訪問日
		CASE 1 TO 5
			IF MAIN_MAP == 3 && (CSVCALLNAME(FLAG:なりきり) == "Meiling" || CSVCALLNAME(FLAG:なりきり) == "Koakuma")
				台詞 = 「%CALLNAME:MASTER%-san, there's a problem.」/「Won't we get scolded if we play too much?」
				表情 = 通常
			ELSEIF MAIN_MAP == 3 && CSVCALLNAME(FLAG:なりきり) == "Player"
				台詞 = 「Ara, Here again?」/「Enjoy yourself in moderation.」
			ELSE
				台詞 = 「Welcome.」/「Please, enjoy yourself.」
			ENDIF
		CASEELSE
			IF MAIN_MAP == 3 && CSVCALLNAME(FLAG:なりきり) == "Player"
				台詞 = 「Ara, back again?」/「Enjoy yourself within reason, please.」
			ELSE
				台詞 = 「Welcome back.」/「Relax and enjoy yourself.」
			ENDIF
		ENDSELECT
		SIF !顔絵付きメッセージ
			PRINTFORML Fairy Maid
		CALL SPTALK, 0, @"%表情%", 17, @"%台詞%", 1
		COUNT_EXIT = 0
		COUNT_ENTER = 1
	ELSEIF COUNT_ENTER == COUNT_EXIT
	;再来店時
		PRINTFORML 
		IF MAIN_MAP == 3 && CSVCALLNAME(FLAG:なりきり) == "Player"
			台詞 = 「Ara, Here again?」/「Enjoy yourself in moderation.」
		ELSE
			台詞 = 「Welcome back.」/「Please relax and enjoy yourself.」
		ENDIF
		SIF !顔絵付きメッセージ
			PRINTFORML Fairy Maid
		CALL SPTALK, 0, "笑顔", 17, @"%台詞%", 1
		COUNT_ENTER ++
	ELSE
		IF !RAND:10
		;この手の賑やかしってあると楽しいと思うんだ
			;はぐれたETC
			PRINTDATAL
			DATALIST
				DATAFORM %PARSE("You hear")% an announcement from the reception.
				DATAFORM 
				DATAFORM Fairy Maid
				DATAFORM 　「Ehー Calling for ××××-sama from the village.」
				DATAFORM 　「There is someone here waiting for you.」
				DATAFORM 　「Please come to the reception desk.」
			ENDLIST
			DATALIST
				DATAFORM %PARSE("You hear")% an announcement from the reception.
				DATAFORM 
				DATAFORM Fairy Maid
				DATAFORM 　「Ehー Calling for ××××-sama from the sunflower field.」
				DATAFORM 　「There is a Sunflower Fairy-sama is waiting for you.」
				DATAFORM 　「Please come to the reception desk.」
			ENDLIST
			DATALIST
				DATAFORM %PARSE("You hear")% an announcement from the reception.
				DATAFORM 
				DATAFORM Fairy Maid
				DATAFORM 　「Ehー Calling for ××××-sama from the Bamboo Forest.」
				DATAFORM 　「Usagi-sama is here looking for you.」
				DATAFORM 　「Please come to the reception desk.」
			ENDLIST
			DATALIST
				DATAFORM %PARSE("You hear")% an announcement from the reception.
				DATAFORM 
				DATAFORM Fairy Maid
				DATAFORM 　「Ehー Calling for ××××-sama from the Moon.」
				DATAFORM 　「Usagi-sama is here looking for you.」
				DATAFORM 　「Please come to the reception desk.」
			ENDLIST
			DATALIST
				DATAFORM %PARSE("You hear")% an announcement from the reception.
				DATAFORM 
				DATAFORM Fairy Maid
				DATAFORM 　「Ehー Calling for ××××-sama from the Youkai Mountain.」
				DATAFORM 　「Tengu-sama is here looking for you.」
				DATAFORM 　「Please come to the reception desk.」
			ENDLIST
			DATALIST
				DATAFORM %PARSE("You hear")% an announcement from the reception.
				DATAFORM 
				DATAFORM Fairy Maid
				DATAFORM 　「Ehー Calling for ××××-sama from the Genbu Ravine.」
				DATAFORM 　「Kappa-sama is here looking for you.」
				DATAFORM 　「Please come to the reception desk.」
			ENDLIST
			DATALIST
				DATAFORM %PARSE("You hear")% an announcement from the reception.
				DATAFORM 
				DATAFORM Fairy Maid
				DATAFORM 　「Ehー Calling for ××××-sama from the Tentacle Palace.」
				DATAFORM 　「Tentacle-sama is here looking for you.」
				DATAFORM 　「Please come to the reception desk.」
			ENDLIST
			DATALIST
			;字面だけみるとすげーコワイ！
				DATAFORM %PARSE("You hear")% an announcement from the reception.
				DATAFORM 
				DATAFORM Fairy Maid
				DATAFORM 　「Ehー Calling for ××××-sama from the Sanzu River.」
				DATAFORM 　「Shinigami-sama is here looking for you.」
				DATAFORM 　「Please come to the reception desk.」
			ENDLIST
			DATALIST
				DATAFORM %PARSE("You hear")% an announcement from the reception.
				DATAFORM 
				DATAFORM Fairy Maid
				DATAFORM 　「Ehー Calling for ××××-sama from the Sanzu River.」
				DATAFORM 　「Yama-sama is here looking for you.」
				DATAFORM 　「Please come to the reception desk with greatest haste possible.」
			ENDLIST
			DATALIST
				DATAFORM %PARSE("You hear")% an announcement from the reception.
				DATAFORM 
				DATAFORM Fairy Maid
				DATAFORM 　「Ehー Calling for ××××-sama from the Youkai Mountain.」
				DATAFORM 　「Dai Tengu-sama is here looking for you.」
				DATAFORM 　「Please come to the reception desk with greatest haste possible.」
			ENDLIST
			
			DATALIST
				DATAFORM %PARSE("You hear")% an announcement from the reception.
				DATAFORM 
				DATAFORM Fairy Maid
				DATAFORM 　「There is a lost item.」
				DATAFORM 　「A paper bag full of cucumbers have been found.」
				DATAFORM 　「If you know anything, please come to the reception desk.」
			ENDLIST
			DATALIST
				DATAFORM %PARSE("You hear")% an announcement from the reception.
				DATAFORM 
				DATAFORM Fairy Maid
				DATAFORM 　「There is a lost item.」
				DATAFORM 　「A Rokurukubi's「Neck-Down」has been found.」
				DATAFORM 　「Please come here「Neck-Up」.」
				DATAFORM 　「Please inform maid who found the「Neck-Down」whose it is.」
			ENDLIST
			DATALIST
				DATAFORM %PARSE("You hear")% an announcement from the reception.
				DATAFORM 
				DATAFORM Fairy Maid
				DATAFORM 　「There is a lost item.」
				DATAFORM 　「An umbrella has been found.」
				DATAFORM 　「It is a strange eggplant-colored umbrella with a longue tongue and an eye printed on the side.」
				DATAFORM 　「If you know whose it is, please come to the reception desk.」
			ENDLIST
			DATALIST
				DATAFORM %PARSE("You hear")% an announcement from the reception.
				DATAFORM 
				DATAFORM Fairy Maid
				DATAFORM 　「There is a lost item.」
				DATAFORM 　「Umm... It is a crystal ball with a roof a bit larger than your hand.」
				DATAFORM 　「An object like that has been found.」
				DATAFORM 　「If you know whose it is, please come to the reception desk.」
			ENDLIST
			DATALIST
				DATAFORM %PARSE("You hear")% an announcement from the reception.
				DATAFORM 
				DATAFORM Fairy Maid
				DATAFORM 　「There is a lost item.」
				DATAFORM 　「Umm... What is this...?」
				DATAFORM 　「A pink lump of...something, about as large as your hand, has been found.」
				DATAFORM 　「If you know whose it is, please come to the reception desk.」
			ENDLIST
			DATALIST
				DATAFORM %PARSE("You hear")% an announcement from the reception.
				DATAFORM 
				DATAFORM Fairy Maid
				DATAFORM 　「There is a lost item.」
				DATAFORM 　「A mallet has been found.」
				DATAFORM 　「The ends are dyed red and it looks a bit worn out.」
				DATAFORM 　「If you know whose it is, please come to the reception desk.」
			ENDLIST
			DATALIST
				DATAFORM A Fairy Maid is talking with another Fairy Maid at the reception.
				DATAFORM 
				DATAFORM Fairy Maid
				DATAFORM 　「Hey, I've got a question.」
				DATAFORM 　「A child from the village says they want to work here.」
				DATAFORM 　「Sakuya-san will give them more instructions.」
				DATAFORM 　「Ah, Thank you.」
			ENDLIST
			DATALIST
				DATAFORM A Fairy Maid is talking with another Fairy Maid at the reception.
				DATAFORM 
				DATAFORM Fairy Maid
				DATAFORM 　「Hey, I've got a question.」
				DATAFORM 　「This person wants to play with Ojousama.」
				DATAFORM 　「Looks like there will be a tasty treat today.」
				DATAFORM 　「Ah, Thank you.」
			ENDLIST
			DATALIST
				DATAFORM A Fairy Maid is talking with another Fairy Maid at the reception.
				DATAFORM 
				DATAFORM Fairy Maid
				DATAFORM 　「Hey, I've got a question.」
				DATAFORM 　「Yes, those who want to attend Ojousama for a meal go there.」
				DATAFORM 　「As always, my regards.」
			ENDLIST
			DATALIST
				DATAFORM A Fairy Maid is talking with another Fairy Maid at the reception.
				DATAFORM 
				DATAFORM Fairy Maid
				DATAFORM 　「Ah, Is it already time to change shifts?」
				DATAFORM 　「Yeah, thank you as always.」
				DATAFORM 　「You first, I apologize.」
			ENDLIST
			
			ENDDATA
		ELSE
			PRINTFORML A Fairy Maid is serving customers.
			PRINTFORML 
			PRINTFORML Fairy Maid
			PRINTDATAL
			DATALIST
				DATAFORM 　「Ara, Are you some type of magician?」
				DATAFORM 　「Welcome.」
				DATAFORM 　「If we have a game where you can use magic?」
				DATAFORM 　「What about the quiz?」
			ENDLIST
			DATALIST
				DATAFORM 　「Oya, What splendid horns you have, dear Oni customer.」
				DATAFORM 　「Something with「All-In Gambling」?」
				DATAFORM 　「How about poker?」
				DATAFORM 　「With a royal straight flush the return is 300-fold.」
			ENDLIST
			DATALIST
				DATAFORM 　「Welcome.」
				DATAFORM 　「A game for「Making Money」?」
				DATAFORM 　「How about the dice game?」
				DATAFORM 　「If you continue winning the reward will keep increasing.」
			ENDLIST
			DATALIST
				DATAFORM 　「Welcome.」
				DATAFORM 　「Thank you for your patronage.」
				DATAFORM 　「『I am looking for the new slot machines』?」
				DATAFORM 　「Ah, That would be on the other side, over there.」
			ENDLIST
			DATALIST
				DATAFORM 　「Excuse me, Customer?」
				DATAFORM 　「Could you please not bring dead bodies into the casino?」
				DATAFORM 　「Ee, We cater to living humans as well you see.」
			ENDLIST
			DATALIST
				DATAFORM 　「This feels like a day to win!」
				DATAFORM 　「Make sure to enjoy yourself.」
				DATAFORM 　「We look forward to your continued patronage.」
			ENDLIST
			DATALIST
				DATAFORM 　「I've finally been able to take on human form!」
				DATAFORM 　「Congratulations!」
				DATAFORM 　「I will be in your care from now on.」
			ENDLIST
			ENDDATA
		ENDIF
	ENDIF
	FLAG:カジノ訪問日 = DAY
CASE "EXIT"
	COUNT_EXIT ++
	CALL CASINO_MEMBER_RANKUP
	PRINTFORML 
	SIF !顔絵付きメッセージ
		PRINTFORML Fairy Maid
	{
	台詞 = 「Thank you for your patronage.」
			/「%TEXTR("Please take care and come back again.
			/Please come visit us again.
			/We look forward to your next visit.")%」
	}
	CALL SPTALK, 0, "笑顔", 17, @"%台詞%", 1
CASE "HELP"
;へるぷ機能
	PRINTFORML 
	PRINTFORML There's a list of game rules posted at casino entrance
	PRINTFORML 
	PRINTFORML [9] - Casino rules
	PRINTFORML [8] - About Charisma
	PRINTFORML 
	PRINTFORML [6] - About Slots
	PRINTFORML [4] - About Blackjack
	PRINTFORML [3] - About the card game
	PRINTFORML [2] - About the dice game
	PRINTFORML [1] - About the quiz game
	PRINTFORML 
	PRINTFORML [0] - Return
	DO
		INPUT
		SELECTCASE RESULT
		CASE 9
			;魔理沙分岐を仕込んだことで注意書きに「窃盗」が無かったなと気づく
			PRINTFORML ☆Casino Rules
			PRINTFORML 　Usage of magic or special abilities, counterfeit money, theft, fraud, violence, threats, blackmail, sexual harassment, 
			PRINTFORML 　Danmaku inside the building, spell cards, Pichuun, partying, sutra chanting, cleaning, bathing, and making tea is prohibited.
			PRINTFORML 　Please heed this warning, any customer caught disturbing the peace will be invited to eat with Ojousama, or become the plaything of her little sister.
			PRINTFORML 
			PRINTFORML 　We hope everyone behaves and enjoys themselves.
			PRINTFORMW 　Thank you from the bottom of our hearts, signed - the staff.
		CASE 8
			PRINTFORML ☆About Charisma
			PRINTFORML 　To prevent fraud at this casino, we use a currency called“Charisma”instead.
			PRINTFORML 　Please go to the front desk if you want to trade.
			PRINTFORMW 　Cash will not be accepted in any casino game.
			PRINTFORML 
			PRINTFORML 　Vendors currently accepting Charisma as payment:
			PRINTFORML 　・%"Pure and Honest",40,LEFT%　「Bunbunmaru Newspaper」
			PRINTFORML 　・%"Rental Library in the Human Village",40,LEFT%　「Suzunaan」
			PRINTFORML 　・%"Profit-Guaranteed",40,LEFT%　「Moriya Lottery」
			PRINTFORML 　・%"Amazing View",40,LEFT%　「Moriya Shrine Cableway」
			PRINTFORML 　・%"Wonderful Omikuji of Paradise",40,LEFT%　「Hakurei Shrine Omikuji」
			PRINTFORML 　・%"Toriyori Eel",40,LEFT%　「Bamboo Forest Eel Stand」
			PRINTFORML 　・%"Find of Lost Things at Byakuren Temple",40,LEFT%　「Dowser Nazrin」
			PRINTFORML 
			PRINTFORML 　Further, the Scarlet Devil Mansion Hobgoblin services accept them.
			PRINTFORML 　・Scarlet Devil Mansion Hobgoblin watering service
			PRINTFORMW 　・Scarlet Devil Mansion Hobgoblin cleaning service
		CASE 6
			CALL HELP_SLOT()
			RESTART
		CASE 4
			PRINTFORML ☆About Blackjack
			PRINTFORML 　※Rules are different from normal Blackjack
			PRINTFORML 　The one with the highest value is the winner.
			PRINTFORML 　But do not go above 21.
			PRINTFORML 　If someone gets above 21 they lose instantly.
			PRINTFORML 　
			PRINTFORML 　Points are calculated as follows:
			PRINTFORML 　　１～10：Worth their own value
			PRINTFORML 　　ＪＱＫ：10 points
			PRINTFORML 　　　　Ａ：10 points unless that puts you over 21, in which case 1 point
			PRINTFORMW 　
			PRINTFORML －－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－
			PRINTFORML 　Two cards will be issued to each player.
			PRINTFORML 　At first only one of the dealer's cards will be visible.
			PRINTFORML 　You will be given these choices.
			PRINTFORML 　　　　   Hit: Gives you 1 more card
			PRINTFORML 　　　   Stand: The dealer reveals their cards and scores are calculated
			PRINTFORML 　　　　Double: Take 1 more card but double the wager
			PRINTFORML 　　 Surrender: You will receive half the wager back and end the game
			PRINTFORML 　
			PRINTFORML 　If you ever exceed 21 you will lose instantly.
			PRINTFORML 　If that's not the case, the dealer will reveal their cards.
			PRINTFORML 　If it's less than 17, the dealer will always take another card.
			PRINTFORML 　You win if the dealer ends up over 21.
			PRINTFORML 　
			PRINTFORML 　If the dealer has less than 17, draws a card, and ends up with less than 21, you win if you have a higher score.
			PRINTFORML 　The wager is returned when new cards are issued.
			PRINTFORMW 　
			PRINTFORML －－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－
			PRINTFORML 　When two cards sum up to 21（Ａ＋10 point card）, it's called a Blackjack.
			PRINTFORML 　It also counts as Blackjack if it's via 3 cards.
			PRINTFORML 　There is a bonus if you win via Blackjack.
			PRINTFORML 　
			PRINTFORML 　When the dealer's card is Ａ, you can choose to get insurance（INS）.
			PRINTFORML 　INS changes half the wagered money to insured money.
			PRINTFORML 　If the dealer gets a Blackjack you get the insured money back.
			PRINTFORML 　If the dealer does not get a Blackjack, you do not get the insured money back.
			PRINTFORML 　
			PRINTFORML 　If you choose INS and get a Blackjack, you can choose to cash out.
			PRINTFORML 　Choosing this gives twice the wager and ends the game.
			PRINTFORMW 　
			PRINTFORML －－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－－
			PRINTFORML 　These are the payouts.
			PRINTFORML 　 Win with BJ: 2.5 Times
			PRINTFORML 　　　　　 Win: 2.0 Times
			PRINTFORML 　　　　　Lose: 0.0 Times
			PRINTFORML 　　Surrender : 0.5 Times
			PRINTFORML 　Win with INS: 1.0 Times
			PRINTFORMW ※Feint of hand not implemented
		CASE 3
			PRINTFORML ☆About Card Game
			PRINTFORML 　It is a game about cheating.
			PRINTFORML 　You play against the Head Fairy Maid.
			PRINTFORML 　Three rounds are played before a winner is determined.
			PRINTFORML 　You may bet 1-50 Charisma.
			PRINTFORML 　Winning against the Head Fairy Maid gives back 2.5 times the wager.
			PRINTFORML 
			PRINTFORML 　During the game you can choose between three options.
			PRINTFORML 　They have a triangular relationship（Cheat＞Fair＞Look＞Cheat）
			PRINTFORML 　　Cheat: Try to win by cheating
			PRINTFORML 　　 Fair: Try to win according to the rules
			PRINTFORML 　　 Look: Prevent the opponent from cheating
			PRINTFORMW 　By the way, you can use cooking skills to cheat better, and cleaning skills to see through cheats.
		CASE 2
			PRINTFORML ☆About Dice Game
			PRINTFORML 　This is a game about rolling odd or even.
			PRINTFORML 　The Head Fairy Maid will watch over the game.
			PRINTFORML 　You must wager 100 Charisma to play.
			PRINTFORML 　You may receive a payout instantly upon winning.
			PRINTFORML 　If you guess right, the wager will be multiplied by 1.5, but if you're wrong it will go to 0.
			PRINTFORML 　Also, you need to pay 100 Charisma each time to play.
			PRINTFORML 　
			PRINTFORML 　The Head Fairy Maid will predict how they will roll, but she might lie.
			PRINTFORMW 　Having good conversational skills lets you see through her lies.
		CASE 1
			PRINTFORML ☆About Quiz
			PRINTFORML 　A normal quiz game, nothing fancy.
			PRINTFORML 　It is supervised by a close friend of the establishment.
			PRINTFORML 　Price is 5 Charisma per question regardless of difficulty.
			PRINTFORML 　If you answer correct, the price money increases depending on the difficulty, and the difficulty of the next question is revealed.
			PRINTFORMW 　Since there is no element of bargaining, you can only rely on your knowledge.
		CASE 0
			RETURN
		CASEELSE
			REUSELASTLINE Invalid input
			CONTINUE
		ENDSELECT
		BREAK
	LOOP 1
	RESTART
ENDSELECT



;D/Pはディーラー、プレイヤー
;ディーラーの能力は咲夜さんより少し弱いくらいでかなり優秀
;POINT 注:9、正:12、イ:15
;戦闘収支
;　注　vs正＝-3、vsイ＝-6or+9
;　正　vs注＝+3、vsイ＝-3
;　イ　vs注＝+6or-9、vs正＝+3
;注視には清掃、イカサマには料理が必要
;当然ながら対応技能がディーラーを下回ってると全然成功しない
;
;じゃんけんとして捉えるとわかりやすいが
;システム上、対応技能が4以下で挑むと長期的な損益は必ずマイナスになる
;イカサマ関連の仕様で「グーがチョキに勝てなかったりする」ことがあるためである
;あんま詳しく検証してないがたぶん技能が拮抗しているときの勝率が1/3未満

@CASINO_CARD
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM LCOUNT
#DIM D_POINT
#DIM D_ACT
#DIM D_STOP_IKASAMA
#DIM D_DO_IKASAMA
#DIM P_BET
#DIM P_POINT
#DIM P_ACT
#DIM P_STOP_IKASAMA
#DIM P_DO_IKASAMA
D_STOP_IKASAMA = POWER(4, 3)
D_DO_IKASAMA = POWER(4, 3)
P_STOP_IKASAMA = POWER(MAX(1, ABL:MASTER:清掃技能), 3)
P_DO_IKASAMA = POWER(MAX(1, ABL:MASTER:料理技能), 3)
IF MONEY:2 <= 0
	PRINTFORML Fairy Maid
	PRINTFORML 　「Customer, we apologize but this casino uses only a currency called “Charisma”.」
	PRINTFORMW 　「We are sorry to bother you, but please exchange for some before playing.」
	RETURN
ENDIF
PRINTFORML Fairy Maid
PRINTFORML 　「Welcome to the Card Game Corner.」
PRINTFORML 　「In this game you compete against the Head Fairy Maid.」
PRINTFORMW 　「Gensokyo's best “Games for Housewives”!」
PRINTFORMW 　(Kaji No Geemu - Casino Game, I can't translate that, it's a pun, sorry)
PRINTFORML 
PRINTFORML Fairy Maid
PRINTFORML 　「If the customer beats me, you will get 3 times the money back.」
PRINTFORML 　「Please enter your wager first.」
PRINTFORML 
CALL CASINO_INPUTLOOP("カード賭け金")
IF !RESULT
	GOTO CANCEL
ELSE
	P_BET = RESULT
	CALL CASINO_MONEY("カリスマ", -P_BET)
	CALL CASINO_MONEY("カモ", P_BET)
ENDIF
PRINTFORML Fairy Maid
PRINTFORML 　「The wager has been received.」
PRINTFORML 　「Now then, let's begin.」
PRINTFORMW 　「I wish you good luck.」
DO
	TIME += 5
	D_POINT = 0
	P_POINT = 0
	PRINTFORML 
	FOR LCOUNT, 1, 4
		PRINTFORML ～Round {LCOUNT}～
		;[3]イカサマ[2]正々堂々[1]注視
		CALL CASINO_INPUTLOOP("カード戦術")
		P_ACT = RESULT
		D_ACT = RAND(1, 4)
		SELECTCASE P_ACT
		CASE 1
		;注視
			P_POINT += 9
			SELECTCASE D_ACT
			CASE 1
			;注視
				D_POINT += 9
				PRINTFORML %PARSE("You")% and the Fairy Maid stare intently at each other.
				PRINTFORML After a moment she blushes and looks away.
			CASE 2
			;正々堂々
				D_POINT += 12
				PRINTFORML The fairy maid doesn't seem nervous about being watched.
			CASE 3
			;イカサマ
				PRINTFORML %PARSE("You stare")% intently at the Fairy Maid who seems to be making suspicious movements.
				LOCAL = D_DO_IKASAMA + P_STOP_IKASAMA
				IF RAND:LOCAL <= D_DO_IKASAMA
					D_POINT += 15
					PRINTFORML But the fairy maid manages to complete her move without getting caught.
				ELSE
					PRINTFORML The fairy maid looks irritated that her move was blocked.
				ENDIF
			ENDSELECT
		CASE 2
		;正々堂々
		P_POINT += 12
			SELECTCASE D_ACT
			CASE 1
			;注視
				D_POINT += 9
				PRINTFORML %PARSE("You play")% fairly, but feel the Fairy Maid staring at %PARSE("you",5)%.
			CASE 2
			;正々堂々
				D_POINT += 12
				PRINTFORML The game moves forward without delay.
			CASE 3
			;イカサマ
				D_POINT += 15
				PRINTFORML %PARSE("You play")% fairly, ignoring the slight smile of the Fairy Maid.
			ENDSELECT
		CASE 3
		;イカサマ
			SELECTCASE D_ACT
			CASE 1
			;注視
				D_POINT += 9
				LOCAL = D_STOP_IKASAMA + P_DO_IKASAMA
				PRINTFORML %PARSE("You try")% to cheat, but the Fairy Maid watches %PARSE("you",5)% closely.
				IF RAND:LOCAL <= D_STOP_IKASAMA
					PRINTFORML Couldn't finish the move.
				ELSE
					P_POINT += 15
					PRINTFORML Succeeded regardless.
				ENDIF
			CASE 2
			;正々堂々
				D_POINT += 12
				P_POINT += 15
				PRINTFORML %PARSE("You cheat")% skillfully while the fairy plays the game fairly.
			CASE 3
			;イカサマ
				D_POINT += 15
				P_POINT += 15
				PRINTFORML %PARSE("You")% and the Fairy Maid both cheat while pretending to play fairly.
			ENDSELECT
		ENDSELECT
		WAIT
	NEXT
	PRINTFORML 
	PRINTFORML ～Game results～
	IF D_POINT >= P_POINT
		PRINTFORMW %PARSE("You were")% defeated.
	ELSE
		PRINTFORMW %PARSE("You")% won.
		PRINTFORML 
		PRINTFORML Fairy Maid
		PRINTFORML 　「Congratulations.」
		PRINTFORML 　「Now then, please accept the payout.」
		PRINTFORML 
		PRINTFORMW Charisma increased by {P_BET * 3}
		CALL CASINO_MONEY("カリスマ", P_BET * 3)
	ENDIF
	IF NEMUKE() >= 700
		PRINTFORML 
		PRINTFORML Fairy Maid
		PRINTFORML 　「Er... Customer?」
		PRINTFORML 　「Today's been a long day.」
		PRINTFORML 　「We'll have to stop playing for now.」
		PRINTFORMW 　「We look forward to seeing you some other time.」
		RETURN
	ELSEIF MONEY:2 <= 0
		PRINTFORML 
		PRINTFORML Fairy Maid
		PRINTFORML 　「Unfortunately, it looks like you've run out of Charisma...」
		PRINTFORMW 　「We look forward to seeing you some other time.」
		RETURN
	ENDIF
	PRINTFORML 
	PRINTFORML Fairy Maid
	PRINTFORML 　「Would you like to play again?」
	PRINTFORML 
	;[2]賭け金の再設定[1]続行[0]やめる
	CALL CASINO_INPUTLOOP("カード続行")
	SELECTCASE RESULT
	CASE 0
		GOTO CANCEL
	CASE 1
		IF MONEY:2 < P_BET
			PRINTFORML Fairy Maid
			PRINTFORML 　「You don't seem to have enough Charisma on hand.」
			PRINTFORML 　「You'll have to wager a different amount.」
			PRINTFORML 
			CALL CASINO_INPUTLOOP("カード賭け金")
			IF !RESULT
				GOTO CANCEL
			ELSE
				P_BET = RESULT
				PRINTFORML 
			ENDIF
		ENDIF
	CASE 2
		PRINTFORML Fairy Maid
		PRINTFORML 　「Then, please announce your wager.」
		PRINTFORML 
		CALL CASINO_INPUTLOOP("カード賭け金")
		IF !RESULT
			GOTO CANCEL
		ELSE
			P_BET = RESULT
			PRINTFORML 
		ENDIF
	ENDSELECT
	CALL CASINO_MONEY("カリスマ", -P_BET)
	CALL CASINO_MONEY("カモ", P_BET)
	PRINTFORML Fairy Maid
	PRINTFORML 　「Yes, the bet of {P_BET} Charisma has been accepted.」
	PRINTFORMW 　「Thank you for playing.」
LOOP 1
$CANCEL
PRINTFORML 
PRINTFORML Fairy Maid
PRINTFORML 　「Certainly.」
PRINTFORMW 　「We look forward to your continued patronage.」



;D/Pはディーラーとプレイヤー
;
;適当に作ったがコレかなり巧妙な手口だなぁ……
;・実は2勝、3勝の賞金よりも1勝の賞金が多い
;・獲得機会の多いであろう3勝以下の賞金は雀の涙
;・不規則なタイミングで正解が判明するためワンチャンあるかもと錯覚する
;
;賞金×1.5の操作とプレイヤー収入
;01回_150　__50
;02回_225　__25
;03回_337　__37
;04回_506　_106
;05回_759　_259
;06回1139　_539
;07回1708　1008
;08回2562　1762
;09回3840　2940
;10回5766　4766
;
;ディーラーの話術は4、これもわりとぶっ飛んだ性能の人だ
;
;プレイヤーの能力に応じた正誤検知率と誤答率
;話術1＝_1→検知率_1/16＝_6％、不明率94％÷2＝誤答率47％
;話術2＝_4→検知率_4/17＝23.5％、不明率76.5％÷2＝誤答率38％
;話術3＝_9→検知率_9/_25＝36％、不明率64％÷2＝誤答率32％
;話術4＝16→検知率_16/32＝50％、不明率50％÷2＝誤答率25％
;話術5＝25→検知率25/41＝61％、不明率39％÷2＝誤答率20％
;話術6＝36→検知率_36/_52＝69％、不明率31％÷2＝誤答率16％

;カンストでの10連勝率 17.4%

;2乗ではなく単純計算だと

;話術1→検知率_1/5＝_20％、不明率80％÷2＝誤答率40％
;話術2→検知率_2/6＝33.3％、不明率66.6％÷2＝誤答率33.3％
;話術3→検知率_3/_7＝36％、不明率42.8％÷2＝誤答率21.4％
;話術4→検知率_4/8＝50％、不明率50％÷2＝誤答率25％
;話術5→検知率5/9＝55.6％、不明率44.4％÷2＝誤答率22.2％
;話術6→検知率6/10＝60％、不明率40％÷2＝誤答率20％

;カンストでの10連勝率 10.7%


;単純計算でディーラーを5に

;話術1→検知率_1/6 不明率83％÷2＝誤答率41.5％
;話術2→検知率_2/7 不明率71.4％÷2＝誤答率35.7％
;話術3→検知率_3/8 不明率62.5％÷2＝誤答率31.3％
;話術4→検知率_4/9 不明率55.6％÷2＝誤答率27.8％
;話術5→検知率5/10 不明率50％÷2＝誤答率25％
;話術6→検知率6/11 不明率45.5％÷2＝誤答率22.8％

;カンストでの10連勝率 7.5%

;

;
;話術6で200回くらいやれば当たることは当たるのかな
;額面は4766だが2000を引いたら儲けは半分くらいだ
@CASINO_DICE
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM HAITOU
#DIM DICE_1
#DIM DICE_2
#DIM DICE_SUM
#DIM D_YOSOU
#DIM D_TALK
#DIM P_YOSOU
#DIM P_TALK
D_TALK = POWER(4, 2)
P_TALK = POWER(MAX(ABL:MASTER:話術技能, 1), 2)
IF MONEY:2 <= 0
	PRINTFORML Fairy Maid
	PRINTFORML 　「Ah, Customer doesn't have any Charisma...」
	PRINTFORMW 　「You can't play without it.」
	RETURN
ELSEIF MONEY:2 < 100
	PRINTFORML Fairy Maid
	PRINTFORML 　「This is played 100 Charisma at a time～」
	PRINTFORMW 　「Sorry～」
	RETURN
ENDIF
PRINTFORML Fairy Maid
PRINTFORML 　「This is the Dice Game Corner.」
PRINTFORMW 　「Please have a seat.」
HAITOU = 100
DO
	TIME += 5
	PRINTFORML 
	PRINTFORML Fairy Maid
	PRINTFORML 　「So, I will collect the 100 Charisma fee.」
	CALL CASINO_MONEY("カリスマ", -100)
	CALL CASINO_MONEY("カモ", 100)
	DICE_1 = FUNC_CASINO("DICE", 1, 6)
	DICE_2 = FUNC_CASINO("DICE", 1, 6)
	DICE_SUM = DICE_1 + DICE_2
	D_YOSOU = RAND(1, 3)
	PRINTFORML 
	PRINTFORML Fairy Maid
	PRINTFORML 　「Then I'll start rolling.」
	PRINTFORML 　「So, even or odd...」
	IF D_YOSOU % 2
		PRINTFORML 　「My prediction is... Odd!」
	ELSE
		PRINTFORML 　「My prediction is... Even!」
	ENDIF
	WAIT
	PRINTFORML 
	LOCAL = D_TALK + P_TALK
	IF RAND:LOCAL <= D_TALK
		PRINTDATAL
			DATAFORM Is she lying?
			DATAFORM Is that the truth?... Maybe not.
			DATAFORM The way she moves her eyebrows, is she lying???
			DATAFORM No matter how much I think I can't tell if it's the truth.
			DATAFORM The Fairy Maid yawns with a big Fuwaa～n.
			DATAFORM The Fairy Maid looks at %PARSE("you")% like %PARSE("you're",1)% an idiot.
			DATAFORM The Fairy Maid seems a bit dull, %PARSE("you're")% not sure what she's thinking.
			DATAFORM The Fairy Maid's nose is twitching... Oh, That's an adorable sneeze.
		ENDDATA
	ELSE
		IF DICE_SUM % 2 == D_YOSOU % 2
			PRINTFORML What that fairy is saying is the truth!
		ELSE
			PRINTFORML That fairy is lying!
		ENDIF
	ENDIF
	PRINTFORML 
	;[2]丁[1]半
	CALL CASINO_INPUTLOOP("丁半予想")
	P_YOSOU = RESULT
	PRINTFORML 
	PRINTFORML Fairy Maid
	PRINTFORML 　「Time to announce the result.」
	PRINTFORML 　「I open the box, and it's...?」
	PRINTFORML 
	PRINTFORML ～Result～
	IF DICE_SUM % 2
		PRINTFORML The dice show {DICE_1} and {DICE_2}, odd.
	ELSE
		PRINTFORML The dice show {DICE_1} and {DICE_2}, even.
	ENDIF
	
	IF DICE_SUM % 2 == P_YOSOU % 2
	;客の勝ち
		TIMES HAITOU, 1.50
		PRINTFORML Fairy Maid
		PRINTFORML 　「Oh, Quite right, you will get 1.5 times {HAITOU} as return.」
		IF MONEY:2 < 100
			PRINTFORML 　「Customer cannot continue at this point, so let's stop here.」
			CALL CASINO_MONEY("カリスマ", HAITOU)
			SIF 500000 - FLAG:カリスマ流通 < 10000
				GOTO CLOSE_SHOP
			PRINTFORML 　「So now then, do you want to continue?」
			;[1]続行[0]やめる
			CALL CASINO_INPUTLOOP("続行")
			IF !RESULT
				PRINTFORML Fairy Maid
				PRINTFORMW 　「Please come again.」
				RETURN
			ELSE
				PRINTFORML Fairy Maid
				PRINTFORMW 　「Ah, Thought so.」
				HAITOU = 100
			ENDIF
		ELSEIF NEMUKE() >= 700
			CALL CASINO_MONEY("カリスマ", HAITOU)
			$EXIT_NEMUI
			PRINTFORML 
			PRINTFORML Fairy Maid
			PRINTFORML 　「N～ I wonder if Customer is sleepy as well.」
			PRINTFORML 　「I think that's it for today.」
			PRINTFORMW 　「Come again～」
			RETURN
		ELSEIF 500000 - FLAG:カリスマ流通 - HAITOU * 15 / 10 < 10000
			CALL CASINO_MONEY("カリスマ", HAITOU)
			$CLOSE_SHOP
			PRINTFORML 
			PRINTFORML Fairy Maid
			PRINTFORML 　「Oops, The house is all out of money.」
			PRINTFORML 　「We will have to stop for today.」
			PRINTFORMW 　「Please come again.」
			RETURN
		ELSE
			PRINTFORML 　「So, do you want to continue? Or do you want to stop?」
			;[2]続行[1]精算して続行[0]精算してやめる
			CALL CASINO_INPUTLOOP("ダイス続行")
			SELECTCASE RESULT
			CASE 0
				PRINTFORML Fairy Maid
				PRINTFORML 　「Please accept the payout of {HAITOU} Charisma.」
				PRINTFORMW 　「Please come again.」
				CALL CASINO_MONEY("カリスマ", HAITOU)
				RETURN
			CASE 1
				PRINTFORML Fairy Maid
				PRINTFORML 　「Careful are you?」
				PRINTFORMW 　「Please accept the payout of {HAITOU} Charisma.」
				CALL CASINO_MONEY("カリスマ", HAITOU)
				HAITOU = 100
			CASE 2
				PRINTFORML Fairy Maid
				PRINTFORMW 　「Thought so, let's keep going.」
			ENDSELECT
		ENDIF
	ELSE
	;客の負け
		PRINTFORML Fairy Maid
		PRINTFORML 　「How regrettable.」
		IF MONEY:2 < 100
			PRINTFORMW 　「You cannot continue without Charisma.」
			RETURN
		ELSEIF NEMUKE() >= 700
			WAIT
			GOTO EXIT_NEMUI
		ELSE
			PRINTFORML 　「Do you want to pay 100 Charisma and continue?」
			;[1]続行[0]やめる
			CALL CASINO_INPUTLOOP("続行")
			IF !RESULT
				PRINTFORML Fairy Maid
				PRINTFORMW 　「Please come again.」
				RETURN
			ELSE
				PRINTFORML Fairy Maid
				PRINTFORMW 　「Thought so, let's keep going.」
				HAITOU = 100
			ENDIF
		ENDIF
	ENDIF
LOOP 1



;D/Pはディーラーとプレイヤー
;最高賞金は90で消費カリスマの4.5倍という屈指のコスパを誇るも教養6でようやく正答率50％
;さらに動く額面が固定して小さいため大して稼げない
;確率で正解が決定するためいかに教養を上げても誤答率は0にはならないという地味な罠もある
@CASINO_QUIZ
#LOCALSIZE 1
#LOCALSSIZE 10
#DIM Q_LV
#DIM Q_COUNT
#DIM Q_CHECK
#DIM Q_BET
Q_COUNT = 1
Q_BET = 5
SIF !STRLENS(LOCALS)
	SPLIT "/is a question that even a fairy could answer/is a question suitable for ordinary humans/is a question suitable for extraordinary humans/is a question you'd need to be a wizard to answer/question would be difficult even for a wizard/is a tricky problem that can stump anyone", "/", LOCALS
PRINTFORML Fairy Maid
PRINTFORML 　「Alright it's time to begin!」
PRINTFORML 　「Under the perfect supervision of 『Unmoving Great Library』Magician Patchouli-sama!」
PRINTFORMW 　「It's time for the “Patchouli-quiz”!!」
IF MONEY:2 < Q_BET
	PRINTFORMW 　「... Unfortunately, the guest doesn't have enough Charisma.」
	RETURN
ENDIF

Q_LV = 1
PRINTFORML Fairy Maid
PRINTFORMW 　「Let's begin right away with question #1!」
DO
	TIME += 5
	CALL CASINO_MONEY("カリスマ", -Q_BET)
	CALL CASINO_MONEY("カモ", Q_BET)
	PRINTFORML 　・
	PRINTFORML 　・
	PRINTFORML 　・
	PRINTFORML 
	PRINTFORMW Answered the question
	PRINTFORML 

	Q_CHECK = POWER(Q_LV, 3)
	LOCAL = Q_CHECK + POWER(MAX(ABL:MASTER:教養, 1), 3)
	IF RAND:LOCAL <= Q_CHECK
		PRINTFORML Fairy Maid
		PRINTFORML 　「Bad luck! Wrong answer～」
	ELSE
		PRINTFORML Fairy Maid
		PRINTFORML 　「Amazing, spot on!」
		PRINTFORML 　「You win {Q_LV * Q_BET}!!!」
		CALL CASINO_MONEY("カリスマ", Q_LV * Q_BET)
	ENDIF
	CALL RECOVER(MASTER,-50,"気力")
	WAIT
	PRINTFORML 
	IF MONEY:2 < Q_BET
		PRINTFORML Fairy Maid
		PRINTFORML 　「Aww, the guest has run out of Charisma～!!」
		PRINTFORMW 　「Please come back some other time～」
		RETURN
	ELSEIF FUNC_CASINO("CLOSE_CASINO")
		PRINTFORML Fairy Maid
		PRINTFORML 　「Aww, that's it for today!!」
		PRINTFORMW 　「Please come back some other time～」
		RETURN
	ELSEIF NEMUKE() >= 700 || BASE:MASTER:気力 < 500
		PRINTFORML Fairy Maid
		PRINTFORML 　「Oya～oya～ The guest seems completely exhausted!!」
		PRINTFORMW 　「Please come back some other time～」
		RETURN
	ENDIF

	Q_COUNT ++
	SELECTCASE RAND(1, 101)
	CASE 1 TO 8
		Q_LV = 1
	CASE 9 TO 22
		Q_LV = 2
	CASE 23 TO 50
		Q_LV = 3
	CASE 51 TO 78
		Q_LV = 4
	CASE 79 TO 92
		Q_LV = 5
	CASE 93 TO 100
		Q_LV = 6
	ENDSELECT
	PRINTFORML Fairy Maid
	PRINTFORML 　「Now for question #{Q_COUNT}!」
	PRINTFORML 　「This %LOCALS:Q_LV%.」
	PRINTFORML 　「If you're scared, we can stop here～」
	PRINTFORML 　「What will you doー!?」
	PRINTFORML 
	;[1]続行[0]やめる
	CALL CASINO_INPUTLOOP("続行")
	IF !RESULT
		PRINTFORML Fairy Maid
		PRINTFORML 　「Thank you for playing!!」
		PRINTFORMW 　「Come back anytime～」
		RETURN
	ENDIF
	PRINTFORML Fairy Maid
	PRINTFORMW 　「Then, I'll read the next question!」
LOOP 1



;カジノ用ファンクション
@FUNC_CASINO(TYPE, ARG, ARG:1)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS TYPE
#DIM LCOUNT
#DIM YEN
#DIM CHARISMA
SELECTCASE TYPE
CASE "DICE"
;汎用で使えるがダイス関数
;個人的に改造のたびになんやかんや毎回作って入れてるような気がする
	LOCAL = 0
	FOR LCOUNT, 0, ARG
		LOCAL += RAND(1, ARG:1)
	NEXT
CASE "TRADE"
;紅魔館用の為替取引レート
;基本レート￥200 == 1カリスマ
	LOCAL = CHARISMA_NOW()
CASE "CLOSE_CASINO"
;カジノ休業判定
;廃止になったような気もするが、読みやすいように
	IF 500000 - FLAG:カリスマ流通 < 10000
		LOCAL = 1
	ELSE
		LOCAL = 0
	ENDIF
ENDSELECT
RETURNF LOCAL



;カジノ金銭取引関数
;あなたが1万￥を得た場合には
;CALL CASINO_MONEY("￥", 10000)
;あなたが100カリスマを失った場合には
;CALL CASINO_MONEY("カリスマ", -100)
;
;あなたの最高収入はたぶん一ヶ月で100万には届かない
;紅魔館側で1億＄と50万カリスマを用意
@CASINO_MONEY(TYPE, ARG)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS TYPE
SELECTCASE TYPE
CASE "￥"
	;払い戻し上限
	IF ARG > 0
		LOCAL = MIN(ARG, 100000000 - FLAG:￥流通)
		SIF LOCAL < 0
			LOCAL = 0
	ELSE
		LOCAL = ARG
	ENDIF
	MONEY += LOCAL
	FLAG:￥流通 += LOCAL
CASE "カリスマ"
	;払い戻し上限
	IF ARG > 0
		LOCAL = MIN(ARG, 500000 - FLAG:カリスマ流通)
		SIF LOCAL < 0
			LOCAL = 0
	ELSE
		LOCAL = ARG
	ENDIF
	MONEY:2 += LOCAL
	FLAG:カリスマ流通 += LOCAL
CASE "カモ"
	FLAG:カモられ指数 += ARG
ENDSELECT



;汎用性もクソもねぇ
;さして意味もないDO-LOOPがうざかった
@CASINO_INPUTLOOP(TYPE)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS TYPE
#DIM C_MAX
#DIM LCOUNT
SELECTCASE TYPE
CASE "カード賭け金"
	C_MAX = MIN(50, MONEY:2, (500000 - FLAG:カリスマ流通 - 10000) / 2)
	PRINTFORML Charisma: %三桁区切(MONEY:2)%  (Bet limit: %三桁区切(C_MAX)%)
	SIF C_MAX >= 1
		PRINTFORML [{1, LOG10(C_MAX) + 1}] - 1 Charisma
	SIF C_MAX >= 10
		PRINTFORML [{10, LOG10(C_MAX) + 1}] - 10 Charisma
	SIF C_MAX >= 4
		PRINTFORML [{C_MAX / 2, LOG10(C_MAX) + 1}] - Half the maximum
	PRINTFORML [{C_MAX, LOG10(C_MAX) + 1}] - Maximum bet
	DO
		INPUT
		IF INRANGE(RESULT, 0, C_MAX)
			BREAK
		ELSE
			REUSELASTLINE Invalid input
			CONTINUE
		ENDIF
	LOOP 1
CASE "カード戦術"
	PRINTFORML The chosen strategy is...
	PRINTFORML [3] - Cheat
	PRINTFORML [2] - Play fair
	PRINTFORML [1] - Watch
	DO
		INPUT
		IF INRANGE(RESULT, 1, 3)
			BREAK
		ELSE
			REUSELASTLINE Invalid input
			CONTINUE
		ENDIF
	LOOP 1
CASE "カード続行"
	PRINTFORML [2] - Change wager
	PRINTFORML [1] - Continue
	PRINTFORML [0] - Stop
	DO
		INPUT
		IF INRANGE(RESULT, 0, 2)
			BREAK
		ELSE
			REUSELASTLINE Invalid input
			CONTINUE
		ENDIF
	LOOP 1
CASE "続行"
	PRINTFORML [1] - Continue
	PRINTFORML [0] - Stop
	DO
		INPUT
		IF INRANGE(RESULT, 0, 1)
			BREAK
		ELSE
			REUSELASTLINE Invalid input
			CONTINUE
		ENDIF
	LOOP 1
CASE "丁半予想"
	PRINTFORML ～\@THIRD_PERSON?Pick a#Your\@ guess～
	PRINTFORML [2] - Even
	PRINTFORML [1] - Odd
	DO
		INPUT
		IF INRANGE(RESULT, 1, 2)
			BREAK
		ELSE
			REUSELASTLINE Invalid input
			CONTINUE
		ENDIF
	LOOP 1
CASE "ダイス続行"
	PRINTFORML [2] - Continue
	PRINTFORML [1] - Cash out and continue
	PRINTFORML [0] - Cash out and stop
	DO
		INPUT
		IF INRANGE(RESULT, 0, 2)
			BREAK
		ELSE
			REUSELASTLINE Invalid input
			CONTINUE
		ENDIF
	LOOP 1
ENDSELECT
LOCAL = RESULT
RETURN LOCAL



;カジノ用情報表示関数
;主にブラックジャック関連
@CASINO_DISP(TYPE, ARG, ARG:1)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS TYPE
#DIM PL
#DIMS HAND_OP
SELECTCASE TYPE
CASE "BJ_CLOSE"
;	DRAWLINEFORM ━
;	PRINTFORML 山札残り{52 - CARD_DECK:0}
	DRAWLINEFORM ━
	PRINTFORML 【Fairy Maid】
	CALL CARD_DISP("CARD", CARD_HAND:0:0)
	CALL CARD_DISP("CARD_L")
	DRAWLINEFORM ━
	CALL CASINO_DISP("BJ_PL_HAND")
	DRAWLINEFORM ━
CASE "BJ_OPEN"
	DRAWLINEFORM ━
	CALL CASINO_DISP("BJ_DL_HAND")
	DRAWLINEFORM ━
	CALL CASINO_DISP("BJ_PL_HAND")
	DRAWLINEFORM ━
CASE "BJ_PL_HAND"
	PRINTFORML 【%CALLNAME:MASTER%】
	CALL CARD_DISP("HAND", 1)
	PRINTFORML 
	CALL CASINO_DISP("BJ_POINT", 1)
	PRINTFORML 
CASE "BJ_DL_HAND"
	PRINTFORML 【Fairy Maid】
	CALL CARD_DISP("HAND", 0)
	PRINTFORML 
	CALL CASINO_DISP("BJ_POINT", 0)
	PRINTFORML 
CASE "BJ_POINT"
	PL = ARG
	PRINTFORM 　Points：
	;情報更新のタイミングに注意
	IF CARD_PLAYER("GET_BJ_BUST", PL)
		SETCOLOR 255, 0, 0
		PRINTFORM %TOSTR(CARD_PLAYER("GET_BJ_POINT", PL)), 2% BUST!!
		RESETCOLOR
	ELSEIF CARD_PLAYER("GET_BJ_BJ", PL)
		SETCOLOR 255, 255, 0
		PRINTFORM %TOSTR(CARD_PLAYER("GET_BJ_POINT", PL)), 2% BLACK JACK!!
		RESETCOLOR
	ELSE 
		PRINTFORM %TOSTR(CARD_PLAYER("GET_BJ_POINT", PL)), 2%
	ENDIF
CASE "RESULT"
	IF ARG
		PRINTL
		PRINTFORML ～Results～
		PRINTFORML 　　%"Wager total", 14%： {ARG:0, 10}
		PRINTFORML 　　%"Payout total", 14%： {ARG:1, 10}
		PRINTFORML 　　%"Profit", 14%： \@ ARG:1 - ARG:0 >= 0 ? + # \@{ARG:1 - ARG:0, 10}
		PRINTW
	ENDIF
ENDSELECT



;ここからカード関連関数群
;カジノ以外でも使えることは使えるはず

;カードゲーム系は汎用化する旨味が大きいと思った


;カード（トランプ）用汎用関数
@PLAY_CARDS(TYPE, ARG:0, ARG:1)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS TYPE
#DIM  PL_ID
SELECTCASE TYPE
CASE "SHUFFLE"
;カードの山札をシャッフルする
;引数＝使用するジョーカーの数（上限8）
	VARSET CARD_DECK
	NUM_JOKER = LIMIT((ARG:0), 0, MAX_JOKER)
	NUM_CARD  = 52 + NUM_JOKER
	;0～52までつくって0を除去する
	CALL FISHER_YATES_SHAFFLE(NUM_CARD + 1)
	ARRAYREMOVE SHAFFLE_ARRAY, FINDELEMENT(SHAFFLE_ARRAY, 0), 1
	ARRAYCOPY "SHAFFLE_ARRAY", "CARD_DECK"
	NEW_CARD = 0
CASE "RESET_HAND"
;全てのプレイヤーの手札を破棄する
;NEW_CARDの数値はリセットしない
	VARSET NUM_HAND
	VARSET CARD_HAND
CASE "HIT"
;特定プレイヤーがカードを山札から引く
;引数＝プレイヤー番号
	CARD_HAND:(ARG:0):(NUM_HAND:(ARG:0)) = CARD_DECK:NEW_CARD
	NUM_HAND:(ARG:0) ++
	NEW_CARD ++
CASE "HIT_BJ"
;ブラックジャック用
	CALL PLAY_CARDS("HIT", (ARG:0))
	CALLF CARD_PLAYER("RENEW_BJ_POINT", (ARG:0))

CASE "NUM_HIT"
;ARG:0のプレイヤーの手札をARG:1枚だけ補充する
	FOR LOCAL, NUM_HAND:(ARG:0) , NUM_HAND:(ARG:0) + ARG:1
		CALL PLAY_CARDS("HIT", ARG)
	NEXT
CASE "REHIT"
;ARG:0のプレイヤーの手札、ARG:1を「上書き」する
;元の手札は参照できない（「捨てる」ではない）
;手札枚数は変わらない
;現状で捨て札の記録が必要になるシステムは無いのでとりあえずこれで実装
	CARD_HAND:(ARG:0):(ARG:1) = CARD_DECK:NEW_CARD
	NEW_CARD ++

CASE "POKER_FIRST_HAND"
;ポーカーの最初の手札、ARG:0のプレイヤーの手札を5枚
	CALL PLAY_CARDS("NUM_HIT", ARG:0, 5)

ENDSELECT



;カード表示関数
@CARD_DISP(TYPE, ARG)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS TYPE
#DIM PL
#DIMS HAND_OP
#DIM C_HAND

SELECTCASE TYPE
CASE "CARD", "CARD_L"
;汎用のカード表示用
	;0とJKは別扱い
	IF !ARG
		PRINT ［ ？ ］
		SIF TYPE == "CARD_L"
			PRINTL
		RETURN
	ELSEIF CARD_CHECK("JOKER", ARG)
		PRINT ［ JK ］
		SIF TYPE == "CARD_L"
			PRINTL
		RETURN
	ENDIF
	PRINT ［
	;スート表示, スペード, クラブ, ハート, ダイヤの順
	SELECTCASE CARD_CHECK("SUIT", ARG)
	CASE 1
		SETCOLOR C_BLUE
	CASE 2
		SETCOLOR C_GREEN
	CASE 3
		SETCOLOR C_RED
	CASE 4
		SETCOLOR C_YELLOW
	ENDSELECT
	PRINTFORM %CARD_SUIT:CARD_CHECK("SUIT", ARG)% 
	RESETCOLOR
	;数字表示, あまり0は13, 遊びで９を⑨にしたり色を変えたりしてます
	;記号やＡＡで絵札を表現するのも面白そう（めんどくさい）
	SELECTCASE CARD_CHECK("RANK", ARG)
	CASE 1
		SETCOLOR 255, 0, 0
		PRINTFORM %CARD_RANK:CARD_CHECK("RANK", ARG)%
		RESETCOLOR
	CASE 9
		SETCOLOR 0, 255, 255
		PRINTFORM %CARD_RANK:CARD_CHECK("RANK", ARG)%
		RESETCOLOR
	CASEELSE
		PRINTFORM %CARD_RANK:CARD_CHECK("RANK", ARG)%
	ENDSELECT
	PRINT ］　
	SIF TYPE == "CARD_L"
		PRINTL
CASE "HAND", "HAND_L", "HAND_BUTTON"
;汎用の手札表示用
	PL = ARG
	HAND_OP = %TYPE%
	FOR C_HAND, 0, NUM_HAND:PL
		SIF CARD_HAND:PL:C_HAND == 0
			BREAK
		SIF HAND_OP == "HAND_BUTTON"
			PRINTFORM [{C_HAND, 2}]
		IF HAND_OP == "HAND_L"
			CALL CARD_DISP("CARD_L", CARD_HAND:PL:C_HAND)
		ELSE
			CALL CARD_DISP("CARD", CARD_HAND:PL:C_HAND)
		ENDIF
	NEXT
ENDSELECT



;カードのプレイヤー変数の管理用F関数
;"GET"のないTYPEはCALLFで使う
@CARD_PLAYER(TYPE, PL)
#FUNCTION
#DIMS TYPE     ;処理内容
#DIM DYNAMIC PL;判定対象プレイヤー
#DIM PL_HAND   ;手札判定ループ用
#DIM PL_ID     ;プレイヤー判定ループ用

;ブラックジャック用変数
;バースト・ブラックジャックあたりはBITつかえば1変数で済むけどこれでいいや
#DIM P_BJ,    MAX_CARD_PL ;各プレイヤーの得点
#DIM BJ_ACE,  MAX_CARD_PL ;所持するエースの数
#DIM BJ_BJ,   MAX_CARD_PL ;ブラックジャックを達成しているかどうか
#DIM BJ_BUST, MAX_CARD_PL ;バーストしたかどうか

;ポーカー用変数
;とりあえず複数人プレイは考慮していない
#DIM PL_RANK,  5  ;手札情報の一時取得用
#DIM PL_SUIT,  5  ;手札情報の一時取得用
#DIM PK_GROUP     ;連番判定用
#DIM CNT_RANK, 14 ;各数字の枚数取得
#DIM CNT_SUIT, 5  ;各スートの枚数取得
#DIM PK_RATE      ;得られた倍率、戻り値として使用（％）
SELECTCASE TYPE

;こっからブラックジャック用
CASE "CLEAR_BJ_ALL"
;計算用数値の初期化
	VARSET P_BJ
	VARSET BJ_ACE
	VARSET BJ_BJ
	VARSET BJ_BUST
CASE "CLEAR_BJ"
;特定プレイヤー向け
	P_BJ:PL = 0
	BJ_ACE:PL = 0
	BJ_BJ:PL = 0
	BJ_BUST:PL = 0
CASE "RENEW_BJ_POINT_ALL"
;全プレイヤーの状況を再計算
	CALLF CARD_PLAYER("CLEAR_BJ_ALL", PL)
	FOR PL_ID, 0, NUM_CARD_PL
		CALLF CARD_PLAYER("CALC_BJ_POINT", PL_ID)
	NEXT
CASE "RENEW_BJ_POINT"
;特定プレイヤーの状況を再計算
	CALLF CARD_PLAYER("CLEAR_BJ", PL)
	CALLF CARD_PLAYER("CALC_BJ_POINT", PL)
CASE "CALC_BJ_POINT"
;ブラックジャックの点数計算用
;特定のプレイヤーの現在の手札の得点を計算する
	;12枚で確実にバーストするが一応計算はする
	FOR PL_HAND, 0, NUM_HAND:PL
		;引いてない（ありえなそうだが一応）
		SIF CARD_HAND:PL:PL_HAND == 0
			BREAK
		;ジョーカー分岐だけつくったけどちょっとまだ対応してないよ
		IF CARD_CHECK("JOKER", CARD_HAND:PL:PL_HAND)
		ELSEIF CARD_CHECK("RANK", CARD_HAND:PL:PL_HAND) == 1
			BJ_ACE:PL ++
		ELSE
			P_BJ:PL += CARD_CHECK("BJ_POINT", CARD_CHECK("RANK", CARD_HAND:PL:PL_HAND))
		ENDIF
	NEXT
	IF BJ_ACE:PL
	;4パターン、1or11,2or12,3or13,4or14
	;2枚以上を11扱いするとそれだけでバーストなので考慮する必要はない
		IF P_BJ:PL + 10 + BJ_ACE:PL <= 21
			P_BJ:PL += 10 + BJ_ACE:PL
		ELSE
			P_BJ:PL += BJ_ACE:PL
		ENDIF
	ENDIF
	SIF NUM_HAND:PL == 2 && P_BJ:PL == 21
		BJ_BJ:PL = 1
	SIF P_BJ:PL > 21
		BJ_BUST:PL = 1
CASE "GET_BJ_POINT"
;特定のプレイヤーの得点
	RETURNF P_BJ:PL
CASE "GET_BJ_BJ"
;特定のプレイヤーがブラックジャックを達成しているかどうか
	RETURNF BJ_BJ:PL
CASE "GET_BJ_BUST"
;特定のプレイヤーがバーストしたかどうか
	RETURNF BJ_BUST:PL


CASE "GET_POKER_RATE"
;ポーカーのレート計算
;いまんとこ手札5枚の1人プレイしか想定してない
;ベットが出来たほうが「らしい」けどAIとか評価関数とか考えたくないよ
	VARSET PL_RANK
	VARSET PL_SUIT
	VARSET PK_GROUP
	VARSET CNT_RANK
	VARSET CNT_SUIT
	VARSET PK_RATE
	;手札配列のコピー
	FOR PL_HAND, 0, NUM_HAND:PL
		PL_RANK:PL_HAND = CARD_CHECK("RANK", CARD_HAND:PL:PL_HAND)
		PL_SUIT:PL_HAND = CARD_CHECK("SUIT", CARD_HAND:PL:PL_HAND)
	NEXT
	PK_GROUP = 0
	
	;枚数カウント
	FOR LOCAL, 1, 14
		;ランクの枚数取得
		CNT_RANK:LOCAL = MATCH(PL_RANK, LOCAL)
		;ついでにスートのカウントもやっちゃう
		SIF !INRANGE(LOCAL, 1, 4)
			CONTINUE
		CNT_SUIT:LOCAL = MATCH(PL_SUIT, LOCAL)
	NEXT
	
	;ストレート判定処理
	;10, J, Q, K, Aはストレート扱い
	IF GROUPMATCH(1, CNT_RANK:1, CNT_RANK:10, CNT_RANK:11, CNT_RANK:12, CNT_RANK:13) == 5
		PK_GROUP = 5
	ELSEIF MATCH(CNT_RANK, 1) == 5
		;ペア無しかつ「手札最大値 - 手札最小値 == 4」がストレート判定条件
		;A～5、9～Kなど
		SIF FINDLASTELEMENT(CNT_RANK, 1) - FINDELEMENT(CNT_RANK, 1) == 4
			PK_GROUP = 5
	ENDIF
	
	;レートは％表記デス（小数は返せないので）
	IF MATCH(CNT_SUIT, 5) && GROUPMATCH(1, CNT_RANK:1, CNT_RANK:10, CNT_RANK:11, CNT_RANK:12, CNT_RANK:13) == 5
	;Royal Straight
		PK_RATE = 30000
	ELSEIF MATCH(CNT_SUIT, 5) && PK_GROUP == 5
	;Straight Flush
		PK_RATE = 5000
	ELSEIF MATCH(CNT_RANK, 4)
	;Four of a Kind
		PK_RATE = 3000
	ELSEIF MATCH(CNT_RANK, 3) && MATCH(CNT_RANK, 2)
	;Full House
		PK_RATE = 2000
	ELSEIF MATCH(CNT_SUIT, 5)
	;Flush
		PK_RATE = 1000
	ELSEIF PK_GROUP == 5
	;Straight
		PK_RATE = 500
	ELSEIF MATCH(CNT_RANK, 3)
	;Three of a Kind
		PK_RATE = 300
	ELSEIF MATCH(CNT_RANK, 2) == 2
	;Two Pair
		PK_RATE = 100
	ELSEIF MATCH(CNT_RANK, 2)
	;One Pair
		PK_RATE = 50
	ELSE
	;No Pair
		PK_RATE = 0
	ENDIF
	RETURNF PK_RATE
ENDSELECT



;カードゲーム判定F関数
;カードの一枚一枚に対する判定
@CARD_CHECK(TYPE, D_ARG)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS TYPE
#DIM DYNAMIC D_ARG ;適当にDYNAMIC化したかっただけなんだどいい名前が思いつかない
#DIM P_BJ
#DIM ACE_COUNT
#DIM C_HAND
SELECTCASE TYPE
CASE "JOKER"
;RANK/SUITは戻り値0でジョーカー/数値無しの判別が不能のため
;先にJOKERをチェックして下さい
	SIF D_ARG > 52
		RETURNF 1
	RETURNF 0
CASE "RANK"
;戻り値1～13
;ジョーカーのとき0
	SIF !D_ARG
		RETURNF
	SIF D_ARG > 52
		RETURNF 0
	SIF !(D_ARG % 13)
		RETURNF 13
	RETURNF D_ARG % 13
CASE "SUIT"
;戻り値1～4
;ジョーカーのとき0
	SIF !D_ARG
		RETURNF
	SIF D_ARG > 52
		RETURNF 0
	RETURNF (D_ARG - 1) / 13 + 1

CASE "BJ_POINT"
;ブラックジャックの点数計算用
;ACEのポイントが手札次第で変わってしまってこの関数だけでは扱えない
;変数の上書き問題が面倒なので独立させた
;引数はRANKにして下さい
;ちなジョーカー非対応
	SIF GROUPMATCH(D_ARG, 11, 12, 13)
		RETURNF 10
	RETURNF D_ARG

CASE "BJ_BUST"
	SIF D_ARG > 21
		RETURNF 1
	RETURNF 0
ENDSELECT


@CASINO_MEMBER_RANKUP


IF FLAG:カモられ指数 >= 100 && FLAG:会員ランク < 1
	PRINTFORMW As %PARSE("you try")% to leave the casino, a Fairy Maid calls %PARSE("you",5)% over.
	PRINTFORML 「Thank you for your continued patronage of the casino as always.」
	PRINTFORM 「As so happens, Customer's Casino Rank is now a 
	CALL COLORMESSAGE("Bronze Rank Member",C_YELLOW)
	PRINTFORML , how much Charisma you can hold has been increased, and the ban on selling Charisma has been lifted.」
	PRINTFORMW 「We look forward to your continued support in the future.」
	FLAG:会員ランク = 1
	CM_COMMISSION = 25
	CM_LIMIT = 500
	FLAG:カモられ指数 = 0
ENDIF
IF FLAG:カモられ指数 >= 3000 && FLAG:会員ランク < 2
	PRINTFORMW As %PARSE("you try")% to leave the casino, a Fairy Maid calls %PARSE("you",5)% over.
	PRINTFORML 「Thank you for your continued patronage of the casino as always.」
	PRINTFORM 「As so happens, Customer's Casino Rank is now a 
	CALL COLORMESSAGE("Silver Rank Member",C_YELLOW)
	PRINTFORML , how much Charisma you can hold as well as how much you make when selling it have increased.」
	PRINTFORMW 「We look forward to your continued support in the future.」
	FLAG:会員ランク = 2
	CM_COMMISSION = 20
	CM_LIMIT = 1000
	FLAG:カモられ指数 = 0
ENDIF

IF FLAG:カモられ指数 >= 10000 && FLAG:会員ランク < 3
	PRINTFORMW As %PARSE("you try")% to leave the casino, a Fairy Maid calls %PARSE("you",5)% over.
	PRINTFORML 「Thank you for your continued patronage of the casino as always.」
	PRINTFORM 「As so happens, Customer's Casino Rank is now a 
	CALL COLORMESSAGE("Gold Rank Member",C_YELLOW)
	PRINTFORML , how much Charisma you can hold as well as how much you make when selling it have increased.」
	PRINTFORMW 「We look forward to your continued support in the future.」
	FLAG:会員ランク = 3
	CM_COMMISSION = 18
	CM_LIMIT = 3000
	FLAG:カモられ指数 = 0
ENDIF
[SKIPSTART]
IF !ITEM:紅魔カジノVIP会員証
PRINTFORML 
PRINTFORMW 店を出ようとするとメイド妖精に呼び止められた
PRINTFORML 
;VIPるーむの仕様が固まりきらないので封印
;封印しといて文章だけ拡充するのってどうなんだ
;れみふらさくやは顔パスでも良さそうだけどな
	SELECTCASE CSVCALLNAME(FLAG:なりきり)
	CASE "レミリア", "フラン", "パチュリー"
		PRINTFORML メイド妖精
		PRINTFORML 　%CALLNAME:MASTER%様、少しお時間を頂いてもよろしいでしょうか？
		PRINTFORML 　ご存知のこととは思いますが当店にはVIPルームがございます
		PRINTFORML 　随分とカジノをお気に召したご様子でしたのでVIP会員証を用意させていただきました
	CASE "咲夜"
		PRINTFORML メイド妖精
		PRINTFORML 　%CALLNAME:MASTER%さん、VIP会員証をお持ちしました
		PRINTFORMW 　VIPルームにご案内しますね
	CASE "美鈴", "小悪魔"
		PRINTFORML メイド妖精
		PRINTFORML 　%CALLNAME:MASTER%さん、完全にハマっちゃってますよね
		PRINTFORML 　はい、これVIP会員証です
		PRINTFORMW 　VIPルームに案内しますからついてきて下さい
	CASE "あなた"
		IF MAIN_MAP == 3
			PRINTFORML メイド妖精
			PRINTFORML 　もうすっかり常連さんですね
			PRINTFORML 　うちのディーラーになっちゃいますか？
			PRINTFORML 　はい、これVIP会員証です
			PRINTFORMW 　VIPルームに案内しますからついてきて下さい
		ELSE
			PRINTFORML メイド妖精
			PRINTFORML 　少しお時間を頂いてもよろしいでしょうか？
			PRINTFORML 　いつも当店をご利用いただきありがとうございます
			PRINTFORML 　お客様は当店のVIP会員として認められました
			PRINTFORMW 　こちらがVIP会員証でございます
		ENDIF
	CASEELSE
			PRINTFORML メイド妖精
			PRINTFORML 　少しお時間を頂いてもよろしいでしょうか？
			PRINTFORML 　いつも当店をご利用いただきありがとうございます
			PRINTFORML 　お客様は当店のVIP会員として認められました
			PRINTFORMW 　こちらがVIP会員証でございます
	ENDSELECT
	PRINTFORML 
	PRINTFORML メイド妖精はあなたに会員証を手渡すとついてくるようにと促した
	PRINTFORMW 案内された先は何の変哲もない紅い壁の前に見えるが……？
	PRINTFORML 
	SELECTCASE CSVCALLNAME(FLAG:なりきり)
	CASE "レミリア", "フラン", "パチュリー"
		PRINTFORML メイド妖精
		PRINTFORMW 　%CALLNAME:MASTER%様、先ほどの会員証をこちらの壁の前におかざしになって下さい
	CASE "咲夜"
		PRINTFORML メイド妖精
		PRINTFORMW 　%CALLNAME:MASTER%さん、先ほどの会員証をこちらの壁の前にかざしてみて下さい
	CASE "美鈴", "小悪魔"
		PRINTFORML メイド妖精
		PRINTFORMW 　%CALLNAME:MASTER%さん、さっきの会員証をこちらの壁の前にかざしてみて下さい
	CASE "あなた"
		IF MAIN_MAP == 3
			PRINTFORML メイド妖精
			PRINTFORMW 　さっきの会員証をこちらの壁の前にかざしてみて下さい
		ELSE
			PRINTFORML メイド妖精
			PRINTFORMW 　お客様、先ほどの会員証をこちらの壁の前におかざしになって下さい
		ENDIF
	CASEELSE
			PRINTFORML メイド妖精
			PRINTFORMW 　お客様、先ほどの会員証をこちらの壁の前におかざしになって下さい
	ENDSELECT
	PRINTFORML 
	PRINTFORMW %CALLNAME:MASTER%が言われたとおりにすると、かすかな機械音と共に目の前の壁が開いた
	PRINTFORML 
	PRINTFORML メイド妖精
	PRINTFORML 　こちら、当店自慢のVIPルームでございます
	PRINTFORML 　VIPルーム内部ではさまざまなサービスをご提供いたしております
	PRINTFORML 　貴重なお時間をいただきありがとうございました
	PRINTFORML 　それでは気をつけてお帰り下さいませ
	PRINTFORMW 　店員一同、またのご来店を心よりお待ちしていおります
	PRINTFORML 
	PRINTFORMW VIPルームが利用可能になった
	ITEM:紅魔カジノVIP会員証 = 1
ENDIF
[SKIPEND]