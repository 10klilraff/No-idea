;FileName_INFO.ERB --------------------------------- Rev1.01
;ターン処理
;CALL		SYSTEM
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@SHOW_STATUS
#DIM 天気非表示
#DIM 元の天気
VARSET LOCAL
IF STR:(8000 + (CFLAG:MASTER:現在位置)) == "" && AT_HOME(MASTER)
	PRINTFORML The current position is invalid.（Currently＞{CFLAG:MASTER:現在位置}）
	PRINTFORMW To avoid errors, you will be moved to %NAME_FROM_PLACE(CFLAG:MASTER:初期位置)%.
	CFLAG:MASTER:現在位置 = CFLAG:MASTER:初期位置
	CFLAG:MASTER:デート中 = MAIN_MAP
ENDIF
;Custom code, resets the used words array
VARSET USED_WORDS
USED_WORDS_I = 0
;-------------------------------------------------
;TFLAG:調教中COMABLE管理表示制御
;-------------------------------------------------
TFLAG:調教中COMABLE管理 = TFLAG:調教自動実行管理
SIF TFLAG:調教自動実行管理 == 1
	TFLAG:調教中COMABLE管理 = 0
SIF TFLAG:調教自動実行管理
	TFLAG:調教自動実行管理 --
;ウフフ中なら接触部位表示
SIF TFLAG:102 == 2
	CALL SHOW_TOUCH

;-------------------------------------------------
;料理の時間経過
;-------------------------------------------------
SIF DISH_NAME != ""
	CALL DISH_FRESHNESS
;-------------------------------------------------
;畑の時間経過
;-------------------------------------------------
SIF TIME:5 >= 4
	FLAG:803 = 0
	FLAG:807 = 0

;-------------------------------------------------
;キャラの態度
;-------------------------------------------------
CALL CHARA_ATTITUDE

;-------------------------------------------------
;キャラの思考
;-------------------------------------------------
;CALL CHARA_MIND

;-------------------------------------------------
;時間経過、キャラの移動
;-------------------------------------------------
CALL INFO_TIME_PASSING

;-------------------------------------------------
;デート終了タイムアップ処理
;-------------------------------------------------
SIF CHK_DATENOW(CFLAG:MASTER:デート中)
	CALL デート終了タイムアップ処理
;-------------------------------------------------
;TARGET関連のキャラ変数の更新
;-------------------------------------------------
CALL INFO_RENEW_TARGET

;-------------------------------------------------
;ウフフ終了TEQUIP処理
;EQUIPの更新もここに仕込んでおく
;-------------------------------------------------
CALL INFO_RENEW_EQUIP

;-------------------------------------------------
;MASTERの同行フラグチェック
;-------------------------------------------------
SIF !同行人数()
	CFLAG:MASTER:同行 = 0

;-------------------------------------------------
;背景色設定
;-------------------------------------------------
IF FLAG:時間停止
	SETBGCOLOR 1, 24, 60
ELSE
	RESETBGCOLOR
ENDIF

;-------------------------------------------------
;情報表示
;-------------------------------------------------
PRINTL
;天気の表示/非表示の更新
;天界への行き来の際の天気の更新
CALL INFO_CULC_WEATHER(天気非表示)

;「一日が終わりました」表示と宴会関連の更新処理など
CALL INFO_GO_NEXTDAY
CALL CHANGE_TIMEZONE

;子供の描写
CALL CHILD_DESCRIPTION

;帰宅時間等の予告通知
CALL TIMEUP_NOTICE(FLAG:デート相手)

;時計画像表示
CALL INFO_CLOCK_IMAGE(TIME)

;ここからゲーム画面に表示されるインターフェイス
DRAWLINEFORM *
;日時と祝日の表示（改行なし）
CALL INFO_TIME_WATCH

;各種フラグ確認用 デバッグ中以外は非表示で
;PRINTFORM うふふ:{CFLAG:TARGET:うふふ}
;PRINTFORM イタズラ:{CFLAG:イタズラ}
;PRINTFORM 睡眠深度:{TCVAR:TARGET:睡眠深度}

;天気と月齢の表示
SIF !天気非表示
	CALL INFO_SHOW_WEATHER

PRINTPLAIN
;MASTERの眠気など
CALL INFO_SITUATION
;時間停止関連
CALL INFO_TIMESTOP

;-------------------------------------------------
;現在位置情報
;-------------------------------------------------
CALL INFO_GPS
PRINTL
;-------------------------------------------------
;TARGET情報とモード切り替え
;-------------------------------------------------
VARSET LOCAL
;TARGET関連の簡易情報表示関数群
CALL INFO_TARGET_DATAGROUP
CALL PRINTBUTTON_EX(TFLAG:Status非表示 ? "▽[＋][Status]--------" # "▼[－][Status]--------", 3001, TFLAG:Status非表示)
PRINTBUTTON "[Abilities Display]",803
PRINTL 
IF !TFLAG:Status非表示
	CALL INFO_BASE(MASTER)
	SIF TARGET
		CALL INFO_BASE(TARGET)
	CALL INFO_STATUS
ENDIF

SIF TARGET
	CALL SHOW_INFO_PALAM

IF !TARGET && TFLAG:抱きつきモード
	TFLAG:抱きつきモード = 0
	TFLAG:102 = 1
ENDIF

IF CFLAG:MASTER:うふふ == 2
	SIF !TFLAG:PALAM非表示
		PRINTL 
	DRAWLINE
	CALL カウンター呼び出し処理
ENDIF
;FileName_INFO.ERB --------------------------------- Rev1.00
;汚れ表示処理
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@STAIN_INFO
;主人の汚れ
CALL INFO_CHARA_STAIN(MASTER)
SIF TARGET > 0
	CALL INFO_CHARA_STAIN(TARGET)
SIF ASSI > 0
	CALL INFO_CHARA_STAIN(ASSI)
WAIT


;汚れ表示処理の汎用化
@INFO_CHARA_STAIN(C_ID)
#DIM        C_ID
#DIM        BODY_PARTS
#DIMS CONST PARTS_NAME = "Mouth", "Hands", "Ｐ", "Ｖ", "Ａ", "Ｂ"
#DIM        TYPE_STAIN
#DIMS CONST STAIN_NAME = "Love Juice", "Penis", "Semen", "Anus", "Milk", "Mucus", "Hymen Blood", "Chocolate"
PRINTFORML 【%CALLNAME:C_ID%'s dirtiness】
FOR BODY_PARTS, 0, 6
	SIF PARTS_NAME:BODY_PARTS == "Ｐ" && !(TALENT:C_ID:2 & 2)
		CONTINUE
	SIF GROUPMATCH(PARTS_NAME:BODY_PARTS, "Ｖ", "Ｂ") && !(TALENT:C_ID:2 & 1)
		CONTINUE
	PRINTFORM 　%PARTS_NAME:BODY_PARTS%：
	FOR TYPE_STAIN, 0, 7
		SIF GETBIT(STAIN:C_ID:BODY_PARTS, TYPE_STAIN)
			PRINTFORM <%STAIN_NAME:TYPE_STAIN%>
	NEXT
	IF GETBIT(STAIN:C_ID:BODY_PARTS, FINDELEMENT(STAIN_NAME, "Chocolate"))
		IF GETBIT(STAIN:C_ID:BODY_PARTS, FINDELEMENT(STAIN_NAME, "Milk"))
			PRINT <Chocolate milk>
		ELSE
			PRINT <Chocolate>
		ENDIF
	ENDIF
	PRINTL 
NEXT
PRINTL 



@SAVEINFO
;場所は妖怪の山（山麓）で半角16文字分、名前はクラウンピースで14文字分必要
PUTFORM {DAY,3,RIGHT}日目 %GET_MAPNAME(MAIN_MAP),16,LEFT% %NAME:MASTER,14,LEFT% Ver %SAVESTR:本体バージョン,9,LEFT%

;FileName_INFO.ERB --------------------------------- Rev1.00
;プレイ状態表示処理
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	まとめて表示するように改良,TARGETの＜道具や使用状態＞以外の状態はここ
;-----------------------------------------------------------
@SHOW_EQUIP_2
;状況の表示
;調教対象には使わない、特殊な着脱式の道具を追加した場合はここに追加をしてください
;（場所変更、マスターや助手が使用する道具等）
LOCALS = （

IF TALENT:TARGET:機嫌 == 1
	LOCALS += "・Happy"
ELSEIF TALENT:TARGET:機嫌 == -1
	LOCALS += "・Sullen"
ENDIF

SIF TCVAR:TARGET:祝福 ;boosts pregnancy chance if raw sex is allowed
	LOCALS += "・Bless"
SIF TCVAR:TARGET:エンチャント
	LOCALS += "・P-UP" ;power up for danmaku against the opponent
SIF TCVAR:TARGET:加護 ;divine protection, weakens the opponent in danmaku
	LOCALS += "・Div.Protec."

IF CFLAG:TARGET:睡眠
	SELECTCASE TCVAR:TARGET:睡眠薬強度
	CASE 1
		LOCALS += "・Deep Sleep"
	CASE 2
		LOCALS += "・Very Deep Sleep"
	CASE 3
		LOCALS += "・Dead Asleep"
	CASEELSE
		LOCALS += "・Sleeping"
	ENDSELECT
ELSEIF TCVAR:TARGET:睡眠薬強度
	SELECTCASE TCVAR:TARGET:睡眠薬強度
	CASE 1
		LOCALS += "・Drowsy"
	CASE 2
		LOCALS += "・Very Drowsy"
	CASE 3
		LOCALS += "・Extremely Drowsy"
	ENDSELECT
ENDIF

SIF TCVAR:TARGET:発情
	LOCALS += "・In Heat"
SIF TCVAR:TARGET:泥酔
	LOCALS += "・Wasted"

SELECTCASE TCVAR:TARGET:願掛け補正
CASE 23
	LOCALS += "・Sexual Interest+"
CASE 31
	LOCALS += "・Liberated+"
CASE 41
	LOCALS += "・Wetness+"
CASE 70
	LOCALS += "・Enjoys Pleasure+"
CASE 80
	LOCALS += "・Perverted+"
CASE 82
	LOCALS += "・Sadistic+"
CASE 83
	LOCALS += "・Masochistic+"
CASE 101
	LOCALS += "・C Sensitivity+"
CASE 102
	LOCALS += "・V Sensitivity+"
CASE 103
	LOCALS += "・A Sensitivity+"
CASE 104
	LOCALS += "・B Sensitivity+"
CASE 106
	LOCALS += "・M Sensitivity+"
ENDSELECT

;カラー表示,上に行くほど優先度が高い
IF CFLAG:TARGET:睡眠
	SETCOLOR C_BLUE
ELSEIF TCVAR:TARGET:発情
	SETCOLOR C_H_PINK
ELSEIF TCVAR:TARGET:泥酔
	SETCOLOR C_ORANGE
ELSEIF TCVAR:TARGET:睡眠薬強度
	SETCOLOR C_BLUE
ELSEIF TALENT:TARGET:機嫌 == 1
	SETCOLOR C_AQUA
ELSEIF TALENT:TARGET:機嫌 == -1
	SETCOLOR C_RED
ENDIF

;表示部分
PRINTFORM %NAME:TARGET%

;qol custom code
IF !CHARA_HOLIDAY(TARGET) && BASE:仕事量 && TCVAR:仕事開始 < TIME + 180 && SHIRAHU(TARGET)
	IF CFLAG:お招き
		CALL COLORMESSAGE(@" Plan To Work",C_ORANGE,0,1)
	ELSE
		CALL COLORMESSAGE(@" \@ TCVAR:仕事開始 - TIME > 0 ? Work In {TCVAR:仕事開始 - TIME}min # Working\@",C_ORANGE,0,1)
	ENDIF
ENDIF
	
;通常時
IF LOCALS== "（"
	RETURN
;状態時
ELSE
	LOCALS += "）"
	PRINTFORM %REPLACE(LOCALS, "（・", "（")%
ENDIF
RESETCOLOR

;FileName_INFO.ERB --------------------------------- Rev1.00
;道具状態表示処理
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	TARGETの使用状態
;-----------------------------------------------------------
@SHOW_EQUIP_1
;使用中の道具を表示する
;一度使用したら解除するまで止まらない道具や調教を
;追加した場合はここに追加をしてください
SIF !CHARA_EQUIP(1)
	RETURN
PRINTFORM In Use (%NAME:TARGET%) 

SIF TCVAR:TARGET:ピル == 1
	PRINTFORM <Birth Control>
SIF TEQUIP:TARGET:クリキャップ
	PRINTFORM <Clit Cap>
SIF TEQUIP:TARGET:オナホール
	PRINTFORM <Onahole>
SIF TEQUIP:TARGET:バイブ
	PRINTFORM <Vibrator>
SIF TEQUIP:TARGET:アナルバイブ
	PRINTFORM <Anal Vibrator>
SIF TEQUIP:TARGET:アナルビーズ
	PRINTFORM <Anal Beads>
SIF TEQUIP:TARGET:ニプルキャップ
	PRINTFORM <Nipple Cap>
SIF TEQUIP:TARGET:搾乳機
	PRINTFORM <Milker>
SIF TEQUIP:TARGET:アイマスク
	PRINTFORM <Blindfold>
SIF TEQUIP:TARGET:縄
	PRINTFORM <Rope Bondage>
SIF TEQUIP:TARGET:ボールギャグ
	PRINTFORM <Ball Gag>
SIF TEQUIP:TARGET:浣腸
	PRINTFORM <Enema + Butt Plug>
SIF TEQUIP:TARGET:拡張バルーン
	PRINTFORM <Expansion Balloon>
SIF TEQUIP:TARGET:アナル電極
	PRINTFORM <Anal Electrod>
;SIF TEQUIP:TARGET:24
;	PRINTFORM <乳房電極装着中>
SIF TEQUIP:TARGET:拘束
	PRINTFORM <Restrained>
;TEQUIPの方は使われていない模様
;SIF TEQUIP:TARGET:媚薬
;	PRINTFORM <媚薬効果中>
;SIF TEQUIP:TARGET:利尿剤
;	PRINTFORM <利尿剤効果中>
SIF TCVAR:TARGET:媚薬
	PRINTFORM <Aphrodisiac>
SIF TCVAR:TARGET:利尿剤
	PRINTFORM <Diuretic>
SIF TCVAR:TARGET:ポイズンゲージ
	PRINTFORM <Aphrodisiac Poisoning>
SIF TEQUIP:TARGET:ビデオ撮影
	PRINTFORM <Video Shooting({TEQUIP:TARGET:ビデオ撮影})>
SIF TEQUIP:TARGET:野外プレイ
	PRINTFORM <Outdoors Play>
SIF TEQUIP:TARGET:羞恥プレイ == 1
	PRINTFORM <Shame Play>
SIF TEQUIP:TARGET:お風呂場プレイ
	PRINTFORM <Bathroom Play>
SIF TEQUIP:TARGET:シャワー
	PRINTFORM <Shower>
SIF TEQUIP:TARGET:新妻プレイ
	PRINTFORM <Newly Wed Play>
SIF TEQUIP:TARGET:挿入禁止
	PRINTFORM <Insertion Prohibition>
;SIF TEQUIP:TARGET:継続キス
;	PRINTFORM <キス中>
SIF TEQUIP:TARGET:コンドーム ;com custom code
	PRINTFORM <Condom>

PRINTL 

;ココより↑にTEQUIPに登録したものを書き込む

;FileName_INFO.ERB --------------------------------- Rev1.00
;ローター表示処理
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	あなたの使用状態
;-----------------------------------------------------------
@SHOW_EQUIP_3
#DIM キスマークの相手

SIF !CHARA_EQUIP(3)
	RETURN
キスマークの相手 = TFLAG:キスマーク
PRINTFORM Carry status (%CALLNAME:PLAYER%) 
;料理の情報
SIF DISH_NAME != ""
	CALL INFO_DISH
;custom code
SIF TFLAG:水汲み
	PRINTFORM <Carrying %STR_TR(TFLAG:水汲み + 7000)%>

IF TFLAG:キスマーク && !TFLAG:包帯
	PRINTFORM <%CALLNAME:キスマークの相手%'s kiss mark>
ELSEIF TFLAG:キスマーク && TFLAG:包帯
	PRINTFORM <%CALLNAME:キスマークの相手%'s kiss mark (covered)>
ENDIF
SIF TEQUIP:MASTER:コンドーム
	PRINTFORM <Condom>
SIF FLAG:パンツ輸送
	PRINTFORM <Panties>
SIF CFLAG:PLAYER:ローター挿入
	PRINTFORM <Rotor (Inserted)>
SIF CFLAG:PLAYER:ローターA挿入
	PRINTFORM <Anal Rotor (Inserted)>
SIF TFLAG:パール期限 > 0 ;custom code
	PRINTFORM <Anal Beads ({TFLAG:パール挿入個数} inside \@ TFLAG:パール期限 > TIME ? %" "%{TFLAG:パール期限 - TIME} minutes left# \@)>
;エプロンはTARGET（紛らわしい）
SIF TEQUIP:TARGET:エプロン
	PRINTFORM <Apron>
	
;custom code
SIF TCVAR:惚れ薬
	CALL COLORMESSAGE("<Love Potion>", C_PINK, 0, 1)
	
;あなたに効果を発揮している薬などを表記
IF TFLAG:仙香玉兎
	IF TFLAG:仙香玉兎 >= 120
		CALL COLORMESSAGE("<Lunar Incense>", C_PINK, 0, 1)
	ELSE
		CALL COLORMESSAGE("<Lunar Incense>", C_D_PINK, 0, 0)
	ENDIF
ENDIF
SIF TCVAR:MASTER:国士無双の薬
	PRINTFORM <Peerless Patriot's Elixir×{TCVAR:MASTER:国士無双の薬}>

IF TFLAG:ポイズンボディ
	CALL COLORMESSAGE("<Poison Body>", C_D_PINK, 0, TFLAG:ポイズンボディ >= 120)
ENDIF
;custom code
SIF CFLAG:MASTER:現在位置 == TFLAG:GG設置位置
	CALL COLORMESSAGE("<Gassing Garden Spot>", C_PINK, 0, 1)


;幸運補正
IF TFLAG:幸運補正 > 0
	PRINTFORM <Fortune>
ELSEIF TFLAG:幸運補正 < 0
	PRINTFORM <Misfortune>
ENDIF

;料理バフ
SIF TCVAR:MASTER:祝福
	PRINTFORM <Blessing>
SIF TCVAR:MASTER:エンチャント
	PRINTFORM <Power Up>
SIF TCVAR:MASTER:加護
	PRINTFORM <Divine Protection>
SIF TCVAR:MASTER:豊穣
	PRINTFORM <Good Harvest>

PRINTL 

;FileName_INFO.ERB --------------------------------- Rev1.00
;装備状態判定
;@SHOW_EQUIP_1、@SHOW_EQUIP_3で使うので装備品追加したら追記のこと
;ARG = SHOW_EQUIP_{ARG} での参照元
;PLAYER_EQUIPを改良しました
;-----------------------------------------------------------
@CHARA_EQUIP(ARG)
#FUNCTION
IF ARG == 1
	SIF TCVAR:TARGET:ピル == 1
		RETURNF 1
	SIF TEQUIP:TARGET:クリキャップ
		RETURNF 1
	SIF TEQUIP:TARGET:オナホール
		RETURNF 1
	SIF TEQUIP:TARGET:バイブ
		RETURNF 1
	SIF TEQUIP:TARGET:アナルバイブ
		RETURNF 1
	SIF TEQUIP:TARGET:アナルビーズ
		RETURNF 1
	SIF TEQUIP:TARGET:ニプルキャップ
		RETURNF 1
	SIF TEQUIP:TARGET:搾乳機
		RETURNF 1
	SIF TEQUIP:TARGET:アイマスク
		RETURNF 1
	SIF TEQUIP:TARGET:縄
		RETURNF 1
	SIF TEQUIP:TARGET:ボールギャグ
		RETURNF 1
	SIF TEQUIP:TARGET:浣腸
		RETURNF 1
	SIF TEQUIP:TARGET:拡張バルーン
		RETURNF 1
	SIF TEQUIP:TARGET:アナル電極
		RETURNF 1
;	SIF TEQUIP:TARGET:24
;		RETURNF 1
	SIF TEQUIP:TARGET:拘束
		RETURNF 1
	;TEQUIPは使われていない模様
;	SIF TEQUIP:TARGET:媚薬
;		RETURNF 1
;	SIF TEQUIP:TARGET:利尿剤
;		RETURNF 1
	SIF TCVAR:TARGET:媚薬
		RETURNF 1
	SIF TCVAR:TARGET:利尿剤
		RETURNF 1
	SIF TCVAR:TARGET:ポイズンゲージ
		RETURNF 1
	SIF TEQUIP:TARGET:ビデオ撮影
		RETURNF 1
	SIF TEQUIP:TARGET:野外プレイ
		RETURNF 1
	SIF TEQUIP:TARGET:羞恥プレイ == 1
		RETURNF 1
	SIF TEQUIP:TARGET:お風呂場プレイ
		RETURNF 1
	SIF TEQUIP:TARGET:シャワー
		RETURNF 1
	SIF TEQUIP:TARGET:新妻プレイ
		RETURNF 1
	SIF TEQUIP:TARGET:挿入禁止
		RETURNF 1
;	SIF TEQUIP:TARGET:継続キス
;		RETURNF 1
	SIF TEQUIP:コンドーム ;com custom code
		RETURNF 1
ELSEIF ARG == 3
	SIF TFLAG:キスマーク
		RETURNF 1
	SIF FLAG:パンツ輸送
		RETURNF 1
	SIF CFLAG:PLAYER:ローター挿入
		RETURNF 1
	SIF CFLAG:PLAYER:ローターA挿入
		RETURNF 1
	SIF TFLAG:パール期限
		RETURNF 1
	SIF TFLAG:仙香玉兎
		RETURNF 1
	SIF TCVAR:MASTER:国士無双の薬
		RETURNF 1
	;custom code
	SIF TCVAR:惚れ薬
		RETURNF 1
	SIF TFLAG:ポイズンボディ
		RETURNF 1
	;custom code
	SIF CFLAG:MASTER:現在位置 == TFLAG:GG設置位置
		RETURNF 1
	SIF TEQUIP:MASTER:コンドーム
		RETURNF 1
	;エプロンはTARGET（紛らわしい）
	SIF TEQUIP:TARGET:エプロン
		RETURNF 1
	SIF DISH_NAME != ""
		RETURNF 1
	SIF TFLAG:幸運補正
		RETURNF 1
	SIF TCVAR:MASTER:祝福
		RETURNF 1
	SIF TCVAR:MASTER:エンチャント
		RETURNF 1
	SIF TCVAR:MASTER:加護
		RETURNF 1
	SIF TCVAR:MASTER:豊穣
		RETURNF 1
	;custom code
	SIF TFLAG:水汲み
		RETURNF 1
ENDIF
RETURNF 0

;FileName_INFO.ERB --------------------------------- Rev1.00
;パラメータ表示処理
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@SHOW_INFO_PALAM
CALL PRINTBUTTON_EX(TFLAG:PALAM非表示 ? "▽[＋][Palam]---------" # "▼[－][Palam]---------", 3000, TFLAG:PALAM非表示)
PRINTL 
VARSET LOCAL
IF !TFLAG:PALAM非表示
	FOR LOCAL,0,100
		SIF !STRLENS(PALAMNAME:LOCAL)
			CONTINUE
		SIF LOCAL <= 4 && EX:TARGET:(LOCAL + 20)
			SETCOLOR C_YELLOW
		PRINTFORM  %PALAMNAME:LOCAL, 6, LEFT%Lv{GETPALAMLV(PALAM:LOCAL,15),2}
		RESETCOLOR
		IF GETPALAMLV(PALAM:LOCAL,15) >= 15
			CALL PRINT_COLORBAR, 0, 1, 8, UNICODE(0x2584), UNICODE(0x2584), BARCOLORSET("青"), RESULT:1
		ELSE
			;現在のPALAMLV
			LOCAL:1 = PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1) - PALAMLV:(GETPALAMLV(PALAM:LOCAL,15))
			LOCAL:4 = PALAM:LOCAL - PALAMLV:(GETPALAMLV(PALAM:LOCAL,15))
			IF LOCAL < 4
				CALL PRINT_COLORBAR, PALAM:LOCAL, 10000, 8, UNICODE(0x2584), UNICODE(0x2584), BARCOLORSET("青"), RESULT:1
			ELSE
				CALL PRINT_COLORBAR, LOCAL:4, LOCAL:1, 8, UNICODE(0x2584), UNICODE(0x2584), BARCOLORSET("青"), RESULT:1
			ENDIF
	;		NEXT
		ENDIF
		LOCAL:1 = PALAM:LOCAL
		LOCAL:2 = 0
		;これだけ特殊
		IF LOCAL == 9 && LOCAL:1 >= 100000
			;0 friction, but that doesn't quite fit
			PRINTFORM %"SMOOTH",6% 
		ELSE
			;桁合わせ
			FOR LOCAL:10,0,10
				IF LOCAL:1 > 999999
					LOCAL:1 /= 1000
					LOCAL:2 ++
				ENDIF
			NEXT
			PRINTFORM {LOCAL:1,6}
			SELECTCASE LOCAL:2
				CASE 0
					PRINT  
				CASE 1
					PRINT k
				CASE 2
					PRINT M
				CASE 3
					PRINT G
				CASE 4
					PRINT T
				CASE 5
					PRINT P
			ENDSELECT
		ENDIF
		LOCAL:3 ++
		SIF LOCAL:3 % PRINTCPERLINE() == 0
			PRINTL 
	NEXT
ENDIF

;-------------------------------------------------
;関数名:SORT_CFLAG
;CFLAG:ARGの昇順でRESULT:1にキャラ番号を代入する
;ARG CFLAGの番号
;-------------------------------------------------
@SORT_CFLAG(ARG)
VARSET LOCAL
VARSET RESULT
FOR LOCAL:10,1,CHARANUM
	LOCAL:1 = 0
	FOR LOCAL,1,CHARANUM
		SIF MATCH(RESULT,LOCAL)
			CONTINUE
		IF CFLAG:LOCAL:ARG >= LOCAL:1
			LOCAL:1 = CFLAG:LOCAL:ARG
			LOCAL:2 = LOCAL
		ENDIF
	NEXT
	RESULT:(LOCAL:10) = LOCAL:2
NEXT
;FileName_INFO.ERB --------------------------------- Rev1.00
;料理鮮度チェック
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@DISH_FRESHNESS
#DIM FRESH_MESSAGE
SIF DISH_LIMIT >= DISH_MAXLIMIT
	FRESH_MESSAGE = 0
DISH_LIMIT -= TIME_PROGRESS(TIME:1)
SIF DISH_LIMIT <= DISH_MAXLIMIT * 4 / 5
	DISH_TASTE '= REPLACE(DISH_TASTE, "できたて\/", "")
IF DISH_LIMIT <= DISH_MAXLIMIT * -1
	CALL COLORMESSAGE(DISHNAME_TR(DISH_NAME) + " simply cannot be eaten!", C_RED, 2)
	PRINTL
	CALL COLORMESSAGE(@"%PARSE("You discard")% " + DISHNAME_TR(DISH_NAME) + ".", C_RED, 2)
	CALL RESET_DISH
	FRESH_MESSAGE = 0
ELSEIF DISH_LIMIT <= 0 && FRESH_MESSAGE < 2
	CALL COLORMESSAGE(DISHNAME_TR(DISH_NAME) + " started to smell bad...", C_ORANGE, 2)
	DISH_TASTE '= REPLACE(DISH_TASTE, "冷たい\/", "")
	DISH_TASTE '= REPLACE(DISH_TASTE, "温かい\/", "")
	DISH_TASTE += "劣化/" * 2
	FRESH_MESSAGE = 2
ELSEIF DISH_LIMIT <= DISH_MAXLIMIT * 1 / 2 && FRESH_MESSAGE < 1
	IF STRCOUNT(DISH_TASTE, "アイス/")
		PRINTFORMW %DISHNAME_TR(DISH_NAME)% started melting...
		DISH_TASTE '= REPLACE(DISH_TASTE, "冷たい\/", "")
	ELSEIF STRCOUNT(DISH_TASTE, "冷たい/")
		PRINTFORMW %DISHNAME_TR(DISH_NAME)% has cooled down...
		DISH_TASTE '= REPLACE(DISH_TASTE, "冷たい\/", "")
	ELSE
		PRINTFORMW %DISHNAME_TR(DISH_NAME)% became cold...
		DISH_TASTE '= REPLACE(DISH_TASTE, "温かい\/", "")
	ENDIF
	DISH_TASTE += "劣化/"
	FRESH_MESSAGE = 1
ENDIF

;addition custom code
IF CFLAG:MASTER:現在位置 == CFLAG:MASTER:初期位置 && STRCOUNT(DISH_TASTE, "劣化")
	PRINTFORML
	PRINTFORMW You dispose of the rotten food.
	CALL RESET_DISH
ENDIF
;--------------------

;-------------------------------------------------
;古典時刻関数@CLASSICAL_TIME
;	引数0：対象の変数（デフォでは秒）
;	引数1：1なら引数0を秒で、2なら時で計算する
;	引数2：表示形式（デフォ「○の○つ」、1=「○の刻、○つ」、2=「○の○つ　-○○-」）
;式中で使用する関数です。引数に記録されている数字を秒として、現在の時刻を文字列で返します
;古典時刻表記なので、30分単位未満の端数は切り捨てます。
;-------------------------------------------------
@CLASSICAL_TIME(ARG, ARG:1, ARG:2)
#FUNCTIONS
;0:00が子の三つなので、60分を足す
ARG += 60
;日付も管理しているケースを考え、24時間（1440分）で割ってあまりを取る（子の一つ～二つもここで対応される）
ARG %= 1440
;表示
SIF GET_CLASSICAL_HOUR(ARG, 1) == "Ox" && (ARG%120)/30 == 2
	RETURNF "Witching Hour"
;custom code, reordered
RETURNF @"%TOKANJI(((ARG%120)/30)+1)% Of The %GET_CLASSICAL_HOUR(ARG, 1)%\@ ARG:2 == 1 ? 刻、 # \@ \@ ARG:2 == 2 ?  -%GET_CLASSICAL_HOUR(ARG, 1, 1)%- # \@"

;時を取得
;引数2を真にすると、大まかな時間の様子を取る
@GET_CLASSICAL_HOUR(ARG, ARG:1, ARG:2)
#FUNCTIONS
;分に変換する
SELECTCASE ARG:1
	;時で渡す場合
	CASE 2
		ARG *= 60
	;分で渡す場合（処理なし）
	CASE 1
	;秒で渡す場合（デフォルト）
	CASEELSE
		ARG /= 60
ENDSELECT

SELECTCASE ARG/120
	CASE 0
		RETURNF \@ ARG:2 ? Midnight # Rat \@
	CASE 1
		RETURNF \@ ARG:2 ? Early dawn # Ox \@
	CASE 2
		RETURNF \@ ARG:2 ? Dawn # Tiger \@
	CASE 3
		RETURNF \@ ARG:2 ? Sunrise # Rabbit \@
	CASE 4
		RETURNF \@ ARG:2 ? Early morning # Dragon \@
	CASE 5
		RETURNF \@ ARG:2 ? Morning # Snake \@
	CASE 6
		RETURNF \@ ARG:2 ? Noon # Horse \@
	CASE 7
		RETURNF \@ ARG:2 ? High noon # Goat \@
	CASE 8
		RETURNF \@ ARG:2 ? Afternoon # Monkey \@
	CASE 9
		RETURNF \@ ARG:2 ? Sunset # Rooster \@
	CASE 10
		RETURNF \@ ARG:2 ? Evening # Dog \@
	CASE 11
		RETURNF \@ ARG:2 ? Late evening # Pig \@
ENDSELECT

;-------------------------------------------------
;数値→漢数字変換関数@TOKANJI
;TOSTR関数チックに、引数の数値を漢数字の文字列で返します
;変数の最大値は約922京であるため、一、万、億、兆、京の5回処理します
;-------------------------------------------------
@TOKANJI(ARG)
#FUNCTIONS
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM LCOUNT, 1
VARSET LOCALS
FOR LCOUNT, 0, 5
	LOCAL = ARG/POWER(10000, (4-LCOUNT))
	ARG %= POWER(10000, (4-LCOUNT))
	IF LOCAL
		LOCALS += TOKANJIT(LOCAL)
		SELECTCASE LCOUNT
			CASE 0
				LOCALS += "京"
			CASE 1
				LOCALS += "兆"
			CASE 2
				LOCALS += "億"
			CASE 3
				LOCALS += "万"
		ENDSELECT
	ENDIF
NEXT
RETURNF \@ LOCALS != "" ? %LOCALS% # 零 \@

;内部用or簡易用
;四桁以内の漢字変換。万以上与えるとバグ
@TOKANJIT(ARG)
#FUNCTIONS
SIF ARG >= 10000
	RETURNF "Bug"
LOCALS = \@ ARG/1000 ? %TOKANJIS(ARG/1000, 1)%千 # \@
ARG %= 1000
LOCALS += \@ ARG/100 ? %TOKANJIS(ARG/100, 1)%百 # \@
ARG %= 100
LOCALS += \@ ARG/10 ? %TOKANJIS(ARG/10, 1)%十 # \@
ARG %= 10
RETURNF LOCALS + TOKANJIS(ARG)

;一桁の変換。引数1を真にすると一を省略する。2以上にすると零を表示する
@TOKANJIS(ARG, ARG:1)
#FUNCTIONS
SELECTCASE ARG
	CASE 0
		RETURNF \@ ARG:1 > 1 ? 零 #  \@
	CASE 1
		RETURNF \@ ARG:1 ?  # First \@
	CASE 2
		RETURNF "Second"
	CASE 3
		RETURNF "Third"
	CASE 4
		RETURNF "Fourth"
	CASE 5
		RETURNF "Fifth"
	CASE 6
		RETURNF "Sixth"
	CASE 7
		RETURNF "Seventh"
	CASE 8
		RETURNF "Eighth"
	CASE 9
		RETURNF "Ninth"
	CASE 10
		RETURNF "Tenth"
	CASEELSE
		RETURNF "Bug"
ENDSELECT



;-------------------------------------------------
;時間経過、キャラの移動
;-------------------------------------------------
@INFO_TIME_PASSING
#DIM MOODBREAKER
IF TFLAG:遠距離移動
	;TFLAGを戻す（遠距離移動フラグ）
	TFLAG:遠距離移動 = 0
ELSEIF TIME_PROGRESS(TIME:1)
	;キャラ移動
	CALL CHARA_MOVEMENT
	IF TCVAR:MASTER:馴れ合い >= 1
		MOODBREAKER = IN_ROOM_MEMBER("MIN", CFLAG:MASTER:現在位置, "CFLAG", 6)
		LOCAL:2 = CFLAG:MOODBREAKER:態度
		;馴れ合い強度０以下だと空気が変わる
		IF LOCAL:2 <= 0 && MOODBREAKER > 0 && CFLAG:MOODBREAKER:現在位置 != CFLAG:MOODBREAKER:前ターン位置 && !CFLAG:MOODBREAKER:睡眠
			SIF PREVCOM != 400
				PRINTFORMW As %CALLNAME:MOODBREAKER% arrives, the atmosphere suddenly shifts...
			FOR LOCAL, 0, CHARANUM
				;同室判定前なので同室フラグは使えない
				SIF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置 && CFLAG:LOCAL:態度 >= 1 && !CFLAG:LOCAL:睡眠 && PREVCOM != 400
					CALL KOJO_MESSAGE_SEND("EVENT",5,LOCAL,MOODBREAKER)
				;イタズラOFF
				CALL SET_PRANK(0,LOCAL,2)
				;隠密中OFF
				CALL SET_SNEAK(0,LOCAL,2)
			NEXT
		ENDIF
	ENDIF
ENDIF
TIME:1 = TIME + 1440 * DAY
CALL SET_SLEEP(0,MASTER,0)



;-------------------------------------------------
;TARGET関連のキャラ変数の更新
;-------------------------------------------------
@INFO_RENEW_TARGET
CALL TARGETSET_CHACK("「同室更新無し」")
;MASTERと同室フラグ
FOR LOCAL, 1, CHARANUM
	;口上の基本フラグ設定
	CALL KOJO_MESSAGE_SEND("FLAGSETTING", 0, LOCAL)

	;現在位置がMASTERと同じなら同室
	IF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置
		;まだ同室フラグが立っていない(現ターンで同室化）かつ眠っていない
		SIF !CFLAG:LOCAL:同室 && !CFLAG:LOCAL:睡眠
			CALL AFFAIR_DISCLOSURE(LOCAL, 0)
		;情事の発覚 AFFAIR_DISCLOSURE で立ち去った場合は同室が立たない
		SIF !RESULT
			CFLAG:LOCAL:同室 = 1
	ELSE
		;相手からこちらが見えていて、相手かこちらかが移動したかこちらが押し倒した
		IF CAN_MOVE(CFLAG:LOCAL:現在位置, CFLAG:MASTER:現在位置) & 2 && !CFLAG:LOCAL:睡眠 && PREVCOM >= 0 && FLAG:70 != 1
			SIF (CFLAG:MASTER:前ターン位置 != CFLAG:MASTER:現在位置 || CFLAG:LOCAL:前ターン位置 != CFLAG:LOCAL:現在位置 || TRAINNAME:PREVCOM == "押し倒す")
				CALL AFFAIR_DISCLOSURE(LOCAL, 1)
		ENDIF
		CFLAG:LOCAL:同室 = 0
	ENDIF
NEXT

;馴れ合い強度決定
;TARGET側
FOR LOCAL, 1, GET_TARGETNUM() + 1
	;合意か弱味がないと3にならない
	IF GETBIT(CFLAG:(TARGET:LOCAL):既成事実, 0) || CFLAG:(TARGET:LOCAL):弱み握られ
		LOCAL:1 = CFLAG:(TARGET:LOCAL):態度
	ELSE
		LOCAL:1 = MIN(CFLAG:(TARGET:LOCAL):態度, 2)
	ENDIF
	TCVAR:(TARGET:LOCAL):馴れ合い = LOCAL:1
	;TARGET:2以降は、MIN(直前のTARGETのTCVAR:130,自身のCFLAG:6)
	SIF LOCAL >= 2
		TCVAR:(TARGET:LOCAL):馴れ合い = MIN(TCVAR:(TARGET:(LOCAL - 1)):馴れ合い, TCVAR:(TARGET:LOCAL):馴れ合い)
NEXT

;MASTERはTARGET:1～のCFLAG:6の最低値
TCVAR:MASTER:馴れ合い = TCVAR:(IN_ROOM_MEMBER("MIN", CFLAG:MASTER:現在位置, "TCVAR", 130)):馴れ合い


;-------------------------------------------------
;ウフフ終了TEQUIP処理
;EQUIPの更新もここに仕込んでおく
;-------------------------------------------------
@INFO_RENEW_EQUIP
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:うふふ && !BATHROOM(CFLAG:LOCAL:現在位置) && !睡眠時間(LOCAL)
		CONTINUE
	SIF LOCAL == TARGET && CFLAG:LOCAL:現在位置 != CFLAG:LOCAL:前ターン位置
		CALL ENDUFUFU(LOCAL)
	;服装更新系
	;メンドイのでまとめて
	SIF !BATHROOM(CFLAG:LOCAL:現在位置)
		TEQUIP:(LOCAL):お風呂場プレイ = 0
	;風呂の中なら全裸
	IF BATHROOM(CFLAG:LOCAL:現在位置)
		SIF EQUIP:LOCAL:0 && !(TFLAG:泡風呂 && TALENT:LOCAL:風俗嬢)  
			CALL 脱衣処理(LOCAL, "全裸", "「表示なし」")
		IF CFLAG:LOCAL:うふふ
			; || TCVAR:LOCAL:馴れ合い > 2
			TEQUIP:(LOCAL):お風呂場プレイ = 1
		ELSE
			TEQUIP:(LOCAL):お風呂場プレイ = 0
		ENDIF
	;うふふ/イタズラ中は衣装リセットを抑制
	ELSEIF !(CFLAG:LOCAL:うふふ || CFLAG:LOCAL:イタズラ)
		CALL CHANGE_CLOTHES(LOCAL, "衣装リセット")
	ENDIF
	CALL CLOTHES_SETTING_TRAIN(LOCAL)
	
	;相合い傘フラグ（同行依存）
	;相合い傘できるのはターゲットだけ
	SIF LOCAL != 0 && !CHK_DATENOW(CFLAG:LOCAL:デート中) && (!CFLAG:LOCAL:同行 || LOCAL != TARGET)
		TEQUIP:LOCAL:201 = 0
	;相合い傘フラグ（同行依存）
	;デート中持続させる処理＋ターゲットと相合い傘する処理＋連れ出した後外に出た時雨が降っていたら相合い傘する処理
	SIF LOCAL != 0 && (CHK_DATENOW(CFLAG:LOCAL:デート中) || CFLAG:LOCAL:同行) && LOCAL == TARGET && TIME:5 > 3 && TIME:5 < 14 && OUTROOF(CFLAG:MASTER:現在位置) && (ITEM:36 || (TARGET == 16 || TARGET == 26 || TARGET == 40 || TARGET == 68))
		TEQUIP:LOCAL:201 = 1
NEXT


;天気の表示/非表示の更新
;天界への行き来の際の天気の更新
@INFO_CULC_WEATHER(天気非表示)
#DIM REF 天気非表示
#DIM     元の天気
天気非表示 = 0
IF CFLAG:MASTER:デート中 == 9 || CFLAG:MASTER:現在位置 == 槐安通路
	天気非表示 = 1
ELSEIF CFLAG:MASTER:現在位置 == OMANEKIBEYA() && GROUPMATCH(CFLAG:MASTER:お招き, 36, 37, 38, 49, 60, 64, 83, 84)
	天気非表示 = 1
ENDIF
IF CFLAG:MASTER:現在位置 == 天界 && !元の天気
	元の天気 = TIME:5
	TIME:5 = 1
ELSEIF CFLAG:MASTER:現在位置 != 天界 && 元の天気
	TIME:5 = 元の天気
	元の天気 = 0
ENDIF



;「一日が終わりました」表示と宴会関連の更新処理など
@INFO_GO_NEXTDAY
SIF TIME <= 1440
	RETURN
PRINTL  
IF !CFLAG:MASTER:うふふ
	IF FESTIVAL() == "大晦日"
		PRINTW HAPPY NEW YEAR!!
		PRINTW Since it's Sazae-san's space-time, it's still 20XX!
		FOR LOCAL, 1, CHARANUM
			;ハッピーニューイヤー口上
			SIF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置
				CALL KOJO_MESSAGE_SEND("EVENT", 33, LOCAL)
		NEXT
	ELSE
		PRINTW The day has ended.
	ENDIF
ENDIF
PRINTL  
;宴会当日に一日が終わると宴会フラグOFF
IF DAY > FLAG:開始日 && FLAG:宴会開催フラグ
	;会場となった場所は荒れ果てる、拠点外でも対象地点の汚れ増加させる
	SIF 100 + FLAG:32 < 300
		YOGORE:(FLAG:32) += 1000
	FLAG:宴会開催フラグ = 0
	FLAG:31 = 0
	FLAG:32 = 0
	TFLAG:宴会場 = 0
	FLAG:33 = 0
	FLAG:開始日 = 0
	FLAG:35 = 0
	FLAG:36 = 0
	FOR LOCAL,0,CHARANUM
		CFLAG:LOCAL:350 = 0
		MAXBASE:LOCAL:仕事量 = 0
		BASE:LOCAL:仕事量 = 0
		CFLAG:LOCAL:20 = 0
		CFLAG:LOCAL:行動 = 0
	NEXT
ENDIF
;宴会の開催判定
SIF FLAG:3 && FLAG:宴会開催フラグ == 0
	CALL ENKAI_SETTING
;---------------------------
;時間の経過
DAY ++
DAY:3 ++
TIME -= 1440
SIF TFLAG:パール期限 > 0 ;bug fix
	TFLAG:パール期限 -= 1440
CALL CHANGE_MONTH
;---------------------------
;宴会の参加者決定
IF FLAG:3 && FLAG:宴会開催フラグ != 98 && FLAG:30 != 99 && DAY >= FLAG:開始日
	CALL ENKAI_ENTRY
	FOR LOCAL, 1, CHARANUM
		;自宅在住キャラなら参加者来訪しない
		SIF CFLAG:LOCAL:神社在住 > 0
			CONTINUE
		IF CFLAG:LOCAL:宴会参加
			CFLAG:LOCAL:拠点来訪 = 0
			CFLAG:LOCAL:遭遇位置 = 0
		ENDIF
	NEXT
ENDIF


;日時と祝日の表示（改行なし）
@INFO_TIME_WATCH
;qol custom code
#DIM YEAR_INFO
YEAR_INFO = (DAY:0 / 125) + 1
PRINTFORM Day {DAY}, %ORDINAL(DAY:3)% of %GET_MONTH(,1)%, Year {YEAR_INFO} (%GET_DAY_TR()%) 
;----------------
SETCOLOR C_RED
PRINTFORM %FESTIVAL_TR()% 
RESETCOLOR
PRINTFORM %時刻表示(TIME)% 
PRINTFORM (%CLASSICAL_TIME(TIME, 1, 2)%)

;-------------------------------------------------
;	時計画像を表示
;
;	ARG		：時刻
;	iSize	：フォントに対するサイズ％数値　縦基準　縦横比維持　省略で1000(10倍)
;	sAlign	：align属性　省略で右寄せ　left, center, rightの三種が指定できます
;-------------------------------------------------
@INFO_CLOCK_IMAGE(ARG, iSize=500,sAlign="right")
#DIM iSize
#DIMS sAlign
#DIMS sPrint_Text
#DIM iEnter_Con
#DIM iFont_Hei_mag
#DIM YPOS

iEnter_Con = 4
iFont_Hei_mag = GETCONFIG("一行の高さ") * iSize*10 / GETCONFIG("フォントサイズ")
iFont_Hei_mag = (iFont_Hei_mag/10) + (iFont_Hei_mag%10 >= 5?1#0)
YPOS = iEnter_Con * iFont_Hei_mag / 10+1
sPrint_Text =  <p align='%sAlign%'><nobr>
sPrint_Text += @"<img src='%GET_CLOCK_IMAGE_NAME(ARG)%' height='{iFont_Hei_mag}'"
sPrint_Text += @"ypos='{YPOS}'>"
sPrint_Text += @"</p>"

;PRINTFORM %sPrint_Text%
HTML_PRINT sPrint_Text

;-------------------------------------------------
;	時計画像名を取得
;
;	ARG	：時刻
;-------------------------------------------------
@GET_CLOCK_IMAGE_NAME(ARG)
#FUNCTIONS
#DIM HOUR
#DIM MINUTE
#DIMS IMAGE_NAME
HOUR   = ARG / 60 % 12
MINUTE = ARG % 60 / 5 * 5
IMAGE_NAME = 時計_%TOSTR(HOUR,"00")%_%TOSTR(MINUTE,"00")%
RETURNF IMAGE_NAME

;天気と月齢の表示
@INFO_SHOW_WEATHER
SIF FLAG:異常気象
	SETCOLOR C_PINK
PRINTFORM ― %GET_WEATHER_TR()% ― 
RESETCOLOR
;06時から18時までは昼間
IF TIME:5 == 0 || TIME:5 == 1
	IF TIME:2 >= 2 && TIME:2 <= 4
		SETCOLOR 0xFF9640
		PRINTFORM %UNICODE(0x2600)%
	ELSE
		;19時から06時までは夜間
		SETCOLOR 0xFFFF40
		PRINTFORM %UNICODE(0x262A)%
			;ダサいので保留
			;IF MOON_WATCH(DAY:3) > 0
			;PRINTFORM %UNICODE(0x262A)%
			;ELSE
			;UNICODE 0x2728
			;PRINT ★
			;ENDIF
	ENDIF
ELSEIF TIME:5 == 2 || TIME:5 == 3
	SETCOLOR 0x9C9C9C
	PRINTFORM %UNICODE(0x2601)%
ELSEIF (TIME:5 == 4 || TIME:5 == 5 || TIME:5 == 6 || TIME:5 == 7) && TEQUIP:TARGET:201 && OUTROOF(CFLAG:MASTER:現在位置)
	SETCOLOR 0xFF99CC
	PRINTFORM %UNICODE(0x2602)% 
	SETCOLOR 0xFFCCFF
	CALL HEART
	PRINTFORM Sharing An Umbrella
	CALL HEART
ELSEIF TIME:5 == 4 || TIME:5 == 5 || TIME:5 == 6 || TIME:5 == 7
	SETCOLOR 0x0000FF
	PRINTFORM %UNICODE(0x2602)%
ELSEIF (TIME:5 >= 8 && TIME:5 <= 13) && TEQUIP:TARGET:201 && OUTROOF(CFLAG:MASTER:現在位置)
	SETCOLOR 0xFFCCFF
	PRINTFORM %UNICODE(0x2603)% 
	SETCOLOR 0xFF99CC
	CALL HEART
	PRINTFORM Sharing An Umbrella
	CALL HEART
ELSEIF TIME:5 >= 8 && TIME:5 <= 13
	SETCOLOR 0xFFFFFF
	PRINTFORM %UNICODE(0x2603)%
ENDIF
RESETCOLOR
IF TIME:7 == 1
	SETCOLOR 0xFF0000
	PRINT /
	SETCOLOR 0xFFCC00
	PRINT /
	SETCOLOR 0xFFFF00
	PRINT /
	SETCOLOR 0x66CC00
	PRINT /
	SETCOLOR 0x3399FF
	PRINT /
	SETCOLOR 0x0033CC
	PRINT /
	SETCOLOR 0x990099
	PRINT /
ELSEIF TIME:7 == 2
	SETCOLOR 0xFFFFFF
	PRINTFORM +。*:゜+:
ENDIF
RESETCOLOR
CALL INFO_MOON

;月齢の表示
@INFO_MOON
#DIM MOON
MOON = MOON_WATCH(DAY:3)
SIF MOON <= 0
	RETURN
SETCOLOR 0xFFFF40
;custom code
PRINTFORM %NAME_MOON(DAY:3, 1)%
RESETCOLOR


;時間停止関連
;TSPバーとコマンドの位置はどちらが良い？
;暫定に倣って停止解除の位置とX軸が近いのでコマンドを右側にしていますが、スペース（"　"）があるので左側にもできます。
@INFO_TIMESTOP
CALL BASE_BAR(MASTER, "TSP", 30, "水色", "「種類表記無し」")
;時間停止コマンドを参考に条件追記
IF BASE:MASTER:気力 <= 0 || BASE:MASTER:TSP <= 0 || (!FLAG:70 && (CFLAG:うふふ || CFLAG:イタズラ || CFLAG:添い寝中))
	SETCOLOR C_L_GRAY
	PRINT [Time Stop Not Possible]
	RESETCOLOR
ELSEIF !FLAG:70
	IF TFLAG:妄想中 == 1
		PRINT [Under Delusion]
	ELSEIF CFLAG:MASTER:イタズラ
		SETCOLOR C_L_GRAY
		PRINT [Time Stop Not Possible]
		RESETCOLOR
	ELSE
		CALL PRINTBUTTON_EX("[Time Stop]",355,0,FLAG:70,)
	ENDIF
ELSEIF CFLAG:うふふ
	CALL PRINTBUTTON_EX("[Time Stopped While Teasing]",999,0,FLAG:70)
ELSE
	CALL PRINTBUTTON_EX("[Time Stopped]",999,0,FLAG:70)
ENDIF
;Custom Code
IF FLAG:CHEATMENU
	IF !FLAG:CHEATMENUCONFIRM
		CALL PRINTBUTTON_EX("[Cheat]",2000,0,,)
	ELSEIF FLAG:CHEATMENUCONFIRM
		CALL PRINTBUTTON_EX("[Really cheat?]",2000,0,,)
	ENDIF
ENDIF

PRINTL 
;MASTERの眠気など
@INFO_SITUATION
SIF TEQUIP:焦らしモード
	PRINT 　[Orgasm Denial]
PRINTFORM 　[%HUNGER(MASTER)%]　
RESETCOLOR
;qol custom code
PRINTFORM [%BATHED(MASTER)%]　
RESETCOLOR
;Custom code
SIF FLAG:守矢くじデート相手
	CALL COLORMESSAGE(@"<Moriya Date with %CALLNAME:(FLAG:守矢くじデート相手)%>",C_YELLOW,1)
IF NEMUKE() > 720
	LOCALS = Sleeping Possible
	IF NEMUKE() > 960
		SETCOLOR C_RED
		LOCALS = Need Sleep
	ELSEIF NEMUKE() > 840
		SETCOLOR C_YELLOW
	ELSE
		SETCOLOR C_P_YELLOW
	ENDIF
	SIF CFLAG:MASTER:眠気覚まし
		LOCALS += "・Sleep Res"
	PRINTFORML <%LOCALS%>
	RESETCOLOR
ELSEIF FLAG:デート相手
	SETCOLOR 255,51,153
	IF CFLAG:(FLAG:デート相手):現在位置 == OMANEKIBEYA()
		;custom code
		PRINTFORM   <\@ TFLAG:泡風呂 ? Getting service in # Visiting \@ %CALLNAME:(FLAG:デート相手)%'s room\@ TFLAG:泡風呂 - TIME:1 > 0 ? %" "%(%PRINT_PLUR("minute", TFLAG:泡風呂 - TIME:1)% left)#\@ >
	ELSE
		PRINTFORM   <Dating %CALLNAME:(FLAG:デート相手)%>
	ENDIF
	RESETCOLOR
	;MASTERの眠気が限界に近いなら警告
	IF NEMUKE() > 660
		PRINT (It's about time to finish up)
	ENDIF
	PRINTL
ELSEIF CFLAG:MASTER:眠気覚まし
	SELECTCASE CFLAG:MASTER:眠気覚まし
		CASE IS > 240
			PRINTL <Sleep Res・Lots>
		CASE IS > 180
			PRINTL <Sleep Res・Strong>
		CASE IS > 120
			PRINTL <Sleep Res・Middle>
		CASEELSE
			PRINTL <Sleep Res・Weak>
	ENDSELECT
ELSE
	PRINTL
ENDIF



;現在位置情報
@INFO_GPS
IF TFLAG:デート道中
	PRINTFORMS "Heading To %DATENAME_PLACE()%　　　(Time Until Arrival: {TFLAG:デート道中}mins)"
ELSEIF CHK_DATENOW(CFLAG:MASTER:デート中) && CFLAG:MASTER:現在位置 != OMANEKIBEYA()
	PRINTFORMS "%DATENAME_PLACE()%  - %CAPITALIZE(DATENAME_SPOT())% -"
ELSE
	PRINTFORMS THE_CASE_SHIFT(NAME_FROM_PLACE(CFLAG:MASTER:現在位置))
	;ヒノキ風呂設置場所はそれとわかるように
	;bug fix
	SIF ITEM:ヒノキ風呂 && NEAR_BATH() == CFLAG:MASTER:現在位置
		PRINTFORMS " <Cypress Bath Lv{ITEM:ヒノキ風呂}>"
	;私室にいる時、家具シリーズの表記
	IF CFLAG:MASTER:現在位置 == CFLAG:MASTER:初期位置
		SIF ITEM:キリのベッド
			PRINTFORMS " <Paulownia Bed Lv{ITEM:キリのベッド}>"
		SIF ITEM:ケヤキの机椅子
			PRINTFORMS " <Zelkova Desk Lv{ITEM:ケヤキの机椅子}>"
		SIF ITEM:ナラのソファー
			PRINTFORMS " <Oak Sofa Lv{ITEM:ナラのソファー}>"
	ENDIF
	;custom code
	SIF ITEM:イチョウのまな板 && KITCHEN(CFLAG:MASTER:現在位置)
		PRINTFORMS " <Ginkgo Cutting Board Lv{ITEM:イチョウのまな板}>"
ENDIF
;ウフフ中は気が散るので表示させない,お招き部屋98は表示させるので99未満
IF TFLAG:102 != 2 && !FLAG:1002 && AT_HOME(MASTER) == 1
	PRINTPLAIN  
	PRINTFORM %SEIKETU(CFLAG:MASTER:現在位置)%
	RESETCOLOR
ENDIF
;custom code
IF GET_TARGETNUM() > 1
	PRINTPLAIN  
	PRINTFORM There's %PRINT_PLUR("person", GET_TARGETNUM())% around
ENDIF
;-------------------------------------------------
;料理の情報
;-------------------------------------------------
@INFO_DISH
PRINTFORM <Holding %DISHNAME_TR(EX_DISH_NAME)%
SIF STRCOUNT(DISH_TASTE, "できたて") == 1
	PRINT (fresh)
SIF STRCOUNT(DISH_TASTE, "劣化") == 1
	PRINT (stale)
SIF STRCOUNT(DISH_TASTE, "劣化") >= 2
	PRINT (rotting)
SIF DISH_DRG
	PRINTFORM (%ITEMNAME_TR(DISH_DRG)%)
SIF DISH_DRG_2
	PRINTFORM (%ITEMNAME_TR(DISH_DRG_2)%)
;addition custom code
SIF DISH_LEWD
	PRINTFORM %ADD_LewdFoodAdd_Show()%
;--------------------
PRINT >

;-------------------------------------------------
;空腹具合の表示関数
;-------------------------------------------------
@HUNGER(ARG)
#FUNCTIONS
LOCAL = TCVAR:ARG:空腹時刻 - TIME:1
SELECTCASE LOCAL
	CASE IS > 270
	SETCOLOR C_GREEN
		RETURNF "Full"
	CASE IS > 180
	SETCOLOR C_P_GREEN
		RETURNF "Well fed"
	CASE IS > 0
	SETCOLOR C_P_YELLOW
		RETURNF "Fed"
	CASEELSE
		RETURNF "Can eat"
ENDSELECT

;TARGET関連の簡易情報表示関数群
@INFO_TARGET_DATAGROUP
IF TARGET <= 0
	TFLAG:102 = 0
	TFLAG:パンツ収奪 = 0
	RETURN
ENDIF
IF !CFLAG:睡眠
	CALL 押し倒され
ELSEIF CFLAG:イタズラ <= 1
	;うふふ解除
	CVARSET CFLAG, GETNUM(CFLAG, "うふふ"), 0
ENDIF
;ターゲット切替ボタンの設置＆同行者とお持ち帰りの表示
CALL INFO_TARGET_BUTTON
;TARGET本人の簡易情報
CALL INFO_TARGET_SITUATION
IF CFLAG:睡眠
	IF CFLAG:イタズラ == 1
		TFLAG:102 = 1
	ELSEIF CFLAG:イタズラ == 2
		CFLAG:TARGET:うふふ = 1
		CFLAG:MASTER:うふふ = 1
		TFLAG:102 = 2
	ELSE
		TFLAG:102 = 0
	ENDIF
ENDIF



;ターゲット切替ボタンの設置＆同行者とお持ち帰りの表示
;旧@INFO_TARGET_GROUPと統合して一行にまとめる
@INFO_TARGET_BUTTON
#DIM 同行人数
#DIM 表示済み, 200
;同行者とお持ち帰りの表示部分

同行人数 = 0
VARSET 表示済み
FOR LOCAL, 1, GET_TARGETNUM() + 1
	SIF TARGET:LOCAL <= 0
		CONTINUE
	SIF !CFLAG:(TARGET:LOCAL):同行
		CONTINUE
	同行人数 ++
	表示済み:LOCAL = 1
	SIF 同行人数 > 1
		PRINT , 
	IF TARGET == TARGET:LOCAL
		SETCOLOR C_AQUA
	ELSEIF FLAG:デート相手
		SETCOLOR C_PINK
	ELSE
		SETCOLOR 255, 200, 100
	ENDIF
	PRINTBUTTON "[" + CALLNAME:(TARGET:LOCAL) + "]", (1000 + TARGET:LOCAL)
	RESETCOLOR
NEXT
IF 同行人数
	IF CHK_DATENOW(CFLAG:MASTER:デート中)
		PRINTFORM  \@ TFLAG:泡風呂 ? is receiving service # is dating \@
	ELSE
		PRINT  is accompanying
	ENDIF
ENDIF
IF TFLAG:運搬 > 0
	IF TARGET == TFLAG:運搬
		SETCOLOR C_AQUA
	ELSE
		SETCOLOR 200, 50, 200
	ENDIF
	IF FINDELEMENT(TARGET, TFLAG:運搬, 1) != -1
		PRINTBUTTON "[" + CALLNAME:(TFLAG:運搬) + "]", (1000 + TARGET:FINDELEMENT(TARGET, TFLAG:運搬, 1))
	ELSE
		PRINTFORM [%CALLNAME:(TFLAG:運搬)%]
	ENDIF
	RESETCOLOR
	PRINT  is being carried
	表示済み:FINDELEMENT(TARGET, TFLAG:運搬, 1) = 1
ENDIF
SIF MATCH(表示済み, 1) && GET_TARGETNUM() != MATCH(表示済み, 1)
	PRINT ／

;ターゲット切替ボタン部分
TFLAG:パンツ収奪 = 0
FOR LOCAL, 1, GET_TARGETNUM() + 1
	SIF CFLAG:(TARGET:LOCAL):現在位置 != CFLAG:MASTER:現在位置
		CONTINUE
	SIF 表示済み:LOCAL
		CONTINUE
	CALL QoL_HighlightRequestChara(TARGET:LOCAL) ;qol custom code
	SIF TARGET == TARGET:LOCAL
		SETCOLOR C_AQUA
	PRINTBUTTON "[" + CALLNAME:(TARGET:LOCAL) + "]", (1000 + TARGET:LOCAL)
	RESETCOLOR
	SIF FLAG:70 == 1 && EQUIP:(TARGET:LOCAL):0 && EQUIP:(TARGET:LOCAL):下半身下着２ != 0 && FLAG:700 >= 100
		TFLAG:パンツ収奪++
	SIF LOCAL < GET_TARGETNUM()
		PRINT , 
NEXT
PRINTL

;TARGET本人の簡易情報
@INFO_TARGET_SITUATION
LOCAL = TARGET + 4000
CALL SHOW_EQUIP_2
PRINTFORM （Favorability:
CALL PRINT_RANK(CFLAG:2, "MAX_50000")
PRINTFORM  {CFLAG:2}, Reliability:
CALL PRINT_RANK(CFLAG:信頼度, "MAX_20000")
PRINTFORM  {CFLAG:信頼度}
IF NOTICE_LUST(TARGET)
	PRINTFORM ,　Sexual Frustration: {CFLAG:TARGET:溜まってる度 / 10}\%）
ELSE
	PRINT ）
ENDIF

;PRINTFORM [%HUNGER(TARGET)%]　
DEBUGPRINTFORML  Agreement Level:{CFLAG:合意判定}

LOCAL = MIN(BASE:怒り / 200, 5)
IF CFLAG:睡眠
	IF CFLAG:ブチギレ
		SETCOLOR 255,0,0
		PRINT Angry
		RESETCOLOR
	ELSEIF CFLAG:MASTER:イタズラ == 2
		SELECTCASE TCVAR:TARGET:睡眠深度
			CASE 0 TO 500
				SETCOLOR 255,0,0
				PRINTFORM Sleep status: Can wake up any moment now
			CASE 501 TO 1500
				SETCOLOR 255,255,0
				PRINTFORM Sleep status: Restless
			CASE 1501 TO 3000
				SETCOLOR 0,255,0
				PRINTFORM Sleep status: Light
			CASE 3001 TO 5000
				SETCOLOR 0,255,255
				PRINTFORM Sleep status: Asleep
			CASE IS > 5000
				SETCOLOR C_BLUE ;custom code
				PRINTFORM Sleep status: Soundly asleep
			CASEELSE
		ENDSELECT
		RESETCOLOR
	ELSE
		PRINTFORM Awoken:%"！" * LOCAL%
	ENDIF
ELSE
	IF CFLAG:ブチギレ
		IF TALENT:度胸 < 0
			SETCOLOR C_BLUE
			PRINT Sad
		ELSE
			SETCOLOR 255,0,0
			PRINT Mad
		ENDIF
		RESETCOLOR
	ELSEIF CFLAG:イタズラ || TCVAR:泥酔
		PRINTFORM Awoken:%"！" * LOCAL%
	ELSE
		PRINTFORM Anger:%"！" * LOCAL%
		PRINTFORM   %"　" * (5 - LOCAL)%
		SELECTCASE CFLAG:TARGET:眠気覚まし
			CASE IS > 240
				PRINT <Sleep Res・Lots>
			CASE IS > 180
				PRINT <Sleep Res・Strong>
			CASE IS > 120
				PRINT <Sleep Res・Middle>
			CASE IS > 0
				PRINT <Sleep Res・Weak>
			CASEELSE
				PRINT
		ENDSELECT
	ENDIF
ENDIF
PRINTL 

;SHOW_STATUS用のバー関数と状態をまとめて表示する関数
@INFO_BASE(CHARA)
#DIM CHARA
PRINTFORM %CALLNAME:CHARA, 20, LEFT%
;BASE_BAR
IF CFLAG:CHARA:睡眠 && CHARA != MASTER
	CALL BASE_BAR_2(CHARA, "体力", "Sta", 10, "水色")
ELSE
	CALL BASE_BAR_2(CHARA, "体力", "Sta", 10, "緑")
ENDIF
CALL BASE_BAR_2(CHARA, "気力", "Ene", 10, "青")
CALL BASE_BAR_2(CHARA, "酒気", "Drnk", 10, "赤")
IF HAS_PENIS(CHARA)
	CALL BASE_BAR_2(CHARA, "精力", "Vig", 10, "黄")
ELSE
	PRINTFORM %" " * 22%
ENDIF
;その他の状態
CALL COLORMESSAGE(ALCOHOL_FACE_TR(CHARA), C_ORANGE, 0)
SIF HAS_PENIS(CHARA)
	CALL INFO_SHOW_BOKKI(CHARA)
PRINTL 

;勃起状態のアナログ表示
@INFO_SHOW_BOKKI(CHARA)
#DIM CHARA
SETCOLOR C_P_PURPLE
IF BASE:CHARA:勃起 == MAXBASE:CHARA:勃起
	FONTBOLD
	PRINTFORM Erection!!
	FONTREGULAR
ELSEIF BASE:CHARA:勃起 >= 1000
	PRINTFORM Hard
ELSEIF BASE:CHARA:勃起 >= 500
	PRINTFORM Soft
ELSE
;	PRINTFORM 通常
ENDIF
RESETCOLOR

;TARGETおよびあなたの情報・状態
@INFO_STATUS
IF TARGET
	CALL INFO_EMOTION
	SIF FLAG:生理周期可視化
		CALL CYCLE_PRINT(TARGET)
	SIF EX:TARGET:膣内精液 > 0
		PRINTFORM Cum in Vagina: %DIGIT_GROUP(EX:TARGET:膣内精液),5,LEFT%
	SIF EX:TARGET:アナル内精液 > 0
		PRINTFORM Cum in Anus: %DIGIT_GROUP(EX:TARGET:アナル内精液),5,LEFT%
	SIF TFLAG:ゴム内 > 0 ;com custom code
		PRINTFORM Cum in Condom: %DIGIT_GROUP(TFLAG:ゴム内),5,LEFT%
	PRINTL
ENDIF
;あなたの状態
CALL SHOW_EQUIP_3
;TARGETの状態
CALL SHOW_EQUIP_1
;射精/母乳ゲージ表示関数
CALL INFO_GAUGEGROUP

;ムードや理性などの表示
@INFO_EMOTION
LOCAL = LIMIT(BASE:TARGET:ムード / 200,0,5)
PRINTFORM Mood:
SIF BASE:TARGET:ムード == MAXBASE:TARGET:ムード
	SETCOLOR 255,51,153
IF BASE:TARGET:ムード == MAXBASE:TARGET:ムード && CHK_DATENOW(CFLAG:TARGET:デート中)
	FONTBOLD
	PRINTFORM OverDrive!!
	FONTREGULAR
ELSE
	CALL HEARTMARK(LOCAL)
	PRINTFORM %" " * (13 - LOCAL * 2)%
ENDIF
RESETCOLOR

LOCAL = MAX(BASE:TARGET:理性 / 200,1)
PRINT Composure:
SIF BASE:TARGET:理性 < 100
	SETCOLOR 255,255,0
IF BASE:TARGET:理性 == 0 && CHK_DATENOW(CFLAG:TARGET:デート中)
	FONTBOLD
	PRINTFORM LimitBreak!!
	FONTREGULAR
ELSE
	PRINTFORM %"★" * LOCAL%
	PRINTFORM %" " * (14 - LOCAL * 2)%
ENDIF
RESETCOLOR


;射精/母乳ゲージ表示関数
@INFO_GAUGEGROUP
;bug fix failsafe
BASE:MASTER:精力 = MAX(0, BASE:MASTER:精力)
BASE:精力 = MAX(0, BASE:精力)

SIF !MAXBASE:2
	MAXBASE:2 = 10000
IF ASSI > 0
	SIF !MAXBASE:ASSI:2
		MAXBASE:ASSI:2 = 10000
ENDIF
;addition custom code
SIF TFLAG:EjacChange > 0
	GOTO SKIP
IF TALENT:MASTER:絶倫
	MAXBASE:MASTER:2 = 5000
ELSE
	MAXBASE:MASTER:2 = 10000
ENDIF
$SKIP ;addition custom code
SIF !GROUPMATCH(TFLAG:102, 2, 3)
	RETURN

CALL SHOW_SAMEN_GAUGE(MASTER)
CALL SHOW_SAMEN_GAUGE(TARGET)

IF TALENT:TARGET:母乳体質
	IF TALENT:TARGET:淫乳
		MAXBASE:3 = 5000
	ELSE
		MAXBASE:3 = 10000
	ENDIF
	CALL INFO_GAUGE_BAR(TARGET, "母乳", "EXTRA")
	PRINTFORML [{BASE:3,5}/{MAXBASE:3,5}]
ENDIF



;SHOW_STATUS用のバー関数の共通設定、射精/母乳ゲージ用
@INFO_GAUGE_BAR(C_ID, NAME_BASE, NAME_COLOR)
#DIM C_ID
#DIMS NAME_BASE
#DIMS NAME_COLOR
;Custom code
LOCALS '= @"　%BASE_TR(NAME_BASE),5,LEFT%(%CALLNAME:C_ID%)"
PRINTFORM %LOCALS,22,LEFT%
RESETCOLOR
CALL BASE_BAR(C_ID, NAME_BASE, 18, NAME_COLOR, "「種類表記無し」「数字表記無し」")

@SHOW_SAMEN_GAUGE(ARG)
SIF !HAS_PENIS(ARG)
	RETURN
CALL INFO_GAUGE_BAR(ARG, "射精", "水色")
IF TCVAR:ARG:イきそう
	SETCOLOR C_RED
	PRINTFORM [!IMMINENT!]
	RESETCOLOR
ELSE
	PRINTFORM [{BASE:ARG:2,5}/{MAXBASE:ARG:2,5}]
ENDIF
LOCALS = ENDURE
SELECTCASE TCVAR:ARG:射精我慢
	CASE 0
		LOCALS = NORMAL
	CASE 1
		SETCOLOR C_AQUA
	CASE 2
		SETCOLOR C_YELLOW
	CASE 3
		SETCOLOR C_RED
ENDSELECT
PRINTBUTTON "["+LOCALS+"]", 5000 + ARG
RESETCOLOR
PRINTFORML 

;------------------------------------------
;帰宅予告とか
;1,外出時のあなた帰宅予告
;2,デート時の相手の帰宅予告
;3,要睡眠通知
;------------------------------------------
@TIMEUP_NOTICE(DATE)
#DIM DATE
#DIM TIMEUP_MESSAGE, 4
IF NEMUKE() > 660 && CFLAG:MASTER:デート中 != MAIN_MAP && !TIMEUP_MESSAGE:1
	CALL COLORMESSAGE("It's about time to go home...", C_ORANGE, 2)
	TIMEUP_MESSAGE:1 = 1
ELSEIF NEMUKE() < 660
	TIMEUP_MESSAGE:1 = 0
ENDIF
IF DATE && !TIMEUP_MESSAGE:2
	IF CFLAG:DATE:神社在住
		IF INRANGE(TIME, CFLAG:DATE:就寝時間 - 60, CFLAG:DATE:就寝時間)
			CALL COLORMESSAGE(@"%CALLNAME:DATE% looks sleepy\@ TIMEUP_MESSAGE:1 ? %" "%as well #  \@.", C_ORANGE, 2)
			TIMEUP_MESSAGE:2 = 1
		ENDIF
	ELSE
		IF INRANGE(TIME, CFLAG:DATE:帰宅時間 - 60, CFLAG:DATE:帰宅時間)
			CALL COLORMESSAGE(@"It's about time for %CALLNAME:DATE% to go home\@ TIMEUP_MESSAGE:1 ? %" "%as well #  \@.", C_ORANGE, 2)
			TIMEUP_MESSAGE:2 = 1
		ENDIF
	ENDIF
ELSEIF !DATE
	TIMEUP_MESSAGE:2 = 0
ENDIF
IF NEMUKE() > 960 && !TIMEUP_MESSAGE:3 && !CFLAG:MASTER:うふふ
	CALL COLORMESSAGE("It's getting pretty late...", C_RED, 2)
	TIMEUP_MESSAGE:3 = 1
ELSEIF NEMUKE() <= 960
	TIMEUP_MESSAGE:3 = 0
ENDIF
