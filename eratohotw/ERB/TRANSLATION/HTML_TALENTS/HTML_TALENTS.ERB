@PRINT_STATE_TALENT_MOD(nCharacter)
#DIM nCharacter
#DIM MEMO_STR
#DIMS strToPrint
#DIMS OUTPUT
#DIM DYNAMIC TLNT_CNT, 6
#DIM DYNAMIC TLNT_NO, 6, 100

TOOLTIP_SETDELAY 0

CALL PRINTBUTTON_EX("Personality",165,TFLAG:165, , , 1)
IF !TFLAG:165
	OUTPUT = 　
	MEMO_STR = 0
	;TALENT:200(汎用キャラ)は表示させない
	FOR LOCAL, 0, 300
		;only acquired talents
		SIF !STRLENS(GET_TALENTNAME_TR(LOCAL, TALENT:nCharacter:LOCAL,nCharacter))
			CONTINUE
		;Gender-specific
		SIF LOCAL == GETNUM(TALENT, "処女") && !(TALENT:nCharacter:性別 & 1)
			CONTINUE
		;童貞
		SIF LOCAL == GETNUM(TALENT, "非童貞") && !(TALENT:nCharacter:性別 & 2)
			CONTINUE
		;男絶壁
		IF LOCAL == GETNUM(TALENT, "バストサイズ") && (TALENT:nCharacter:性別 == 2)
			SIF (TALENT:nCharacter:バストサイズ == -2)
				CONTINUE
		ENDIF
		;掃除係は住んでる時だけ表示
		SIF LOCAL == GETNUM(TALENT, "掃除係") && !CFLAG:nCharacter:神社在住
			CONTINUE
			
		;Moved around so it goes "if adding this would break it, break the line, and then consider this added"
		strToPrint = [%GET_TALENTNAME_TR(LOCAL, TALENT:nCharacter:LOCAL,nCharacter)%]
		MEMO_STR += STRLENSU(strToPrint)
		;PRINTFORMW %strToPrint%, length {STRLENS(strToPrint)}
		IF MEMO_STR > 126 ;to be in line with "addiction" column
			;break line with another HTML_PRINT because <br> doesn't add to LINECOUNT
			HTML_PRINT OUTPUT
			OUTPUT = 　
			MEMO_STR = STRLENSU(strToPrint) ;grab the length of the current talent to print as a starting point of a new line
		ENDIF

		LOCALS '= TALENT_INFO(LOCAL, TALENT:nCharacter:LOCAL)
		IF GROUPMATCH(LOCAL,0,3,73,74,75,76,77,78,122)
			OUTPUT += MO_TALENT(strToPrint, LOCALS, "FF78C8", 1)
			MEMO_STR += 2 ;try to compensate for BOLD fucking up the alignment
		ELSEIF LOCAL == 8
			OUTPUT += MO_TALENT(strToPrint, LOCALS, "FFCCFF")
		ELSE
			OUTPUT += MO_TALENT(strToPrint, LOCALS)
		ENDIF
	NEXT
	HTML_PRINT OUTPUT
ENDIF

;take care of all the html garbage
@MO_TALENT(strToPrint, strToolTip, strFont, nBold)
#FUNCTIONS
#DIMS strToPrint
#DIMS strToolTip
#DIMS strFont
#DIM nBold
;get current font color by default
SIF !STRLENS(strFont)
	strFont '= TOSTR(GETCOLOR(),"X6")

LOCALS '= HTML_ESCAPE(strToPrint)

SIF nBold
	LOCALS = <b>%LOCALS%</b>

RETURNF @"<font color='#%strFont%'><nonbutton title='%HTML_ESCAPE(strToolTip)%'>%LOCALS%</nonbutton></font>"

@PRINT_STATE_TALENT_MOD2(選択中キャラID)
#DIM 選択中キャラID
#DIM TLNT_CNT, 6
#DIM TLNT_NO, 6, 100
#DIMS CONST TALENT_TYPE, 6 = "  Species  :", "　Sexual   :", "  Physical :", "  Mental   :", "  Practical:", "　Other    :"
#DIMS OUTPUT

VARSET TLNT_CNT
VARSET TLNT_NO
VARSET OUTPUT
CALL PRINTBUTTON_EX("Personality",165,TFLAG:165, , , 1)
IF !TFLAG:165
	;TALENT:200(汎用キャラ)は表示させない
	FOR LOCAL, 0, 300
		SIF !STRLENS(GET_TALENTNAME(LOCAL, TALENT:選択中キャラID:LOCAL))
			CONTINUE
		IF GROUPMATCH(LOCAL,90,91,164,165,166,189,190,191,192,193,194,195,196,197)
			;種族,132=吸血鬼は被るので身体特徴に移動
			TLNT_NO:0:(TLNT_CNT:0) = LOCAL
			TLNT_CNT:0 ++
		ELSEIF GROUPMATCH(LOCAL,0,1,3,4,5,6,7,8,9,60,70,72,73,74,75,76,77,78,82,83,122,142,152,183,184,185,250,254,255,256,257,258,259,260,261,262,263,264)
			;性的特徴
			TLNT_NO:1:(TLNT_CNT:1) = LOCAL
			TLNT_CNT:1 ++
		ELSEIF GROUPMATCH(LOCAL,2,16,17,40,41,54,56,57,92,100,101,102,103,104,105,106,121,130,132,133,134,137,138,143,146,149)
			;身体的特徴
			TLNT_NO:2:(TLNT_CNT:2) = LOCAL
			TLNT_CNT:2 ++
		ELSEIF GROUPMATCH(LOCAL,9,10,11,12,13,14,18,19,20,21,22,23,24,25,26,30,31,32,33,61,62,71,80,81,84,85,86,94,151,155)
			;精神的特徴
			TLNT_NO:3:(TLNT_CNT:3) = LOCAL
			TLNT_CNT:3 ++
		ELSEIF GROUPMATCH(LOCAL,46, 47, 48, 49, 50,51,52,53,55,93,135,136,144,161,162)
			;技術的特徴
			TLNT_NO:4:(TLNT_CNT:4) = LOCAL
			TLNT_CNT:4 ++
		ELSE
			;その他
			TLNT_NO:5:(TLNT_CNT:5) = LOCAL
			TLNT_CNT:5 ++
		ENDIF
	NEXT

	FOR LOCAL:2, 0, 6
		OUTPUT = %TALENT_TYPE:(LOCAL:2)%
		LOCALS = ""
		;If the character doesn't have any other race talents
		IF LOCAL:2 == 0 && TLNT_CNT:0 == 0
			OUTPUT += PRINT_種族抽出_MOD(選択中キャラID)
		ENDIF
		FOR LOCAL:1, 0, TLNT_CNT:(LOCAL:2)
			LOCAL = TLNT_NO:(LOCAL:2):(LOCAL:1)
			;童貞
			SIF LOCAL == 1 && !(TALENT:選択中キャラID:性別 & 2)
				CONTINUE
			;男絶壁
			IF LOCAL == 105 && (TALENT:選択中キャラID:性別 == 2)
				SIF (TALENT:選択中キャラID:バストサイズ == -2) && (TALENT:選択中キャラID:性別 == 2)
					CONTINUE
			ENDIF
			;掃除係は住んでる時だけ表示
			SIF LOCAL == GETNUM(TALENT, "掃除係") && !CFLAG:選択中キャラID:神社在住
				CONTINUE
			LOCALS += GET_TALENTNAME(LOCAL, TALENT:選択中キャラID:LOCAL)
			IF STRLENS(LOCALS) > 60
				LOCALS = " "
				OUTPUT += "\n　　　　　　"
			ENDIF
			LOCALS:1 '= @"[%GET_TALENTNAME_TR(LOCAL,TALENT:選択中キャラID:LOCAL, 選択中キャラID)%]"
			LOCALS:2 '= TALENT_INFO(LOCAL, TALENT:選択中キャラID:LOCAL)
			;素質によっては色変え
			IF GROUPMATCH(LOCAL,0,3,4,6,7,73,74,75,76,77,78,122,142)
				SETCOLOR 255, 120, 200
				FONTBOLD
				OUTPUT += MO_TALENT(LOCALS:1, LOCALS:2, "FF78C8", 1)
			ELSEIF LOCAL == 8
				SETCOLOR C_PINK
				OUTPUT += MO_TALENT(LOCALS:1, LOCALS:2, "FFCCFF")
			ELSE
				OUTPUT += MO_TALENT(LOCALS:1, LOCALS:2)
			ENDIF
			RESETCOLOR
			FONTREGULAR
		NEXT
		SIF LOCALS != " "
			HTML_PRINT OUTPUT
	NEXT
ENDIF

;ARG - talent number
;
@TALENT_HTML(ARG)
;RETURNF @"<font color='#%strFont%'><nonbutton title='%HTML_ESCAPE(strToolTip)%'>%LOCALS%</nonbutton></font>"

@PRINT_種族抽出_MOD(選択中キャラID)
#FUNCTIONS
#DIM 選択中キャラID
#DIM TMP_RESULT
#DIMS STR_LIST, 10
VARSET STR_LIST
LOCALS =  %CSVCSTR(選択中キャラID,10)%
IF STRFIND(LOCALS, "●") > -1
	SPLIT LOCALS, "●", STR_LIST
	FOR LOCAL, 0, RESULT
		IF STRFIND(STR_LIST:LOCAL, "Race:") > -1
			LOCALS:1 = %REPLACE(STR_LIST:LOCAL, "Race:", "")%
			LOCALS:1 = %REPLACE(LOCALS:1, "　", "")%
			LOCALS = [%LOCALS:1%]
			BREAK
		ENDIF
	NEXT
ELSEIF 選択中キャラID == [[小悪魔]]
	LOCALS = [Demon]
;ELSEIF TALENT:選択中キャラID:行きずり
;	PRINTFORM [人間]
ENDIF

RETURNF @"<nonbutton title='%HTML_ESCAPE(RACE_INFO(選択中キャラID))%'>%HTML_ESCAPE(LOCALS)%</nonbutton>"