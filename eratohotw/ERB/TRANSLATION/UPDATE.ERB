@UPDATE_TR
#DIM C_ID
#DIM 更新能力番号
#DIM 最低能力値
#DIM 最低経験数
#DIM 育児中の子供
#DIM 子供設定初回
#DIM 口上実装チェック, キャラクタ数上限
#DIM リセット画面

#DIM 技能素質更新用
#DIMS rname

IF SAVESTR:本体バージョン != eraTW_Version
	PRINTL This save is from a previous version, so this option will/may add: new characters, traits and flags that were introduced in the new versions.
ELSE
	PRINTL This save is already at the latest version, running this function might fix some problems though. Other things might get reset though.
	PRINTL Please confirm to continue.
	CALL ASK_YN
	SIF RESULT
		RETURN
ENDIF

;Custom code, changes old items for new items
CALL UPDATE_VERSIONBASED()

CALL FIX_DIALOGUE_STATUS
CALL CHARACTER_DIALOG_STATUS

;they already get deleted automatically
; PRINTL
; PRINTL Delete any random characters encountered via the 'hunt for girls' command?
; PRINTL ※If they work correctly, this might not be necessary, they can always be deleted later as well.
; CALL ASK_YN
; LOCAL:5 = !RESULT

;these two vars are used to track the KOJO updates; the first holds the updated ones and the second hold if any have been updated
VARSET 口上実装チェック
リセット画面 = 0
;まずはモブを削除 mob girl is deleted right away, she's always the last one (she is then added back promptly)
DELCHARA CHARANUM - 1

FOR C_ID,1,CHARANUM ;CSVに名前を参照してNOを書き直す
	VARSET LOCAL
	FOR LOCAL,1,キャラクタ数上限
		SIF !EXISTCSV(LOCAL)
			CONTINUE
		IF NAME:C_ID == CSVNAME(LOCAL)
			NO:C_ID = LOCAL
			BREAK
		ENDIF
	NEXT
NEXT

FOR C_ID,1,キャラクタ数上限 ;終わったら新キャラ追加
	IF GETCHARA(C_ID) == -1 && EXISTCSV(C_ID)
		;add new character, check if they have KOJO and set a variable if they do
		ADDCHARA C_ID
		TARGET = C_ID
		RESULT = 0
	ENDIF
	IF TALENT:C_ID:行きずり
		RANDOM_CHARANUM = C_ID
		BREAK
	ENDIF
NEXT

FOR C_ID, 1, CHARANUM
	IF NO:C_ID != C_ID && NO:C_ID != 0 ;もしずれたら並べなおす
		IF NO:C_ID > C_ID ;CSVNOが今のNOより後ろなら、SWAPして続く
			SWAPCHARA C_ID,NO:C_ID
			FOR LOCAL,0,CHARANUM
				SWAP RELATION:LOCAL:C_ID,RELATION:LOCAL:(NO:C_ID)
			NEXT
		ELSE
			SWAPCHARA C_ID,NO:C_ID ;そうじゃなかったらSWAPして一から並べなおす
			FOR LOCAL,0,CHARANUM
				SWAP RELATION:LOCAL:C_ID,RELATION:LOCAL:(NO:C_ID)
			NEXT
			C_ID = 1
		ENDIF
	ENDIF
	IF TALENT:C_ID:行きずり
		RANDOM_CHARANUM = C_ID
		BREAK
	ENDIF
NEXT

;update number of children MASTER has by checking those numbers for everyone else
PRINTFORML Recalculate number of children and children requiring child support.
CFLAG:MASTER:子供人数 = 0
CFLAG:MASTER:育児人数 = 0
FOR C_ID, 1, CHARANUM
	SIF TALENT:C_ID:行きずり
		CONTINUE
	CFLAG:MASTER:子供人数 += CFLAG:C_ID:子供人数
	CFLAG:MASTER:育児人数 += CFLAG:C_ID:育児人数
NEXT
PRINTFORML Children: {CFLAG:MASTER:子供人数} Children requiring child support: {CFLAG:MASTER:育児人数}

;set the limit for maximum carisma held
SIF !CM_LIMIT
	CM_LIMIT = 100
;removes dirt from the lakebottom of misty lake
YOGORE:348 = 0

;check with the player for stuff that should be reset
PRINTL
PRINTL Update sexual experience of characters according to the CSV definitions?
PRINTL ※If virginity and such was manually at the beginning, this will overwrite that.
CALL COLORMESSAGE("Generally no need to, unless you're resetting characters.",C_YELLOW,1,1)
CALL ASK_YN
LOCAL:3 = !RESULT

PRINTL
PRINTL Update basic skill experience of characters according to the CSV definitions?
PRINTL ※This is mostly to fix characters having skill levels but not the experience to reach those levels
PRINTL ※Includes combat, cleaning, speech, knowledge, cooking, music performance, singing, and fishing experience
CALL COLORMESSAGE("Generally no need to, unless you're resetting characters.",C_YELLOW,1,1)
CALL ASK_YN
LOCAL:4 = !RESULT

;addition custom code
CALL ADD_SET_RELATION
LOCAL:5 = !RESULT

FOR C_ID, 1, CHARANUM
	;CSTR:仕事情報をCSTR:3に移行する手続き
	;moves the job description CSTR from :1 to :3 in attempt to fixe double-useage of :1
	;spoiler: it doesn't fix it because most characters still have that defined as :1 in the csv and :3 is empty for thoose; jap handiwork
	SIF TALENT:C_ID:行きずり
		CONTINUE
	IF CSTR:C_ID:3 == "" && CSVCSTR(C_ID, 3) != ""
		CSTR:C_ID:3 = %CSVCSTR(C_ID, 3)%
		CSTR:C_ID:1 = 
	ENDIF
	;unsets talent 186 since that no longer exists; this was already in the old TW
	TALENT:C_ID:186 = 0
	;if love unset other talents(specifically the other falling talents)
	;FLAG:陥落路線変化 is suppossed to make love possible while セフレ and 愛欲 are there
	;looking at the code, I don't think it really works but lets at least not break it here
	IF TALENT:C_ID:恋慕 && !FLAG:陥落路線変化
		TALENT:C_ID:思慕 = 0
		TALENT:C_ID:セフレ = 0
		TALENT:C_ID:愛欲 = 0
	ENDIF
	;function that attempts to fix the CSTRs for firsts.
	;all in jap, translate or skip?
	CALL 履歴文字列変換_TR(C_ID)
	;if a character is parenting, set the child they are parenting
	IF TALENT:C_ID:育児中
		FOR LOCAL:1, 1, 11
			SIF (CHILD_BIRTHDAY:C_ID:(LOCAL:1) > CHILD_BIRTHDAY:C_ID:(LOCAL:1 - 1)) || (LOCAL:1 - 1) == 0
				育児中の子供 = LOCAL:1
		NEXT
		TALENT:C_ID:育児中 = 育児中の子供
	ENDIF

	;if not pregnant, stop being aware of pregnancy
	SIF !TALENT:C_ID:妊娠
		CFLAG:C_ID:妊娠自覚状態 = 0
	
	;CLAGs related to IRAI(requests); new system may not use thoose flags though?(this has been in UPDATE for a long time)
	;used to be
	;SIF !CFLAG:LOCAL:404
	;	CFLAG:LOCAL:403 = 0
	;依頼状況 = 403 and 依頼内容 = 404; so the names where switched?
	;Might be that it used to be broken for a long while and they fixed it?
	SIF !CFLAG:C_ID:依頼状況
		CFLAG:C_ID:依頼内容 = 0
;	;固有料理フラグ
;	SIF !LEARNED_MENU:C_ID
;		LEARNED_MENU:C_ID = CFLAG:C_ID:固有料理
	
	;EFFECTIVE_PATTERN_V seems to related to COMF317?
	FOR LOCAL:1, 0, 3
		SIF !EFFECTIVE_PATTERN_V:C_ID:(LOCAL:1)
			EFFECTIVE_PATTERN_V:C_ID:(LOCAL:1) = 1 + RAND:3
		SIF !EFFECTIVE_PATTERN_A:C_ID:(LOCAL:1)
			EFFECTIVE_PATTERN_A:C_ID:(LOCAL:1) = 1 + RAND:3
	NEXT

	;種族系素質を更新
	;update talents 190-197 according to CSV values; They are only race identifiers(e.g. Human)
	FOR LOCAL:1, 190, 198
		TALENT:C_ID:(LOCAL:1) = CSVTALENT(C_ID, LOCAL:1)
	NEXT
	;this was added to update some other race-ish talents
	;水棲、氷精、種族(未使用？)、半白澤、動物耳も変動しないと思われるので更新
	FOR LOCAL:1, 163, 167
		TALENT:C_ID:(LOCAL:1) = CSVTALENT(C_ID, LOCAL:1)
	NEXT
	
	;技能系素質,移行完了
;	技能素質更新用 = MAX(ABL:C_ID:釣り技能, CSVTALENT(C_ID, 47))
	TALENT:C_ID:釣りLv = MAX(TALENT:C_ID:釣りLv, CSVTALENT(C_ID, 47))
	CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "釣り経験"), TALENT:C_ID:釣りLv)
	
;	技能素質更新用 = MAX(ABL:C_ID:採集技能, CSVTALENT(C_ID, 48))
	TALENT:C_ID:採集Lv = MAX(TALENT:C_ID:採集Lv, CSVTALENT(C_ID, 48))
	CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "採集経験"), TALENT:C_ID:採集Lv)
	
;	技能素質更新用 = MAX(ABL:C_ID:調合技能, CSVTALENT(C_ID, 49))
	TALENT:C_ID:調合Lv = MAX(TALENT:C_ID:調合Lv, CSVTALENT(C_ID, 49))
	CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "調合経験"), TALENT:C_ID:調合Lv)

;	技能素質更新用 = CSVTALENT(C_ID, 46)
	TALENT:C_ID:伐採Lv = MAX(TALENT:C_ID:伐採Lv, CSVTALENT(C_ID, 46))
	CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "伐採経験"), TALENT:C_ID:伐採Lv)

	;update pain sensitivity according to CSV; may be undesired
	TALENT:C_ID:痛覚 = CSVTALENT(C_ID, GETNUM(TALENT, "痛覚"))
	;update animal ears talent
	TALENT:C_ID:動物耳 = CSVTALENT(C_ID, GETNUM(TALENT, "動物耳"))
	;update 回復速度 according to CSV; may be undesired
	SIF TALENT:C_ID:回復速度 < CSVTALENT(C_ID, GETNUM(TALENT, "回復速度")) && !TALENT:C_ID:妊娠 && CFLAG:C_ID:出産経過日 > 20
		TALENT:C_ID:回復速度 = CSVTALENT(C_ID, GETNUM(TALENT, "回復速度"))
	;restructured code added
	;多分大丈夫だと思うが、後天的な取得処理を見込んで判定
	;check if the character should be a cleaner but isn't and fix it
	SIF TALENT:C_ID:掃除係 == 0
		TALENT:C_ID:掃除係 = CSVTALENT(C_ID, GETNUM(TALENT, "掃除係"))
	;same for sense of sound
	SIF TALENT:C_ID:音感 == 0
		TALENT:C_ID:音感 = CSVTALENT(C_ID, GETNUM(TALENT, "音感"))
	;and for the singing talent
	SIF TALENT:C_ID:音楽知識 == 0
		TALENT:C_ID:音楽知識 = CSVTALENT(C_ID, GETNUM(TALENT, "音楽知識"))
	;tinko check has been moved here
	SIF GETBIT(TALENT:C_ID:性別, 1) && !TALENT:C_ID:形状
		CALL TINKO_DECIDE(C_ID)

	;来訪確率補正を読み込み
	;updates some flags to their csv values
	;same code used to be further down, japs removed it though
	FOR LOCAL:1, 420, 432
		CFLAG:C_ID:(LOCAL:1) = CSVCFLAG(C_ID, LOCAL:1)
	NEXT
	;来訪位置を読み込み
	FOR LOCAL:1, 450, 462
		CFLAG:C_ID:(LOCAL:1) = CSVCFLAG(C_ID, LOCAL:1)
	NEXT
	;update character relations/affinity for each other according to CSV setting; should be no problem, thoose never change
	FOR LOCAL:1, 1, CHARANUM
		RELATION:C_ID:(LOCAL:1) = CSVRELATION(C_ID, LOCAL:1)
	NEXT
	;addition custom code
	SIF LOCAL:5
		CALL ADD_SET_RELATION_PROC(C_ID)
	;差し替え画像の有無を確認
	IF CFLAG:C_ID:別顔有 < CSVCFLAG(C_ID, 562) || CFLAG:C_ID:別立ち有 < CSVCFLAG(C_ID, 563)
		PRINTFORML Detected a new replacement image for %CALLNAME:C_ID%.
		IF CFLAG:C_ID:別顔有 < CSVCFLAG(C_ID, 562)
			CALL 画像セット(@"顔絵_服_通常_{C_ID}", 0, 0, 100, 0, 0, "", "デフォルト顔絵")
			FOR LOCAL:1, 1, CSVCFLAG(C_ID, 562)+1
				CALL 画像セット(@"\@LOCAL:1 >= 2?別顔{LOCAL:1}#別顔\@_服_通常_{C_ID}", 0, 0, 100, 0, 0, "", "差し替え顔絵")
			NEXT
			CALL 画像一斉表示("左", 0, 1)
		ENDIF
		IF CFLAG:C_ID:別立ち有 < CSVCFLAG(C_ID, 563)
			CALL 画像セット(@"立ち絵_服_通常_{C_ID}", 0, 0, 100, 0, 0, "", "デフォルト立ち絵")
			FOR LOCAL:1, 1, CSVCFLAG(C_ID, 563)+1
				CALL 画像セット(@"\@LOCAL:1 >= 2?別立ち{LOCAL:1}#別立ち\@_服_通常_{C_ID}", 0, 0, 100, 0, 0, "", "差し替え立ち絵")
			NEXT
			CALL 画像一斉表示("左", 0, 1)
		ENDIF
		PRINT [0] Don't replace　
		FOR LOCAL:1, 1, MAX(CSVCFLAG(C_ID, 562), CSVCFLAG(C_ID, 563)) + 1
			PRINTFORM [{LOCAL:1}] Replace with {LOCAL:1}　
		NEXT
		INPUT
		CFLAG:C_ID:差し替え適用 = RESULT
	ENDIF
	CFLAG:C_ID:別顔有 = CSVCFLAG(C_ID, 562)
	CFLAG:C_ID:別立ち有 = CSVCFLAG(C_ID, 563)
	;setting several flags according to CSV values
	;大家フラグ
	CFLAG:C_ID:大家候補 = CSVCFLAG (C_ID, GETNUM(CFLAG, "大家候補"))
	CFLAG:C_ID:住み込み必要信頼度 = CSVCFLAG (C_ID, GETNUM(CFLAG, "住み込み必要信頼度"))
	CFLAG:C_ID:家賃 = CSVCFLAG (C_ID, GETNUM(CFLAG, "家賃"))
	CFLAG:C_ID:素材販売 = CSVCFLAG (C_ID, GETNUM(CFLAG, "素材販売"))
	CFLAG:C_ID:固有コマンド = CSVCFLAG (C_ID, GETNUM(CFLAG, "固有コマンド"))
	CFLAG:C_ID:地位 = CSVCFLAG (C_ID, GETNUM(CFLAG, "地位"))
	CFLAG:C_ID:初期位置 = CSVCFLAG (C_ID, GETNUM(CFLAG, "初期位置"))
	;set the initial position to 神社在住 if 神社在住 is set
	;used to be further up
	SIF FLAG:一時滞在 == C_ID
		CFLAG:C_ID:初期位置 = CFLAG:C_ID:神社在住
	CFLAG:C_ID:来訪時間 = CSVCFLAG (C_ID, GETNUM(CFLAG, "来訪時間"))
	CFLAG:C_ID:帰宅時間 = CSVCFLAG (C_ID, GETNUM(CFLAG, "帰宅時間"))
	CFLAG:C_ID:就寝時間 = CSVCFLAG (C_ID, GETNUM(CFLAG, "就寝時間"))
	CFLAG:C_ID:起床時間 = CSVCFLAG (C_ID, GETNUM(CFLAG, "起床時間"))
	CFLAG:C_ID:よく行く地域 = CSVCFLAG (C_ID, GETNUM(CFLAG, "よく行く地域"))
	CFLAG:C_ID:自宅位置 = CSVCFLAG (C_ID, GETNUM(CFLAG, "自宅位置"))
	CFLAG:C_ID:移動率補正 = CSVCFLAG (C_ID, GETNUM(CFLAG, "移動率補正"))
	CFLAG:C_ID:移動節度 = CSVCFLAG (C_ID, GETNUM(CFLAG, "移動節度"))
	CFLAG:C_ID:来訪抑制値 = CSVCFLAG(C_ID, GETNUM(CFLAG, "来訪抑制値"))
	CFLAG:C_ID:弾幕特能 = CSVCFLAG (C_ID, GETNUM(CFLAG, "弾幕特能"))

	SIF !CFLAG:C_ID:弾幕特能
		CALL 弾幕特殊能力設定(C_ID)
	CFLAG:C_ID:口調 = CSVCFLAG (C_ID, GETNUM(CFLAG, "口調"))
	;adjust maximum values for some stats if they are lower than base
	;最大気力体力がCSV値より低い場合CSVに合わせる
	IF !CFLAG:C_ID:回復速度ダウン
		SIF MAXBASE:C_ID:0 < CSVBASE (C_ID, 0)
			MAXBASE:C_ID:0 = CSVBASE (C_ID, 0)
		SIF MAXBASE:C_ID:1 < CSVBASE (C_ID, 1)
			MAXBASE:C_ID:1 = CSVBASE (C_ID, 1)
	ENDIF
	CFLAG:C_ID:産休タイプ = CSVCFLAG (C_ID, GETNUM(CFLAG, "産休タイプ"))
	;酒耐性
	MAXBASE:C_ID:酒気 = CSVBASE(C_ID, GETNUM(BASE, "酒気"))

	;update character basic skill EXP according to CSV abilities, unless set as skip above
	IF LOCAL:4
		FOR 更新能力番号,0,100
			最低能力値 = CSVABL(C_ID, 更新能力番号)
			SIF 最低能力値 == 0
				CONTINUE
			;戦闘能力
			;changed from refernce by number to reference by name. 戦闘能力 is combat ability
			IF ABLNAME:更新能力番号 == "戦闘能力"
				;戦闘能力修正
				SIF ABL:C_ID:更新能力番号 < 最低能力値 && EXP:C_ID:戦闘経験 == CSVEXP(C_ID, GETNUM(EXP, "戦闘経験"))
					ABL:C_ID:更新能力番号 = 最低能力値
				;new function that does the actual work
				CALL SET_MINIMUM_EXP_TR(C_ID, GETNUM(EXP, "戦闘経験"), 最低能力値)
				CONTINUE
			ENDIF

		SIF ABL:C_ID:更新能力番号 < 最低能力値
			ABL:C_ID:更新能力番号 = 最低能力値
		;they switched handling here to a switch
		SELECTCASE ABLNAME:更新能力番号
		CASE "清掃技能"
			CALL SET_MINIMUM_EXP_TR(C_ID, GETNUM(EXP, "清掃経験"), 最低能力値)
		CASE "話術技能"
			CALL SET_MINIMUM_EXP_TR(C_ID, GETNUM(EXP, "会話経験"), 最低能力値)
		CASE "教養"
			CALL SET_MINIMUM_EXP_TR(C_ID, GETNUM(EXP, "学習経験"), 最低能力値)
		CASE "料理技能"
			CALL SET_MINIMUM_EXP_TR(C_ID, GETNUM(EXP, "料理経験"), 最低能力値)
		CASE "音楽技能"
			SELECTCASE TALENT:C_ID:音楽知識
			CASE 1
				CALL SET_MINIMUM_EXP_TR(C_ID, GETNUM(EXP, "演奏経験"), 最低能力値)
			CASE 2
				CALL SET_MINIMUM_EXP_TR(C_ID, GETNUM(EXP, "歌唱経験"), 最低能力値)
			CASE 3
				CALL SET_MINIMUM_EXP_TR(C_ID, GETNUM(EXP, "舞踏経験"), 最低能力値)
			ENDSELECT
	;移行完了
;	CASE "釣り技能"
;		ABL:C_ID:更新能力番号 = MAX(ABL:C_ID:更新能力番号, CSVTALENT(C_ID, 47))
;		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "釣り経験"), 最低能力値)
;		TALENT:C_ID:釣りLv = MAX(TALENT:C_ID:釣りLv, ABL:C_ID:釣り技能)
;	CASE "採集技能"
;		ABL:C_ID:更新能力番号 = MAX(ABL:C_ID:更新能力番号, CSVTALENT(C_ID, 48))
;		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "採集経験"), 最低能力値)
;		TALENT:C_ID:採集Lv = MAX(TALENT:C_ID:採集Lv, ABL:C_ID:採集技能)
;	CASE "調合技能"
;		ABL:C_ID:更新能力番号 = MAX(ABL:C_ID:更新能力番号, CSVTALENT(C_ID, 49))
;		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "調合経験"), 最低能力値)
;		TALENT:C_ID:調合Lv = MAX(TALENT:C_ID:調合Lv, ABL:C_ID:調合技能)
		ENDSELECT
		NEXT
		PRINTL
	ENDIF
	;update all EXP according to CSV values, unless set to skip
	IF LOCAL:3
		FOR 更新能力番号,0,111
			SIF EXP:C_ID:更新能力番号 < CSVEXP (C_ID,更新能力番号)
				EXP:C_ID:更新能力番号 = (EXP:C_ID:更新能力番号 + CSVEXP (C_ID,更新能力番号))
		NEXT
	ENDIF
	;remove panty types that don't exist from the collection
	;パンツ種類非存在時消去
	FOR LOCAL:1, 2, MAXPANTS
		SIF CFLAG:C_ID:(LOCAL:1 + パンツ管理) && GET_STR(C_ID, "下半身下着_ずらし可能", LOCAL:1, "名前") == ""
			CFLAG:C_ID:(LOCAL:1 + パンツ管理) = 0
	NEXT
	;set child gender and date of birth if missing
	;子供の性別と生年月日
	IF EXP:C_ID:出産経験
		FOR LOCAL:1, 1, 11
			;生まれている子供の設定
			IF CHILD_BIRTHDAY:C_ID:(LOCAL:1)
				IF !CHILD_BIRTHDATE:C_ID:(LOCAL:1)
					;生年月日設定
					CHILD_BIRTHDATE:C_ID:(LOCAL:1) = DAY_TO_DATE(CHILD_BIRTHDAY:C_ID:(LOCAL:1))
					;天気は遡れないので晴れってことで
					CHILD_WEATHER:C_ID:(LOCAL:1) = たしか晴れ
					;エレンの子供は例外
					IF C_ID == [[エレン]]
						CHILD_SEX:C_ID:(LOCAL:1) = 0
					ELSE
						;初回
						IF !子供設定初回 && !FLAG:子供の性別
							子供設定初回 = 1
							PRINTL Child's sex is not set.
							PRINTL Make a choice:
							CALL ASK_M("Random", 1, "Female", 1, "Male", 1)
							SIF RESULT == 1 || RESULT == 2
								FLAG:子供の性別 = RESULT
						ENDIF
						IF !FLAG:子供の性別
							CHILD_SEX:C_ID:(LOCAL:1) = RAND:2 + 1
						ELSE
							CHILD_SEX:C_ID:(LOCAL:1) = FLAG:子供の性別
						ENDIF
						PRINTFORM %CHILDNAME:C_ID:(LOCAL:1), 16, LEFT% set as a \@ CHILD_SEX:C_ID:(LOCAL:1) == 1 ? girl # boy \@;  Date of birth:  
						CALL PRINT_DATE(CHILD_BIRTHDAY:C_ID:(LOCAL:1))
						PRINTFORML .
					ENDIF
				ENDIF
				;初期愛情度の設定
				IF !CHILD_LOVE:C_ID:(LOCAL:1)
					;無自覚妊娠情報は失われているので陥落無しと判定
					IF TALENT:C_ID:恋慕
						CHILD_LOVE:C_ID:(LOCAL:1) = 300
					ELSEIF TALENT:C_ID:思慕
						CHILD_LOVE:C_ID:(LOCAL:1) = 200
					ELSE
						CHILD_LOVE:C_ID:(LOCAL:1) = 100
					ENDIF
				ENDIF
				;自立先の設定
				SIF !CHILD_AREA:C_ID:(LOCAL:1)
					CALL 自立先選定(C_ID, LOCAL:1)
			ENDIF
		NEXT
	ENDIF
	;贈り物情報の修正
	IF !GIFT_GOT:C_ID:1
		CFLAG:C_ID:贈り物ストック = 0
		IF CFLAG:C_ID:貰った贈り物
			CFLAG:C_ID:貰った贈り物 -= (GET_GIFTDATA(CFLAG:C_ID:貰った贈り物, "回数") * 10000000000000000)
			CFLAG:C_ID:貰った贈り物 += 1 * 10000000000000000
			GIFT_GOT:C_ID:1 = CFLAG:C_ID:貰った贈り物
		ENDIF
	ENDIF
NEXT
;set masters home to the initial position
CFLAG:MASTER:神社在住 = CFLAG:MASTER:初期位置

;fix for the character that moved in with the New Shrine Dweller event at area 0
IF FLAG:住み込みキャラ && SUMIKOMI_ROOM
	CFLAG:(FLAG:住み込みキャラ):神社在住 = SUMIKOMI_ROOM
	CFLAG:(FLAG:住み込みキャラ):初期位置 = SUMIKOMI_ROOM
	CFLAG:(FLAG:住み込みキャラ):自宅位置 = SUMIKOMI_ROOM
ENDIF
;initiate the flag for sake production if not set
SIF !FLAG:酒虫 
	FLAG:酒虫 = 1


IF GETBIT(FLAG_DAILY_EV23, 0) && !FLAG_DAILY_EV23_販売員
	PRINTFORML The door-to-door salesman...
	CALL ASK_YN("Has her hair tied back behind her","Has short boyish hair")
	FLAG_DAILY_EV23_販売員 = RESULT + 1
ENDIF




;character specific adjustments
;a good portion of theese values are old and might not be conforming with the CSV values
;the name and callname things in particular break name translation
;a good part of this was changed in the latest version(cleaner and race setting was removed)

;bring all names from csv for them to get translated via trchara
FOR LOCAL, 1, CHARANUM
	SIF EXISTCSV(LOCAL) < 1 ;failsafe
		CONTINUE
	SIF CFLAG:LOCAL:出禁 == 2 ;skip randos
		CONTINUE
	NAME:LOCAL = %CSVNAME(LOCAL)%
	CALLNAME:LOCAL = %CSVCALLNAME(LOCAL)%
NEXT

;霊夢
NAME:[[霊夢]] = %CSVNAME(1)%
;博麗 霊夢

;る～こと

;カナ

;魅魔

;サニー

;ルナ

;スター

;ちゆり

;夢美

;萃香

;魔理沙

;ルーミア

;大ちゃん

;⑨

;メイド長

;おぜう

;アリス

;白百合

;黒百合

;リリカ

;メルラン

;ルナサ

;妖夢

;橙

;藍

;紫

;リグル

;ミスチー
;文

;映姫様

;早苗

;神奈子様

;諏訪子様

;てんこ

;衣玖さん

;おりんりん
;おくう


;こいし

;ナズ

;小傘

;ぬえ

;はたて

;華扇

;エレン

;理香子

;明羅

;里香

;ルイズ

;さとりん

;フラン

;にとり

;鈴仙
NAME:[[鈴仙]] = %CSVNAME(52)%
;鈴仙・優曇華院・イナバ
;てゐ
;パチュリー

;白蓮
NAME:[[白蓮]] = %CSVNAME(55)%
;聖 白蓮

;神子様

;こころ
;CFLAG:57:神社在住 = CFLAG:57:初期位置

;美鈴

;こぁ

;パルスィ

;妹紅

;姫様

;影狼
;勇儀

;椛
;ゆゆさま
;慧音
NAME:[[慧音]] = %CSVNAME(67)%
;上白沢 慧音

;幽香
;TALENT:68:105 = 1

;マミゾウ

;小鈴

;針妙丸
NAME:[[針妙丸]] = %CSVNAME(71)%
;少名 針妙丸
;CFLAG:71:神社在住 = CFLAG:71:初期位置
;TALENT:71:100 = -5

;永琳
NAME:[[永琳]] = %CSVNAME(72)%
;八意 永琳

;蛮奇

;レティ

;メディスン

;小町

;静葉

;穣子

;雛

;阿求

;蓮子

;メリー

;キスメ

;ヤマメ

;一輪

;水蜜

;星

;響子
;芳香

;青娥

;屠自古

;布都

;わかさぎ姫

;弁々

;八橋

;雷鼓

;正邪

;依姫
NAME:[[依姫]] = %CSVNAME(98)%


;豊姫
NAME:[[豊姫]] = %CSVNAME(99)%
;綿月 豊姫

;レイセン

;朱鷺子

;神綺

;夢子

;ユキ

;マイ

;菫子

;清蘭

;鈴瑚

;ドレミー

;サグメ

;クラウンピース
CALLNAME:[[クラウンピース]] = %CSVCALLNAME(111)%

;純狐

;ヘカーティア
CALLNAME:[[ヘカーティア]] = %CSVCALLNAME(113)%

;隠岐奈

;千亦
CALLNAME:[[千亦]] = %CSVCALLNAME(141)%

;give 55 virgin if no V exp
SIF !EXP:[[白蓮]]:Ｖ経験
	TALENT:[[白蓮]]:0 = 1
;move tracking of aquantance from cflag 13 to 18; old code
;初対面管理をFLAG:13からCFLAG:18に移行
IF FLAG:13 != 0
	FOR C_ID, 1, 63
		SIF GETBIT(FLAG:13, C_ID)
			CFLAG:C_ID:面識 = 1
	NEXT
	FLAG:13 = 0
ENDIF
;clothing flags? used to be old code but got a recent update
;衣裳フラグの修正
CFLAG:[[霊夢]]:基本服装セット = 101
CFLAG:[[お空]]:基本服装セット = 137
CFLAG:[[にとり]]:基本服装セット = 151
CFLAG:[[鈴仙]]:基本服装セット = 152
CFLAG:[[てゐ]]:基本服装セット = 153
CFLAG:[[パチュリー]]:基本服装セット = 154
CFLAG:[[白蓮]]:基本服装セット = 155
CFLAG:[[神子]]:基本服装セット = 156
CFLAG:[[こころ]]:基本服装セット = 157
CFLAG:[[針妙丸]]:基本服装セット = 171

;This section has been moved here from further up
;あなた関連処理

;本来つかない筈の不機嫌がMASTERについてる場合の削除処理
SIF CFLAG:MASTER:不機嫌 == 1
	CFLAG:MASTER:不機嫌 = 0
SIF CFLAG:MASTER:ブチギレ == 1
	CFLAG:MASTER:ブチギレ = 0
SIF TALENT:MASTER:機嫌 == -1
	TALENT:MASTER:機嫌 = 0

;set maxbase for player 酒気(Drunkness), without checking current value. Can this reduce it? code used to be further up
;yes it can, there's an event where this can be fortified (DAIRY_EV7 アルハラ.ERB)
SIF MAXBASE:MASTER:酒気 < 1500
	MAXBASE:MASTER:酒気 = 1500
;set futa bust size? Or something? used to be further up
;MASTERが女、ふたなり時
IF GETBIT(TALENT:MASTER:性別, 0)
	IF FLAG:周回数 && TALENT:MASTER:処女 == 1 && CSTR:MASTER:処女喪失履歴 != ""
		CSTR:MASTER:処女喪失履歴 = 
		CFLAG:MASTER:処女喪失記念日 = 0
		CFLAG:MASTER:処女喪失対象 = 0
	ENDIF
;MASTERが男
ELSE
	TALENT:MASTER:バストサイズ = -2
ENDIF
;if a character is set as male but doesn't have a size for their tool defined, do so now
;used to be in a loop over all characters, but I guess the assumption is that everyone but master is female?
SIF GETBIT(TALENT:MASTER:性別, 1) && !TALENT:MASTER:形状
	CALL TINKO_DECIDE(MASTER)

;adjustments to player body shape and something else; old code
;小槌の代償が終了せずにずっと粗末な物になっている場合の修正
;要注意大きくした後に二本にした場合、及び二本にした後に大きくした場合、粗末な物がデフォルトになる可能性があります
;その場合、自分で元の形状を再設定しなければなりません
SIF TALENT:MASTER:形状 != CFLAG:MASTER:382 && CFLAG:MASTER:魔力回収 < 0
	TALENT:MASTER:形状 = CFLAG:MASTER:382
SIF CFLAG:MASTER:魔力回収 > 0 && CFLAG:MASTER:小槌 == 0
	CFLAG:MASTER:魔力回収 = 0
;Newly added duplicate code(same as: ;fix that resets master body-type to default)
;IF TALENT:MASTER:体型 == -3
;	TALENT:MASTER:体型 = 0
;	PRINTFORML ---%CALLNAME:MASTER%の体型を再設定します---
;	PRINTL 
;ENDIF

;fixes to items;old code
;折り畳み傘
SIF ITEM:36 == 0 && ITEMSTOCK(36) == 2
	ITEMSALES:36 ++
;ローター
SIF ITEM:0 < 0
	ITEM:0 = 0
SIF ITEM:0 == 0 && ITEMSTOCK(0) == 2
	ITEM:0 = 1

;fix for confined character number; old-ish code
FLAG:出禁人数 = 0
FOR C_ID, 1, CHARANUM
	SIF TALENT:C_ID:行きずり
		CONTINUE
	SIF CFLAG:C_ID:出禁
		FLAG:出禁人数 ++
NEXT

;old code and not really a fix. Maybe FLAG:3 used to mean banquet enabled at value 0 and this is just a note that it has been inverteed?
SIF FLAG:3 == 0
	PRINTL Possibility to have a banquet is now DISABLED.

;check KOJO for updates and run them if available
;save target in local, new code
LOCAL = TARGET
FOR C_ID, 1, CHARANUM
	SIF CFLAG:C_ID:出禁 ;skip disabled characters
		CONTINUE
	RESULT = 0
	TARGET = C_ID
	CALL KOJO_MULTIPLE(C_ID)
	IF RESULT
		;新規口上追加時のリセット処理のスイッチ
		IF RESULT == 2
			口上実装チェック:C_ID = 1
			リセット画面 = 1
		ENDIF
		RESULTS =
		;口上の存在判定 and RESULTSに文字列代入
		IF CFLAG:C_ID:口上セレクタ
			TRYCALLFORM M_KOJO_K{C_ID}_{CFLAG:C_ID:口上セレクタ}
		ELSE
			TRYCALLFORM M_KOJO_K{C_ID}
		ENDIF
		PRINTFORML Trying to UPDATE dialogue for %CALLNAME:C_ID%%RESULTS%
		TRYCALLFORM M_KOJO%RESULTS%_UPDATE_K{C_ID}
		PRINTL 
	ENDIF
NEXT
TARGET = LOCAL

;apparetly flag:34 is supposed to be DAY + 1?;old code
;changed to 開始日 recently, so, might do something now?
SIF FLAG:開始日 < DAY
	FLAG:開始日 = DAY + 1

;reset some flags and local
VARSET FLAG, 0, 300, 500
VARSET LOCAL

;moves collection if it uses old storage version; old code
;パンツをCFLAGに移行
;SAVESTR:10に2文字以上あれば実行
IF STRLENS(SAVESTR:10) > 1
	FLAG:700 = 0
	FOR LOCAL,1,CHARANUM
		FOR LOCAL:1,0,MAXPANTS
			LOCALS = /%CALLNAME:LOCAL%{LOCAL:1}/
			CFLAG:LOCAL:(LOCAL:1 + パンツ管理) = STRCOUNT(SAVESTR:10,LOCALS)
			SIF CFLAG:LOCAL:(LOCAL:1 + パンツ管理)
				FLAG:700 ++
		NEXT
	NEXT
	SAVESTR:10 =
	PRINTL Your precious panty collection has been moved.
ENDIF

;initialize some peasant stuff if it hasn't been set;old code
;小作人あぷで
IF FLAG:地主 && !FLAG:農民難易度
	CALL MODE_NOUMIN
	CALL LOAN_NOUMIN
ENDIF

;set up the carisma market if it hasn't; old code
;紅魔カジノ用
SIF !FLAG:カリスマ変動状況
	CALL SET_CHARISMA("NEW")

;reset requests depending on user decision
PRINTFORML If the save data is old, the requests will probably not behave correctly.
PRINTFORML Reset all requests now?
CALL ASK_YN
SIF !RESULT
	CALL IRAI_RESET

;虫トラップ破棄
IF MUSHI_TRAP
	PRINTFORML Destroy all bug traps?
	CALL ASK_YN
	SIF !RESULT
		MUSHI_TRAP = 0
ENDIF


;old inventory adjustment fix code
;アイテム類の在庫管理
FOR LOCAL, 0, 1000
	CALL ITEMDATA(LOCAL, "SHOP:食料品店")
	RESULT:1 = RESULT
	CALL ITEMDATA(LOCAL, "SHOP:酒屋")
	RESULT:2 = RESULT
	CALL ITEMDATA(LOCAL, "SHOP:花屋")
	RESULT:3 = RESULT
	CALL ITEMDATA(LOCAL, "SHOP:八百屋")
	RESULT:4 = RESULT
	IF RESULT:1 && !INRANGE(ITEMSALES:LOCAL, 1, 30)
		ITEMSALES:LOCAL = LIMIT(ITEMSALES:LOCAL, 10, 30)
	ELSEIF RESULT:2 && !INRANGE(ITEMSALES:LOCAL, 1, 30)
		ITEMSALES:LOCAL = LIMIT(ITEMSALES:LOCAL, 5, 30)
	ELSEIF RESULT:3 && !INRANGE(ITEMSALES:LOCAL, 0, 3)
		ITEMSALES:LOCAL = 3
	ELSEIF RESULT:4 && !INRANGE(ITEMSALES:LOCAL, 0, 5)
		ITEMSALES:LOCAL = RAND:5
	ENDIF
	;ついでに所持数制限,換金はしません
	IF ITEM:LOCAL > 所持数制限 && !GROUPMATCH(ITEMNAME:LOCAL, "お気に入りアイテム")
		ITEM:LOCAL = 所持数制限
		PRINTFORMW Carrying too many %ITEMNAME_TR(LOCAL)%, so some were lost.
	ENDIF
	;ついでのついでに存在外アイテムの削除
	IF ITEMNAME:LOCAL == ""
		ITEM:LOCAL = 0
		ITEMSALES:LOCAL = 0
	ENDIF
NEXT
;fix that resets master body-type to default
IF TALENT:MASTER:体型 == -3
	TALENT:MASTER:体型 = 0
	PRINTFORML ---%CALLNAME:MASTER%'s figure/body type will be reset---
	PRINTL 
ENDIF
;adjusts banquest holding position if banquet is enabled
IF !FLAG:宴会開催フラグ
ELSEIF !FLAG:32
	;必要なのかわからんが念のため、です
	;暫定処理でとりあえず博麗神社境内を指定、主催者がズレたりしても気にするな
	FLAG:32 = 2
	PRINTFORML Banquet holding location has been moved
ELSEIF FLAG:32 == 230
	;バグ報告でのFLAG:32の内容はおそらくこっち
	FLAG:32 = 218
	PRINTFORML Banquet holding location has been moved
ENDIF

CALL SET_ENKAI_LOCATIONDISPLAY
SAVESTR:本体バージョン = %eraTW_Version%

;allows user to reset characters that have had their kojo updated
IF リセット画面
	PRINTFORML The following characters have had a new dialogue type, or their dialogue type have been altered.
	PRINTFORML Please select characters to reset their favorability, experience, abilities, etc.
	$PRINT_RESETCHARA
	FOR LOCAL,1,CHARANUM
		SIF !口上実装チェック:LOCAL
			CONTINUE
		PRINTFORMLC [{LOCAL}] %CALLNAME:LOCAL%
	NEXT
	PRINTFORML 
	DRAWLINE
	PRINTFORML [999] End
	INPUT
	SIF キャラクタ数上限 < RESULT
		GOTO UPDATE_END
	SIF !口上実装チェック:RESULT
		GOTO UPDATE_END
	IF 口上実装チェック:RESULT
		口上実装チェック:RESULT = 0
		CALL RESET_KOJO_CHARA_TR(RESULT)
		GOTO PRINT_RESETCHARA
	ENDIF
ENDIF

$UPDATE_END

PRINTFORML Recalculate the number of children, and how many nursing mothers there are.
CFLAG:MASTER:子供人数 = 0
CFLAG:MASTER:育児人数 = 0
FOR C_ID, 1, CHARANUM
	SIF TALENT:C_ID:行きずり
		CONTINUE
	CFLAG:MASTER:子供人数 += CFLAG:C_ID:子供人数
	CFLAG:MASTER:育児人数 += CFLAG:C_ID:育児人数
NEXT
PRINTFORML Children: {CFLAG:MASTER:子供人数} / Mothers: {CFLAG:MASTER:育児人数}

PRINTL 
;拠点マップの再設定
IF CFLAG:MASTER:現在位置 != OMANEKIBEYA() && !FLAG:気絶中断
	CALL MAP_DEFAULTRESIDENT(MAIN_MAP, 1)
	PRINTFORML Map positions have been reconfigured.
ELSE
	CALL COLORMESSAGE(@"※Could not reset map settings", C_YELLOW, 1)
	IF CFLAG:MASTER:現在位置 == OMANEKIBEYA()
		PRINTL Reason: Staying over
	ELSEIF FLAG:気絶中断
		PRINTL Reason: Fainted
	ENDIF
	FORCEWAIT
ENDIF


PRINTW Update is complete.

;old-ish function, never called?
;アプデでCSV読み込みによる更新されない素質
@TALENT_NOT_UPDATED_TR(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 15,28,29,35 TO 39, 42 TO 49, 58, 59, 63 TO 69, 72 TO 79, 87 TO 91, 95 TO 99, 107 TO 120, 122 TO 129
		RETURNF 1
	CASEELSE
		RETURNF 0
ENDSELECT

;function to reset experience string(s)
@履歴文字列変換_TR(ARG)
#DIM 日付
VARSET LOCALS
IF CFLAG:ARG:ファーストキス喪失記念日 && CSTR:ARG:ファーストキス喪失履歴 == ""
	日付 = CFLAG:ARG:ファーストキス喪失記念日
	LOCALS = %PRINT_DATE_F(日付)%、%CALLNAME:(CFLAG:ARG:ファーストキス喪失相手)%に
	SIF CSTR:ARG:ファーストキス位置 != ""
		LOCALS += @"%CSTR:ARG:ファーストキス位置%で"
	IF CFLAG:ARG:ファーストキス喪失対象 & (喪失_自分で | 喪失_恋慕 | 喪失_思慕)
		LOCALS += "ファーストキスを捧げた"
	ELSE
		SIF CFLAG:ARG:ファーストキス喪失対象 & 喪失_なし崩し
			LOCALS += "流されるままに"
		LOCALS += "ファーストキスを奪われた"
	ENDIF
	CSTR:ARG:ファーストキス喪失履歴 = %LOCALS%
	PRINTFORML %CALLNAME:ARG%'s first kiss history has been converted to a new format
ENDIF

IF CFLAG:ARG:アナル処女喪失記念日 && CSTR:ARG:アナル処女喪失履歴 == ""
	日付 = CFLAG:ARG:アナル処女喪失記念日
	LOCALS:1 = %PRINT_DATE_F(日付)%、\@ CFLAG:ARG:アナル処女喪失対象 & 喪失_時間停止? 時間停止中# \@
	SIF CSTR:ARG:Ａ処女喪失位置 != ""
		LOCALS:1 += @"%CSTR:ARG:Ａ処女喪失位置%で"
	IF GETBIT (CFLAG:ARG:アナル処女喪失対象,3)
		LOCALS:1 += @"%CALLNAME:(CFLAG:ARG:アナル処女喪失相手)%にペニスバンドで"
	ELSEIF GETBIT (CFLAG:ARG:アナル処女喪失対象,2)
		LOCALS:1 += @"%CALLNAME:(CFLAG:ARG:アナル処女喪失相手)%にアナルバイブで"
	ELSEIF GETBIT (CFLAG:ARG:アナル処女喪失対象,1)
		LOCALS:1 += @"%CALLNAME:(CFLAG:ARG:アナル処女喪失相手)%のペニスに"
	ENDIF
	IF CFLAG:ARG:アナル処女喪失対象 & 喪失_時間停止
		LOCALS:1 += "尻穴処女を奪われた"
	ELSE
		LOCALS:1 += "尻穴処女を捧げた"
	ENDIF
	CSTR:ARG:アナル処女喪失履歴 = %LOCALS:1%
	PRINTFORML %CALLNAME:ARG%'s anal virginity loss history has been converted to a new format
ENDIF

IF CFLAG:ARG:処女喪失記念日 && CSTR:ARG:処女喪失履歴 == ""
	日付 = CFLAG:ARG:処女喪失記念日
	LOCAL = CFLAG:ARG:処女喪失対象
	LOCALS:2 = %PRINT_DATE_F(日付)%、
	SIF LOCAL & 喪失_時間停止
		LOCALS:2 += "時間停止中に"
	SIF GETBIT (LOCAL,9)
		LOCALS:2 += "酔いつぶれているとき"
	SIF GETBIT (LOCAL,16)
		LOCALS:2 += "眠っている間に"
	SIF CSTR:ARG:処女喪失位置 != ""
		LOCALS:2 += @"%CSTR:ARG:処女喪失位置%で\@ LOCAL & 喪失_恋慕 ? 愛する# \@%CALLNAME:(CFLAG:ARG:処女喪失相手)%"
	IF GETBIT (LOCAL,3)
		LOCALS:2 += "にペニスバンドで"
	ELSEIF GETBIT (LOCAL,2)
		LOCALS:2 += "にバイブで"
	ELSEIF GETBIT (LOCAL,1)
		LOCALS:2 += "のペニスに"
	ENDIF
	SIF LOCAL & 喪失_なし崩し
		LOCALS:2 += "なし崩し的に"
	IF (GETBIT (LOCAL, 11) || GETBIT (LOCAL, 12)) && ((!GETBIT (LOCAL,9) && !GETBIT (LOCAL,13) && !GETBIT (LOCAL,16)) || GETBIT (LOCAL,14))
		LOCALS:2 += "処女を捧げた"
	ELSE
		SIF LOCAL & 喪失_無理矢理
			LOCALS:2 += "力ずくで"
		LOCALS:2 += @"処女を\@ARG == MASTER ? 与えた# 奪われた\@"
	ENDIF
	CSTR:ARG:処女喪失履歴 = %LOCALS:2%
	PRINTFORML %CALLNAME:ARG%'s virginity loss history has been converted to a new format
ENDIF

;reset character for KOJO
@RESET_KOJO_CHARA_TR(ARG)
CFLAG:ARG:好感度 = 0
CFLAG:ARG:信頼度 = 0
CFLAG:ARG:弱み握り = 0
CFLAG:ARG:弱み握られ = 0
CFLAG:ARG:既成事実 = 0
CFLAG:ARG:生解禁 = 0
CFLAG:ARG:面識 = 0
CFLAG:ARG:ダメ出し = 0
CFLAG:ARG:オナバレ = 0
CFLAG:ARG:眠姦発覚 = 0
CFLAG:ARG:睡眠姦 = 0
CFLAG:ARG:泥酔姦 = 0
CFLAG:ARG:バレンタイン = 0
CFLAG:ARG:最後に会った日 = 0
CFLAG:ARG:無自覚妊娠 = 0
CFLAG:ARG:妊娠による追加サイズ = 0
CFLAG:ARG:清い交際 = 0
CFLAG:ARG:出産経過日 = 0
CFLAG:ARG:育児人数 = 0
CFLAG:ARG:身に覚え_母 = 0
CFLAG:ARG:身に覚え_父 = 0
CFLAG:ARG:妊娠自覚状態 = 0
CFLAG:ARG:下戸飲み = 0
CFLAG:ARG:無理矢理膣射 = 0
CFLAG:ARG:にんっしんっ = 0
CFLAG:ARG:妊娠経過日数 = 0
CFLAG:ARG:子供人数 = 0
CFLAG:ARG:子宝補正 = 0
CFLAG:ARG:産休中 = 0
CFLAG:ARG:累計膣内精液 = 0
CFLAG:ARG:累計アナル精液 = 0
CFLAG:ARG:累計精飲 = 0
CFLAG:ARG:累計ぶっかけ = 0
CFLAG:ARG:ヤラせちゃった = 0
MARK:ARG:時姦刻印 = 0
MARK:ARG:反発刻印 = 0
MARK:ARG:反発取得履歴 = 0
MARK:ARG:苦痛刻印 = 0
MARK:ARG:快楽刻印 = 0
MARK:ARG:不埒刻印 = 0

;CSTR:ARG:浮気バレ =
;CSTR:ARG:浮気公認 =
CSTR:ARG:処女喪失履歴 =
CSTR:ARG:アナル処女喪失履歴 =
CSTR:ARG:ファーストキス喪失履歴 =
CSTR:ARG:童貞喪失履歴 =
;経験
EX:ARG:噴乳 = 0
EX:ARG:射精 = 0
EX:ARG:放尿 = 0
EX:ARG:射精量 = 0
EX:ARG:膣内精液 = 0
EX:ARG:アナル内精液 = 0
EX:ARG:子宮内精液 = 0

;reset FIRSTTIME and ONCE
CSTR:ARG:0 =
CSTR:ARG:1 =


;addition custom code, reset custom variable
FOR LOCAL, 0, 10
	TRYCALLFORM CUSTOM_RESET_VAR_{LOCAL}(ARG)
NEXT

MASTERNAME:ARG =

UWAKI_KNOWN:ARG:0 =
UWAKI_ACCEPTED:ARG:0 =

;陥落
TALENT:ARG:弱味 = 0
TALENT:ARG:恋慕 = 0
TALENT:ARG:思慕 = 0
TALENT:ARG:淫乱 = 0
TALENT:ARG:愛欲 = 0
TALENT:ARG:セフレ = 0
TALENT:ARG:妊娠 = 0
TALENT:ARG:育児中 = 0
TALENT:ARG:母性 = 0
TALENT:ARG:恋人 = 0
TALENT:ARG:初潮 = 0
TALENT:ARG:母乳体質 = 0
SIF TALENT:MASTER:恋人 == ARG
	TALENT:MASTER:恋人 = 0
;淫乱系
TALENT:ARG:マゾ = 0
TALENT:ARG:自慰狂い = 0
TALENT:ARG:淫壺 = 0
TALENT:ARG:尻穴狂い = 0
TALENT:ARG:淫乳 = 0
TALENT:ARG:キス魔 = 0
TALENT:ARG:ポルチオ性感 = 0

IF CFLAG:ARG:回復速度ダウン
	MAXBASE:ARG:体力 += 500
	MAXBASE:ARG:気力 += 500
	TALENT:ARG:回復速度 = CFLAG:ARG:回復速度ダウン - 2
	CFLAG:ARG:回復速度ダウン = 0
ENDIF
IF CFLAG:ARG:乳ランクアップ == 1 && TALENT:ARG:バストサイズ > -2
	TALENT:ARG:バストサイズ -= 1
	CFLAG:ARG:乳ランクアップ = 0
ENDIF
TALENT:ARG:痛覚 = CSVTALENT(ARG,40)
CFLAG:ARG:アナル処女喪失記念日 = 0
CFLAG:ARG:処女喪失記念日 = 0
CSTR:ARG:アナル処女喪失履歴 =
CSTR:ARG:処女喪失履歴 =
CSTR:ARG:ファーストキス喪失履歴 =

FOR LOCAL,0,100
	ABL:ARG:LOCAL = CSVABL(ARG,LOCAL)
	EXP:ARG:LOCAL = CSVEXP(ARG,LOCAL)
NEXT
VARSET JUEL:ARG:0, 0
VARSET CHILD_BIRTHDAY:ARG:0, 0
VARSET CHILD_WEATHER:ARG:0, ""
VARSET CHILD_SEX:ARG:0, 0
VARSET CHILDNAME:ARG:0, ""
CFLAG:ARG:デート後イベントフラグ = 0
CFLAG:ARG:童貞喪失記念日 = 0
;reset custom dialogue CFLAG:1000～1999
VARSET CFLAG:ARG:0, 0, 1000, 2000
SIF !EXP:ARG:キス経験
	TALENT:ARG:キス未経験 = 1
TALENT:ARG:0 = CSVTALENT(ARG,0)
PRINTFORMW %CALLNAME:ARG% has been reset.


;new function that re/sets the minimum exp to fit with ability levels
;CSV更新用
;非エロ系ABL、40～49において
;CSV準拠で能力が上昇した場合に最低限のEXPを取得し、表示する
@SET_MINIMUM_EXP_TR(C_ID, VAR_ID, MINIMUM_ABL)
#DIM C_ID
#DIM VAR_ID
#DIM MINIMUM_ABL
#DIM MINIMUM_EXP
MINIMUM_EXP = EXPLV:(MINIMUM_ABL + 2)
SIF EXP:C_ID:VAR_ID >= MINIMUM_EXP
	RETURN
PRINTFORM %CALLNAME:C_ID%'s %EXPNAME:VAR_ID% has risen from {EXP:C_ID:VAR_ID}
EXP:C_ID:VAR_ID = MINIMUM_EXP
PRINTFORML  to {EXP:C_ID:VAR_ID}.
;PRINTFORML %CALLNAME:C_ID%の%EXPNAME:VAR_ID%が{EXP:C_ID:VAR_ID}に上がりました

;-------------------------------------------------
;CSVを使わず弾幕特殊能力を個別に設定する関数
;新しくキャラクターを追加する際にCSVだと手間なので増設したものです
;▼memo
;0	【言笑自若】: 相手のダイス数Down
;	何があっても決して慌てず、落ち着いていることのたとえ。
;1	【温柔敦厚】: 相手のダイス面Down
;	優しく穏やかで、思いやりがあること。
;2	【勇気凛々】: %CALLNAME:OPPONENT%のダイス数Up
;	失敗や危険をかえりみず、勇敢に物事に立ち向かっていこうとするさま。
;3	【豪放磊落】: %CALLNAME:OPPONENT%のダイス面Up
;	心が広く大らかで、些細なことは気にしないこと。または、そのような様子。
;4	【怪力乱神】: %CALLNAME:OPPONENT%のダイス値が相手のダイス値より小さくても、その差が一定数以下なら、残機が減らない
;	人の知識では理解することが出来ない、怪しく奇怪な現象や物事のこと。
;5	【洽覧深識】: %CALLNAME:OPPONENT%の初期残機が4になる
;	体験して得た経験や知識が多くあり、様々なことを知っていること。
;6	【残忍酷薄】: 相手の被弾からの復帰時のボム数が-1される
;	人のことを気遣うこともなく、むごいこと。
;7	【乾坤一擲】: %CALLNAME:OPPONENT%が一定確率でボムを使うようになる
;	自身の運命を賭けて、運まかせの大博打をすること。
;8	【狷介孤高】: %CALLNAME:OPPONENT%のダイス修正値Up
;	意志を曲げずに、他人と協力しないこと。
;9	【幽愁暗恨】: 相手のダイス修正値Down
;	誰にも知られることのない深い恨みや憂いのこと。
;10  【百載無窮】: 全ての特殊能力をあわせ持つ。最強
;	いつまでも無くなることがないこと。
;-------------------------------------------------

;------------------------------------
;Trigger the dialogue reset after the dialogue has been translated for old saves (to see all of the content without starting a new game)
;Takes specified nFlag to keep track of the dialogue version in nVer (just in case if more changes are needed later), and custom character id in nChara
;------------------------------------
@UPDATE_DIALOGUERESET(nFlag = -1, nVer = -1, nChara = -1)
#DIM DYNAMIC nFlag
#DIM DYNAMIC nVer
#DIM DYNAMIC nChara
#DIM nInit

RESULT = 0
IF MIN(nFlag, nVer, nChara) < 0
	CALL COLORMESSAGE("UPDATE_DIALOGUERESET: Not all flags have been specified!",C_RED,2,1)
	CALL COLORMESSAGE(@"nFlag: {nFlag}, nVer: {nVer}, nChara: {nChara}",C_RED,2,1)
	RETURN 0
ENDIF

;apply latest version to dialogues when starting the game
; IF nInit
	; CFLAG:nChara:nFlag = nVer
	; RETURN 1
; ENDIF

DRAWLINE
IF CFLAG:nChara:nFlag < nVer
	IF gDialogueUpdateInit
		PRINTFORML New game translation set for %CALLNAME:nChara%...
		PRINTFORML Would you like to initialize %CALLNAME:nChara%'s dialogue? You may be able to customize %HIM_HER(nChara)% in some way.
		PRINTFORML Choosing this later will completely reset %HIM_HER(nChara)% however.
	ELSE
		PRINTFORML Since %CALLNAME:nChara%'s dialogue has been translated or otherwise updated, would you like to reset %HIM_HER(nChara)%?
		PRINTFORML (Choose if using old save without the translation or applied updates)
		CALL COLORMESSAGE(@"Warning: This choice will reset ALL of %HIS_HER(nChara)% stats and favor back to %HIS_HER(nChara)% csv values!",C_RED,2,1)
	ENDIF
	CALL ASK_YN
	LOCAL = RESULT
	IF LOCAL == 0	
		CALL RESET_KOJO_CHARA_TR(nChara)
		PRINTFORMW Success!
		RESULT = 1
	ELSE
		RESULT = 0
	ENDIF
ENDIF
CFLAG:nChara:nFlag = nVer
DRAWLINE
RETURN RESULT


@UPDATE_VERSIONBASED()
SIF SAVESTR:本体バージョン == eraTW_Version
	RETURN

IF GET_VERSION_VALUE(SAVESTR:本体バージョン) <= 4764
	PRINTL With Update 4.781 some items had their ids moved.
	PRINTL Moving the inventory accordingly to compensate
	;;the ids got lower, so we can move the items easily
	;;if we go over the limit that way the inventory cleanup code further down takes care of that
	;キュウリの苗
	ITEM:161 += ITEM:162
	;トマトの苗
	ITEM:162 += ITEM:163
	;watermelon seeds(スイカの苗) no longer exist but might as well move them to the follow-up item(タマネギの苗)
	ITEM:163 += ITEM:164
	
	;ナスの苗
	ITEM:171 += ITEM:175
	;サツマイモの苗 is gone イモの苗 is the new one
	ITEM:172 += ITEM:176
	;ブドウの苗 is gone カボチャの苗 is the new one
	ITEM:173 += ITEM:177
	
	;ユリの苗 is gone ハクサイの苗 is the new one
	ITEM:181 += ITEM:187
	;ニンジンの苗 is moved
	ITEM:182 += ITEM:188
	;ネギの苗 is moved
	ITEM:183 += ITEM:189
	
	;こやし is moved
	ITEM:191 += ITEM:195
	
	;キュウリ is moved
	ITEM:361 += ITEM:362
	;トマト is moved
	ITEM:362 += ITEM:363
	;スイカ => タマネギ
	ITEM:363 += ITEM:364
	
	;ナス moved
	ITEM:371 += ITEM:375
	;サツマイモ => イモ
	ITEM:372 += ITEM:376
	;ブドウ => カボチャ
	ITEM:373 += ITEM:377
	
	;ユリネ => ハクサイ
	ITEM:381 += ITEM:387
	;ニンジン moved
	ITEM:382 += ITEM:388
	;ネギ moved
	ITEM:383 += ITEM:389
ENDIF

@GET_VERSION_VALUE(バージョン文字列)
#FUNCTION
#DIMS バージョン文字列
;RESULT退避
LOCAL = RESULT
SPLIT バージョン文字列, ".", LOCALS
RESULT = LOCAL

RETURNF TOINT(LOCALS:0) * 1000 + TOINT(LOCALS:1)
