@QoL_TSP_INCREASE
#DIM DYNAMIC nChosen
#DIM nTalent = 49, 48, 47, 46
#DIM nLow = 20, 8, 5
#DIM nHigh = 30, 15, 10
#DIM nScore
#DIM nTab = 35
;abort if TSP wasn't maxed prior
SIF MAXBASE:MASTER:TSP < 4000
	RETURN 0
PRINTFORML Would you like to sacrifice one of your maxed skills (Lv5+) in exchange for a permanent (but slight) boost to your TSP?
PRINTFORML (You will get to choose which skills to sacrifice for a combined bonus.)
CALL ASK_YN
IF !RESULT
	PRINTFORML Current TSP: {MAXBASE:MASTER:TSP}
	DRAWLINE
	gLineCount = LINECOUNT
	REDRAW 0
	DO
		SIF LINECOUNT - gLineCount > 0
			CLEARLINE LINECOUNT - gLineCount
		nScore = 0
		;abl skills
		FOR LOCAL,0, 6
			SIF ABLNAME:(LOCAL+40) == "" || ABL:MASTER:(LOCAL+40) < 5
				CONTINUE
				
			PRINTFORM [{LOCAL,2}]%ABLNAME:(LOCAL+40), nTab, LEFT%Lv{ABL:MASTER:(LOCAL+40)}　
			IF GETBIT(nChosen, LOCAL)
				LOCAL:1 = ABL:MASTER:(LOCAL+40) > 5 ? nHigh:0 # nLow:0
				CALL COLORMESSAGE(@"Sacrifice: +{LOCAL:1}TSP",C_RED,0,0)
				nScore += LOCAL:1
			ENDIF
			PRINTL
		NEXT
		;talent-based skills
		FOR LOCAL, 0, 4
			SIF TALENT:MASTER:(nTalent:LOCAL) < 5
				CONTINUE
				
			PRINTFORM [{LOCAL+6,2}]%TALENTNAME:(nTalent:LOCAL), nTab, LEFT%Lv{TALENT:MASTER:(nTalent:LOCAL)}　
			IF GETBIT(nChosen, LOCAL+6)
				LOCAL:1 = TALENT:MASTER:(nTalent:LOCAL) > 5 ? nHigh:0 # nLow:0
				CALL COLORMESSAGE(@"Sacrifice: +{LOCAL:1}TSP",C_RED,0,0)
				nScore += LOCAL:1
			ENDIF
			PRINTL
		NEXT
		;sex skills
		FOR LOCAL,0, 3
			SIF ABLNAME:(LOCAL+50) == "" || ABL:MASTER:(LOCAL+50) < 5
				CONTINUE
			PRINTFORM [{LOCAL+10,2}]%ABLNAME:(LOCAL+50), nTab, LEFT%Lv{ABL:MASTER:(LOCAL+50)}　
			IF GETBIT(nChosen, LOCAL+10)
				LOCAL:1 = ABL:MASTER:(LOCAL+50) > 5 ? nHigh:1 # nLow:1
				CALL COLORMESSAGE(@"Sacrifice: +{LOCAL:1}TSP",C_RED,0,0)
				nScore += LOCAL:1
			ENDIF
			PRINTL
		NEXT
		;these are all tied together so I have to reset them all
		IF ABL:MASTER:腰 >= 5 || ABL:MASTER:膣 >= 5 || ABL:MASTER:アナル >= 5
			PRINTFORM [13]Sacrifice Sexing Skills (V/A/Waist)　
			IF GETBIT(nChosen, 13)
				LOCAL:1 = 0
				SIF ABL:MASTER:腰 >= 5
					LOCAL:1 += ABL:MASTER:腰 > 5 ? nHigh:2 # nLow:2
				SIF ABL:MASTER:膣 >= 5
					LOCAL:1 += ABL:MASTER:膣 > 5 ? nHigh:2 # nLow:2
				SIF ABL:MASTER:アナル >= 5
					LOCAL:1 += ABL:MASTER:アナル > 5 ? nHigh:2 # nLow:2
				
				CALL COLORMESSAGE(@"Sacrifice: +{LOCAL:1}TSP",C_RED,0,0)
				nScore += LOCAL:1
			ENDIF
			PRINTL
		ENDIF
		DRAWLINE
		PRINTFORML \@ nScore ? Total Gains: {nScore} # \@
		PRINTFORML
		PRINTFORML [ 99] \@ nChosen ? Deselect # Select \@ All
		PRINTFORML [100] Confirm
		INPUT
		
		SELECTCASE RESULT
			CASE 100
				LOCAL:1 = 0
				FOR LOCAL, 0, 6
					SIF GETBIT(nChosen, LOCAL) > 0 && ABL:MASTER:(LOCAL+40) >= 5
						LOCAL:1 += ABL:MASTER:(LOCAL+40) > 5 ? nHigh:0 # nLow:0
				NEXT
				FOR LOCAL, 0, 4
					SIF GETBIT(nChosen, LOCAL+6) && TALENT:MASTER:(nTalent:LOCAL) >= 5
						LOCAL:1 += TALENT:MASTER:(nTalent:LOCAL) > 5 ? nHigh:0 # nLow:0
				NEXT
				FOR LOCAL, 0, 3
					SIF GETBIT(nChosen, LOCAL+10) > 0 && ABL:MASTER:(LOCAL+50) >= 5
						LOCAL:1 += ABL:MASTER:(LOCAL+50) > 5 ? nHigh:1 # nLow:1
				NEXT
				
				IF GETBIT(nChosen, 13) && (ABL:MASTER:腰 >= 5 || ABL:MASTER:膣 >= 5 || ABL:MASTER:アナル >= 5)
					SIF ABL:MASTER:腰 >= 5
						LOCAL:1 += ABL:MASTER:腰 > 5 ? nHigh:2 # nLow:2
					SIF ABL:MASTER:膣 >= 5
						LOCAL:1 += ABL:MASTER:膣 > 5 ? nHigh:2 # nLow:2
					SIF ABL:MASTER:アナル >= 5
						LOCAL:1 += ABL:MASTER:アナル > 5 ? nHigh:2 # nLow:2
				ENDIF
				IF LOCAL:1 <= 0
					CALL COLORMESSAGE(@"None sacrificed.",C_GREEN,2,1)
					RETURN
				ENDIF
					
				CALL COLORMESSAGE(@"Max TSP has been increased by {LOCAL:1}!",C_RED,2,1)
				MAXBASE:MASTER:TSP += LOCAL:1
				BASE:MASTER:TSP = MAXBASE:MASTER:TSP
				
				IF GETBIT(nChosen, 0)
					ABL:MASTER:清掃技能 = 0
					EXP:MASTER:清掃経験 = 0
					EXP:MASTER:異常清掃経験 = 0
				ENDIF
				IF GETBIT(nChosen, 1)
					ABL:MASTER:話術技能 = 0
					EXP:MASTER:会話経験 = 0
				ENDIF
				IF GETBIT(nChosen, 2)
					ABL:MASTER:戦闘能力 = 0
					EXP:MASTER:戦闘経験 = 0
				ENDIF
				IF GETBIT(nChosen, 3)
					ABL:MASTER:教養 = 0
					EXP:MASTER:学習経験 = 0
				ENDIF
				IF GETBIT(nChosen, 4)
					ABL:MASTER:料理技能 = 0
					EXP:MASTER:料理経験 = 0
				ENDIF
				IF GETBIT(nChosen, 5)
					ABL:MASTER:音楽技能 = 0
					EXP:MASTER:演奏経験 = 0
					EXP:MASTER:歌唱経験 = 0
					EXP:MASTER:舞踏経験 = 0
				ENDIF
				IF GETBIT(nChosen, 6)
					TALENT:MASTER:釣りLv = 0
					EXP:MASTER:釣り経験 = 0
				ENDIF
				IF GETBIT(nChosen, 7)
					TALENT:MASTER:採集Lv = 0
					EXP:MASTER:採集経験 = 0
				ENDIF
				IF GETBIT(nChosen, 8)
					TALENT:MASTER:調合Lv = 0
					EXP:MASTER:調合経験 = 0
				ENDIF
				IF GETBIT(nChosen, 9)
					TALENT:MASTER:伐採Lv = 0
					EXP:MASTER:伐採経験 = 0
				ENDIF
				
				IF GETBIT(nChosen, 10)
					ABL:MASTER:指 = 0
					EXP:MASTER:手淫経験 = 0
				ENDIF
				IF GETBIT(nChosen, 11)
					ABL:MASTER:舌 = 0
					EXP:MASTER:口淫経験 = 0
				ENDIF
				IF GETBIT(nChosen, 12)
					ABL:MASTER:胸 = 0
					EXP:MASTER:パイズリ経験 = 0
					EXP:MASTER:Ｂ経験 = 0
					EXP:MASTER:噴乳経験 = 0
				ENDIF
				IF GETBIT(nChosen, 13)
					ABL:MASTER:腰 = 0
					EXP:MASTER:Ｖ性交経験 = 0
					EXP:MASTER:Ａ性交経験 = 0
					EXP:MASTER:挿入経験 = 0
					EXP:MASTER:Ｖ経験 = 0
					EXP:MASTER:Ｖ拡張経験 = 0
					EXP:MASTER:Ａ経験 = 0
					EXP:MASTER:Ａ拡張経験 = 0
				ENDIF
				BREAK
			CASE 0 TO 5
				SIF ABLNAME:(RESULT+40) == "" || ABL:MASTER:(RESULT+40) < 5
					CONTINUE
				INVERTBIT nChosen, RESULT
			CASE 6 TO 9
				SIF TALENT:MASTER:(nTalent:(RESULT-6)) < 5
					CONTINUE
				INVERTBIT nChosen, RESULT
			CASE 10 TO 12
				SIF ABLNAME:(RESULT+40) == "" || ABL:MASTER:(RESULT+40) < 5
					CONTINUE
				INVERTBIT nChosen, RESULT
			CASE 13
				SIF !(ABL:MASTER:腰 >= 5 || ABL:MASTER:膣 >= 5 || ABL:MASTER:アナル >= 5)
					CONTINUE
				INVERTBIT nChosen, 13
			CASE 99 ;what a clusterfuck
				IF nChosen
					nChosen = 0
					CONTINUE
				ENDIF
				
				FOR LOCAL, 0, 14
					RESULT = LOCAL
					SELECTCASE LOCAL
						CASE 0 TO 5
							SIF ABLNAME:(RESULT+40) == "" || ABL:MASTER:(RESULT+40) < 5
								CONTINUE
							INVERTBIT nChosen, RESULT
						CASE 6 TO 9
							SIF TALENT:MASTER:(nTalent:(RESULT-6)) < 5
								CONTINUE
							INVERTBIT nChosen, RESULT
						CASE 10 TO 12
							SIF ABLNAME:(RESULT+40) == "" || ABL:MASTER:(RESULT+40) < 5
								CONTINUE
							INVERTBIT nChosen, RESULT
						CASE 13
							SIF !(ABL:MASTER:腰 >= 5 || ABL:MASTER:膣 >= 5 || ABL:MASTER:アナル >= 5)
								CONTINUE
							INVERTBIT nChosen, 13
					ENDSELECT
				NEXT
		ENDSELECT
	LOOP 1
ENDIF
