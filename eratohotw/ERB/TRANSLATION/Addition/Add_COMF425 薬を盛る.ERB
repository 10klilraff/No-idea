;-------------------------------------------------
;薬を盛る
;日常系コマンド、レベル1
;-------------------------------------------------
@COM425_TR
#DIM nItem = 41, 42, 250, 252
IF !TFLAG:131 && (DISH_NAME != "" && !(DISH_DRG + DISH_DRG_2 + DISH_LEWD)) ;if both available
	PRINTL What do you want to spike?
	CALL ASK_YN("Tea", "Food")
	IF RESULT
		CALL COM425_Additives
		RETURN RESULT
	ENDIF
ELSEIF TFLAG:131 ;only food
	CALL COM425_Additives
	RETURN RESULT
ENDIF
PRINTL Select something to spike the tea with.
;custom code
{
	CALL ASK_M(
	ITEMNAME_TR(41) + @"({ITEM:41})",ITEM:41,
	ITEMNAME_TR(42) + @"({ITEM:42})",ITEM:42,
	ITEMNAME_TR(250) + @"({ITEM:250})",ITEM:250,
	ITEMNAME_TR(252) + @"({ITEM:252})",ITEM:252,
	"Cancel", 1
	)
}

SELECTCASE RESULT
	CASE 0, 1, 2, 3
		IF ITEM:(nItem:RESULT)
			TFLAG:131 = (nItem:RESULT)
			PRINTFORML %PARSE("You get")% tea mixed with %ITEMNAME_TR((nItem:RESULT))%.
			ITEM:(nItem:RESULT) -= 1
			IF FLAG:70
				BASE:MASTER:TSP -= 30
			ELSE
				TIME += 10
			ENDIF
			RETURN 1
		ENDIF
CASEELSE
	RETURN 0
ENDSELECT

;mix medicine into an already prepared food
@COM425_Additives
#DIMS strDrugs, 6
#DIM SELECT_DRG
#DIM SELECT_DRG_2
#DIM SELECT_LEWD
#DIM nLineCnt_Head

strDrugs '= "None", "媚薬", "利尿剤", "惚れ薬", "睡眠薬", TALENT:MASTER:禁断の知識 ? "排卵誘発剤" # ""

nLineCnt_Head = LINECOUNT
REDRAW 0
DO
	SIF LINECOUNT - nLineCnt_Head > 0
		CLEARLINE LINECOUNT - nLineCnt_Head + 1
	
	PRINTFORML Currently carrying:
	;custom code, show description again
	LOCALS = 【%DISHNAME_TR(DISH_NAME)%】%DISHCOMMENT_TR(FLAG:料理)%
	LOCAL = LINECOUNT
	PRINTFORML %BREAKENG(LOCALS, 0, "\n" + BL(STRLENS(@"【%DISHNAME_TR(DISH_NAME)%】")))%
	PRINT 　
	CALL SHOW_TASTE(DISH_TASTE, "DISH_DATA")
	DRAWLINE

	IF !DISH_DRG
		PRINTPLAIN Preparation: 
		FOR LOCAL, 0, VARSIZE("strDrugs")
			SIF !STRLENS(strDrugs:LOCAL)
				CONTINUE
			IF LOCAL == 5
				PRINTL 
				PRINTPLAIN 　　　　　　 
			ENDIF
			IF LOCAL == 0
				SIF !SELECT_DRG
					SETCOLOR C_AQUA
				PRINTFORM [{LOCAL+21, 2}]%DISHTYPE_TR(strDrugs:LOCAL), 24, LEFT%
			ELSE
				IF ITEMNAME:SELECT_DRG == strDrugs:LOCAL
					SETCOLOR C_AQUA
				ELSEIF ITEM:GETNUM(ITEM, strDrugs:LOCAL) <= 0
					SETCOLOR C_GRAY
				ENDIF
				LOCALS = [{LOCAL+21, 2}]%DISHTYPE_TR(strDrugs:LOCAL)%({ITEM:GETNUM(ITEM, strDrugs:LOCAL), 2})
				PRINTFORM %LOCALS, 28, LEFT%
			ENDIF
			RESETCOLOR
		NEXT
		PRINTL 
	ENDIF
	IF !DISH_DRG_2
		PRINTPLAIN   Additives: 
		FOR LOCAL, 31, 33
			SELECTCASE LOCAL
			CASE 31
				LOCALS = なし
			CASE 32
				LOCALS = 栄養強化剤
			ENDSELECT
			IF LOCAL == 31
				SIF !SELECT_DRG_2
					SETCOLOR C_AQUA
				PRINTFORM [{LOCAL, 2}]%DISHTYPE_TR(LOCALS), 24, LEFT%
			ELSE
				IF ITEMNAME:SELECT_DRG_2 == LOCALS
					SETCOLOR C_AQUA
				ELSEIF ITEM:GETNUM(ITEM, LOCALS) <= 0
					SETCOLOR C_GRAY
				ENDIF
				LOCALS = [{LOCAL, 2}]%DISHTYPE_TR(LOCALS)%({ITEM:GETNUM(ITEM, LOCALS), 2})
				PRINTFORM %LOCALS, 28, LEFT%
			ENDIF
			RESETCOLOR
		NEXT
		PRINTL 
	ENDIF
	IF !DISH_LEWD
		CALL ADD_LewdFoodAdd(SELECT_LEWD)
		PRINTL 
	ENDIF
	DRAWLINE
	IF SELECT_DRG || SELECT_DRG_2 || SELECT_LEWD
		PRINTFORML [999] Confirm
	ELSE
		PRINTFORML
	ENDIF
	PRINTFORML
	PRINTFORML [ -1] Cancel
	INPUT
	;drugs
	IF INRANGE(RESULT, 21, 30) && !DISH_DRG
		RESULT -= 21
		IF RESULT == 0
			SELECT_DRG = 0
		ELSEIF STRLENS(strDrugs:RESULT) && ITEM:GETNUM(ITEM, strDrugs:RESULT) > 0
			SIF strDrugs:RESULT == "排卵誘発剤" && !TALENT:MASTER:禁断の知識 ;failsafe
				CONTINUE
			SELECT_DRG = GETNUM(ITEM, strDrugs:RESULT)
		ENDIF
	;additives
	ELSEIF INRANGE(RESULT, 31, 40) && !DISH_DRG_2
		IF RESULT == 31
			SELECT_DRG_2 = 0
		ELSEIF RESULT == 32 && ITEM:GETNUM(ITEM, "栄養強化剤") > 0
			SELECT_DRG_2 = GETNUM(ITEM, "栄養強化剤")
		ENDIF
	;lewd
	ELSEIF INRANGE(RESULT, 91, 99) && !DISH_LEWD
		CALL ADD_LewdFoodAdd_Process(SELECT_LEWD, RESULT)
	ELSEIF RESULT == 999 && (SELECT_DRG || SELECT_DRG_2 || SELECT_LEWD)
		BREAK
	ELSEIF RESULT == -1
		RETURN 0
	ENDIF
LOOP 1

LOCAL = DISH_BASEPOINT
DISH_DRG = SELECT_DRG
DISH_DRG_2 = SELECT_DRG_2
IF SELECT_DRG
	DISH_BASEPOINT -= 20
	ITEM:SELECT_DRG --
ENDIF
IF SELECT_DRG_2
	DISH_BASEPOINT -= 20
	ITEM:SELECT_DRG_2 --
ENDIF
CALL ADD_LewdFoodAdd_Process2(SELECT_LEWD)

PRINTFORML %DISHNAME_TR(DISH_NAME)% will now have extra properties: 
SIF DISH_DRG
	PRINTFORM (%ITEMNAME_TR(DISH_DRG)%)
SIF DISH_DRG_2
	PRINTFORM (%ITEMNAME_TR(DISH_DRG_2)%)
SIF DISH_LEWD
	PRINTFORM %ADD_LewdFoodAdd_Show()%
PRINTL
PRINTFORM Dish Points Change: 
CALL 出来栄え(LOCAL)
PRINTFORM  -> 
CALL 出来栄え(DISH_BASEPOINT)
PRINTFORML (Spiked)
RETURN 1

@Add_Food_MixOvulDrug(SELECT_DRG)
#DIM SELECT_DRG
SIF !TALENT:MASTER:禁断の知識
	RETURN 0
;allow mixing in ovulation drugs with forbidden knowledge
FOR LOCAL, 21, 27
	SELECTCASE LOCAL
	CASE 21
		LOCALS = なし
	CASE 22
		LOCALS = 媚薬
	CASE 23
		LOCALS = 利尿剤
	CASE 24
		LOCALS = 惚れ薬
	CASE 25
		LOCALS = 睡眠薬
	CASE 26
		LOCALS = 排卵誘発剤
	ENDSELECT
	IF LOCAL == 26
		PRINTL 
		PRINTPLAIN 　　　　　　 
	ENDIF
	IF LOCAL == 21
		SIF !SELECT_DRG
			SETCOLOR C_AQUA
		PRINTFORM [{LOCAL, 2}]%DISHTYPE_TR(LOCALS), 24, LEFT%
	ELSE
		IF ITEMNAME:SELECT_DRG == LOCALS
			SETCOLOR C_AQUA
		ELSEIF ITEM:GETNUM(ITEM, LOCALS) <= 0
			SETCOLOR C_GRAY
		ENDIF
		LOCALS = [{LOCAL, 2}]%DISHTYPE_TR(LOCALS)%({ITEM:GETNUM(ITEM, LOCALS), 2})
		PRINTFORM %LOCALS, 28, LEFT%
	ENDIF
	RESETCOLOR
NEXT
RETURN 1
