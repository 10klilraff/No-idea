;tsp slip out
@TSP_SLIP_OUT(ARG)
IF MAXBASE:MASTER:TSP >= 500 && BASE:MASTER:TSP >= 500
	PRINTFORML Use TSP to slip out?
	CALL ASK_YN
	IF !RESULT
		PRINTFORML %PARSE("You use your")% power to accelerate time for %CALLNAME:ARG%, exhausting them.
		BASE:MASTER:TSP -= 500
		BASE:ARG:気力 = 200
		RETURN 1
	ENDIF
ENDIF

;allow for the purchase of 4 additional fields (original code ny11)
@ADD_BuyMoreFields
PRINTFORML With this, %PARSE("you")% can plant an additional field in a garden.
PRINTFORML It will cost \\{3000 * (1 + FLAG:畑拡張)}.
CALL ASK_YN("Purchase","Nevermind")
IF !RESULT
	IF MONEY >= (3000 * (1 + FLAG:畑拡張))
		MONEY -= (3000 * (1 + FLAG:畑拡張))
		FLAG:畑拡張 ++
		PRINTFORMW %PARSE("You")% can now plant an additional field for a total of {1 + FLAG:畑拡張}.
	ELSE
		PRINTFORMW %PARSE("You don't")% have enough money.
	ENDIF
ENDIF

;Custom code, lets you avoid your food from getting _found_ in the first place, thus staying compatible with KOJO (original code Zergrush)
@Add_PreventFoodTheft
IF ABL:TARGET:戦闘能力 <= ABL:MASTER:戦闘能力
	PRINTFORMW \@THIRD_PERSON?%CALLNAME:MASTER%#You\@ suddenly feel%THIRD_PERSON?"s"#""% like \@THIRD_PERSON?%HIS_HER(MASTER)%#your\@ %TOLOWER(EX_DISH_NAME)% is in danger...
	LOCAL:50 = ABL:TARGET:戦闘能力 >= ABL:MASTER:戦闘能力 ? 100 # 50
	IF BASE:MASTER:TSP > LOCAL:50
		PRINTFORMW Stop time and hide it?
		CALL ASK_YN()
		IF RESULT == 0
			PRINTFORMW \@THIRD_PERSON?%CALLNAME:MASTER%#You\@ stop%THIRD_PERSON?"s"#""% time, hide%THIRD_PERSON?"s"#""% \@THIRD_PERSON?%HIS_HER(MASTER)%#your\@ %TOLOWER(EX_DISH_NAME)%, wait%THIRD_PERSON?"s"#""% until the danger has passed and then retrieve%THIRD_PERSON?"s"#""% it.
			LOCAL:51 = 3
			CALL RECOVER(MASTER,-LOCAL:50,"TSP")
			RETURN 1
		ENDIF
	ELSE
		PRINTFORMW But \@THIRD_PERSON?%HE_SHE(MASTER)% doesn't#you don't\@ have enough time to hide it!
	ENDIF
ENDIF

;Custom code, each time the theft is blocked because you stopped time earlier, the check is reduced
;unused?
	; ELSEIF LOCAL:51 != 0
		; LOCAL:51--

;-------------------------------------------------
;Mystia food cart, added sparrow sake
;-------------------------------------------------
@ADD_MystiaFoodCart
#DIM 同行人数カウント
#DIM 飲酒可能人数
#DIM ATSUKAN
#DIM OOMORI
#DIM KAIKEI
#DIM KAKAKU,3
#DIM PREV_DAY

同行人数カウント = 1
飲酒可能人数 = 0
ATSUKAN = 0
KAIKEI = 0
OOMORI = 0
VARSET KAKAKU
SIF CAN_DRINK(MASTER)
	飲酒可能人数 = 1
FOR LOCAL,1,CHARANUM
	SIF !CFLAG:LOCAL:同行
		CONTINUE
	 SIF !TIME_PROGRESS(TCVAR:LOCAL:空腹時刻)
	 	CONTINUE
	同行人数カウント ++
	SIF CAN_DRINK(LOCAL)
		飲酒可能人数 ++
NEXT
CALL SPTALK, [[ミスティア]], "笑顔", 0, @"「Welcome! {同行人数カウント} \@同行人数カウント>1?people#person\@?」", 1
KAKAKU:0 = 3 * 同行人数カウント
KAKAKU:1 = 5 * 同行人数カウント
PRINTFORML Charisma: %DIGIT_GROUP(MONEY:2)%
CALL ASK_M("Assorted Skewers　(One portion 3CM)",MONEY:2 >= KAKAKU:0 && TIME_PROGRESS(TCVAR:MASTER:空腹時刻),"Assorted Skewers, Big　(One portion 5CM)", MONEY:2 >= KAKAKU:1 && TIME_PROGRESS(TCVAR:MASTER:空腹時刻),"Take-out　(5CM)",MONEY:2 >= 5 && !ITEM:ヤツメ串, "Sake", CAN_DRINK(MASTER), "Return",1)
IF RESULT == 4
	CALL SPTALK, [[ミスティア]], "笑顔", 0, @"「Come again!」", 1
	RETURN -1
ENDIF
IF RESULT == 2
	ITEM:ヤツメ串 ++
	MONEY:2 -= 5
	CALL SPTALK, [[ミスティア]], "笑顔", 0, @"「Please eat it before it gets cold!」", 1
	SOURCE:歓楽 += 200
	TFLAG:98 = 1
	TIME += 5
	RETURN 1
ENDIF
IF RESULT == 3
	IF PREV_DAY == DAY
		CALL SPTALK, [[ミスティア]], "通常", 0, @"「Unfortunately, I'm all out of special sake for today. Come see me next time if you want more!」", 1
		RETURN -1
	ENDIF
	
	CALL SPTALK, [[ミスティア]], "笑顔", 0, @"「Fufu～n, you want some sake? ...Right now, I've got some really good one.」/「This here is called sparrow sake, the legendary sake made by sparrows～」/「It's a homage to our sparrow ancestors～ Do you want some?」", 1
	
	KAKAKU:2 = 100 * 同行人数カウント
	CALL ASK_M("Have it　(100CM per person)",MONEY:2 >= KAKAKU:2, "Too expensive...", 1)
	IF RESULT == 0
		PREV_DAY = DAY
		RESULT = 2
		ATSUKAN = 2
		PRINTFORMDL You spend a while drinking this special sake...
		PRINTFORMDL You feel rejuvenated and uplifted, to the point you can hardly contain the urge to start dancing wildly!
	ELSE
		CALL SPTALK, [[ミスティア]], "通常", 0, @"「Well, it takes time and effort to make one after all...」", 1
		WAIT
		CALL SPTALK, [[ミスティア]], "笑顔", 0, @"「But please make sure to enjoy our normal assortment of goods, I always keep them affordable～!」", 1
		RETURN -1
	ENDIF
ENDIF

MONEY:2 -= KAKAKU:RESULT
KAIKEI = KAKAKU:RESULT

SIF RESULT == 1
	OOMORI = 1

IF MONEY:2 >= 2 * 飲酒可能人数 && RESULT != 2
	CALL SPTALK, [[ミスティア]], "通常", 0, @"「Would you also like some warm sake at 2 Charisma per person?」", 1
	PRINTFORML (Increases ENE and DRUNK)
	CALL ASK_YN
	IF !RESULT
		ATSUKAN = 1
		KAIKEI += 2 * 飲酒可能人数
		MONEY:2 -= 2 * 飲酒可能人数
	ENDIF
ENDIF
;信頼
TFLAG:98 += 3
;同行中全員で回す

FOR LOCAL,0,CHARANUM
	SIF !CFLAG:LOCAL:同行 && LOCAL
		CONTINUE
	 SIF !TIME_PROGRESS(TCVAR:LOCAL:空腹時刻)
	 	CONTINUE
	CALL ADD_EAT_YATSUME(LOCAL,OOMORI,ATSUKAN)
NEXT

SOURCE:歓楽 += KAIKEI * 100
IF ATSUKAN == 2
	PRINTFORMDL You realize you spent a total of {KAIKEI} Charisma... This better be worth it.
	CALL SPTALK, [[ミスティア]], "笑顔", 0, @"「Hehe～, come again!」", 1
ELSE
	CALL SPTALK, [[ミスティア]], "笑顔", 0, @"「That will be a total of {KAIKEI} Charisma.」/「Please come again!」", 1
ENDIF
;time passage is already set in EAT_YATSUME 
;TIME += 30

@ADD_EAT_YATSUME(対象,ARG,ARG:1)
#DIM 回復前体力
#DIM 回復前気力
#DIM 回復前精力
#DIM 回復後体力
#DIM 回復後気力
#DIM 回復後精力
#DIM 対象

回復前体力 = BASE:対象:体力
回復前気力 = BASE:対象:気力
回復前精力 = BASE:対象:精力

;sparrow sake skip
IF ARG:1 == 2
;大盛りの場合
ELSEIF ARG == 1
	CALL 満腹度上昇(対象,"主食")
	CALL RECOVER_PERMIL(対象,100,"体力", 0, " ")
	CALL RECOVER_PERMIL(対象,200,"気力", 0, " ")
	CALL RECOVER_PERMIL(対象,100,"精力", 0, " ")
ELSE
	CALL 満腹度上昇(対象,"軽食")
	CALL RECOVER_PERMIL(対象,50,"体力", 0, " ")
	CALL RECOVER_PERMIL(対象,100,"気力", 0, " ")
	CALL RECOVER_PERMIL(対象,50,"精力", 0, " ")
ENDIF
IF 対象
;固定で獲得するソース
SOURCE:対象:歓楽 = 1000
SOURCE:対象:情愛 = 300

;ABL:欲望をみる
IF ABL:対象:欲望 <= 1
	SOURCE:対象:歓楽 += (ABL:対象:欲望 * 60)
ELSEIF ABL:対象:欲望 <= 3
	SOURCE:対象:歓楽 += 400 + (ABL:対象:欲望 * 60)
ELSEIF ABL:対象:欲望 <= 5
	SOURCE:対象:歓楽 += 800 + (ABL:対象:欲望 * 60)
ELSEIF ABL:対象:欲望 <= 8
	SOURCE:対象:歓楽 += 1000 + (ABL:対象:欲望 * 75)
ELSEIF ABL:対象:欲望 <= 11
	SOURCE:対象:歓楽 += 1700 + (ABL:対象:欲望 * 75)
ELSE
	SOURCE:対象:歓楽 += 2500 + (ABL:対象:欲望 * 35)
ENDIF

;ABL:対象:奉仕精神をみる
IF ABL:対象:奉仕精神 <= 1
	SOURCE:対象:情愛 += (ABL:対象:奉仕精神 * 30)
ELSEIF ABL:対象:奉仕精神 <= 3
	SOURCE:対象:情愛 += 200 + (ABL:対象:奉仕精神 * 30)
ELSEIF ABL:対象:奉仕精神 <= 5
	SOURCE:対象:情愛 += 500 + (ABL:対象:奉仕精神 * 30)
ELSEIF ABL:対象:奉仕精神 <= 8
	SOURCE:対象:情愛 += 700 + (ABL:対象:奉仕精神 * 45)
ELSEIF ABL:対象:奉仕精神 <= 11
	SOURCE:対象:情愛 += 1200 + (ABL:対象:奉仕精神 * 45)
ELSE
	SOURCE:対象:情愛 += 1800 + (ABL:対象:奉仕精神 * 20)
ENDIF

;好感度をみる
IF CFLAG:対象:2 <= 500
	SOURCE:対象:歓楽 += CFLAG:対象:2
ELSEIF CFLAG:対象:2 <= 5000
	SOURCE:対象:歓楽 += 750 + (CFLAG:対象:2 - 500) / 3
ELSE
	SOURCE:対象:歓楽 += 2000 + (CFLAG:対象:2 - 5000) / 4
ENDIF
SIF SOURCE:対象:歓楽 < 0
	SOURCE:対象:歓楽 = 0
SOURCE:対象:受動 = 200 + 110 * ABL:対象:従順
SOURCE:対象:征服 = 200 + 110 * ABL:対象:サドっ気

SIF TALENT:健啖
	TIMES SOURCE:対象:歓楽 , 1.50
ENDIF
;sparrow sake
IF ARG:1 == 2 && CAN_DRINK(対象)
	TIMES SOURCE:対象:歓楽 , 2.00
	CALL RECOVER_PERMIL(対象,50, "体力", 0, " ")
	CALL RECOVER_PERMIL(対象,250,"気力", 0, " ")
	CALL RECOVER_PERMIL(対象,150,"精力", 0, " ")
	SIF 対象 != MASTER
		CALL KIGEN_CHANGE(対象,100,1)
	CALL DRUNKEN(対象,53)
	CALL BUFF_BASE(対象, BASE_体力, 250, 1)
	CALL BUFF_BASE(対象, BASE_気力, 500, 1)
	CALL BUFF_BASE(対象, BASE_精力, 300, 1)
;熱燗頼んだ場合
ELSEIF ARG:1 && CAN_DRINK(対象)
	TIMES SOURCE:対象:歓楽 , 1.10
	CALL RECOVER_PERMIL(対象,50,"気力", 0, " ")
	SIF 対象 != MASTER
		CALL KIGEN_CHANGE(対象,50,1)
	CALL DRUNKEN(対象,52)
ENDIF

回復後体力 = BASE:対象:体力
回復後気力 = BASE:対象:気力
回復後精力 = BASE:対象:精力
PRINTFORMW 
TIME += 20
SIF ARG == 1
	TIME += 10
	
;decide if you want to move together with the 2hu when they go to work
@Add_Move_Job(ARG)
IF CAN_HELP_JOB(ARG)
	PRINTFORML %CALLNAME:ARG% asks whether you would like to tag along.
	CALL KOJO_MESSAGE_SEND("EVENT",13,ARG,97,0)
	CALL ASK_YN
	IF !RESULT
		;remove all "stragglers"
		FOR LOCAL, 1, GET_TARGETNUM() + 1
			SIF CFLAG:(TARGET:LOCAL):同行 
				CFLAG:(TARGET:LOCAL):同行 = 0
			;相合い傘もなくなる
			TEQUIP:(TARGET:LOCAL):201 = 0
		NEXT
		CFLAG:MASTER:同行 = 0
	
		CALL KOJO_MESSAGE_SEND("EVENT",13,ARG,98,0)
		PRINTFORMW Before you know it, you get transported together with %CALLNAME:ARG% to a new location!
		CFLAG:MASTER:現在位置 = CFLAG:ARG:職場
		CFLAG:MASTER:デート中 = GET_MAPID(CFLAG:ARG:職場)
	ELSE
		CALL KOJO_MESSAGE_SEND("EVENT",13,ARG,99,0)
	ENDIF
ENDIF
	
;based on Follow command + some Date checks
@CAN_HELP_JOB(CHARA)
#FUNCTION
#DIM CHARA
SIF CHARA == MASTER
	RETURNF 0
SIF CFLAG:MASTER:イタズラ || CFLAG:MASTER:うふふ ;during sex
	RETURNF 0
SIF FLAG:70 ;tsp
	RETURNF 0
SIF BASE:MASTER:気力 < 500 ;tired
	RETURNF 0
SIF NEMUKE() >= 720 ;sleepy
	RETURNF 0
SIF TCVAR:CHARA:手伝えない == 1 ;must be able to help
	RETURNF 0
SIF CFLAG:CHARA:ブチギレ ;no pissed off characters
	RETURNF 0
SIF FLAG:デート相手 != CHARA && FLAG:デート相手 != 0 ;no dating at the moment
	RETURNF 0
SIF CFLAG:CHARA:負け犬 ;for generics?
	RETURNF 0
SIF !SHIRAHU(CHARA) ;any abnormal status
	RETURNF 0
SIF CFLAG:CHARA:態度 > 0 && CFLAG:CHARA:睡眠 == 0 ;accompany-able
	RETURNF 1
SIF (TALENT:CHARA:思慕 + TALENT:CHARA:恋慕 + TALENT:CHARA:愛欲) ;accompany-able
	RETURNF 1
	
@Add_Move_Follow(ARG)
IF CAN_MOVE_FOLLOW(ARG) == 2
	PRINTFORML %CALLNAME:ARG% asks whether you would like to tag along.
	CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,97,0)
	CALL ASK_YN
	IF !RESULT
		;remove all "stragglers"
		FOR LOCAL, 1, GET_TARGETNUM() + 1
			SIF CFLAG:(TARGET:LOCAL):同行 
				CFLAG:(TARGET:LOCAL):同行 = 0
			;相合い傘もなくなる
			TEQUIP:(TARGET:LOCAL):201 = 0
		NEXT
		CFLAG:MASTER:同行 = 0
	
		CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,98,0)
		PRINTFORMW Before you know it, you get transported together with %CALLNAME:ARG% to a new location!
		CFLAG:MASTER:現在位置 = CFLAG:ARG:現在位置
		CFLAG:MASTER:デート中 = GET_MAPID(CFLAG:ARG:現在位置)
	ELSE
		CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,99,0)
	ENDIF
ELSEIF CAN_MOVE_FOLLOW(ARG) == 1
	PRINTFORML Would you like to follow after %HIM_HER(ARG)%?
	CALL ASK_YN
	IF !RESULT
		;remove all "stragglers"
		FOR LOCAL, 1, GET_TARGETNUM() + 1
			SIF CFLAG:(TARGET:LOCAL):同行 
				CFLAG:(TARGET:LOCAL):同行 = 0
			;相合い傘もなくなる
			TEQUIP:(TARGET:LOCAL):201 = 0
		NEXT
		CFLAG:MASTER:同行 = 0
		
		PRINTFORML You follow shortly after %CALLNAME:ARG%, who is going towards %GET_MAPNAME(GET_MAPID(CFLAG:ARG:現在位置))%.
		PRINTFORML You keep your distance somewhat, hoping %HE_SHE(ARG)% won't notice you.
		PRINTFORMW Somehow you end up arriving there without any time passing at all.
		CFLAG:MASTER:現在位置 = CFLAG:ARG:現在位置
		CFLAG:MASTER:デート中 = GET_MAPID(CFLAG:ARG:現在位置)
	ENDIF
ENDIF

@CAN_MOVE_FOLLOW(CHARA)
#FUNCTION
#DIM CHARA
SIF CHARA == MASTER
	RETURNF 0
SIF CFLAG:MASTER:イタズラ || CFLAG:MASTER:うふふ ;during sex
	RETURNF 0
SIF FLAG:70 ;tsp
	RETURNF 0
SIF BASE:MASTER:気力 < 500 ;tired
	RETURNF 0
SIF NEMUKE() >= 720 ;sleepy
	RETURNF 0
SIF CFLAG:CHARA:現在位置 == SUKIMA() ;end location must exist
	RETURNF 0
SIF CFLAG:CHARA:ブチギレ ;no pissed off characters
	RETURNF 0
SIF FLAG:デート相手 != CHARA && FLAG:デート相手 != 0 ;no dating at the moment
	RETURNF 0
SIF !SHIRAHU(CHARA) ;any abnormal status
	RETURNF 0
SIF CFLAG:CHARA:態度 > 0 && CFLAG:CHARA:睡眠 == 0 ;accompany-able
	RETURNF 2
SIF (TALENT:CHARA:思慕 + TALENT:CHARA:恋慕 + TALENT:CHARA:愛欲) ;accompany-able
	RETURNF 2
RETURNF 1
	
;based on old feature for book reading, added back
@Add_SexED()
IF TFLAG:194 == 2
	PRINTFORML Seems like %CALLNAME:TARGET% is paying attention. Would you like to educate her on sexual matters?
	CALL ASK_YN
	IF RESULT
		TFLAG:194 = 0
		RETURN 0
	ENDIF
	PRINTFORML %BREAKENG(@"Up until now, the book that showed how men and women join their bodies together only made %CALLNAME:TARGET% tilt %HIS_HER(TARGET)% head in marvel.")%
	;性的経験ありの場合（めんどくさいから、自慰も無自覚も睡姦も全部含めちゃる（そもそも、教養2以上かつMASTERの教養3以上の時点でそれなりに時間経ってる筈（てか教養Lv1で無知取れても良いんじゃね？　とは思わないでもないけど、学習経験5で取れる時点で、（性的）無知は取れなくても仕方ないよねと思ったりも
	FOR LOCAL,0,66
		SIF CSVEXP(TARGET,LOCAL) != EXP:TARGET:LOCAL
			LOCAL:1 ++
	NEXT
	IF LOCAL:1
		PRINTFORML %BREAKENG(@"It certainly feels good to do that %HIMSELF_HERSELF(TARGET)%, but %HE_SHE(TARGET)%'s starting to raise questions about the purpose of such acts.")%
	ELSE
		PRINTFORML %HE_SHE(TARGET,1)%'s wondering what the fun is in doing this, asking with natural curiosity.
	ENDIF
	IF LOCAL:1 && (EXP:調教自慰経験 || EXP:自慰経験 < (EXP:Ｃ経験 + EXP:Ｖ経験 + EXP:Ａ経験 + EXP:Ｂ経験 + EXP:Ｍ経験))
		PRINTFORML %BREAKENG(@"You properly instill in %HIM_HER(TARGET)% the meaning and purpose of sexual acts, despite some twinge of guilt considering that you've done to %HIM_HER(TARGET)%.")%
		IF !TALENT:思慕 && !TALENT:恋慕 && !TALENT:恋人 && !TALENT:セフレ && !TALENT:愛欲
			PRINTFORML %CALLNAME:TARGET% glares at you with a look full of shock and contempt.
			CALL CHANGE_CFLAG(2, TARGET, -100)
			CALL CHANGE_CFLAG(4, TARGET, -100)
		ELSE
			PRINTFORML %BREAKENG(@"%HE_SHE(TARGET,1)% sighs in exasperation, but then smiles, thinking it was okay because if it feels good then it must be a great thing after all.")%
		ENDIF
	ELSEIF LOCAL:1
		PRINTFORML Trying to be as accurate as possible, you properly instill in %HIM_HER(TARGET)% the meaning and purpose of sexual acts.
		IF !TALENT:思慕 && !TALENT:恋慕 && !TALENT:恋人 && !TALENT:セフレ && !TALENT:愛欲
			PRINTFORML For some reason, %CALLNAME:TARGET% is turning deep red in the face.
		ELSE
			PRINTFORML %BREAKENG(@"While blushing, %CALLNAME:TARGET% lets out a sight, realizing that masturbation alone won't give %HIM_HER(TARGET)% any babies.")%
		ENDIF
	ELSE
		PRINTFORML You properly instill in %HIM_HER(TARGET)% the meaning and purpose of sexual acts in great detail.
		PRINTFORML %CALLNAME:TARGET% listens very carefully with a look of admiration for your superior knowledge.
	ENDIF
	PRINTFORML 
	PRINTFORML 
	PRINTFORML 
	CALL COLORMESSAGE(@"%CALLNAME:TARGET% is no longer [Ignorant]!",C_ORANGE,2,1)
	TALENT:無知 = 0
	TIME += 60 ;takes another hour
ENDIF

@Add_SexED_Hint()
IF TALENT:無知 && TFLAG:194 != 2
	IF !INRANGE(TFLAG:193, 6, 8) ;if reading a wrong book
		PRINTFORML ...You have a feeling you would be able to educate %CALLNAME:TARGET% on sexual matters if you used raunchier selection of books.
	ELSEIF ABL:教養 < 2 ;not educated enough
		PRINTFORML ...On the second thought, perhaps %CALLNAME:TARGET% is a little underprepared in terms of knowledge at the moment to grasp such concepts.
	ELSEIF ABL:教養 >= ABL:MASTER:教養 ;too smart, won't listen
		PRINTFORML ...You doubt %CALLNAME:TARGET% will be willing to listen to someone undereducated than %HIM_HER(TARGET)%.
	ELSE ;random
		PRINTFORML ...Perhaps the opportunity to educate %CALLNAME:TARGET% will present itself if you try a little bit more.
	ENDIF
ENDIF

;fish/forage on the way when taking detour
@Add_ForageFish
#DIM DYNAMIC nInitiative ;who suggests to do the act
#DIM DYNAMIC nType ;type of potential gathering
#DIM DYNAMIC nAct ;selected type, 1 - fishing, 2 - foraging
#DIM nPos ;remember initial position, then change current position for the purpose of correct fishing/foraging yields
#DIM nFlag, 3 ;remember flags

SIF !TARGET
	RETURN 0
	
DEBUGPRINTFORML ------------------------
DEBUGPRINTFORML Rand check:
IF CFLAG:MASTER:今日の運勢 > 0 && RAND:(CFLAG:MASTER:今日の運勢) >= 600
	DEBUGPRINTFORML Master wins
	nInitiative = 1
ELSEIF CFLAG:TARGET:今日の運勢 > 0 && RAND:(CFLAG:TARGET:今日の運勢) >= 600
	DEBUGPRINTFORML Target wins
	nInitiative = 2
ELSE
	DEBUGPRINTFORML Fail
	DEBUGPRINTFORML ------------------------
	RETURN 0
ENDIF


DEBUGPRINTFORML Gathering materials on the way, nInitiative - {nInitiative}

SIF ABL:TARGET:親密 >= 5 - TALENT:TARGET:釣りLv && ITEM:釣り竿 && (TARGET != [[わかさぎ姫]]) ;fishing
	SETBIT nType, 0

SIF ABL:TARGET:親密 >= 5 - TALENT:TARGET:採集Lv && (BASE:MASTER:気力 >= 650 && BASE:MASTER:体力 >= 680) ;foraging
	SETBIT nType, 1

DEBUGPRINTFORML nType - {nType}, bit0 - {GETBIT(nType, 0)}, bit1 - {GETBIT(nType, 1)}

SIF nType == 0
	RETURN 0
	
IF GETBIT(nType, 0) && GETBIT(nType, 1) ;if both type apply, use either random or the one with greatest skill
	IF nInitiative == 2
		IF !RAND:4 || TALENT:TARGET:釣りLv == TALENT:TARGET:採集Lv
			nAct = RAND:2 + 1
		ELSE
			nAct = TALENT:TARGET:釣りLv > TALENT:TARGET:採集Lv ? 1 # 2
		ENDIF
	ELSE
		IF !RAND:4 || TALENT:TARGET:釣りLv == TALENT:TARGET:採集Lv
			nAct = RAND:2 + 1
		ELSE
			nAct = TALENT:MASTER:釣りLv > TALENT:MASTER:採集Lv ? 1 # 2
		ENDIF
	ENDIF
ELSEIF GETBIT(nType, 0)
	nAct = 1
ELSEIF GETBIT(nType, 1)
	nAct = 2
ENDIF

DEBUGPRINTFORML nAct - {nAct} \@ nAct == 1 ? Fishing # Foraging\@

SELECTCASE nInitiative
	CASE 1	
		PRINTFORML \@ nAct == 1 ? You spot a nice river nearby... Could be nice to fish there for a bit. # This looks like a good place to gather materials from...\@
	CASE 2
		PRINTFORML %CALLNAME:TARGET% suggests you two go \@ nAct == 1 ? fishing # foraging \@ while you're at it...
ENDSELECT
PRINTFORML What do you think?
PRINTFORML (Might take extra 30-60 minutes)
CALL ASK_YN
SIF RESULT
	RETURN 0
	
PRINTFORML You try to \@ nAct == 1 ? fish # forage\@ with %CALLNAME:TARGET% on the way while you're at it.
nPos = CFLAG:MASTER:現在位置 ;remember the position
nFlag:0 = TFLAG:192
nFlag:1 = TFLAG:193
nFlag:2 = TFLAG:194

DEBUGPRINTFORML Init flags:
DEBUGPRINTFORML CFLAG:MASTER:現在位置 - {CFLAG:MASTER:現在位置}
DEBUGPRINTFORML TFLAG:192 - {TFLAG:192}
DEBUGPRINTFORML TFLAG:193 - {TFLAG:193}
DEBUGPRINTFORML TFLAG:194 - {TFLAG:194}
DEBUGPRINTFORML Set flags:

SELECTCASE nAct
	CASE 1
		;set positions
		SELECTCASE CFLAG:MASTER:現在位置
			CASE 0 TO 599 ;misty lake
				CFLAG:MASTER:現在位置 = 霧の湖
			CASE 600 TO 699
				CFLAG:MASTER:現在位置 = 三途の川
			CASE 700 TO 799
				CFLAG:MASTER:現在位置 = RAND:3 ? 玄武の沢 # 大蝦蟇の池
			CASE 800 TO 899
				CFLAG:MASTER:現在位置 = RAND:3 ? 山の湖 # 九天の滝
			CASE 900 TO 999
				CFLAG:MASTER:現在位置 = 地獄の深道
		CASEELSE ;exception: set to genbu river
			CFLAG:MASTER:現在位置 = 玄武の沢
		ENDSELECT
		DEBUGPRINTFORML CFLAG:MASTER:現在位置 - {CFLAG:MASTER:現在位置}
		;call com and description
		SKIPDISP 1
		CALL COM621
		SKIPDISP 0
		CALL MESSAGE_COM621
		CALL COM_KOJO_CALL(TARGET, 621)
	CASE 2
		;set positions
		SELECTCASE CFLAG:MASTER:現在位置
			CASE 0 TO 99
				CFLAG:MASTER:現在位置 = 50
			CASE 100 TO 199
				CFLAG:MASTER:現在位置 = 墓地
			CASE 200 TO 399
				CFLAG:MASTER:現在位置 = 霧の湖
			CASE 400 TO 499
				CFLAG:MASTER:現在位置 = RAND:3 ? 斜角の竹林 # 無名の丘
			CASE 500 TO 599
				CFLAG:MASTER:現在位置 = RAND:3 ? 古木の大樹 # 無縁塚
			CASE 600 TO 699
				CFLAG:MASTER:現在位置 = RAND:3 ? 彼岸 # 630
			CASE 700 TO 799
				CFLAG:MASTER:現在位置 = RAND:3 ? 玄武の沢 # 大蝦蟇の池
			CASE 800 TO 899
				CFLAG:MASTER:現在位置 = RAND:3 ? 山の湖 # 絶景の丘
			CASE 900 TO 999
				CFLAG:MASTER:現在位置 = RAND:3 ? 地獄の深道 # 幻想風穴
			CASE 1000 TO 1099
				CFLAG:MASTER:現在位置 = RAND:3 ? 静かの海 # 桃林～砂浜
		CASEELSE ;exception: set to fairy tree
			CFLAG:MASTER:現在位置 = 50
		ENDSELECT
		DEBUGPRINTFORML CFLAG:MASTER:現在位置 - {CFLAG:MASTER:現在位置}
		;call com and description
		SKIPDISP 1
		CALL COM445
		SKIPDISP 0
		CALL MESSAGE_COM445
		CALL COM_KOJO_CALL(TARGET, 445)
ENDSELECT
DEBUGPRINTFORML TFLAG:192 - {TFLAG:192}
DEBUGPRINTFORML TFLAG:193 - {TFLAG:193}
DEBUGPRINTFORML TFLAG:194 - {TFLAG:194}

;reset values
CFLAG:MASTER:現在位置 = nPos
TFLAG:192 = nFlag:0
TFLAG:193 = nFlag:1
TFLAG:194 = nFlag:2
DEBUGPRINTFORML Reset flags:
DEBUGPRINTFORML CFLAG:MASTER:現在位置 - {CFLAG:MASTER:現在位置}
DEBUGPRINTFORML TFLAG:192 - {TFLAG:192}
DEBUGPRINTFORML TFLAG:193 - {TFLAG:193}
DEBUGPRINTFORML TFLAG:194 - {TFLAG:194}
DEBUGPRINTFORML ------------------------
WAIT
RETURN 1

;custom kojo call for specified com
@COM_KOJO_CALL(ARG, ARG:1, ARGS = "COM")
SIF FLAG:6 <= 0
	RETURN 0
RESULT = 0
RESULTS =
;口上の存在判定 and RESULTSに文字列代入
IF CFLAG:ARG:口上セレクタ
	TRYCALLFORM M_KOJO_K{NO:ARG}_{CFLAG:ARG:口上セレクタ}
ELSE
	TRYCALLFORM M_KOJO_K{NO:ARG}
ENDIF
SIF !RESULT
	RETURN 0
SIF FLAG:口上色
	CALL SET_KOJO_COLOR() ;custom tweak, set color here because not all actions show TRAIN_MESSAGE from dialogue
TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_%ARGS%_K{NO:ARG}_{ARG:1}
CATCH
	;埋めていないところは汎用的な処理が可能
	RESULT = 0
	TRYCALLFORM M_KOJO%RESULTS%_MESSAGE_COM_K{NO:ARG}_00
	IF !RESULT
		RESETCOLOR
		RETURN 0
	ENDIF
ENDCATCH
RESETCOLOR
RETURN 1

@Add_TiredBitchesGotoSleep
FOR LOCAL, 1, CHARANUM
	SIF TARGET == LOCAL ;skip the current target, already processed by the main function
		CONTINUE
		
	;if meetable + exhausted + not already asleep
	IF CAN_MEET(LOCAL) && BASE:LOCAL:体力 < 500 && !TCVAR:LOCAL:休憩中 && !FLAG:70 ;tsp method not needed?
		IF CFLAG:MASTER:現在位置 != OMANEKIBEYA()
			SIF !CFLAG:LOCAL:衰弱
				CFLAG:LOCAL:衰弱 = 360
			IF CAN_SEE(LOCAL) || CFLAG:LOCAL:デート中 == MAIN_MAP ;show message only when nearby
				PRINTFORMW (It appears that %CALLNAME:LOCAL%'s strength neared its limit)
			ELSE ;debug
				;#; PRINTFORMW %CALLNAME:LOCAL% is tired and goes back to spawn area (STA - {BASE:LOCAL:体力})
			ENDIF
			
			;CALL CHARA_SLEEP, LOCAL, 1
			CALL 拠点から帰る(LOCAL) ;skip sleep function mumbo jumbo and just send them straight to bed
			;FOR LOCAL,0,CHARANUM
				CALL ENDUFUFU(LOCAL)
				CALL SET_PRANK(0,LOCAL,1)
				;SIF LOCAL
					CFLAG:LOCAL:デート中 = MAIN_MAP
				CFLAG:LOCAL:同行 = 0
				TCVAR:LOCAL:休憩中 = 1
				SIF CAN_SEE(LOCAL)
					CALL KOJO_MESSAGE_SEND("EVENT", 3, LOCAL, 1, 0)
			;NEXT
			;FLAG:デート相手 = 0
			;TFLAG:デート前好感度 = 0
			;TFLAG:106 = 0
		ENDIF
	ENDIF
NEXT

@Add_Vig_Check
#DIM 変動値
;code by AK_Spectre
IF SUMARRAY(BONUS_4_ABLE, 2, 13) + BONUS_4_ABLE:14 >= 10
	CALL COLORMESSAGE("Maximum STA and ENE have temporarily increased, with a small permanent increase as a bonus for impregnating 10 races!",C_YELLOW,2,1)
	MAXBASE:MASTER:体力 += MAXBASE:MASTER:体力/200
	MAXBASE:MASTER:気力 += MAXBASE:MASTER:気力/200
ELSE
	CALL COLORMESSAGE("Maximum TSP and ENE have temporarily increased",C_YELLOW,2,1)
ENDIF

IF FLAG:獲得パンツ種数 >= 2000
	CALL COLORMESSAGE("Maximum VIG has also increased by a small potion permanently",C_YELLOW,2,1)
	nVigIncrease += MAXBASE:MASTER:精力/50
	MAXBASE:MASTER:精力 += nVigIncrease
ELSE
	CALL COLORMESSAGE("Maximum VIG has also increased by a tiny bit permanently",C_YELLOW,2,1)
	nVigIncrease += MAXBASE:MASTER:精力/1000
	MAXBASE:MASTER:精力 += nVigIncrease
ENDIF
変動値 = MIN(MAXBASE:MASTER:気力 / 10, 300)
CALL BUFF_BASE(MASTER,BASE_気力,変動値)
変動値 = MIN(MAXBASE:MASTER:TSP / 2, 500)
CALL BUFF_BASE(MASTER,BASE_TSP,変動値)

@Add_Mob_Oni_Message
SELECTCASE RAND:4
CASE 0
	PRINTFORML Most oni are preoccupied with drinking rather than paying attention to what you're doing right now...
CASE 1
	PRINTFORML Looks like no one is objecting. Perhaps this is the norm among the oni...?
CASE 2
	PRINTFORML Some oni leer and cheer, seeing you and your partner getting intimate with each other...
CASE 3
	PRINTFORML Most oni seem to be either too drunk, or just don't care enough to get in the way...
ENDSELECT

@EVENTTRAIN ;when starting the day
nSexDone = 0
nVigCheck = 0

;-------------------------------
;Custom config section
;-------------------------------
@CUSTOM_OPTION_5
PRINTFORML [  5] - Show [Facial] images for the current target only：\@ nCloseAndPersonal ? Yes # No \@

@CUSTOM_OPTION_RESULT_5
nCloseAndPersonal = !nCloseAndPersonal