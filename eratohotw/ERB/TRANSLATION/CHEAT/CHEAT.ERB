@CHEAT_MENU
#DIM CREDRAW
#DIM FIRST_LINE
FIRST_LINE = LINECOUNT
$REPRINT
CLEARLINE LINECOUNT - FIRST_LINE
CREDRAW = CURRENTREDRAW()
REDRAW 0
DRAWLINE
SETCOLOR C_RED
PRINTL Cheat menu
PRINTL   ◎Messing with debug options can break the game
PRINTL   ◎Abusing cheats can ruin enjoyment of the game
PRINTL   Please use this only understanding the above
RESETCOLOR
PRINTL 

PRINTFORML [ 0] Set money
PRINTFORML [ 1] Get panties
PRINTFORML [ 2] Display detailed pregnancy chances at the end of the day: \@ FLAG:PREG_SHOW_PROB ? ON # OFF \@
PRINTFORML [ 3] Pregnancy chance multiplier: x{FLAG:PREG_PROB_MULT == 0 ? 1 # FLAG:PREG_PROB_MULT}
IF FLAG:TUTORIAL_INITIAL_D
	PRINTPLAIN [ 4] I lost to slow text
	PRINTL
ELSE
	PRINTFORML [ 4] Please save me from the tutorial speed I beg you
ENDIF

PRINTL
PRINTL [10] Edit character
PRINTL [11] Edit characters (Offers more customization)
PRINTL [12] Reset character
PRINTL

PRINTFORML [30] Toggle Incompetent Mode (Currently: \@FLAG:甲斐性無 && !FLAG:LUSTMODE? ON # OFF\@)
PRINTFORML [31] Change lust mode (Currently: \@FLAG:LUSTMODE? ON # OFF\@)
PRINTFORML [32] Toggle Time Compression (Currently: \@FLAG:TIME_COMPRESSION ? ON # OFF \@)
PRINTL
PRINTFORML [50] Change character properties (make everyone virgin, futa, etc.)
PRINTL
PRINTFORML [60] Pregnancy Information
PRINTFORML [62] Set Pregnancy Speed (Currently: \@FLAG:PREG_SPEED? {FLAG:PREG_SPEED}x # 1x\@)
PRINTL
PRINTFORML [70] Charisma uses real-time day to calculate course (Currently: \@FLAG:CHARISMA_INGAME_DAY? ON#OFF\@), using in-game is not advised
PRINTL
PRINTL [99] Exit
INPUT
SELECTCASE RESULT
	CASE 0
		PRINTL
		PRINTFORML Please enter your desired money level. (You currently have \\{MONEY} in your possession)
		INPUT
		IF RESULT < 0
			PRINTFORML I apologize but I'd hate to see you in debt.
			GOTO REPRINT
		ELSE
			MONEY = RESULT
			PRINTFORML Very well, you now have \\{MONEY} in your possession
		ENDIF
	CASE 1
		LOCAL = FLAG:700
		CALL ROLL_PANTIES
		LOCAL = (LOCAL - FLAG:700) * -1
		PRINTFORMW The weather seems most peculiar, there was a sudden downpour of {LOCAL} panties.
	CASE 2
		FLAG:PREG_SHOW_PROB = !FLAG:PREG_SHOW_PROB 
	CASE 3
		PRINTL
		PRINTFORML Input pregnancy multiplier (or any other non-listed number)
		PRINTFORML [  1]    x1
		PRINTFORML [ 10]   x10
		PRINTFORML [ 50]   x50
		PRINTFORML [100]  x100
		INPUT
		IF RESULT > 0
			FLAG:PREG_PROB_MULT = RESULT
		ENDIF
	CASE 4
		PRINTFORMW You really can't stand the tutorial? But it was all you ever wanted.
		PRINTFORMW It's even slowed down so people like you can understand it.
		PRINTFORMW Is this excessive? Yeah, it kinda' is, but why do half-measures?
		PRINTFORMW So, wanna make the tutorial text faster?
		CALL ASK_YN
		IF RESULT
			PRINTFORMW Why press this then? Whatever.
		ELSE
			PRINTFORMW Alright.
			FLAG:TUTORIAL_INITIAL_D = 1
		ENDIF
	CASE 10
		CALL CHEAT_EDIT_CHARA
	CASE 11
		CALL CHEAT_EDIT_CHARA_MULTI
	CASE 12
		CALL CHEAT_EDIT_CHARARESET
	CASE 30
		IF FLAG:甲斐性無
			PRINTW Incompetent mode turned OFF
		ELSE
			PRINTL Incompetent mode turned ON 
			PRINTFORMW %PARSE("You")% now %PARSE("have")% a feeble personality but other characters are more aggressive
		ENDIF
		IF FLAG:LUSTMODE
			PRINTW Lust mode turned OFF
			FLAG:LUSTMODE = 0
		ENDIF
		FLAG:甲斐性無 = !FLAG:甲斐性無
	CASE 31
		IF FLAG:LUSTMODE
			PRINTW Lust mode Turned OFF
		ELSE
			PRINTL Lust mode turned ON
			PRINTW Girls will be as pushy as in incompetent mode, but no actions (such as confessing) will be blocked.
		ENDIF
		FLAG:LUSTMODE = !FLAG:LUSTMODE
	CASE 32
		IF FLAG:TIME_COMPRESSION
			PRINTW Time Compression Turned OFF
		ELSE
			PRINTW Time Compression Turned ON
			PRINTW Some actions will take less time
		ENDIF
		FLAG:TIME_COMPRESSION = !FLAG:TIME_COMPRESSION
	CASE 50
		CALL VIRGIN_SET
	CASE 62
		PRINTC 1x[1]
		PRINTC 2x[2]
		PRINTC 5x[5]
		PRINTC 10x[10]
		INPUT
		IF RESULT == 1
			FLAG:PREG_SPEED = 0
		ELSE
			FLAG:PREG_SPEED = RESULT
		ENDIF
	CASE 60
		CALL CHEAT_PREGNANCY_CHECK
	CASE 70
		FLAG:CHARISMA_INGAME_DAY = !FLAG:CHARISMA_INGAME_DAY
		FLAG:前回カリスマ設定月 = 0
	CASE 99
		RETURN
ENDSELECT
GOTO REPRINT

@CHEAT_EDIT_CHARA
#DIM CREDRAW
#DIM FIRST_LINE
FIRST_LINE = LINECOUNT
$REPRINT
CLEARLINE LINECOUNT - FIRST_LINE
CREDRAW = CURRENTREDRAW()
REDRAW 0
DRAWLINE
PRINTL Please select a character.
PRINTL 
FOR LOCAL, 0, CHARANUM
	IF LOCAL == STALKED && STALKED != 0
		CALL COLOR_PRINT(@"[{LOCAL,3}] %CALLNAME:LOCAL,14,LEFT%", "C_PINK")
	ELSE
		PRINTFORM [{LOCAL,3}] %CALLNAME:LOCAL,14,LEFT%
	ENDIF
	SIF LOCAL == 97
		PRINTL
NEXT
PRINTL
PRINTL [999] Back
INPUT
SELECTCASE RESULT
	CASE 0 TO CHARANUM-1
		CALL CHEAT_OTHERS(RESULT)
	CASE CHARANUM
		PRINTFORMW I'm sorry, but I have no idea who or what "モブ子" is supposed to be.
	CASE 999
		RETURN
ENDSELECT
GOTO REPRINT

@CHEAT_EDIT_CHARA_MULTI
#DIM FIRST_LINE
#DIM SELECTED_CHARA, 150
#DIM SELECTED_CHARA_NUM
VARSET SELECTED_CHARA
FIRST_LINE = LINECOUNT
SELECTED_CHARA_NUM = 0
$REPRINT
CLEARLINE LINECOUNT - FIRST_LINE
REDRAW 0
DRAWLINE
PRINTL Please select characters to modify. This pretty much voids all warranty.
PRINTL 
FOR LOCAL, 0, CHARANUM
	SIF SELECTED_CHARA:LOCAL
		SETCOLOR C_YELLOW
	PRINTFORM [{LOCAL,3}] %CALLNAME:LOCAL,14,LEFT%
	RESETCOLOR
	SIF (LOCAL+1) % 4 == 0
		PRINTL
NEXT

PRINTL
PRINTL  [995] Invert selection [996] Select all [997] Deselect all
SIF SELECTED_CHARA_NUM > 0
	PRINTFORM  [998] Select these {SELECTED_CHARA_NUM} character(s)
PRINTL  [999] Back
INPUT
SELECTCASE RESULT
	CASE 0 TO CHARANUM-1
		SIF SELECTED_CHARA:RESULT
			SELECTED_CHARA_NUM --
		SIF !SELECTED_CHARA:RESULT
			SELECTED_CHARA_NUM ++
		SELECTED_CHARA:RESULT = !SELECTED_CHARA:RESULT
		;CALL CHEAT_OTHERS(RESULT)
	CASE 995
		FOR LOCAL, 0, CHARANUM
			SIF SELECTED_CHARA:LOCAL
				SELECTED_CHARA_NUM --
			SIF !SELECTED_CHARA:LOCAL
				SELECTED_CHARA_NUM ++
			SELECTED_CHARA:LOCAL = !SELECTED_CHARA:LOCAL
		NEXT
	CASE 996
		FOR LOCAL, 0, CHARANUM
			SIF !SELECTED_CHARA:LOCAL
				SELECTED_CHARA_NUM ++
			SELECTED_CHARA:LOCAL = 1
		NEXT
	CASE 997
		FOR LOCAL, 0, CHARANUM
			SIF SELECTED_CHARA:LOCAL
				SELECTED_CHARA_NUM --
			SELECTED_CHARA:LOCAL = 0
		NEXT
	CASE 998
		CALL CHEAT_MULTI(SELECTED_CHARA)
	CASE 999
		RETURN
ENDSELECT
GOTO REPRINT


@CHEAT_EDIT_CHARARESET
#DIM CREDRAW
#DIM FIRST_LINE
#DIM C_ID
FIRST_LINE = LINECOUNT
PRINTL This will completely reset the character, as if they just received a new dialogue and got updated.
PRINTL Proceed?
CALL ASK_YN
$REPRINT
CLEARLINE LINECOUNT - FIRST_LINE
CREDRAW = CURRENTREDRAW()
REDRAW 0
DRAWLINE
SIF RESULT
	RETURN
PRINTL Select a character to reset.
PRINTL 
FOR LOCAL, 0, CHARANUM
	IF LOCAL == STALKED && STALKED != 0
		CALL COLOR_PRINT(@"[{LOCAL,3}] %CALLNAME:LOCAL,14,LEFT%", "C_PINK")
	ELSE
		PRINTFORM [{LOCAL,3}] %CALLNAME:LOCAL,14,LEFT%
	ENDIF
	SIF LOCAL == 97
		PRINTL
NEXT
PRINTL
PRINTL [999] Back
INPUT
SELECTCASE RESULT
	CASE 0 TO CHARANUM-1
		C_ID = RESULT
		CALL COLORMESSAGE(@"Warning: This choice will reset ALL of %HIS_HER(C_ID)% stats and favor back to %HIS_HER(C_ID)% csv values!",C_RED,1,1)
		PRINTFORML Confirm?
		CALL ASK_YN
		IF RESULT
			RESULT = 0
			GOTO REPRINT
		ENDIF
		PRINTFORML Choose reset method. Full reset will also reset STA/ENE values, and ALL traits.
		CALL ASK_YN("Full", "Dialogue")
		IF !RESULT
			;literally re-add csv, swap old with the new one and delete the old one
			ADDCHARA NO:C_ID
			SWAPCHARA CHARANUM-1, C_ID
			DELCHARA CHARANUM-1
		ELSE
			CALL RESET_KOJO_CHARA_TR(C_ID)
		ENDIF
		PRINTFORML Success!
		PRINTFORML Applying dialogue selection and update...
		LOCAL = TARGET
		RESULT = 0
		TARGET = C_ID
		CALL KOJO_MULTIPLE(C_ID)
		IF RESULT
			RESULTS =
			IF CFLAG:C_ID:口上セレクタ
				TRYCALLFORM M_KOJO_K{C_ID}_{CFLAG:C_ID:口上セレクタ}
			ELSE
				TRYCALLFORM M_KOJO_K{C_ID}
			ENDIF
			PRINTFORML Trying to UPDATE dialogue for %CALLNAME:C_ID% %RESULTS:1%
			TRYCALLFORM M_KOJO%RESULTS%_UPDATE_K{C_ID}
			PRINTL 
			RESULT = 0
		ENDIF
		PRINTFORMW Finished.
	CASE CHARANUM
		PRINTFORMW I'm sorry, but I have no idea who or what "モブ子" is supposed to be.
	CASE 999
		RETURN
ENDSELECT
GOTO REPRINT

@CHEAT_MULTI(SELECTED_CHARA)
#DIM REF SELECTED_CHARA
#DIM FIRST_LINE
#DIM SELECTED_INDEX
#DIM SELECTED_AMOUNT
#DIM SKIP_ALL
#DIMS WHAT
#DIM WHAT_TYPE
#DIM CURRENT_LEVEL
#DIM SKIP_SAME
FIRST_LINE = LINECOUNT
$REPRINT
VARSET LOCAL
VARSET SKIP_ALL
CLEARLINE LINECOUNT - FIRST_LINE
REDRAW 0
PRINTL
PRINTFORML Select what to do with these characters.
PRINTL [0] Change ABL
PRINTL [1] Change TALENT
PRINTL [2] Change TCVAR
PRINTL [3] Change EXP
PRINTL [4] Change CFLAG
PRINTL [5] Return
INPUT
SELECTCASE RESULT
	CASE 0 TO 4
		WHAT_TYPE = RESULT
		IF RESULT == 0
			CALL PRINT_ABL_CHEAT()
		ELSEIF RESULT == 1
			CALL PRINT_TALENT_CHEAT()
		ELSEIF RESULT == 2
			CALL PRINT_TCVAR_CHEAT()
		ELSEIF RESULT == 3
			CALL PRINT_EXP_CHEAT()
		ELSEIF RESULT == 4
			CALL PRINT_CFLAG_CHEAT()	
		ENDIF
		SELECTED_INDEX = RESULT
		WHAT '= @"%WHAT_TYPE==0?(ABLNAME:SELECTED_INDEX)#(WHAT_TYPE==1?TALENTNAME:SELECTED_INDEX#(WHAT_TYPE==2?TCVARNAME:SELECTED_INDEX#(WHAT_TYPE==3?EXPNAME:SELECTED_INDEX#CFLAGNAME_TR(CFLAGNAME:SELECTED_INDEX))))%"
		PRINTFORML Enter the new value for %WHAT% for all selected characters.
		PRINTFORML [-99999] Cancel
		INPUT
		IF RESULT == -99999
			PRINTW Cancelling.
			GOTO REPRINT
		ELSE
			SELECTED_AMOUNT = RESULT
			WHAT '= @"%WHAT_TYPE==0?(ABLNAME:SELECTED_INDEX)#(WHAT_TYPE==1?TALENTNAME:SELECTED_INDEX#(WHAT_TYPE==2?TCVARNAME:SELECTED_INDEX#(WHAT_TYPE==3?EXPNAME:SELECTED_INDEX#CFLAGNAME_TR(CFLAGNAME:SELECTED_INDEX))))%"
			PRINTFORML Now changing everyone's 「%WHAT%」 to {RESULT}.
			FOR LOCAL, 0, CHARANUM
				IF SELECTED_CHARA:LOCAL
					CURRENT_LEVEL = WHAT_TYPE==0?(ABL:LOCAL:SELECTED_INDEX)#(WHAT_TYPE==1?(TALENT:LOCAL:SELECTED_INDEX)#(WHAT_TYPE==2?TCVAR:LOCAL:SELECTED_INDEX#(WHAT_TYPE==3?EXP:LOCAL:SELECTED_INDEX#CFLAG:LOCAL:SELECTED_INDEX)))
					IF SKIP_ALL
						SELECTCASE WHAT_TYPE
							CASE 0
								ABL:LOCAL:SELECTED_INDEX = SELECTED_AMOUNT
							CASE 1
								TALENT:LOCAL:SELECTED_INDEX = SELECTED_AMOUNT
							CASE 2
								TCVAR:LOCAL:SELECTED_INDEX = SELECTED_AMOUNT
							CASE 3
								EXP:LOCAL:SELECTED_INDEX = SELECTED_AMOUNT
							CASE 4
								CFLAG:LOCAL:SELECTED_INDEX = SELECTED_AMOUNT
						ENDSELECT
					ELSE
						SIF SELECTED_AMOUNT == CURRENT_LEVEL && SKIP_SAME
							CONTINUE
						PRINTFORML Changing %CALLNAME:LOCAL%'s「%WHAT%」to {SELECTED_AMOUNT} - currently {CURRENT_LEVEL}.
						PRINTFORM [0] Yes [1] No [2] Yes to all [3] No to all 
						SIF SELECTED_AMOUNT == CURRENT_LEVEL 
							PRINTFORM [4] Skip everyone whose「%WHAT%」is already {SELECTED_AMOUNT}
						PRINTL
						INPUT
						SELECTCASE RESULT
							CASE 0
								SELECTCASE WHAT_TYPE
									CASE 0
										ABL:LOCAL:SELECTED_INDEX = SELECTED_AMOUNT
									CASE 1
										TALENT:LOCAL:SELECTED_INDEX = SELECTED_AMOUNT
									CASE 2
										TCVAR:LOCAL:SELECTED_INDEX = SELECTED_AMOUNT
									CASE 3
										EXP:LOCAL:SELECTED_INDEX = SELECTED_AMOUNT
									CASE 4
										CFLAG:LOCAL:SELECTED_INDEX = SELECTED_AMOUNT
								ENDSELECT
							CASE 1
							CASE 2
								SELECTCASE WHAT_TYPE
									CASE 0
										ABL:LOCAL:SELECTED_INDEX = SELECTED_AMOUNT
									CASE 1
										TALENT:LOCAL:SELECTED_INDEX = SELECTED_AMOUNT
									CASE 2
										TCVAR:LOCAL:SELECTED_INDEX = SELECTED_AMOUNT
									CASE 3
										EXP:LOCAL:SELECTED_INDEX = SELECTED_AMOUNT
									CASE 4
										CFLAG:LOCAL:SELECTED_INDEX = SELECTED_AMOUNT
								ENDSELECT
								SKIP_ALL = 1
							CASE 3
								BREAK
							CASE 4
								SKIP_SAME = 1
						ENDSELECT
					ENDIF
				ENDIF
			NEXT
			SKIP_ALL = 0
			SKIP_SAME = 0
			GOTO REPRINT
		ENDIF
	CASE 5
		RETURN
ENDSELECT
;FOR LOCAL, 0, CHARANUM
;	SIF SELECTED_CHARA:LOCAL
;		PRINTFORML %CALLNAME:LOCAL%
;NEXT
;WAIT
GOTO REPRINT

@PRINT_ABL_CHEAT()
#DIM LINE_BREAK_THING
VARSET LINE_BREAK_THING
PRINTFORML Select which ABL to change.
FOR LOCAL, 0, 56
	IF INRANGE(LOCAL,0,4) || INRANGE(LOCAL,9,18) || INRANGE(LOCAL,31,35) || INRANGE(LOCAL,40,45) || LOCAL == 47 || INRANGE(LOCAL,50,55)
		LINE_BREAK_THING ++
		PRINTFORM [{LOCAL,2}] %ABLNAME:LOCAL,30,LEFT%
		IF LINE_BREAK_THING > 2
			PRINTFORML
			LINE_BREAK_THING %= 3
		ENDIF
	ENDIF
NEXT
INPUT
RETURN RESULT

@PRINT_TALENT_CHEAT()
#DIM LINE_BREAK_THING
VARSET LINE_BREAK_THING
PRINTFORML Select which TALENT to change. This voids the warranty extra hard.
FOR LOCAL, 0, 301
	SELECTCASE LOCAL
	ENDSELECT
	IF IS_TALENT(LOCAL)
		LINE_BREAK_THING ++
		PRINTFORM [{LOCAL,3}] %TALENTNAME:LOCAL,30,LEFT%
		IF LINE_BREAK_THING > 2
			PRINTFORML
			LINE_BREAK_THING %= 3
		ENDIF
	ENDIF
NEXT
INPUT
RETURN RESULT

;Returns true if it's a talent listed in talent.csv
@IS_TALENT(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 0 TO 27, 30 TO 34, 40, 41, 50 TO 57, 60 TO 62, 70 TO 78, 80 TO 86, 90 TO 94, 100 TO 106, 120 TO 122, 130, 132 TO 138
	CASE 141 TO 146, 149 TO 167, 169 TO 175, 182 TO 185, 189 TO 197, 250, 256 TO 264, 300
	CASEELSE
		RETURNF 0
ENDSELECT
RETURNF 1


@PRINT_TCVAR_CHEAT()
#DIM LINE_BREAK_THING
VARSET LINE_BREAK_THING
PRINTFORML Select which TCVAR to change. Note that they're like all untranslated, please wait eternally.
FOR LOCAL, 0, 480
	SELECTCASE LOCAL
	ENDSELECT
	IF IS_TCVAR(LOCAL)
		LINE_BREAK_THING ++
		PRINTFORM [{LOCAL,3}] %TCVARNAME:LOCAL,30,LEFT%
		IF LINE_BREAK_THING > 2
			PRINTFORML
			LINE_BREAK_THING %= 3
		ENDIF
	ENDIF
NEXT
INPUT
RETURN RESULT

@IS_TCVAR(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 2 TO 4,12,15,17,20,21,30,40,50,60 TO 62,100 TO 109,111,112,115,117,118,120 TO 123,130 TO 135
	CASE 138 TO 150,160 TO 162,170,180 TO 184,190,191,200,201,250,251,298 TO 305, 308 TO 310, 312, 313 TO 331
	CASE 400 TO 409,490,410 TO 415, 420 TO 428, 440 TO 443, 450 TO 456, 470 TO 474, 479
	CASEELSE
		RETURNF 0
ENDSELECT
RETURNF 1

@PRINT_EXP_CHEAT()
#DIM LINE_BREAK_THING
VARSET LINE_BREAK_THING
PRINTFORML Select which EXP to change.
FOR LOCAL, 0, 111
	SELECTCASE LOCAL
	ENDSELECT
	IF IS_EXP(LOCAL)
		LINE_BREAK_THING ++
		PRINTFORM [{LOCAL,3}] %EXPNAME:LOCAL,30,LEFT%
		IF LINE_BREAK_THING > 2
			PRINTFORML
			LINE_BREAK_THING %= 3
		ENDIF
	ENDIF
NEXT
INPUT
RETURN RESULT

@IS_EXP(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 0 TO 17,20 TO 29,40 TO 44,50 TO 59,60 TO 65, 70 TO 82,101 TO 110
	CASEELSE
		RETURNF 0
ENDSELECT
RETURNF 1

@PRINT_CFLAG_CHEAT()
#DIM LINE_BREAK_THING
VARSET LINE_BREAK_THING
PRINTFORML Select which CFLAG to change.
FOR LOCAL, 0, 999
	SELECTCASE LOCAL
	ENDSELECT
	IF IS_CFLAG(LOCAL)
		LINE_BREAK_THING ++
		PRINTFORM [{LOCAL,3}] %CFLAGNAME_TR(CFLAGNAME:LOCAL),30,LEFT%
		IF LINE_BREAK_THING > 3
			PRINTFORML
			LINE_BREAK_THING %= 4
		ENDIF
	ENDIF
NEXT
INPUT
RETURN RESULT

@IS_CFLAG(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 1 TO 4,6 TO 8,10 TO 20, 30 TO 35, 40 TO 43
	CASE 284 TO 295,300,301,310,311,313,316 TO 328,
	CASE 330 TO 338, 340 TO 364, 370 TO 374, 380 TO 384, 400 TO 404,
	CASE 411 TO 415, 500 TO 512, 515 TO 518, 527 TO 531, 550 TO 553
	CASE 560, 591, 592, 595, 596, 597, 650 TO 656, 700, 900 TO 914, 920
	CASEELSE
		RETURNF 0
ENDSELECT
RETURNF 1

@CHEAT_OTHERS(ARG)
#DIM CREDRAW
#DIM FIRST_LINE
FIRST_LINE = LINECOUNT
$REPRINT
CLEARLINE LINECOUNT - FIRST_LINE
CREDRAW = CURRENTREDRAW()
REDRAW 0
PRINTL Please select an option.
SIF ARG != MASTER
	PRINTFORML [0] Change relations with %CALLNAME:MASTER%
PRINTL [1] Character editor
PRINTL [2] Character editor (everything unlocked) 
SIF ARG != MASTER
	PRINTL [3] Change marks
PRINTL [4] Change sex technique level
SIF ARG == MASTER
	PRINTL [5] Change Time Stop proficiency
SIF ARG == MASTER
	PRINTL [6] Change VIG
SIF GETBIT(TALENT:ARG:2,1)
	PRINTL [7] Change dick type
PRINT [99] Back

INPUT
SELECTCASE RESULT
	CASE 0
		PRINTL Relationship problems sir?
		CALL CHEAT_RELATIONS(ARG)
	CASE 1 TO 2
		PRINTL Do be careful as everything is applied instantly, meaning things can break.
		CALL CHAR_EDITOR(ARG,RESULT-1)
	CASE 3
		CALL CHEAT_MARK(ARG)
	CASE 4
		CALL CHEAT_SEXTECH(ARG)
	CASE 5
		CALL CHEAT_TIME()
	CASE 6
		CALL CHEAT_VIG()
	CASE 7
		CALL CHEAT_DICK_TYPE(ARG)
	CASE 99
		IF TALENT:ARG:行きずり ;custom edit to support random characters
			SAVECHARA TOSTR(CFLAG:ARG:モブ子セーブスロット), CSTR:ARG:モブ子プロフィール１, ARG
		ENDIF
		RETURN
ENDSELECT
GOTO REPRINT


@CHEAT_RELATIONS(ARG)
#DIM CREDRAW
#DIM FIRST_LINE
FIRST_LINE = LINECOUNT
$REPRINT
CLEARLINE LINECOUNT - FIRST_LINE
CREDRAW = CURRENTREDRAW()
REDRAW 0
DRAWLINE
PRINTL What is it would you like to change?
PRINTFORML [0] Favorability    (current: {CFLAG:ARG:好感度})
PRINTFORML [1] Reliability     (current: {CFLAG:ARG:信頼度})
PRINT [99] Back
INPUT
IF RESULT == 0
	PRINTL Enter desired favorability value, sir
	INPUT
	IF RESULT >= 0
		CFLAG:ARG:好感度 = RESULT
	ELSE
		PRINTL I apologize sir, but I can't.
	ENDIF
ELSEIF RESULT == 1
	PRINTL Enter desired reliability value, sir
	INPUT
	IF RESULT >= 0
		CFLAG:ARG:信頼度 = RESULT
	ELSE
		PRINTL I apologize sir, but I can't.
	ENDIF
ELSEIF RESULT == 99
	RETURN
ENDIF
GOTO REPRINT

@CHEAT_SEXTECH(ARG)
#DIM CREDRAW
#DIM FIRST_LINE
FIRST_LINE = LINECOUNT
$REPRINT
CLEARLINE LINECOUNT - FIRST_LINE
CREDRAW = CURRENTREDRAW()
REDRAW 0
LOCAL:1 = 5
LOCAL:2 = 0
DRAWLINE
PRINTFORML %CALLNAME:ARG%'s sex techniques:
FOR LOCAL, 50, 60
	SIF !STRLENS(ABLNAME:LOCAL)
		CONTINUE
	SIF !(TALENT:ARG:2 & 1) && (LOCAL == 52 || LOCAL == 54) ;Checks if it's male (no chest and vagina skills)
		CONTINUE
	PRINTFORM %ABLNAME:LOCAL,10%:
	CALL PRINT_アルファベット評価(ARG,3,LOCAL) ;Shows level's letter
	PRINTFORM  {ABL:ARG:LOCAL,5,LEFT} 
	IF ABL:ARG:LOCAL > 0
		;it's -22 so index of [-] buttons will always be less thant 50
		PRINTBUTTON "[-]", LOCAL - 22
	ELSE
		SETCOLOR C_GRAY
		PRINT [-]
		RESETCOLOR
	ENDIF
	IF ABL:ARG:LOCAL < 6
		PRINTBUTTON "[+]", LOCAL
	ELSE
		SETCOLOR C_GRAY
		PRINT [+]
		RESETCOLOR
	ENDIF
	LOCAL:2 ++
	IF LOCAL:2 % 3 == 0
		PRINTL 
		LOCAL:1 ++
	ENDIF
NEXT
PRINTL
PRINTL [99] Done
INPUT
IF INRANGE(RESULT,28,33)
	ABL:ARG:(RESULT + 22) --
ELSEIF INRANGE(RESULT,50,55)
	ABL:ARG:(RESULT) ++
ELSEIF RESULT == 99
	RETURN
ENDIF
GOTO REPRINT

@CHEAT_MARK(ARG)
#DIM CREDRAW
#DIM FIRST_LINE
FIRST_LINE = LINECOUNT
$REPRINT
CLEARLINE LINECOUNT - FIRST_LINE
CREDRAW = CURRENTREDRAW()
REDRAW 0
DRAWLINE

FOR LOCAL, 0, 6
	SIF !STRLENS(MARKNAME:LOCAL)
		CONTINUE
	PRINTFORM %MARKNAME:LOCAL, 12%: 
	IF LOCAL == 3 || LOCAL == 4 || LOCAL == 5
		SETCOLOR C_RED
		PRINTFORM %"×" * (MARK:ARG:LOCAL), 6, LEFT%
		RESETCOLOR
	ELSE
		SETCOLOR 255, 255, 0
		PRINTFORM %"★" * (MARK:ARG:LOCAL), 6, LEFT%
		RESETCOLOR
	ENDIF
	
	IF MARK:ARG:LOCAL > 0
		PRINTBUTTON "[-]", LOCAL
	ELSE
		SETCOLOR C_GRAY
		PRINT [-]
		RESETCOLOR
	ENDIF
	IF MARK:ARG:LOCAL < 3
		PRINTBUTTON "[+]", LOCAL + 10
	ELSE
		SETCOLOR C_GRAY
		PRINT [+]
		RESETCOLOR
	ENDIF
	PRINTL
	
NEXT
PRINTL
PRINTFORML [99] Done
INPUT
IF RESULT < 10
	SIF MARK:ARG:RESULT > 0
		MARK:ARG:RESULT --
ELSEIF RESULT != 99
	SIF MARK:ARG:(RESULT-10) < 3
		MARK:ARG:(RESULT-10) ++
ELSEIF RESULT == 99
	RETURN
ENDIF
GOTO REPRINT


@CHEAT_TIME()
#DIM CREDRAW
#DIM FIRST_LINE
FIRST_LINE = LINECOUNT
$REPRINT
CLEARLINE LINECOUNT - FIRST_LINE
CREDRAW = CURRENTREDRAW()
REDRAW 0
DRAWLINE

PRINTFORML Select desired Time Stop proficiency (currently: {MAXBASE:MASTER:TSP})
PRINTL 
PRINTBUTTON "[ 100 TSP]", 100
PRINTL 
PRINTBUTTON "[ 200 TSP]", 200
PRINTL 
PRINTBUTTON "[ 500 TSP]", 500
PRINTL 
PRINTBUTTON "[ 800 TSP]", 800
PRINTL 
PRINTBUTTON "[1500 TSP]", 1500
PRINTL 
PRINTBUTTON "[2500 TSP]", 2500
PRINTL 
PRINTBUTTON "[4000 TSP]", 4000
PRINTL 
PRINTBUTTON "[Custom]", 9999
PRINTL 
PRINT [99] Return

INPUT
IF GROUPMATCH(RESULT,100,200,500,800,1500,2500,4000)
	MAXBASE:MASTER:TSP = RESULT
	BASE:MASTER:TSP = RESULT
	PRINTFORMW Max TSP is now {RESULT}
ELSEIF RESULT == 9999
	PRINTFORML Input custom TSP value (0～99999)
	DO
		INPUT
		CLEARLINE 1
	LOOP !INRANGE(RESULT, 0, 99999)
	MAXBASE:MASTER:TSP = RESULT
	BASE:MASTER:TSP = RESULT
	PRINTFORMW Max TSP is now {RESULT}
ELSEIF RESULT == 99
	RETURN
ELSE
	PRINTFORMW I apologize, but I can only offer these preset values.
ENDIF
GOTO REPRINT



@CHEAT_VIG()
#DIM CREDRAW
#DIM FIRST_LINE
FIRST_LINE = LINECOUNT
$REPRINT
CLEARLINE LINECOUNT - FIRST_LINE
CREDRAW = CURRENTREDRAW()
REDRAW 0
DRAWLINE

PRINTFORML Select desired Time Stop proficiency (currently: {MAXBASE:MASTER:精力})
PRINTL 
PRINTBUTTON "[  100 VIG]", 100
PRINTL 
PRINTBUTTON "[  500 VIG]", 500
PRINTL 
PRINTBUTTON "[ 1000 VIG]", 1000
PRINTL 
PRINTBUTTON "[ 1500 VIG]", 1500
PRINTL 
PRINTBUTTON "[ 3000 VIG]", 3000
PRINTL 
PRINTBUTTON "[ 5000 VIG]", 5000
PRINTL 
PRINTBUTTON "[10000 VIG]", 10000
PRINTL 
PRINTBUTTON "[99999 VIG]", 99999
PRINTL 
PRINTBUTTON "[Custom]", 9999
PRINTL 
PRINT [99] Return

INPUT
IF GROUPMATCH(RESULT,100,500,1000,1500,3000,5000,10000,99999)
	MAXBASE:MASTER:精力 = RESULT
	BASE:MASTER:精力 = RESULT
	PRINTFORMW Max VIG is now {RESULT}
ELSEIF RESULT == 9999
	PRINTFORML Input custom VIG value (0～99999)
	DO
		INPUT
		CLEARLINE 1
	LOOP !INRANGE(RESULT, 0, 99999)
	MAXBASE:MASTER:精力 = RESULT
	BASE:MASTER:精力 = RESULT
	PRINTFORMW Max VIG is now {RESULT}
ELSEIF RESULT == 99
	RETURN
ELSE
	PRINTFORMW I apologize, but I can only offer these preset values.
ENDIF
GOTO REPRINT



@CHEAT_DICK_TYPE(ARG)
#DIM CREDRAW
#DIM FIRST_LINE
#DIMS CONST DICKNAME = "None", "Childish", "Small", "Normal", "Luminescent", "Bumpy", "Large"
FIRST_LINE = LINECOUNT
$REPRINT
CLEARLINE LINECOUNT - FIRST_LINE
CREDRAW = CURRENTREDRAW()
REDRAW 0
DRAWLINE


PRINTFORML Change the properties of %CALLNAME:ARG%'s penis  (Currently: %DICKNAME:(TALENT:ARG:形状)%)
RESETCOLOR

FOR LOCAL,0,7
	PRINTFORML [{LOCAL,2}] %DICKNAME:LOCAL%
NEXT
PRINTFORML [99] Return

INPUT
IF INRANGE(RESULT,0,6)
	TALENT:ARG:形状 = RESULT
	PRINTFORMW %CALLNAME:ARG%'s penis is now "%DICKNAME:RESULT%"
ELSEIF RESULT == 99
	RETURN
ENDIF

GOTO REPRINT

@CHAR_EDITOR(ARG,ARG:1=0)
#DIM CREDRAW
#DIM FIRST_LINE
FIRST_LINE = LINECOUNT
$REPRINT
CLEARLINE LINECOUNT - FIRST_LINE
CREDRAW = CURRENTREDRAW()
REDRAW 0
PRINT STA:
BAR BASE:ARG:0,MAXBASE:ARG:0,16
PRINTFORM ({BASE:ARG:0,4}/{MAXBASE:ARG:0,4}) 
PRINT ENE:
BAR BASE:ARG:1,MAXBASE:ARG:1,16
PRINTFORM ({BASE:ARG:1,4}/{MAXBASE:ARG:1,4})
PRINTL 
;能力・性技表示
CALL PRINT_STATE_ABL(ARG)
PRINTL 
;素質表示
CALL PRINT_STATE_TALENT(ARG)
PRINTL 
;経験表示
CALL PRINT_STATE_EXP(ARG)
PRINTL
DRAWLINE
;最終決定
IF LOCAL:100 == 1000
	DRAWLINE
	PRINTL Finished?
;名前の設定
ELSEIF LOCAL:100 == 1001
	CALL NAME_CUSTOM(ARG)
	LOCAL:100 = 0
	GOTO REPRINT
;ベース設定
ELSEIF LOCAL:100 == 1002
	CALL BASE_CUSTOM_CHEAT(ARG,ARG:1)
;素質設定
ELSEIF LOCAL:100 == 1003
	CALL TALENT_CUSTOM_CHEAT(ARG,LOCAL:101,ARG:1)
;能力設定
ELSEIF LOCAL:100 == 1004
	CALL ABL_CUSTOM_CHEAT(ARG,ARG:1)
;経験設定
ELSEIF LOCAL:100 == 1005
	CALL EXP_CUSTOM(ARG)
ELSEIF LOCAL:100 == 1006
	CALL CLOTHES_CUSTOM
ENDIF
DRAWLINE
PRINTFORML [1000] - Done　       　  [1001] - Name change       [1002] - STA and ENE settings
PRINTL [1003] - Talent settings  [1004] - Ability settings  [1005] - Experience settings
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT > 1000
	LOCAL:100 = RESULT
	GOTO REPRINT
ENDIF
;ベース設定
IF LOCAL:100 == 1002
	IF RESULT == 0 && MAXBASE:ARG:0 > 1000
		MAXBASE:ARG:0 -= 100
	ELSEIF RESULT == 1
		MAXBASE:ARG:0 += 100
	ELSEIF RESULT == 2 && MAXBASE:ARG:1 > 1000
		MAXBASE:ARG:1 -= 100
	ELSEIF RESULT == 3
		MAXBASE:ARG:1 += 100
	ENDIF
	BASE:ARG:0 = MAXBASE:ARG:0
	BASE:ARG:1 = MAXBASE:ARG:1
	GOTO REPRINT
;素質設定
ELSEIF LOCAL:100 == 1003
	;ページ送り
	SIF RESULT == 998 && LOCAL:101
		LOCAL:101 --
	SIF RESULT == 999
		LOCAL:101 ++
	SIF RESULT < 0 || RESULT > 500 || TALENTNAME:RESULT == ""
		GOTO REPRINT
	;陥落素質ほか
	IF !ARG:1
		IF NO:ARG == NO:MASTER
			SIF 選べない素質(RESULT)
				GOTO REPRINT
		ELSE
			SIF 選べない素質2_CHEAT(RESULT, NO:ARG)
				GOTO REPRINT
		ENDIF
	ENDIF
	;Code for virginity
	IF RESULT == 0
		IF TALENT:ARG:RESULT == 1
			TALENT:ARG:RESULT = 0
		ELSE
			TALENT:ARG:RESULT++
		ENDIF
	;性別
	ELSEIF RESULT == 2
		IF TALENT:ARG:RESULT == 3
			TALENT:ARG:RESULT = 1
			TALENT:ARG:形状 = 0
			MAXBASE:ARG:精力 = 0
			MAXBASE:ARG:5 = 0
		ELSE
			TALENT:ARG:RESULT ++
			IF TALENT:ARG:形状 == 0 || MAXBASE:ARG:精力 == 0 || MAXBASE:ARG:5 == 0
				TALENT:ARG:形状 = 3
				MAXBASE:ARG:5 = 1500
				MAXBASE:ARG:精力 = 1000
			ENDIF
		ENDIF
	;Age
	ELSEIF RESULT == 17
		TALENT:ARG:RESULT ++
		SIF TALENT:ARG:RESULT == 3
			TALENT:ARG:RESULT = -1
	;性別嗜好
	ELSEIF RESULT == 81
		IF TALENT:ARG:RESULT == 3
			TALENT:ARG:RESULT = -1
		ELSE
			TALENT:ARG:RESULT ++
		ENDIF
	;オンオフ型の素質
	ELSEIF TALENTNAME:RESULT == GET_TALENTNAME(RESULT,1) || RESULT == 1 || RESULT == 6
		TALENT:ARG:RESULT = !TALENT:ARG:RESULT
		;オンオフ型で別途追加が必要な素質
		;母乳体質
		IF RESULT == 149
			IF TALENT:ARG:RESULT
				MAXBASE:ARG:母乳 = 2000
			ELSE
				MAXBASE:ARG:母乳 = 0
			ENDIF
		ENDIF
	;体型
	ELSEIF RESULT == 100
		;体型が「小人(-5)」と「巨人(3)」の場合を独立処理
		IF TALENT:ARG:RESULT == -5
			TALENT:ARG:RESULT = -2
		ELSEIF TALENT:ARG:RESULT == 2
			TALENT:ARG:RESULT = 5
		ELSEIF TALENT:ARG:RESULT == 5
			TALENT:ARG:RESULT = -5
		ELSE
			TALENT:ARG:RESULT ++
		ENDIF
	;M sensitivity
	ELSEIF RESULT == 106
		TALENT:ARG:RESULT++
		SIF TALENT:ARG:RESULT == 3
			TALENT:ARG:RESULT = -1
	;drunkard
	ELSEIF RESULT == 121
		TALENT:ARG:RESULT++
		SIF TALENT:ARG:RESULT == 4
			TALENT:ARG:RESULT = -2
	;Animal Ears
	ELSEIF RESULT == 143
		TALENT:ARG:RESULT++
		TALENT:ARG:RESULT %= 5
	;Music/singing/dancing knowledge
	ELSEIF RESULT == 161
		TALENT:ARG:RESULT++
		TALENT:ARG:RESULT %= 4
	;skillers
	ELSEIF INRANGE(RESULT, 42, 49)
		TALENT:ARG:RESULT++
		TALENT:ARG:RESULT %= 7
	;tone deaf/sense of sound/perfect pitch
	ELSEIF RESULT == 162
		TALENT:ARG:RESULT++
		SIF TALENT:ARG:RESULT == 3
			TALENT:ARG:RESULT = -1
	;ice spirit
	ELSEIF RESULT == 164
		TALENT:ARG:RESULT++
		TALENT:ARG:RESULT %= 3
	;fuck buddy
	ELSEIF RESULT == 185
		TALENT:ARG:RESULT++
		TALENT:ARG:RESULT %= 3
	;non-living
	ELSEIF RESULT == 192
		TALENT:ARG:RESULT++
		TALENT:ARG:RESULT %= 3
	ELSEIF RESULT == 193
		TALENT:ARG:RESULT++
		TALENT:ARG:RESULT %= 4
	;Divine spirit
	ELSEIF RESULT == 185
		IF TALENT:ARG:RESULT == 0
			TALENT:ARG:RESULT = 2
		ELSEIF TALENT:ARG:RESULT == 2
			TALENT:ARG:RESULT = 3
		ELSE
			TALENT:ARG:RESULT = 0
		ENDIF
	ELSEIF RESULT == 194
		TALENT:ARG:RESULT++
		TALENT:ARG:RESULT %= 7
	;量的素質
	ELSE
		;性別
		IF LOCAL == 2
			IF TALENT:ARG:RESULT == 3
				TALENT:ARG:RESULT = 1
			ELSE
				TALENT:ARG:RESULT ++
			ENDIF
		;端まで行くと-1を掛ける
		ELSEIF STRLENS(GET_TALENTNAME(RESULT,TALENT:ARG:RESULT + 1)) || TALENT:ARG:RESULT == -1
			TALENT:ARG:RESULT ++
		ELSE
			TALENT:ARG:RESULT = -1 * TALENT:ARG:RESULT
		ENDIF
	ENDIF
;能力設定
ELSEIF LOCAL:100 == 1004
	LOCAL = RESULT / 2
	SIF !STRLENS(ABLNAME:LOCAL)
		GOTO REPRINT
	;同性愛関係
	IF !(TALENT:ARG:2 & 1) && (LOCAL == 17 || LOCAL == 32)
		GOTO REPRINT
	ELSEIF TALENT:ARG:2 & 1 && (LOCAL == 18 || LOCAL == 33)
		GOTO REPRINT
	ENDIF
	;中毒関係
	SIF LOCAL >= 30 && !ARG:1
		GOTO REPRINT
	;奇数(+)
	IF RESULT % 2
		ABL:ARG:LOCAL ++
	;偶数(-)
	ELSE
		IF ABL:ARG:LOCAL <= 0
			GOTO REPRINT
		ELSE
			ABL:ARG:LOCAL --
		ENDIF
	ENDIF
ELSEIF LOCAL:100 == 1005
	LOCAL = RESULT
	SIF !STRLENS(EXPNAME:LOCAL)
		GOTO REPRINT
	;同性愛関係
	IF !(TALENT:ARG:2 & 1) && LOCAL == 41
		GOTO REPRINT
	ELSEIF TALENT:ARG:2 & 1 && LOCAL == 42
		GOTO REPRINT
	ENDIF
	PRINTFORM Please enter a value for %EXPNAME:LOCAL%.      
	PRINTBUTTON "[Cancel]",-1
	INPUT
	SIF RESULT < 0
		GOTO REPRINT
	EXP:ARG:LOCAL = MIN(RESULT,30)
	SIF ARG:1
		EXP:ARG:LOCAL = RESULT
ELSEIF RESULT == 1006
ENDIF
GOTO REPRINT

@BASE_CUSTOM_CHEAT(ARG,ARG:1)
PRINT STA:
BAR BASE:ARG:0,MAXBASE:ARG:0,16
PRINTFORM ({BASE:ARG:0,4}/{MAXBASE:ARG:0,4})
IF BASE:ARG:0 != 1000
	PRINTBUTTON "[−]", 0
ELSE
	SETCOLOR 0x969696
	PRINT [−]
	RESETCOLOR
ENDIF
IF BASE:ARG:0 != 2500 || ARG:1
	PRINTBUTTON "[＋]", 1
ELSE
	SETCOLOR 0x969696
	PRINT [＋]
	RESETCOLOR
ENDIF
PRINTL
PRINT ENE:
BAR BASE:ARG:1,MAXBASE:ARG:1,16
PRINTFORM ({BASE:ARG:1,4}/{MAXBASE:ARG:1,4})
IF BASE:ARG:1 != 1000
	PRINTBUTTON "[−]", 2
ELSE
	SETCOLOR 0x969696
	PRINT [−]
	RESETCOLOR
ENDIF
IF BASE:ARG:1 != 2500 || ARG:1
	PRINTBUTTON "[＋]", 3
ELSE
	SETCOLOR 0x969696
	PRINT [＋]
	RESETCOLOR
ENDIF
PRINTL

@TALENT_CUSTOM_CHEAT(ARG,ARG:1,ARG:2=0)
VARSET LOCAL
FOR LOCAL,ARG:1 * 40,40 + ARG:1 * 40
	IF LOCAL % 10 == 0
		PRINTL
		SIF LOCAL:1 % 2
			PRINTL
		LOCAL:1 = 0
	ENDIF
	;陥落素質ほか
	IF !ARG:2
		IF NO:ARG == NO:MASTER
			SIF 選べない素質(LOCAL)
				CONTINUE
		ELSE
			SIF 選べない素質2_CHEAT(LOCAL, NO:ARG)
				CONTINUE
		ENDIF
	ENDIF
	
	SELECTCASE TALENTNAME:LOCAL
	;TALENT.CSVに登録されていない
	CASE ""
		CONTINUE
	;オンオフ型の素質(TALENTNAMEと素質表示が一致)
	CASE GET_TALENTNAME(LOCAL,1)
		IF !TALENT:ARG:LOCAL
			SETCOLOR 150,150,150
		ELSE
			SETCOLOR C_AQUA
		ENDIF
		LOCALS = [{LOCAL,3}] - %TALENTNAME:LOCAL%
	CASE "非童貞"
		SETCOLOR C_AQUA
		SIF GETBIT(TALENT:ARG:LOCAL, 0) || GETBIT(TALENT:ARG:LOCAL, 0) || GROUPMATCH(TALENT:ARG:2, 0, 1)
			SETCOLOR 150,150,150
		LOCALS = [{LOCAL,3}] - %TALENTNAME:LOCAL%
	CASE "キス未経験"
		IF TALENT:ARG:LOCAL
			SETCOLOR 150,150,150
		ELSE
			SETCOLOR C_AQUA
		ENDIF
		LOCALS = [{LOCAL,3}] - %TALENTNAME:LOCAL%
	;量的素質
	CASEELSE
		IF !TALENT:ARG:LOCAL
			SETCOLOR 150,150,150
		ELSE
			SETCOLOR C_AQUA
		ENDIF
		LOCALS =  [{LOCAL,3}] - %TALENTNAME:LOCAL%
		IF STRLENS(GET_TALENTNAME(LOCAL, TALENT:ARG:LOCAL))
			LOCALS:1 =  …【%GET_TALENTNAME(LOCAL,TALENT:ARG:LOCAL)%】
		ELSE
			LOCALS:1 = …
			SETCOLOR 150,150,150
		ENDIF
		LOCALS += LOCALS:1
	ENDSELECT
	PRINTFORM %LOCALS,50,LEFT%
	LOCAL:1 ++
	SIF LOCAL:1 % 2 == 0
		PRINTL
	RESETCOLOR
NEXT
PRINTL

IF ARG:1 != 0
	PRINTBUTTON "[998] - Previous Page                 ",998
ELSE
	SETCOLOR 0x969696
	PRINTFORM %"[---] - Previous Page",35,LEFT%
	RESETCOLOR
ENDIF
IF INRANGE(ARG:1, 0, 6)
	PRINTBUTTON "[999] - Next Page",999
ELSE
	SETCOLOR 0x969696
	PRINTFORM %"[---] - Next Page",35,LEFT%
	RESETCOLOR
ENDIF
PRINTL

@ABL_CUSTOM_CHEAT(ARG,ARG:1)
LOCAL:1 = 0
FOR LOCAL,0,50
	SIF !STRLENS(ABLNAME:LOCAL)
		CONTINUE
	;同性愛関係
	IF !(TALENT:ARG:2 & 1) && (LOCAL == 17 || LOCAL == 32)
		CONTINUE
	ELSEIF TALENT:ARG:2 & 1 && (LOCAL == 18 || LOCAL == 33)
		CONTINUE
	ENDIF
	;中毒も無し
	SIF LOCAL >= 30 && !ARG:1
		CONTINUE
	PRINTFORM %ABLNAME:LOCAL,18%:{ABL:ARG:LOCAL,5,LEFT} 
	IF ABL:ARG:LOCAL != 0
		PRINTBUTTON "[−]", LOCAL * 2
	ELSE
		SETCOLOR 0x969696
		PRINT [−]
		RESETCOLOR
	ENDIF
	IF ABL:ARG:LOCAL != 2 || ARG:1
		PRINTBUTTON "[＋]", LOCAL * 2 + 1
	ELSE
		SETCOLOR 0x969696
		PRINT [＋]
		RESETCOLOR
	ENDIF
	LOCAL:1 ++
	SIF LOCAL:1 % 2 == 0
		PRINTL 
NEXT
PRINTL

@CHEAT_PREGNANCY_CHECK
LOCAL:1 = 0

PRINTL
FOR LOCAL,0,CHARANUM
	IF CFLAG:LOCAL:妊娠経過日数
		PRINTFORM %CALLNAME:(LOCAL)% is pregnant ({CFLAG:LOCAL:妊娠経過日数}％)
		LOCAL:1 ++
	ENDIF
NEXT
PRINTL
PRINTFORM Number of pregnancies: 
SETCOLOR 0xff00aa
PRINTV LOCAL:1
RESETCOLOR
WAIT

@ROLL_PANTIES()
#DIM TYPE
FOR LOCAL,1,CHARANUM
	FOR TYPE,0,MAXPANTS
		IF !RAND:100
			SIF !GET_INT(LOCAL, "下半身下着_ずらし可能", TYPE, "コレクション表示可能")
				CONTINUE
			CFLAG:LOCAL:(TYPE + 100) ++
			FLAG:700 ++
		ENDIF
	NEXT
NEXT

;The following function was removed from the japanese branch, but was used by the cheating branch
;I'll just store it here
@選べない素質2_CHEAT(ARG, ARG:1)
#FUNCTION
SELECTCASE ARG
	CASE IS >= 169
		RETURNF 1
	CASE 3 TO 5, 7 TO 9, 16, 17, 34, 55, 73 TO 79, 84, 120 TO 122, 152 TO 155, 159 TO 162
		RETURNF 1
	CASE 42 TO 49
		RETURNF 1
	CASE 156
		SIF TALENT:(ARG:1):性別 == 2 || !TALENT:(ARG:1):幼稚
			RETURNF 1
	CASEELSE
		RETURNF 0
ENDSELECT

;Functions moved from キャラメイク.ERB, as they were renamed/changed in NEWGAME_CUSTOM
@選べない素質(ARG, CHARA)
#FUNCTION
#DIM CHARA
;CHARAはNO:の形で渡されてくる
;RETURNF 2は表示はさせるもの
SELECTCASE ARG
;劣情察知～,ボーナス素質および特殊陥落,種族,孕ませ系
CASE IS >=157
	RETURNF 1
CASE 42 TO 49
	RETURNF 1
;あなたは選べない１
CASE GETNUM(TALENT, "キス未経験"), GETNUM(TALENT, "習得速度") TO GETNUM(TALENT, "針さばき"), GETNUM(TALENT, "薬毒耐性"), GETNUM(TALENT, "即落ち"), GETNUM(TALENT, "盲信"), GETNUM(TALENT, "小悪魔")
	SIF CHARA == NO:MASTER
		RETURNF 1
;あなたは選べない２
CASE GETNUM(TALENT, "魅力") TO GETNUM(TALENT, "謎の魅力"), GETNUM(TALENT, "回復速度") TO GETNUM(TALENT, "不思議な根"), GETNUM(TALENT, "狂気"), GETNUM(TALENT, "人気")
	SIF CHARA == NO:MASTER
		RETURNF 1
;あなたは選べず、女性器がないと選べない
CASE GETNUM(TALENT, "母乳体質")
	SIF CHARA == NO:MASTER
		RETURNF 1
	SIF !GETBIT(TALENT:CHARA:性別, 0)
		RETURNF 2
;女性器がないと選べない
CASE GETNUM(TALENT, "処女"), GETNUM(TALENT, "ヤリマン"), GETNUM(TALENT, "Ｖ感度")
	SIF !GETBIT(TALENT:CHARA:性別, 0)
		RETURNF 2
;男性器がないと選べない
CASE GETNUM(TALENT, "非童貞")
	SIF !GETBIT(TALENT:CHARA:性別, 1)
		RETURNF 2
;男性器があると選べない
CASE GETNUM(TALENT, "具現")
	SIF GETBIT(TALENT:CHARA:性別, 1)
		RETURNF 2
;あなた以外は選べない
CASE GETNUM(TALENT, "年齢")
	SIF CHARA != NO:MASTER
		RETURNF 1
;全員選べない１
CASE GETNUM(TALENT, "恋慕") TO GETNUM(TALENT, "崩壊"), GETNUM(TALENT, "機嫌"), GETNUM(TALENT, "容姿"), GETNUM(TALENT, "弱味"), GETNUM(TALENT, "調合知識"), GETNUM(TALENT, "調合Lv"), GETNUM(TALENT, "採集Lv"), GETNUM(TALENT, "釣りLv")
	RETURNF 1
;全員選べない２
CASE GETNUM(TALENT, "自慰狂い") TO GETNUM(TALENT, "ポルチオ性感"), GETNUM(TALENT, "嫉妬"), GETNUM(TALENT, "濃厚精液"), GETNUM(TALENT, "精愛味覚"), GETNUM(TALENT, "禁断の知識"), GETNUM(TALENT, "絶倫") TO GETNUM(TALENT, "母性")
	RETURNF 1
;女性器があって幼児か幼稚がないと選べない
CASE GETNUM(TALENT, "初潮")
	SIF !GETBIT(TALENT:CHARA:性別, 0)
		RETURNF 2
	SIF !TALENT:CHARA:幼児／幼児退行 && !TALENT:CHARA:幼稚
		RETURNF 2
ENDSELECT
RETURNF 0

;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1.00
;経験の設定
;CALL		USER
;ARG		MASTERキャラNO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@EXP_CUSTOM(ARG)
VARSET LOCAL
FOR LOCAL,0,100
	SIF !STRLENS(EXPNAME:LOCAL)
		CONTINUE
	;同性愛関係
	IF !(TALENT:ARG:2 & 1) && LOCAL == 41
		CONTINUE
	ELSEIF TALENT:ARG:2 & 1 && LOCAL == 42
		CONTINUE
	ENDIF
	SIF LOCAL == 57 || (60 <= LOCAL && LOCAL <= 65)
		CONTINUE
	PRINTFORM [{LOCAL,2}] - %EXPNAME:LOCAL,22%:{EXP:ARG:LOCAL,5,LEFT} 
	LOCAL:1 += 1
	SIF LOCAL:1 % 3 == 0
		PRINTL 
NEXT
PRINTL

;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1.00
;服装の設定
;CALL		USER
;ARG		MASTERキャラNO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CLOTHES_CUSTOM(ARG)
;あなた以外なら変更しない
SIF NO:ARG != NO:MASTER || FLAG:なりきり
	RETURN 0
;MASTERの性別判定
IF TALENT:ARG:2 & 1
	CFLAG:ARG:7 = 2
ELSE
	CFLAG:ARG:7 = 1
ENDIF
PRINTFORML Select Casual Clothes
DRAWLINE
$LOOP
PRINTL [0] - Naked
PRINTL [1] - Dress Like A Man
PRINTL [2] - Dress Like A Woman
PRINTL [3] - Butler
PRINTL [4] - Maid
PRINTL [5] - Shinto Priest
PRINTL [6] - Monk
PRINTL [7] - Martial Artist
PRINTL [8] - Male Student
PRINTL [9] - Schoolgirl
INPUT
SELECTCASE RESULT
	CASE 0
	PRINTFORMW Too shameless!
	GOTO LOOP
	CASE 1 TO 6
		CFLAG:MASTER:7 = RESULT
	CASE 7 TO 9
		CFLAG:MASTER:7 = RESULT + 1
	CASEELSE
		GOTO LOOP
ENDSELECT
RETURN 0