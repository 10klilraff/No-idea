;This file is for things that uses location strings
;like, "is the player at 夢幻遺跡・第一資料室" instead of the english names
@IS_LIBRARY_TR(PLACE_ID)
#FUNCTION
#DIM PLACE_ID              ;かならずMASTERの現在位置を渡すこと
#DIM DYNAMIC PLACE_LIBRARY
IF GET_MAPID(PLACE_ID) == MAIN_MAP
	SELECTCASE GET_MAPID(PLACE_ID)
	CASE 0
	;MAP_博麗神社　研究助手を毎日やってたせいで誰でも資料閲覧できそうなイメージ
		SIF GROUPMATCH(PLACE_ID, 48, 49, 50)
			PLACE_LIBRARY = PLACE_ID
	CASE 1
	;MAP_命連寺、寺/道場ともに頼めば資料を見せてくれそう
		SIF GROUPMATCH(PLACE_ID, 105, 126)
			PLACE_LIBRARY = PLACE_ID
	CASE 2
	;MAP_人里、貸本屋と個人宅
		SIF GROUPMATCH(PLACE_ID, 312, 222, 224, 221)
			PLACE_LIBRARY = PLACE_ID
	CASE 3
	;MAP_紅魔館、とりあえず使用条件とかないので素通し
		SIF PLACE_ID == 310
			PLACE_LIBRARY = PLACE_ID
	CASE 4
	;MAP_迷いの竹林、このマップ特に図書室的なものが無い
		;永遠亭はたくさん蔵書がありそうなイメージ
		SIF GROUPMATCH(PLACE_ID, 414, 424, 427)
			PLACE_LIBRARY = PLACE_ID
	CASE 5
	;MAP_魔法の森、魔法使いたちは資料持ってるでしょう
		SIF GROUPMATCH(PLACE_ID, 513, 524)
			PLACE_LIBRARY = PLACE_ID
	CASE 7
	;MAP_魔法の森、華扇屋敷のみ
		SIF PLACE_ID == 724
			PLACE_LIBRARY = PLACE_ID
	;Custom code, added library to summit
	CASE 8
		SIF PLACE_ID == 832
			PLACE_LIBRARY = PLACE_ID
	CASE 9
	;MAP_地底、さとりの書斎のみ
		SIF PLACE_ID == 920
			PLACE_LIBRARY = PLACE_ID
	ENDSELECT
ELSE
	;Custom code
	SELECTCASE GET_MAPID(PLACE_ID)
	CASE 0
		SIF STR:(6000 + PLACE_ID / 10) == "夢幻遺跡"
			PLACE_LIBRARY = 49
	CASE 1
		SIF STR:(6000 + PLACE_ID / 10) == "命蓮寺境内"
			PLACE_LIBRARY = 105
		SIF STR:(6000 + PLACE_ID / 10) == "神霊廟道場" ;used to be 聖徳道場
			PLACE_LIBRARY = 126
	CASE 2
		SIF STR:(6000 + PLACE_ID / 10) == "商家町"
			PLACE_LIBRARY = 213
		SIF STR:(6000 + PLACE_ID / 10) == "寺子屋"
			PLACE_LIBRARY = 222
		SIF STR:(6000 + PLACE_ID / 10) == "長屋街"
			PLACE_LIBRARY = 221
	CASE 3
		SIF STR:(6000 + PLACE_ID / 10) == "大図書館"
			PLACE_LIBRARY = 310
	CASE 4
		SIF STR:(6000 + PLACE_ID / 10) == "永遠亭奥ノ院"
			PLACE_LIBRARY = 427
	CASE 5
		SIF STR:(6000 + PLACE_ID / 10) == "霧雨魔法店"
			PLACE_LIBRARY = 513
		SIF STR:(6000 + PLACE_ID / 10) == "人形の洋館"
			PLACE_LIBRARY = 524
	CASE 6
		[SKIPSTART]
		IF STR_TR(6000 + PLACE_ID / 10) == "マヨヒガ"
			PLACE_LIBRARY = 630
		ENDIF
		IF STR_TR(6000 + PLACE_ID / 10) == "白玉楼"
			PLACE_LIBRARY = 690
		ENDIF
		[SKIPEND]
	CASE 7
		IF STR_TR(6000 + PLACE_ID / 10) == "仙人の屋敷"
			PLACE_LIBRARY = 724
		ENDIF
	CASE 8
		[SKIPSTART]
		IF STR_TR(6000 + PLACE_ID / 10) == "天界"
			PLACE_LIBRARY = 890
		ENDIF
		[SKIPEND]
	CASE 9
		SIF PLACE_ID == 920
			PLACE_LIBRARY = 920
	CASE 10
		[SKIPSTART]
		IF STR_TR(6000 + PLACE_ID / 10) == "綿月亭"
			PLACE_LIBRARY = 1060
		ENDIF
		[SKIPEND]
	ENDSELECT
ENDIF
RETURNF CAN_READ_BOOK_COLLECTION_TR(PLACE_LIBRARY)


;蔵書閲覧許諾判定
;・かならず拠点マップの場所ID（詳細な地点の方）を渡すこと
;・MASTERがその場所にいるのが前提で処理を組んでます
@CAN_READ_BOOK_COLLECTION_TR(PLACE_ID)
#FUNCTION
#DIM PLACE_ID
SELECTCASE PLACE_ID
;夢幻遺跡は素通し
CASE 48, 49, 50
	RETURNF PLACE_ID
;寺と聖徳道場は素通し
CASE 105, 126
	RETURNF PLACE_ID
;鈴奈庵と寺子屋は素通し
CASE 213, 222
	RETURNF PLACE_ID
;阿求の屋敷
;思慕/恋慕で解放
CASE 221
	SIF !TALENT:80:恋慕 && !TALENT:80:思慕
		RETURNF 0
	RETURNF PLACE_ID
;慧音宅
CASE 224
	SIF FLAG:なりきり == 67
		RETURNF PLACE_ID
	SIF CFLAG:67:信頼度 < 30
		RETURNF 0
	SIF CFLAG:67:現在位置 == CFLAG:MASTER:現在位置
		RETURNF PLACE_ID
;図書館は素通し
CASE 310
	RETURNF PLACE_ID
;輝夜私室
CASE 414
	SIF GROUPMATCH(FLAG:なりきり, 52, 53, 62, 72)
		RETURNF PLACE_ID
	SIF !TALENT:62:恋慕 && !TALENT:62:思慕
		RETURNF 0
	SIF CFLAG:62:現在位置 == CFLAG:MASTER:現在位置
		RETURNF PLACE_ID
;物置/倉庫の捜索はゆるく
;誰か一人同席してればよし
CASE 424, 427
	SIF GROUPMATCH(FLAG:なりきり, 52, 53, 62, 72)
		RETURNF PLACE_ID
	SIF GROUPMATCH(CFLAG:MASTER:現在位置, CFLAG:52:現在位置, CFLAG:53:現在位置, CFLAG:62:現在位置, CFLAG:72:現在位置)
		RETURNF PLACE_ID
;魔理沙宅の研究室
;多少の信頼度でおｋ
CASE 513
	SIF FLAG:なりきり == 11
		RETURNF PLACE_ID
	SIF CFLAG:11:信頼度 < 30
		RETURNF 0
	SIF CFLAG:11:現在位置 == CFLAG:MASTER:現在位置
		RETURNF PLACE_ID
;人形館の研究室
CASE 524
	SIF GROUPMATCH(FLAG:なりきり, 17, 102)
		RETURNF PLACE_ID
	SIF INRANGE(CFLAG:MASTER:初期位置, 521, 531)
		RETURNF PLACE_ID
	SIF !TALENT:17:恋慕 && !TALENT:17:思慕 && !TALENT:102:恋慕 && !TALENT:102:思慕
		RETURNF 0
	SIF GROUPMATCH(CFLAG:MASTER:現在位置, CFLAG:17:現在位置, CFLAG:102:現在位置)
		RETURNF PLACE_ID
;仙人屋敷の書斎
CASE 724
	SIF FLAG:なりきり == 43
		RETURNF PLACE_ID
	SIF INRANGE(CFLAG:MASTER:初期位置, 720, 730)
		RETURNF PLACE_ID
	SIF !TALENT:43:恋慕 && !TALENT:43:思慕
		RETURNF 0
	SIF CFLAG:43:現在位置 == CFLAG:MASTER:現在位置
		RETURNF PLACE_ID
;Custom code, bhavaagra streets
CASE 832
	RETURNF PLACE_ID
;さとりの書斎
;同居してても信頼度50必要
CASE 920
	SIF FLAG:なりきり == 49
		RETURNF PLACE_ID
	SIF CFLAG:49:信頼度 < 50
		RETURNF 0
	SIF INRANGE(CFLAG:MASTER:初期位置, 902, 926)
		RETURNF PLACE_ID
	SIF !TALENT:49:恋慕 && !TALENT:49:思慕
		RETURNF 0
	SIF CFLAG:49:現在位置 == CFLAG:MASTER:現在位置
		RETURNF PLACE_ID
ENDSELECT