@DateEvent_Checksum(ARG)
#FUNCTION
LOCAL = MARK:ARG:不埒刻印 * 10 + MARK:ARG:快楽刻印 * 10
IF TALENT:ARG:恋慕
	LOCAL += MIN(ABL:ARG:親密 * 10, 100) + MIN(ABL:ARG:欲望 * 10,100) + MIN(ABL:ARG:奉仕精神 * 10,100)
	LOCAL += MIN(EXP:ARG:デート経験,100) + MIN(EXP:ARG:愛情経験,100)
ELSEIF TALENT:ARG:思慕
	LOCAL += MIN(ABL:ARG:親密 * 10, 50) + MIN(ABL:ARG:欲望 * 10,50) + MIN(ABL:ARG:奉仕精神 * 10,50)
	LOCAL += MIN(EXP:ARG:デート経験,50)
ENDIF
LOCAL += GETPALAMLV(PALAM:ARG:欲情,10) * 5 + GETPALAMLV(PALAM:ARG:好意,10) * 5 + GETPALAMLV(PALAM:ARG:恭順,10) * 5
;60
LOCAL += BASE:ARG:ムード / 50 + (1000 - BASE:ARG:理性) / 30
LOCAL += TALENT:ARG:度胸 * 10 + TALENT:ARG:性的興味 * 10 - TALENT:ARG:自制心 * 10 - TALENT:ARG:無関心 * 10 
DEBUGPRINTFORML 【DATE_EVENT({ARG})】DateEvent_Checksum = {LOCAL}
RETURNF LOCAL

@DATE_EVENT_TR(ARG)
#DIM 珠不足
#DIM 経験不足

VARSET LOCAL

;キャンセルフラグ（外部参照にて使用のこと 例:LOCAL@DATE_EVENT:99 = 1
LOCAL:99 = 0
SIF LOCAL:99 ;custom - activate the possibility of cancelling the date event (does this shit even work, lmao?)
	RETURN 1
;パラメータによる発生基礎値生成

LOCAL = DateEvent_Checksum(ARG)
SIF nDateDebug:0
	LOCAL = 9999

;qol custom, check for given gifts from the character right away without burying it
IF nDateDebug:4 || GIFT_DATE_CHECK(ARG)
	CALL GIFT_DATE_EVENT(ARG)
ELSE
	CALL COLORMESSAGE(@"No gift from %CALLNAME:ARG%...",C_GRAY,1)
	PRINTL
ENDIF

;別れ際にプレゼント
CALL GIFT(ARG)
IF ALLSAMES(nDateDebug:0, nDateDebug:1, nDateDebug:2, nDateDebug:3, 0) && RESULT == 1
;合意無し（キス成功してない）
;qol: activate this event even if had sex but never kissed
ELSEIF nDateDebug:1 || ( (!(CFLAG:ARG:1 & 1) || TALENT:ARG:キス未経験) && !(CFLAG:ARG:13 & 1) )
	;qol custom, add visible date score
	CALL COLORMESSAGE(@"Date End! Kiss Route - Agreement Score: {CFLAG:ARG:合意判定}/180, Date Score:{LOCAL} vs {220 + TALENT:ARG:一線越えない * 30 + ABS(TALENT:ARG:キス未経験 * 20)}",C_YELLOW,0,1)
	SIF !(TALENT:ARG:思慕 + TALENT:ARG:恋慕)
		CALL COLORMESSAGE(@" No Love or Yearning!", C_RED, 0, 1)
	;成功判定
	IF nDateDebug:1 || ( (LOCAL > 220 + TALENT:ARG:一線越えない * 30 + ABS(TALENT:ARG:キス未経験 * 20)) && CFLAG:ARG:合意判定 > 180 && (TALENT:ARG:思慕 || TALENT:ARG:恋慕) )
		CALL COLORMESSAGE(@" Success!", C_GREEN, 2, 1)
		
		CALL KOJO_MESSAGE_SEND("SP_EVENT",1,ARG,0)
		;キャンセルフラグ
		IF !TCVAR:ARG:キスキャンセル
			CALL ADD_KISS(ARG)
			SETBIT CFLAG:ARG:デート後イベントフラグ , 0
		ELSEIF TCVAR:ARG:キスキャンセル
			CALL COLORMESSAGE(@"Postponed by dialogue...", C_GRAY, 2)
		ENDIF
	ELSEIF !MARK:ARG:反発刻印
		CALL COLORMESSAGE(@" Fail!", C_RED, 2, 1)
		CALL KOJO_MESSAGE_SEND("SP_EVENT",3,ARG,0)
	ELSE
		CALL COLORMESSAGE(@" Fail!", C_RED, 2, 1)
		IF LOCAL > 50 ;qol added, redirect to hate mark reduction if above failed
			CALL COLORMESSAGE(@"Check for lowering Hate Marks...", C_YELLOW, 1, 1)
			GOTO HATEMARK
		ENDIF
	ENDIF
;反発刻印消去処理
ELSEIF nDateDebug:2 || (MARK:ARG:反発刻印 && LOCAL > 50)
	$HATEMARK
	CALL COLORMESSAGE(@"Date End! Forgiveness Route - Date Score:{LOCAL} vs 50",C_YELLOW,0,1)
	LOCAL:9 = 0
	;ABL:親密LVUP処理取り込み。解読が面倒だったので要所々々取り摘んで…
	FOR LOCAL,0,4
		珠不足 = 0
		経験不足 = 0
		IF !nDateDebug:2 && JUEL_DEMAND(9,LOCAL,0,ARG) == -2
			CALL COLORMESSAGE(@" Fail, not enough Gems\@ LOCAL:9 ? or Experience!#\@", C_RED, 2, 1)
			BREAK
		ENDIF
		IF !nDateDebug:2 && JUEL_DEMAND(9,LOCAL,0,ARG) < 0
			SIF LOCAL == 3 ;display failure message at the last loop cycle
				CALL COLORMESSAGE(@" Fail, not enough Gems\@ LOCAL:9 ? or Experience!#\@", C_RED, 2, 1)
			CONTINUE
		ENDIF
		;必要な宝珠を探す
		FOR LOCAL:1,0,100
			IF JUEL_DEMAND(9,LOCAL,LOCAL:1,ARG) > 0
				IF JUEL:(ARG):(LOCAL:1) < JUEL_DEMAND(9,LOCAL,LOCAL:1,ARG) / 2
					LOCAL:7 = JUEL:(ARG):(LOCAL:1)
					LOCAL:8 = JUEL_DEMAND(9,LOCAL,LOCAL:1,ARG) / 2
					珠不足 ++
					BREAK
				ENDIF
			ENDIF
		NEXT
		IF !nDateDebug:2 && 珠不足
			SIF LOCAL == 3 ;display failure message at the last loop cycle
				CALL COLORMESSAGE(@" Fail, not enough Gems {LOCAL:7}/{LOCAL:8}\@ LOCAL:9 ? or Experience!#\@", C_RED, 2, 1)
			CONTINUE
		ENDIF
		;必要な経験を探す
		FOR LOCAL:1,0,100
			IF EXP_DEMAND(9,LOCAL,LOCAL:1,ARG) > 0
				IF  EXP:(ARG):(LOCAL:1) < EXP_DEMAND(9,LOCAL,LOCAL:1,ARG) / 2
					経験不足 ++
					LOCAL:9 = 1
					BREAK
				ENDIF
			ENDIF
		NEXT
		IF !nDateDebug:2 && 経験不足
			SIF LOCAL == 3 ;display failure message at the last loop cycle
				CALL COLORMESSAGE(@" Fail, not enough Gems\@ LOCAL:9 ? or Experience!#\@", C_RED, 2, 1)
			CONTINUE
		ENDIF
		CALL COLORMESSAGE(@" Success!", C_GREEN, 2, 1)
		;ここまで来れたら反発刻印消去
		;要するにデートでいい雰囲気＋親密LVUP水準の1/2満たしたら反発消去
		MARK:ARG:反発刻印 --
		CALL KOJO_MESSAGE_SEND("SP_EVENT",3,ARG,(MARK:ARG:反発刻印 + 1))
		PRINTL
		IF MARK:ARG:反発刻印
			PRINTFORMW %CALLNAME:ARG%'s Lv {MARK:ARG:反発刻印} hate mark has been lowered!
		ELSE
			PRINTFORMW %CALLNAME:ARG%'s Lv 1 Hate Mark has been removed!
		ENDIF
		BREAK
	NEXT
;告白していない（MASTERに恋人が居ない）
ELSEIF nDateDebug:3 || ( !(CFLAG:ARG:1 & 4) && !(CFLAG:ARG:13 & 2) &&  !FLAG:告白禁止 && CAN_MAKE_恋人() && !TALENT:ARG:恋人 )
	CALL COLORMESSAGE(@"Date End! Confession Route - Date Score:{LOCAL} vs {530 + TALENT:ARG:一線越えない * 20}",C_YELLOW,0,1)
	;成功判定
	IF nDateDebug:3 || (LOCAL > 530 + TALENT:ARG:一線越えない * 20)
		CALL COLORMESSAGE(@" Success!", C_GREEN, 2, 1)
		CALL KOJO_MESSAGE_SEND("SP_EVENT",2,ARG,0)
		PRINTFORML \@THIRD_PERSON?Select how to respond to %CALLNAME:ARG%'s confession#How do you respond to %CALLNAME:ARG%'s confession?\@
		CALL ASK_YN(@"Accept it (Can only have {FLAG:追加恋人枠 + 1} \@FLAG:追加恋人枠?s#\@)","Refuse") 
		IF !RESULT
			SETBIT CFLAG:ARG:既成事実 , 2
			SETBIT CFLAG:MASTER:既成事実, 2
			TALENT:MASTER:恋人 = NO:ARG
			TALENT:ARG:恋人 = NO:ARG
			SETBIT CFLAG:ARG:デート後イベントフラグ , 1
			CALL KOJO_MESSAGE_SEND("SP_EVENT",2,ARG,1)
			PRINTL
			PRINTFORM %PARSE("You")% and %CALLNAME:ARG% are now 
			SETCOLOR C_PINK
			PRINT Lovers
			RESETCOLOR
			PRINTW !
			CALL QoL_Info_Write(ARG, Qol_Dates_GF) ;qol custom code
		ELSE
			CALL KOJO_MESSAGE_SEND("SP_EVENT",2,ARG,2)
		ENDIF
	;贈り物イベント
	;ELSEIF GIFT_DATE_CHECK(ARG) ;moved on top
	;	CALL GIFT_DATE_EVENT(ARG)
	ELSEIF !MARK:ARG:反発刻印
		CALL COLORMESSAGE(@" Fail!", C_RED, 2, 1)
		CALL KOJO_MESSAGE_SEND("SP_EVENT",3,ARG,0)
	ELSE
		CALL COLORMESSAGE(@" Fail!", C_RED, 2, 1)
	ENDIF
;贈り物イベント
;ELSEIF GIFT_DATE_CHECK(ARG) ;moved on top
;	CALL GIFT_DATE_EVENT(ARG)
ELSE
	CALL COLORMESSAGE(@"Date End! No event!",,2,1)
	CALL KOJO_MESSAGE_SEND("SP_EVENT",3,ARG,0)
ENDIF

;reset debug flags after date
VARSET nDateDebug
