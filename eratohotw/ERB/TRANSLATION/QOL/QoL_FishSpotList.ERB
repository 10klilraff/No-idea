@SHOW_FISHING_LIST_SELECT(PLACE)
#DIM PLACE
DRAWLINE
PRINTFORML Fishing spot list (Season: %SEASON_NAME()%)
CALL COLORMESSAGE(@"Finest Fish    ",C_YELLOW)
CALL COLORMESSAGE(@"Available This Season    ",C_P_GREEN)
CALL COLORMESSAGE(@"Super Rare    ",C_RED)
CALL COLORMESSAGE(@"Out of Season",0x606060)
PRINTFORML 
DRAWLINE
SELECTCASE PLACE
	CASE 99
		RETURN -1
	CASEELSE
		CALL SHOW_FISHING_ITEM(PLACE)
		DRAWLINE
		PRINTFORMW
ENDSELECT

@SHOW_FISHING_ITEM(PLACE)
#DIM PLACE

FOR LOCAL,1,61
	SIF LOCAL == 21 ;skip rotor
		CONTINUE
	SIF !STRLENS(FISH_NAME(LOCAL))
		BREAK
		
	IF FISHING_PRODUCT(LOCAL, PLACE)
		SIF INRANGE(LOCAL, 11, 20) || INRANGE(LOCAL, 24, 60)
			SETCOLOR C_YELLOW
		SIF FISHING_SEASON(LOCAL) >= 2
			SETCOLOR C_P_GREEN
		SIF FISH_RATE(LOCAL) >= 6
			SETCOLOR C_RED
		;季節条件を満たしていない場合は灰色表記に
		SIF !FISHING_SEASON(LOCAL)
			SETCOLOR 0x606060
		PRINTFORML %CAPITALIZE(FISH_NAME_TR(LOCAL))% - \@ FISH_RATE(LOCAL) > 5 ? Ex! # %"★"*FISH_RATE(LOCAL)%\@
	ENDIF
	RESETCOLOR
NEXT
;can get some materials too sometimes
SELECTCASE FISHING_SPOT(CFLAG:MASTER:現在位置)
	CASE 1, 6 ;龍涎香or氷の鱗（冬）
		SETCOLOR C_RED
		IF DAY:2 == 4
			PRINTFORML Ice Scale
		ELSE
			PRINTFORML Ambergris
		ENDIF
	CASE 2, 3, 5, 7 ;龍の爪or氷の鱗（冬）
		SETCOLOR C_RED
		IF DAY:2 == 4
			PRINTFORML Ice Scale
		ELSE
			PRINTFORML Dragon's Claw
		ENDIF
	CASE 4 ;カエル
		SETCOLOR C_GREEN
		PRINTFORML Frog
	CASE 8
		SETCOLOR C_RED
		IF DAY:2 == 4
			PRINTFORM Ice Scale　
		ELSE
			PRINTFORM Ambergris　
		ENDIF
		SETCOLOR C_GREEN
		PRINTFORML Frog
ENDSELECT
RESETCOLOR

@FISHING_PRODUCT(ARG:0, ARG:1)
#FUNCTION

SELECTCASE ARG:1
	;霧の湖
	CASE 1, 霧の湖
		SIF GROUPMATCH(ARG:0, 5, 7, 10, 12, 16, 18, 20, 21, 25, 31)
			RETURNF 1
	;三途の川
	CASE 2, 三途の川
		SIF GROUPMATCH(ARG:0, 6, 7, 16, 17, 19, 22, 23, 26)
			RETURNF 1
	;玄武の沢
	CASE 3, 玄武の沢
		SIF GROUPMATCH(ARG:0, 1, 2, 3, 4, 11, 13, 14, 15)
			RETURNF 1
	;大蝦蟇の池
	CASE 4, 大蝦蟇の池
		SIF GROUPMATCH(ARG:0, 5, 12, 18, 22, 27)
			RETURNF 1
	;九天の滝
	CASE 5, 九天の滝
		SIF GROUPMATCH(ARG:0, 1, 2, 3, 4, 11, 13, 14, 28)
			RETURNF 1
	;山の湖
	CASE 6, 山の湖
		SIF GROUPMATCH(ARG:0, 1, 7, 10, 12, 16, 18, 22, 29)
			RETURNF 1
	;地獄の深道
	CASE 7, 地獄の深道
		SIF GROUPMATCH(ARG:0, 6, 8, 9, 17, 19, 24, 30)
			RETURNF 1
	;純狐邸中庭
	CASE 8, 1068
		SIF GROUPMATCH(ARG:0, 6, 8, 17, 19, 22, 32)
			RETURNF 1
ENDSELECT
RETURNF 0

@FISHING_SEASON(ARG:0)
#FUNCTION

SELECTCASE ARG:0
	CASE 6
		SIF DAY:2 >= 3
			RETURNF 2
	CASE 11
		SIF DAY:2 == 3
			RETURNF 2
	CASE 12
		SIF DAY:2 == 4
			RETURNF 2
	CASE 13
		SIF DAY:2 < 3
			RETURNF 2
	CASE 17
		SIF DAY:2 < 3
			RETURNF 2
	CASE 18
		SIF DAY:2 != 4
			RETURNF 2
	CASE 31
		SIF MOON_WATCH(DAY:3) == 1
			RETURNF 2
	CASEELSE
		RETURNF 1
ENDSELECT
RETURNF 0

@FISH_RATE(ARG:0)
#FUNCTION

SELECTCASE ARG:0
	CASE 1, 5, 6
		RETURNF 1
	CASE 2, 7, 8
		RETURNF 2
	CASE 3, 4, 9, 10, 16, 17
		RETURNF 3
	CASE 11 TO 14, 18 TO 22, 24
		RETURNF 4
	CASE 15, 23, 25 TO 30, 32
		RETURNF 5
	CASE 31
		RETURNF 6
ENDSELECT
RETURNF 1
