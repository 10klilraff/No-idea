;-----------------------------------------------------------
;【ニューゲーム特典】
;-----------------------------------------------------------
@NEWGAME_BONUS_SELECT
#DIMS buttonText, 10
SETCOLOR 103,228,126
PRINTFORML 【New Game Bonus】
RESETCOLOR
PRINTFORML Please choose one.
buttonText:0 '= "￥50000"
buttonText:1 '= "Increase initial favorability and reliability"
buttonText:2 '= "Increased Abilities"
buttonText:3 '= "The Millennium Stone of Mirada that Monopolizes Lust"
CALL ASK_BUTTONS(buttonText)
SELECTCASE RESULT
	CASE 0
		ニューゲーム特典 '= "￥50000"
		LOCALS = Start with ￥50000
	CASE 1
		CALL NEWGAME_BONUS_CHARA
		IF !RESULT
			RESTART
		ENDIF
		LOCALS = %ニューゲーム特典%
	CASE 2
		ニューゲーム特典 '= "初期能力アップ"
		LOCALS = Increased Abilities
	CASE 3
		ニューゲーム特典 '= "スケベセット"
		LOCALS = Start with The Millennium Stone of Mirada that Monopolizes Lust
ENDSELECT
PRINTFORMW %LOCALS% selected.
RETURN

;-----------------------------------------------------------
;初期好感度
;-----------------------------------------------------------
@NEWGAME_BONUS_CHARA
PRINTFORML The character will start with 500 favorability and 50 reliability.
PRINTFORML Please select a target.
CALL ASK_CALLNAME
IF RESULT == 999
	RETURN 0
ELSE
	ニューゲーム特典 '= TOSTR(RESULT)
	RETURN 1
ENDIF

;-----------------------------------------------------------
;特典適用
;未指定の場合は所持金
;-----------------------------------------------------------
@NEWGAME_BONUS_APPLY
#DIM C_ID
C_ID = TOINT(ニューゲーム特典)
SELECTCASE ニューゲーム特典
	CASE "なし"
	CASE ""
		MONEY += 50000
	CASE "￥50000"
		MONEY += 50000
	CASE "初期能力アップ"
		ABL:MASTER:清掃技能 = 1
		ABL:MASTER:教養 = 1
		ABL:MASTER:料理技能 = 1
		ABL:MASTER:技巧 = 2
		ABL:MASTER:話術技能 = 3
	CASE "スケベセット"
		ITEM:ミラダの石 += 1
		ITEM:ローター += 1
		ITEM:媚薬 += 5
		MAXBASE:MASTER:TSP = 500
		BASE:MASTER:TSP = MAXBASE:MASTER:TSP
		MONEY:1 = 1800
		ABL:MASTER:技巧 = 2
	CASEELSE
		CFLAG:C_ID:好感度 = 500
		CFLAG:C_ID:信頼度 = 100
		IF CHECK_CHARA(C_ID, "神霊")
			TALENT:MASTER:神霊孕ませ = 1
		ELSEIF CHECK_CHARA(C_ID, "付喪神")
			TALENT:MASTER:付喪神孕ませ = 1
		ELSEIF CHECK_CHARA(C_ID, "蓬莱人")
			TALENT:MASTER:蓬莱孕ませ = 1
		ELSEIF CHECK_CHARA(C_ID, "幽霊")
			TALENT:MASTER:幽霊孕ませ = 1
		ELSEIF CHECK_CHARA(C_ID, "人形")
			TALENT:MASTER:人形孕ませ = 1
		ENDIF
ENDSELECT

;-----------------------------------------------------------
;設定表示用テキスト化
;-----------------------------------------------------------
@NEWGAMEBONUS_TEXT
#FUNCTIONS
SELECTCASE ニューゲーム特典
	CASE ""
		RETURNF "<font color='#67e47e'>" + "Select New Game Bonus" + "</font>"
	CASE "￥50000"
		RETURNF "\\50000 start selected"
	CASE "初期能力アップ"
		RETURNF "The increase in ability will make housework and other tasks easier"
	CASE "スケベセット"
		RETURNF "Some abilities increase, and a special item is added that makes it easier to get laid faster"
	CASEELSE
		RETURNF @"Start with increased favorability and reliability with %CALLNAME:TOINT(ニューゲーム特典)%. That race will also become easier to impregnate."
ENDSELECT
