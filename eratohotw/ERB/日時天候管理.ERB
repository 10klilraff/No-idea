;FileName_COMMON.ERB ------------------------------- Rev1.00
;季節管理
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CHANGE_MONTH
#DIM おみくじ代
;日付が月を跨いだら月変更
IF DAY:3 > 31
	DAY:3 = 1
	DAY:2 ++
	;冬の次は春
	IF DAY:2 > 4
		DAY:2 = 1
		FLAG:年始貸付金 = FLAG:地主貸付金
	ENDIF
	;夏になったらバレンタインフラグリセット
	SIF DAY:2 == 2
		CVARSET CFLAG, 383
	;徴収発生
	SIF FLAG:地主
		FLAG:年貢 = 1
	IF FLAG:水やり代行
		FLAG:水やり代行 = 0
		PRINTFORMW The contract with the watering services has reached its end.
		;custom qol
		PRINTFORMW Send a renewal request?
		CALL ASK_M("Cancel",1,"Renew (￥15000)",MONEY >= 15000,"Renew (70 Charisma)",MONEY:2 >= 70)
		IF RESULT > 0
			SELECTCASE RESULT
				CASE 1
					MONEY -= 15000
				CASE 2
					MONEY:2 -= 70
			ENDSELECT
			FLAG:水やり代行 = 1
			PRINTFORMW Watering services have been renewed.
		ENDIF
	ENDIF
	IF FLAG:新聞購読
		IF MONEY < 3000 && MONEY:2 < 10
			PRINTFORML Ran out of money, so the Bunbunmaru subscription has ended.
		ELSE
			PRINTL
			PRINTFORML ★Renew the Bunbunmaru subscription? （￥3000 or 10 Charisma per month）
			;custom code, show current balance
			PRINTFORML Money:\\%金額表示(MONEY)%, Charisma:CM%三桁区切(MONEY:2)%
			CALL ASK_M("Cancel the subscription",1,"Renew (￥3000)",MONEY >= 3000,"Renew (10 Charisma)",MONEY:2 >= 10)
			SELECTCASE RESULT
				CASE 0
					PRINTFORML Stopped subscribing.
					FLAG:新聞購読 = 0
				CASE 1
					CALL PAY_MONEY_YEN(3000, 2) ;custom code
					PRINTFORML Renewed the subscription.
				CASE 2
					CALL PAY_MONEY_CM(10, 2) ;custom code
					PRINTFORML Renewed the subscription.
			ENDSELECT
		ENDIF
	ENDIF
	IF FLAG:おみくじ契約
		おみくじ代 = 50
		SIF !MAIN_MAP
			おみくじ代 = 30
		IF MONEY:2 < おみくじ代
			PRINTFORML Ran out of charisma, so the Omikuji contract has ended.
			FLAG:おみくじ契約 = 0
		ELSE
			;custom code, show cm cost and current charisma
			PRINTFORML Renew the Omikuji contract? (CM{おみくじ代}, Current Charisma:CM%三桁区切(MONEY:2)%)
			CALL ASK_YN
			IF !RESULT
				PRINTFORML The contract has been renewed.
				CALL PAY_MONEY_CM(おみくじ代, 2) ;custom code
			ELSE
				FLAG:おみくじ契約 = 0
				PRINTFORML The contract will be terminated.
			ENDIF
		ENDIF
	ENDIF
ENDIF
;異常値を固定修正
SIF DAY:3 < 1
	DAY:3 = 1
SIF DAY:2 < 1 || DAY:2 > 4
	DAY:2 = 1

;FileName_COMMON.ERB ------------------------------- Rev1.00
;時間帯管理
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CHANGE_TIMEZONE

;SELECTCASE TIME
;	CASE 240 TO 599
;		LOCAL = 1
;	CASE 600 TO 1139
;		LOCAL = 2
;	CASE 1140 TO 1440 , 0 TO 239
;		LOCAL = 3
;ENDSELECT

SELECTCASE TIME
	;AM4~6　早朝
	CASE 240 TO 359
		LOCAL = 1
	;AM6~12　正午前
	CASE 360 TO 719
		LOCAL = 2
	;PM12~3　昼
	CASE 720 TO 899
		LOCAL = 3
	;PM3~6　昼下がり
	CASE 900 TO 1079
		LOCAL = 4
	;PM6~8　夕
	CASE 1080 TO 1199
		LOCAL = 5
	;PM8~11　夜
	CASE 1200 TO 1379
		LOCAL = 6
	;PM11~AM4　深夜
	CASE 0 TO 239, IS >= 1380
		LOCAL = 7
ENDSELECT


IF LOCAL != TIME:2
	TIME:2 = LOCAL
	CALL CHANGE_WEATHER
	TRYCALLFORM MOB_PLACE_{MAIN_MAP}
	CALL TIMEZONE_MOVE
ENDIF

;FileName_COMMON.ERB ------------------------------- Rev1.00
;季節名取得
;CALL		USER
;ARG		引数がない場合DAY:2
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@GET_MONTH(ARG, ARG:1) ;custom code to return translated name on ARG:1 = 1
#FUNCTIONS
SIF !ARG
	ARG = DAY:2
;custom code
SIF ARG:1
	RETURNF SPLIT_SINGLE("Unknown/Spring/Summer/Autumn/Winter", ARG, "/")
RETURNF SPLIT_SINGLE("無の月/春の月/夏の月/秋の月/冬の月", ARG, "/")

;FileName_COMMON.ERB ------------------------------- Rev1.00
;天気名取得
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@GET_WEATHER
#FUNCTIONS
#DIMS 表示_異常気象,15
IF FLAG:異常気象
	SPLIT " /Ｃ敏感/Ｖ敏感/Ａ敏感/Ｂ敏感/Ｍ敏感/スカート/パンツ/桃色の風/台風/猛吹雪", "/", 表示_異常気象
	RETURNF 表示_異常気象:(FLAG:異常気象)
ENDIF
SIF !STRLENS(LOCALS)
	SPLIT "晴れ/快晴/薄曇/曇り/雨/大雨/霧雨/霧/雪/吹雪/細雪/霧雪/みぞれ/あられ", "/", LOCALS
RETURNF LOCALS:(TIME:5)

;-------------------------------------------------
;平日判定(ARG)
;DAYを渡して月～金曜日なら1
;-------------------------------------------------
@IS_WEEKDAY(ARG)
#FUNCTION
SIF !ARG
	ARG = DAY
RETURNF INRANGE(ARG % 7, 1, 5)

;-------------------------------------------------
;週末判定(ARG)
;DAYを渡して土日なら1
;-------------------------------------------------
@IS_WEEKEND(ARG)
#FUNCTION
SIF !ARG
	ARG = DAY
RETURNF !INRANGE(ARG % 7, 1, 5)

;-------------------------------------------------
;曜日一致判定(ARG)
;DAYと文字列を渡し、一致していれば1
;判定用文字列は日月火水木金土で各曜日を表す（＝複数指定可能）
;-------------------------------------------------
@YOUBI_MATCH(ARG, CHECK_STR)
#FUNCTION
#DIMS  CHECK_STR
SIF !ARG
	ARG = DAY
SIF STRCOUNT(CHECK_STR, GET_DAY(ARG))
	RETURNF 1
RETURNF 0

;-------------------------------------------------
;関数名:GET_DAY
;概　要:曜日を表す文字列を取得する関数
;引　数:引数がない場合DAY
;戻り値:曜日を表す文字列(日/月/火/水/木/金/土)
;備　考:文中関数
;-------------------------------------------------
@GET_DAY(ARG)
#FUNCTIONS
#DIMS CONST YOUBI = "日", "月", "火", "水", "木", "金", "土"
SIF !ARG
	ARG = DAY
RETURNF YOUBI:(ARG % 7)

;FileName_COMMON.ERB ------------------------------- Rev1.00
;天気管理
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CHANGE_WEATHER
#DIM 地底
#DIM 入手パンツ
#DIM 持ち主
#DIM ループ開始
#DIM ループ終了
#DIM ループ加算
DEBUGPRINTL 【CHANGE_WEATHER】CALL CHANGE_WATHER
;天候管理にメッセージ表示処理させてすみましぇん＞＜；
;うふふ中は空気を読む程度の能力
;天候変化しなくても虹は消える
地底 = 0
IF CFLAG:MASTER:デート中 == 9
	地底 = 1
ELSEIF CFLAG:MASTER:現在位置 == OMANEKIBEYA() && GROUPMATCH(CFLAG:MASTER:お招き, 36, 37, 38, 49, 60, 64, 83, 84)
	地底 = 1
ENDIF
TIME:7 = 0
IF !地底
	SELECTCASE FLAG:異常気象
		CASE 7
			;こうでもしないとまたノーパンが飛んできてしまうと思うの
			DO
				CALL RAND_CHARASELECT("主人公以外のキャラリスト")
				持ち主 = RESULT
				入手パンツ = 今日のぱんつ(持ち主)
			LOOP !入手パンツ
			PRINTFORML A whirlwind blows past carrying %CALLNAME:持ち主%'s %PANTSNAME(入手パンツ,持ち主)%.
			CALL ASK_YN("Pick them up","Leave them")
			IF !RESULT
				PRINTFORMW %PARSE("You pick")% up %CALLNAME:持ち主%'s %PANTSNAME(入手パンツ,持ち主)%.
				CALL PANTS_GETTING(持ち主, 入手パンツ)
				;addition custom code, grant some grace period for girls in the same room
				FOR LOCAL, 1, CHARANUM
					SIF CFLAG:LOCAL:現在位置 != CFLAG:MASTER:現在位置
						CONTINUE
					CFLAG:LOCAL:ディレイ = 10
				NEXT
				;------------------------------------------------------------------------
			ELSE
				PRINTFORML The %PANTSNAME(入手パンツ,持ち主)% flew away.
			ENDIF
		CASE 8
			SETCOLOR 255,100,255
			FONTBOLD
			PRINTFORMW A pink wind blows through Gensokyo!
			FOR LOCAL,1,CHARANUM
				SIF CFLAG:LOCAL:出禁 ;addition custom code, prevent disabled characters from receiving pink wind effect
					CONTINUE
				IF !RAND:8 && !TALENT:LOCAL:妊娠
					PRINTFORML %CALLNAME:LOCAL% is engulfed by the pink wind and feels strange...
					CFLAG:LOCAL:溜まってる度 += LIMIT((10 + RAND:20 + RAND:20 + RAND:20) * ABL:LOCAL:欲望,200,1000)
					SIF !TALENT:LOCAL:人形
						TCVAR:LOCAL:発情 = 1
				ENDIF
			NEXT
			RESETCOLOR
			FONTREGULAR
	ENDSELECT
ENDIF

SIF FLAG:異常気象
	RETURN
SIF TIME:5 > 15
	TIME:5 = 0
;天候変化確率（標準は50%）
SELECTCASE TIME:5
	CASE 5
		LOCAL = 16
	CASE 9
		IF DAY:2 == 2
			LOCAL = 12
		ELSE
			LOCAL = 16
		ENDIF
	CASE IS >= 8
		IF DAY:2 == 4
			LOCAL = 24
		ELSEIF DAY:2 == 2
			LOCAL = 12
		ELSE
			LOCAL = 16
		ENDIF
	CASEELSE
		LOCAL = 24
ENDSELECT

IF RAND:LOCAL + 1 <= 12 && CFLAG:MASTER:現在位置 != 天界
	;天候変化発生で虹は消える
	;TIME:7 = 0
	SELECTCASE TIME:5
		;晴れの時
		CASE 0
			;20%で晴天に移行
			IF RAND:5 == 0
				SETBIT TIME:5,0
			;20%で薄曇に移行
			ELSEIF RAND:5 == 0
				SETBIT TIME:5,1
				SIF !CFLAG:MASTER:うふふ && !地底
					PRINTW The weather got a bit cloudy.
			;5%で曇りに移行
			ELSEIF RAND:20 == 0
				TIME:5 = 3
				SIF !CFLAG:MASTER:うふふ && !地底
					PRINTW The weather became cloudy.
			;上記確率に引っかからなかったら晴れ
			ELSE
				TIME:5 = 0
			ENDIF
		CASE 1
			;20%で晴れに移行
			IF RAND:5 == 0
				CLEARBIT TIME:5,0
			;20%で薄曇に移行
			ELSEIF RAND:5 == 0
				TIME:5 = 2
				SIF !CFLAG:MASTER:うふふ && !地底
					PRINTW The weather got a bit cloudy.
			ENDIF
		;曇りの時
		CASE 2,3
			;25%で強化or弱体化
			IF RAND:4 == 0
				INVERTBIT TIME:5,0
			ELSE
				;20%で晴れに移行
				IF RAND:5 == 0
					TIME:5 = 0
					IF !CFLAG:MASTER:うふふ && !地底
						IF TIME:2 >= 5 && TIME:2 <= 7
							PRINTW The stars have come out.
						ELSE
							PRINTW The sun is shining.
						ENDIF
					ENDIF
				;薄曇なら10%で雨か雪に移行
				ELSEIF TIME:5 == 2 && RAND:10 == 0
					IF DAY:2 == 4 && RAND:5
						TIME:5 = 8
						SIF !CFLAG:MASTER:うふふ && !地底
							PRINTW Snow is falling!
					ELSE
						TIME:5 = 4
						SIF !CFLAG:MASTER:うふふ && !地底
							PRINTW It's raining!
					ENDIF
					;更に10%の確率で霧雨,細雪に移行
					SIF RAND:10 == 0
						SETBIT TIME:5,1
				;曇りなら30%で雨に移行
				ELSEIF TIME:5 == 3 && RAND:10 < 3
					IF DAY:2 == 4 && RAND:5
						TIME:5 = 8
						SIF !CFLAG:MASTER:うふふ && !地底
							PRINTW Snow is falling!
					ELSE
						TIME:5 = 4
						SIF !CFLAG:MASTER:うふふ && !地底
							PRINTW It's raining!
					ENDIF
				ENDIF
			ENDIF
		;雨の時
		CASE 4,5,6,7
			;25%で強化or弱体化
			IF RAND:4 == 0
				INVERTBIT TIME:5,0
				IF TIME:5 == 5
					PRINTW The rain became a downpour!
				ELSE
					PRINTW The rain got weaker.
				ENDIF
			ELSE
				;25%で曇りに移行
				IF RAND:4 == 0
					TIME:5 = 3
					SIF !CFLAG:MASTER:うふふ && !地底
						PRINTW It seems to have stopped raining.
					;念のため相合い傘を解除
					FOR LOCAL,0,CHARANUM
						SIF TEQUIP:LOCAL:201
							TEQUIP:LOCAL:201 = 0
					NEXT
				;雨なら10%で霧雨かみぞれに移行
				ELSEIF TIME:5 == 4 && RAND:10 == 0
					IF DAY:2 == 4
						TIME:5 = 12
						SIF !CFLAG:MASTER:うふふ && !地底
							PRINTW Some snow got mixed into the rain.
					ELSE
						TIME:5 = 6
						SIF !CFLAG:MASTER:うふふ && !地底
							PRINTW The rain is clearing up.
					;更に10%の確率で霧,あられに移行
					SIF RAND:10 == 0
						SETBIT TIME:5,0
					ENDIF
				;10%で晴れに移行
				ELSEIF RAND:10 == 0
					TIME:5 = 0
					IF !CFLAG:MASTER:うふふ && !地底
						;かもしれないといいつつ確実に見えるのは気分をよくさせる魔法
						IF TIME:2 >= 5 && TIME:2 <= 7
							PRINTW Suddenly a starry visa appears! Even a lunar rainbow can be seen!
						ELSE
							PRINTW Suddenly the sun comes out! Even a rainbow can be seen!
						ENDIF
					FOR LOCAL,0,CHARANUM
						SIF TEQUIP:LOCAL:201
							TEQUIP:LOCAL:201 = 0
					NEXT
					ENDIF
					;更に低確率で快晴
					SIF RAND:100 < 5
						TIME:5 = 1
					;雨→晴れを処理したら虹が出る
					TIME:7 = 1
					;市場が開かれる
					FLAG:市場の神 = 1
				ENDIF
			ENDIF
		;雪の時
		CASE IS >= 8
			;雪は冬以外降らない
			IF DAY:2 != 4
				;雨になる
				CLEARBIT TIME:5,3
				SETBIT TIME:5,2
				SIF !CFLAG:MASTER:うふふ && !地底
					PRINTW The snow turned into rain.
			;10%で強化
			ELSEIF !GETBIT(TIME:5,0) && RAND:10 == 0
				SETBIT TIME:5,0
				PRINTW It started snowing heavily!
			;50%の確率で弱体化
			ELSEIF GETBIT(TIME:5,0) && RAND:2
				CLEARBIT TIME:5,0
				PRINTW The snow storm died down.
			
			ELSE
				;25%で曇りに移行
				IF RAND:4 == 0
					TIME:5 = 3
					SIF !CFLAG:MASTER:うふふ && !地底
						PRINTW It stopped snowing.
					FOR LOCAL,0,CHARANUM
						SIF TEQUIP:LOCAL:201
							TEQUIP:LOCAL:201 = 0
					NEXT
				;雪なら10%で細雪かみぞれに移行
				ELSEIF TIME:5 == 8 && RAND:10 == 0
					IF RAND:2
						TIME:5 = 10
						SIF !CFLAG:MASTER:うふふ && !地底
							PRINTW The snow is starting to clear up.
					ELSE
						TIME:5 = 12
						SIF !CFLAG:MASTER:うふふ && !地底
							PRINTW Some rain got mixed into the snow.
					ENDIF
					;更に10%の確率で霧雪,あられに移行
					SIF RAND:10 == 0
						SETBIT TIME:5,0
				;10%で晴れに移行
				ELSEIF RAND:10 == 0
					TIME:5 = 0
					IF !CFLAG:MASTER:うふふ && !地底
						IF TIME:2 >= 5 && TIME:2 <= 7
							PRINTW Suddenly a starry visa appears! Even some diamond dust can be seen!
						ELSE
							PRINTW Suddenly the sun comes out! Even some diamond dust can be seen!
						ENDIF
					FOR LOCAL,0,CHARANUM
						SIF TEQUIP:LOCAL:201
							TEQUIP:LOCAL:201 = 0
					NEXT
					ENDIF
					;更に低確率で快晴
					SIF RAND:100 < 5
						TIME:5 = 1
					;雪→晴れを処理したらダイヤモンドダストが出る
					TIME:7 = 2
				ENDIF
			ENDIF
		CASEELSE
			TIME:5 = RAND:5
	ENDSELECT
ENDIF
[SKIPSTART]
@現在気温設定
;高低差を５分割
SELECTCASE TIME
	;PM11~AM4
	CASE 0 TO 299, IS >= 1380
		LOCAL = FLAG:62
	;AM5~7
	CASE 300 TO 479
		LOCAL = FLAG:62 + ((FLAG:61 - FLAG:62) / 5)
	;AM8~9
	CASE 480 TO 599
		LOCAL = FLAG:62 + ((FLAG:61 - FLAG:62) / 5 * 2)
	;AM10~11
	CASE 600 TO 719
		LOCAL = FLAG:62 + ((FLAG:61 - FLAG:62) / 5 * 3)
	;PM12~2
	CASE 720 TO 899
		LOCAL = FLAG:62 + ((FLAG:61 - FLAG:62) / 5 * 4)
	;PM3~5
	CASE 900 TO 1079
		LOCAL = FLAG:61
	;PM6~7
	CASE 1080 TO 1199
		LOCAL = FLAG:62 + ((FLAG:61 - FLAG:62) / 5 * 4)
	;PM8~9
	CASE 1200 TO 1259
		LOCAL = FLAG:62 + ((FLAG:61 - FLAG:62) / 5 * 2)
	;PM9~10
	CASE 1260 TO 1379
		LOCAL = FLAG:62 + ((FLAG:61 - FLAG:62) / 5)
ENDSELECT
;日が出ていないと温度低下
SELECTCASE TIME
	;AM5~PM7
	CASE 300 TO 1199
		SIF TIME:5 > 1
			LOCAL -= ((FLAG:61 - FLAG:62) / 5)
ENDSELECT
;----------------------------------------------------------------
@年間基礎最高気温(ARG, ARG:1)
#FUNCTION
;ARG   指定の月
;ARG:1 指定の日
SELECTCASE ARG
	CASE 1
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 17
			CASE 4, 5
				RETURNF 18
			CASE 6 TO 8
				RETURNF 19
			CASE 9 TO 11
				RETURNF 20
			CASE 12 TO 14
				RETURNF 21
			CASE 15 TO 18
				RETURNF 22
			CASE 19 TO 22
				RETURNF 23
			CASE 23 TO 27
				RETURNF 24
			CASE 28 TO 30
				RETURNF 25
		ENDSELECT
	CASE 2
		SELECTCASE ARG:1
			CASE 1, 2
				RETURNF 25
			CASE 3 TO 7
				RETURNF 26
			CASE 8 TO 13
				RETURNF 27
			CASE 14 TO 17
				RETURNF 28
			CASE 18 TO 22
				RETURNF 29
			CASE 23 TO 28
				RETURNF 30
			CASE 29, 30
				RETURNF 31
		ENDSELECT
	CASE 3
		SELECTCASE ARG:1
			CASE 1, 2
				RETURNF 31
			CASE 3, 4
				RETURNF 30
			CASE 5 TO 7
				RETURNF 29
			CASE 8 TO 10
				RETURNF 28
			CASE 11, 12
				RETURNF 27
			CASE 13, 14
				RETURNF 26
			CASE 15, 16
				RETURNF 25
			CASE 17 TO 19
				RETURNF 24
			CASE 20 TO 22
				RETURNF 23
			CASE 23, 24
				RETURNF 22
			CASE 25 TO 27
				RETURNF 21
			CASE 28, 29
				RETURNF 20
			CASE 30
				RETURNF 19
		ENDSELECT
	CASE 4
		SELECTCASE ARG:1
			CASE 1 TO 4
				RETURNF 19
			CASE 5 TO 7
				RETURNF 18
			CASE 8 TO 10
				RETURNF 17
			CASE 11, 12
				RETURNF 16
			CASE 13, 14
				RETURNF 15
			CASE 15, 16
				RETURNF 14
			CASE 17, 18
				RETURNF 13
			CASE 19, 20
				RETURNF 12
			CASE 21, 22
				RETURNF 11
			CASE 23 TO 25
				RETURNF 10
			CASE 26, 27
				RETURNF 9
			CASE 28, 29
				RETURNF 8
			CASE 30
				RETURNF 7
		ENDSELECT
	CASE 5
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 7
			CASE 4 TO 9
				RETURNF 6
			CASE 10 TO 14
				RETURNF 5
			CASE 15 TO 21
				RETURNF 4
			CASE 22 TO 28
				RETURNF 5
			CASE 29, 30
				RETURNF 6
		ENDSELECT
	CASE 6
		SELECTCASE ARG:1
			CASE 1 TO 6
				RETURNF 6
			CASE 7 TO 12
				RETURNF 7
			CASE 13, 14
				RETURNF 8
			CASE 15, 16
				RETURNF 9
			CASE 17, 18
				RETURNF 10
			CASE 19, 20
				RETURNF 11
			CASE 21, 22
				RETURNF 12
			CASE 23, 24
				RETURNF 13
			CASE 25, 26
				RETURNF 14
			CASE 27, 28
				RETURNF 15
			CASE 29
				RETURNF 16
			CASE 30
				RETURNF 17
		ENDSELECT
ENDSELECT

;----------------------------------------------------------------
@年間基礎最低気温(ARG, ARG:1)
#FUNCTION
;ARG   指定の月
;ARG:1 指定の日
SELECTCASE ARG
	CASE 1
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 11
			CASE 4 TO 6
				RETURNF 12
			CASE 7, 8
				RETURNF 13
			CASE 9 TO 11
				RETURNF 14
			CASE 12 TO 14
				RETURNF 15
			CASE 15 TO 17
				RETURNF 16
			CASE 18 TO 20
				RETURNF 17
			CASE 21 TO 23
				RETURNF 18
			CASE 24 TO 27
				RETURNF 19
			CASE 28 TO 30
				RETURNF 20
		ENDSELECT
	CASE 2
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 20
			CASE 4 TO 7
				RETURNF 21
			CASE 8 TO 10
				RETURNF 22
			CASE 11 TO 14
				RETURNF 23
			CASE 15 TO 22
				RETURNF 24
			CASE 23 TO 30
				RETURNF 25
		ENDSELECT
	CASE 3
		SELECTCASE ARG:1
			CASE 1 TO 5
				RETURNF 25
			CASE 6 TO 9
				RETURNF 24
			CASE 10 TO 12
				RETURNF 23
			CASE 13, 14
				RETURNF 22
			CASE 15 TO 17
				RETURNF 21
			CASE 18 TO 20
				RETURNF 20
			CASE 21 TO 23
				RETURNF 19
			CASE 24, 25
				RETURNF 18
			CASE 26
				RETURNF 17
			CASE 27, 28
				RETURNF 16
			CASE 29
				RETURNF 15
			CASE 30
				RETURNF 14
		ENDSELECT
	CASE 4
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 14
			CASE 4, 5
				RETURNF 13
			CASE 6, 7
				RETURNF 12
			CASE 8, 9
				RETURNF 11
			CASE 10, 11
				RETURNF 10
			CASE 12 TO 14
				RETURNF 9
			CASE 15 TO 17
				RETURNF 8
			CASE 18 TO 20
				RETURNF 7
			CASE 21 TO 23
				RETURNF 6
			CASE 24 TO 26
				RETURNF 5
			CASE 27 TO 29
				RETURNF 4
			CASE 30
				RETURNF 3
		ENDSELECT
	CASE 5
		SELECTCASE ARG:1
			CASE 1, 2
				RETURNF 3
			CASE 3 TO 6
				RETURNF 2
			CASE 7 TO 10
				RETURNF 1
			CASE 11 TO 13
				RETURNF 0
			CASE 14 TO 21
				RETURNF -1
			CASE 22 TO 30
				RETURNF 0
		ENDSELECT
	CASE 6
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 0
			CASE 4 TO 6
				RETURNF 1
			CASE 7, 8
				RETURNF 2
			CASE 9, 10
				RETURNF 3
			CASE 11 TO 14
				RETURNF 4
			CASE 15, 16
				RETURNF 5
			CASE 17, 18
				RETURNF 6
			CASE 19, 20
				RETURNF 7
			CASE 21 TO 23
				RETURNF 8
			CASE 24, 25
				RETURNF 9
			CASE 26 TO 28
				RETURNF 10
			CASE 29, 30
				RETURNF 11
		ENDSELECT
ENDSELECT
[SKIPEND]

;定時遭遇位置再設定
@TIMEZONE_MOVE
#DIMS 移動前
#DIMS 移動後
FOR LOCAL,1,CHARANUM
	SIF 出現状態(LOCAL) != 1
		CONTINUE
	SIF !CHARA_HOLIDAY(LOCAL)
		CONTINUE
	SIF CFLAG:LOCAL:お招き
		CONTINUE
	SIF CFLAG:LOCAL:同行 || CHK_DATENOW(CFLAG:LOCAL:デート中)
		CONTINUE
	SIF !CFLAG:LOCAL:前ターン位置
		CONTINUE
	SIF CFLAG:LOCAL:睡眠
		CONTINUE
	;あなたと同じ場所にいるときは確率が下がる
	IF CFLAG:MASTER:現在位置 == CFLAG:LOCAL:現在位置
		IF TALENT:LOCAL:恋慕
			;恋慕は5分の1
			SIF RAND:5
				CONTINUE
		ELSEIF TALENT:LOCAL:思慕
			;思慕は4分の1
			SIF RAND:4
				CONTINUE
		ELSE
			;それ以外は3分の1
			SIF RAND:3
				CONTINUE
		ENDIF
	ELSE
		;見てない場所なら2分の1
		SIF RAND:2
			CONTINUE
	ENDIF
	CALL 遭遇フラグ決定(LOCAL)
NEXT
;全キャラ決定後、連動キャラを設定する
FOR LOCAL, 1, CHARANUM
	VARSET RESULT
	CALLFORM CHARAMOVE_DATA_{LOCAL}(1)
	;依存連動
	IF RESULT
		CFLAG:LOCAL:遭遇位置 = CFLAG:RESULT:遭遇位置
	;対等連動
	ELSEIF RESULT:1
		;連動設定する
		CFLAG:LOCAL:遭遇位置 = CFLAG:連動キャラ設定(RESULT:1, RESULT:2, RESULT:3):遭遇位置
	ENDIF
NEXT
