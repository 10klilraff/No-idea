@EXIST_FreeAct16
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
@FreeAct16(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
#DIM 経過時間
経過時間 = Activity_Duration:TARGET
SELECTCASE O_DATA
;自由行動に付き合うコマンドの名称
CASE "名称"
	IF GROUPMATCH(TARGET, 5, 6, 7, 14, 111)
		CALLF MAKE_STR(V_NAME, "いたずらに付き合う")
	ELSE
		;custom code - change the name because the other action has the same name
		CALLF MAKE_STR(V_NAME, "様子をうかがう2")
	ENDIF
CASE "CHAIN"
;他キャラに連鎖しない場合コメントアウトすること
	;CALLF MAKE_INT(V_NAME, 1)
CASE "経過時間"
;自由行動に付き合うコマンドでの経過時間
;0だと30分か残り時間すべての小さい方
	CALLF MAKE_INT(V_NAME, 10)
ENDSELECT

@自由行動内容_16(CHARA, ARGS, 時間係数, CHAIN)
#DIM CHARA
#DIM CHAIN
#DIM 現在地
#DIM メッセージ表示
#DIM 時間係数
#DIM 経過時間
#DIM 仕掛人 = 5, 6, 7, 14, 111
#DIM 発起者
#DIM 記録, 200
#DIM 落下者
#DIM ランク
#DIM リセット
現在地 = CFLAG:CHARA:現在位置
メッセージ表示 = 現在地 == CFLAG:MASTER:現在位置 ? 1 # 0
IF リセット != DAY
	VARSET 記録
	発起者 = 0
	落下者 = 0
	リセット = DAY
ENDIF
SELECTCASE ARGS
	CASE "発生"
		;屋外で発生
		SIF !OUTROOF(現在地)
			RETURN 0
		;仕事予定あれば落とし穴を掘らない
		SIF MATCH(仕掛人, CHARA, 0) && !CHARA_HOLIDAY(CHARA)
			RETURN 0
		;三妖精とチルノ・クラウンピースが起点
		IF MATCH(仕掛人, CHARA, 0) && !(発起者 || FA16_HOLE:0)
			発起者 = CHARA
			FA16_HOLE:0 = 現在地
			GOTO ACT
		ENDIF
		;すでに落とし穴があれば一回だけ参加
		IF MATCH(仕掛人, CHARA, 0)
			SIF 現在地 != FA16_HOLE:0 || 記録:CHARA
				RETURN 0
			GOTO ACT
		ENDIF
		
		;落とし穴に落ちる側判定
		SIF !発起者 || 落下者
			RETURN 0
		SIF 記録:CHARA || FA16_HOLE:0 != 現在地
			RETURN 0
		ランク = CSVABL(CHARA, GETNUM(ABL, "戦闘能力"))
		;原作の強キャラに回避補正
		SIF ランク > 4 && !RAND:4
			RETURN 0
		SIF メッセージ表示
			PRINTFORMW %CALLNAME:CHARA% falls down into %CALLNAME:発起者%'s pitfall!!!
		記録:CHARA = 1
		落下者 = CHARA
		;引っかかった人と参加者の行動時間を1に設定
		FOR COUNT, 0, 5
			SIF 記録:(仕掛人:COUNT) != 2 || Activity_Type:(仕掛人:COUNT) != 16 || !Activity_Duration:(仕掛人:COUNT)
				CONTINUE
			Activity_Duration:(仕掛人:COUNT) = 1
		NEXT
		RETURN 1
		
		;落とし穴掘る側処理
		$ACT
		記録:CHARA = 2
		IF メッセージ表示
			PRINTFORMW %CALLNAME:CHARA% \@ CHARA == 発起者 ? starts digging up a pitfall %SPLIT_G("to do a little bit of mischief:for a prank:for amusement purposes")% # begins working on %CALLNAME:発起者%'s pitfall together \@...
			SIF !FA16_HOLE:1
				FA16_HOLE:1 = 1
		ENDIF
		;自由行動にかける時間をRETURNする
		SIF CHARA == 発起者
			RETURN 40 + RAND:30
		SIF !Activity_Duration:発起者
			RETURN 30 + RAND:20
		RETURN Activity_Duration:発起者
	CASE "行動中"
		;行動中の処理、地の文もここで
		SIF !メッセージ表示
			RETURN
		IF 落下者
			SELECTCASE ランク
				CASE IS <= 1
					PRINTFORMW %CALLNAME:CHARA% is %TEXTR(@"giggling gleefully/skipping around excitedly/striking a dramatic triumphant pose/beaming with glee")%.
				CASE 2, 3, 4
					PRINTFORMW %CALLNAME:CHARA% is %TEXTR(@"barely managing to stifle a laugh/sneaking a triumphant pose/sneering smugly")%.
				CASEELSE
					PRINTFORMW %CALLNAME:CHARA%%TEXTR(@" is getting ready to make %HIS_HER(CHARA)% escape/'s face is going pale")%...
			ENDSELECT
		ELSE
			PRINTFORMW %CALLNAME:CHARA% is %TEXTR(@"hiding the dirt %HE_SHE(CHARA)% dug up from the pitfall/working on the pitfall, covering it with \@ DAY:2 == 4 ? snow # fallen leaves \@/making more pitfalls")%.
		ENDIF
	CASE "完了"
		;行動時間を使い切ったときの処理、地の文もここで
		IF !MATCH(仕掛人, CHARA, 0)
			IF メッセージ表示
				SELECTCASE ランク
					CASE IS <= 1
						PRINTFORMW %CALLNAME:CHARA% %TEXTR(@"whirls around in surprise/tumbles down/lies on %HIS_HER(TARGET)% back")% for a while before scrambling to %HIS_HER(CHARA)% feet and frantically crawling out of the pit...
					CASE 2, 3, 4
						PRINTFORMW %CALLNAME:CHARA% quickly jumps out of the pit and %TEXTR(@"starts looking for the culprit/starts scanning the surroundings")%...
					CASEELSE
						PRINTFORML %CALLNAME:CHARA% smoothly emerges from the pit with a soft swoosh.
						PRINTFORMW %TEXTR(@"The aura about %HIM_HER(CHARA)% seems quite dreadful indeed/\@ TALENT:CHARA:機嫌 == 1 ? The fact that %HE_SHE(CHARA)%'s still maintaining %HIS_HER(CHARA)% smile makes it # The absolute lack of emotion on %HIS_HER(CHARA)% face looks \@ all the more terrifying/")%...
				ENDSELECT
			ENDIF
		ELSEIF 落下者
			IF メッセージ表示
				SELECTCASE ランク
					CASE IS <= 1
						PRINTFORMW %CALLNAME:CHARA% %TEXTR(@"pulls %CALLNAME:落下者% up/apologizes to %CALLNAME:落下者% with a laugh")%.
					CASE 2, 3, 4
						PRINTFORMW %CALLNAME:CHARA% tries %HIS_HER(CHARA)% best %TEXTR(@"feigning ignorance/playing stupid/not to laugh")%.
					CASEELSE
						PRINTFORMW %CALLNAME:CHARA% is %TEXTR(@"breaking out in a cold sweat/lying low/in hiding/cowering in fear")%...
				ENDSELECT
			ENDIF
		ELSEIF メッセージ表示
			PRINTFORMW \@ FA16_HOLE:1 == 2 ? No one but %PARSE("you")% fell for it # No one else would fall for it \@, so %CALLNAME:CHARA% %SPLIT_G(@"quickly gets bored:decides %HE_SHE(CHARA)% had enough:no longer finds it amusing:seems to have tired of it")%.
		ENDIF
		Activity_Type:CHARA = 0
		SIF !落下者
			RETURN
		FOR COUNT, 0, 5
			SIF Activity_Type:(仕掛人:COUNT) == 16 && FA16_HOLE:0 == CFLAG:(仕掛人:COUNT):現在位置
				RETURN
		NEXT
		落下者 = 0
		RETURN
	CASE "中断"
		;中断メッセージはうふふ突入時等でも表示されてしまうので表示しない
	CASE "付き合う"
		;自由行動に付き合ったとき用メッセージ
		IF 落下者 || !MATCH(仕掛人, TARGET, 0)
			SIF !MATCH(仕掛人, TARGET, 0)
				落下者 = TARGET
			PRINTFORM %PARSE("You")%\@ TARGET != 落下者 ? %" "%and %CALLNAME:TARGET% # \@ 
			SELECTCASE ランク
				CASE IS <= 1
					PRINTFORML \@TARGET != 落下者?%PARSE("peek")%#peek\@ into the pitfall where %CALLNAME:落下者% had fallen into.
					PRINTFORML %CALLNAME:落下者% can be seen %TEXTR(@"slumped on %HIS_HER(落下者)% butt/upside down with %HIS_HER(落下者)% \@ EQUIP:下半身下着２ ? panties # naked crotch\@ exposed/passed out")% at the bottom of the pit.
				CASE 2, 3, 4
					PRINTFORML \@ TARGET != 落下者 ? %PARSE("take")% a sneak peek at # check out \@ the pitfall.
					PRINTFORML %CALLNAME:落下者% %SPLIT_G(@"screams out curses:curses %HIS_HER(落下者)% luck aloud:is cussing away loudly")% at the bottom of the pit.
				CASEELSE
					PRINTFORML cautiously \@TARGET != 落下者?%PARSE("approach")%#approach\@ the pitfall where %CALLNAME:落下者% had fallen into.
					PRINTFORMW There's an uncontrollable rush of fierce bloodlust rising from the bottom of the pit...
			ENDSELECT
		ELSE
			PRINTFORML %PARSE("You")% %TEXTR(@"%PARSE("help")% %CALLNAME:TARGET% dig another pitfall/%PARSE("help")% %CALLNAME:TARGET% make the pitfall even deeper/%PARSE("work")% on the pitfall with %CALLNAME:TARGET% together, helping to make it less conspicuous")%.
			SIF !FA16_HOLE:1
				FA16_HOLE:1 = 1
		ENDIF
	CASE "ソース"
		;付き合うコマンド選択時の入手ソース、時間係数は付き合った時間の1/10
		IF CHARA == 落下者
			SOURCE:鬱屈 += 50 + ランク * 50
			SOURCE:苦痛 += 350 - ランク * 50
		ELSE
			SOURCE:歓楽 = 20 + ((ABL:親密 * 15)) * 時間係数
			SOURCE:受動 = 10 + ((ABL:従順 * 10)) * 時間係数
		ENDIF
ENDSELECT
