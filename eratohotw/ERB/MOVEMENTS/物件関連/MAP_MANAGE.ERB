;------------------------------------------
;	引っ越しの表示と処理
;------------------------------------------
@SET_MAINHOME
#DIM CHK_TRAINING
#DIM CHK_GARDENING
#DIM 設定前MAIN_MAP
#DIM 設定前酒虫
#DIM 設定前初期位置
#DIM 設定前大家
#DIM ANIMATION_SEED

ANIMATION_SEED = 0

;Map Animation Code
$MAPANIMATION
LINESTART = LINECOUNT
SIF ANIMATERECOLOREDMAPS && !FLAG:70
	ANIMATION_SEED ++
;
;
;メンバーの起床時間再設定
;
IF !引っ越し
	IF ゲーム開始処理中 || ゲーム引き継ぎ処理中
		MAIN_MAP = 0
		TRYCALLFORM ROOMSETTING_{MAIN_MAP}
	ENDIF
	設定前MAIN_MAP = MAIN_MAP
	設定前酒虫 = 拠点_酒虫
	設定前初期位置 = デフォルト初期位置
	設定前大家 = 拠点_大家
	引っ越し = 1
ENDIF
IF FLAG:一時滞在
	PRINTFORMW Unable to move when a person is staying over.
	RETURN
ENDIF
DRAWLINE
;Color map
IF RECOLOREDMAPS
	CALL DRAW_COLOREDMAP(ANIMATION_SEED, "HIKKOSHI")
ELSE
	CALL DRAW_MAP("HIKKOSHI")
ENDIF
DRAWLINE
PRINTFORML ■%GET_MAPNAME(MAIN_MAP)%
CALL 物件データ(MAIN_MAP)
PRINTFORM Foraging spots: 
CALL ShowGatheringSpots(MAIN_MAP)
PRINTFORML 
FONTITALIC
CALLFORM マップ紹介_{MAIN_MAP}
FONTREGULAR
PRINTL 
DRAWLINEFORM =
PRINT Please select a place to stay. 
PRINTBUTTON @"[Switch Preview Map({TFLAG:マップ切り替え})]", 1997
PRINTL 
;custom code
LOCAL:1 = 0
FOR LOCAL, 0, 11
	SIF MAIN_MAP == LOCAL
		SETCOLOR C_AQUA
	IF GET_EXISTMAP(LOCAL)
		PRINTFORM [{LOCAL + 2000}] %GET_MAPNAME(LOCAL), 28, LEFT%
		SIF (++LOCAL:1) % 3 == 0
			PRINTFORML 
	ENDIF
	;SIF GROUPMATCH(LOCAL, 4, 9, 10)
	;	PRINTL 
	RESETCOLOR
NEXT
SIF LOCAL:1 % 3 > 0
	PRINTFORML
;-----------
PRINT [1999] Choose This One 
SIF !ゲーム開始処理中 && !ゲーム引き継ぎ処理中
	PRINT [1998] Cancel
PRINTL  
;Map Animation Code
IF ANIMATERECOLOREDMAPS && !FLAG:70
	CURRENTREDRAW
	ORIREDRAW = RESULT
	REDRAW ORIREDRAW
	TINPUT ANIMATERECOLOREDMAPS,99999,0,""
ELSE
	INPUT
ENDIF
;拠点切り替え
IF RESULT == 99999
	REDRAW 0
	CLEARLINE LINECOUNT - LINESTART
	GOTO MAPANIMATION
ELSEIF RESULT >= 2000 && GET_EXISTMAP(RESULT - 2000)
	MAIN_MAP = RESULT - 2000
	CALLFORM ROOMSETTING_{MAIN_MAP}
	CALLFORM DEFAULT_OWNER_{MAIN_MAP}
	TFLAG:マップ切り替え = 1
	RESTART
;プレビュー切り替え
ELSEIF RESULT == 1997
	TFLAG:マップ切り替え += 1
	SIF TFLAG:マップ切り替え > MAP_PAGES
		TFLAG:マップ切り替え = 1
	RESTART
;キャンセル
ELSEIF RESULT == 1998 && !ゲーム開始処理中 && !ゲーム引き継ぎ処理中
	引っ越し = 0
	MAIN_MAP = 設定前MAIN_MAP
	拠点_酒虫 = 設定前酒虫
	デフォルト初期位置 = 設定前初期位置
	拠点_大家 = 設定前大家
	TFLAG:マップ切り替え = 0
	CALLFORM ROOMSETTING_{MAIN_MAP}
	RETURN
;決定
ELSEIF RESULT == 1999
	SIF 設定前MAIN_MAP == MAIN_MAP
	CALLFORM DEFAULT_OWNER_{MAIN_MAP}
ELSE
	RESTART
ENDIF
;決定後
;FLAG:おみくじ契約 = 0
CALL Qol_MoveIn_Omikuji ;qol custom code
TFLAG:マップ切り替え = 0
CALL SET_ENKAI_LOCATIONDISPLAY
CALL MAP_DEFAULTRESIDENT(MAIN_MAP)
;施錠
VARSET LOCK

FOR LOCAL,1,CHARANUM
	CALL CHARA_SLEEP, LOCAL, 0
NEXT
CHK_TRAINING = 0
CHK_GARDENING = 0
FOR LOCAL,1,100
	SIF ROOMDATA:LOCAL & 場所_訓練
		CHK_TRAINING = 1
	SIF ROOMDATA:LOCAL & 場所_菜園
		CHK_GARDENING = 1
NEXT
SIF !CHK_TRAINING
	CALL COLORMESSAGE(@"[Combat Training] is not available in this map.",C_YELLOW,2)
SIF !CHK_GARDENING
	CALL COLORMESSAGE(@"[Kitchen Garden] is not available in this map.",C_YELLOW,2)
SIF FLAG:なりきり && CFLAG:(FLAG:なりきり):出禁 && MAIN_MAP == GET_MAPID(CSVCFLAG(FLAG:なりきり,311))
	CFLAG:MASTER:初期位置 = CSVCFLAG(FLAG:なりきり,311)
;罠ポイントの張替え
CALL TRAP_MOVING

@HAS_DUPLICATE_KEY, ARG
#FUNCTION
FOR LOCAL, 1, CHARANUM
	IF CFLAG:LOCAL:311 == ARG
		SIF TALENT:LOCAL:恋慕
			RETURNF 1
	ENDIF
NEXT

@MAP_DEFAULTRESIDENT(ARG, アップデート中)
#DIM アップデート中
;ARG=マップ番号
SIF !アップデート中
	FLAG:住み込みキャラ = 0
MAIN_MAP = ARG
FOR LOCAL, 1, CHARANUM
	IF MAP_住人(MAIN_MAP, LOCAL)
		CFLAG:LOCAL:初期位置 = CSVCFLAG (LOCAL, GETNUM(CFLAG, "初期位置"))
		CFLAG:LOCAL:神社在住 = CFLAG:LOCAL:初期位置
		SIF !アップデート中
			DEBUGPRINTFORML %CALLNAME:LOCAL%が拠点に住みます
	ELSE
		PRIVATEROOM:(CFLAG:LOCAL:初期位置 % 100) = 0
		CFLAG:LOCAL:初期位置 = SUKIMA()
		CFLAG:LOCAL:神社在住 = 0
	ENDIF
	CFLAG:LOCAL:自宅位置 = CSVCFLAG (LOCAL, GETNUM(CFLAG, "自宅位置"))
	CFLAG:LOCAL:現在位置 = CFLAG:LOCAL:初期位置
	CFLAG:LOCAL:デート中 = MAIN_MAP
NEXT
SIF !アップデート中
	PRINTFORMW %GET_MAPNAME(ARG)% has been chosen.
CALL ROOMSETTING
IF !アップデート中
	CFLAG:MASTER:初期位置 = デフォルト初期位置
	CFLAG:MASTER:現在位置 = CFLAG:MASTER:初期位置
	引っ越し = 0
ENDIF

@ROOMSETTING
#DIM R_ID
#DIM 私室候補
#DIM CHARA
私室候補 = 0
SUMIKOMI_ROOM = 0
;拠点住人は初期位置をPRIVATEROOMに設定,施錠の都合で先に処理
VARSET PRIVATEROOM
FOR LOCAL, 1, CHARANUM
	IF CFLAG:LOCAL:神社在住 && !CFLAG:LOCAL:出禁
		;PRIVATEROOM変数に、そこの家主の番号を保存
		PRIVATEROOM:(CFLAG:LOCAL:初期位置 % 100) = LOCAL 
	 	SIF CFLAG:LOCAL:大家候補
			私室候補 ++
	ENDIF
NEXT
TRYCCALLFORM ROOMSETTING_{MAIN_MAP}
	PRINTFORMW %GET_MAPNAME(MAIN_MAP)%'s room info has been set.
	;住み込みキャラ用のワンルーム
	IF FLAG:住み込みキャラ && SUMIKOMI_ROOM
		R_ID = SUMIKOMI_ROOM % 100
		ROOMDATA:R_ID |= 場所_ワンルーム
	ENDIF
	FOR LOCAL, 1, 100
		;補完設定
		;MASTERの初期位置は最低限の機能を備える,飲食はできなくなる
		IF CFLAG:MASTER:初期位置 % 100 == LOCAL
			ROOMDATA:LOCAL |= 場所_布団
			ROOMDATA:LOCAL |= 場所_勉強
			ROOMDATA:LOCAL |= 場所_プライベート
			ROOMDATA:LOCAL &= ~場所_飲食
		ENDIF
		;ワンルーム
		IF ROOMDATA:LOCAL & 場所_ワンルーム
			ROOMDATA:LOCAL |= 場所_布団
			ROOMDATA:LOCAL |= 場所_屋内
			ROOMDATA:LOCAL |= 場所_厨房
			ROOMDATA:LOCAL |= 場所_勉強
			ROOMDATA:LOCAL |= 場所_休憩
			ROOMDATA:LOCAL |= 場所_飲食
			ROOMDATA:LOCAL |= 場所_プライベート
			ROOMDATA:LOCAL |= 場所_炬燵
			ROOMDATA:LOCAL &= ~場所_過疎
		ENDIF
		;布団
		IF ROOMDATA:LOCAL & 場所_布団
			ROOMDATA:LOCAL |= 場所_休憩
		ENDIF
		;洋室
		IF ROOMDATA:LOCAL & 場所_洋室
			ROOMDATA:LOCAL |= 場所_屋内
		ENDIF
		;厨房
		IF ROOMDATA:LOCAL & 場所_厨房
			ROOMDATA:LOCAL |= 場所_つまみ食い
		ENDIF
		;施錠チェック,初期位置が変わった場合、施錠を解錠する
		R_ID = LOCAL + MAIN_MAP * 100
		SIF LOCK:R_ID && !PRIVATEROOM:LOCAL
			LOCK:R_ID = 0
	NEXT
CATCH
	PRINTFORMW Failed to set room info.
ENDCATCH
IF (ゲーム開始処理中 || ゲーム引き継ぎ処理中) || !デフォルト初期位置 || 引っ越し
	IF 私室候補
		CALL 引っ越し初期位置設定
	ELSE
		CALL デフォルト引っ越し
	ENDIF
ENDIF

@デフォルト引っ越し
TRYCCALLFORM DEFAULT_OWNER_{MAIN_MAP}
CATCH
	PRINTFORMW Failed to set room info.
ENDCATCH
PRINTFORMW Moved into %NAME_FROM_PLACE(デフォルト初期位置)%.

@引っ越し初期位置設定
#DIM CHARA
#DIM ANIMATION_SEED
ANIMATION_SEED = 0

;Map Animation Code
$MAPANIMATION
LINESTART = LINECOUNT
SIF ANIMATERECOLOREDMAPS && !FLAG:70
	ANIMATION_SEED ++
DRAWLINE
IF RECOLOREDMAPS
	CALL DRAW_COLOREDMAP(ANIMATION_SEED)
ELSE
	CALL DRAW_MAP
ENDIF
DRAWLINE
PRINTFORML %PRINT_COLOR("This map has more than one ", C_WHITE) + PRINT_COLOR("room choice", C_ORANGE) + PRINT_COLOR(".", C_WHITE)%
SIF DAY <= 1
	PRINTFORML The first day, any room can be chosen regardless of reliability.
PRINTBUTTON @"[Switch Preview Map({TFLAG:マップ切り替え})]", 1997
PRINTL 
FOR CHARA, 1, CHARANUM
	IF CFLAG:CHARA:神社在住 && CFLAG:CHARA:大家候補
		SIF CFLAG:CHARA:住み込み必要信頼度 > CFLAG:CHARA:信頼度 && DAY > 1
			SETCOLOR C_L_GRAY
		PRINTBUTTON @"[{CFLAG:CHARA:大家候補 % 100, 3}] - %NAME_FROM_PLACE(CFLAG:CHARA:大家候補), 36, LEFT%　Landlord:%CALLNAME:CHARA, 15, LEFT%　Require Reliability:{CFLAG:CHARA:住み込み必要信頼度}", CHARA
		PRINTL 
		RESETCOLOR
	ENDIF
NEXT
PRINTFORML [  0] - ({デフォルト初期位置 % 100, 2}) %NAME_FROM_PLACE(デフォルト初期位置)%（Default room）
;Map Animation Code
IF ANIMATERECOLOREDMAPS && !FLAG:70
	CURRENTREDRAW
	ORIREDRAW = RESULT
	REDRAW ORIREDRAW
	TINPUT ANIMATERECOLOREDMAPS,1234,0,""
ELSE
	INPUT
ENDIF

IF RESULT == 0
	CALL デフォルト引っ越し
ELSEIF RESULT == 1234
	REDRAW 0
	CLEARLINE LINECOUNT - LINESTART
	GOTO MAPANIMATION
ELSEIF RESULT < CHARANUM && CFLAG:RESULT:神社在住 && CFLAG:RESULT:大家候補
	CHARA = RESULT
	IF CHARA != 0 && CFLAG:CHARA:住み込み必要信頼度 > CFLAG:CHARA:信頼度 && DAY > 1
		PRINTFORMW %CALLNAME:CHARA% doesn't trust %PARSE("you")% enough...
		RESTART
	ELSE
		拠点_大家 = CHARA
		デフォルト初期位置 = CFLAG:CHARA:大家候補
		ROOMDATA:(デフォルト初期位置 % 100) |= 場所_布団
		ROOMDATA:(デフォルト初期位置 % 100) |= 場所_プライベート
		PRINTFORMW Moved into %NAME_FROM_PLACE(CFLAG:CHARA:大家候補)%.
	ENDIF
;プレビュー切り替え
ELSEIF RESULT == 1997
	TFLAG:マップ切り替え += 1
	SIF TFLAG:マップ切り替え > MAP_PAGES
		TFLAG:マップ切り替え = 1
	RESTART
ELSE
	RESTART
ENDIF

;-------------------------------------------------
;引っ越し下見時にROOMDATAを参照して施設の有無を表示する関数
;ARG	MAP_ID
;-------------------------------------------------
@物件データ(ARG)
#DIM MAPDATA
VARSET MAPDATA
VARSET LOCAL
PRINT Facilities: 
;マップデータの参照
FOR LOCAL, 1, 100
	SIF (ROOMDATA:LOCAL & 場所_風呂) != 0
		MAPDATA |= 場所_風呂
	SIF (ROOMDATA:LOCAL & 場所_厨房) != 0
		MAPDATA |= 場所_厨房
	SIF (ROOMDATA:LOCAL & 場所_訓練) != 0
		MAPDATA |= 場所_訓練
	SIF (ROOMDATA:LOCAL & 場所_勉強) != 0
		MAPDATA |= 場所_勉強
	SIF (ROOMDATA:LOCAL & 場所_菜園) != 0
		MAPDATA |= 場所_菜園
	SIF (ROOMDATA:LOCAL & 場所_天の声) != 0
		MAPDATA |= 場所_天の声
NEXT
FONTBOLD
CALL COLORMESSAGE(@"【Bath】", (MAPDATA & 場所_風呂) ? C_WHITE # C_GRAY, 0)
CALL COLORMESSAGE(@"【Kitchen】", (MAPDATA & 場所_厨房) ? C_WHITE # C_GRAY, 0)
CALL COLORMESSAGE(@"【Training】", (MAPDATA & 場所_訓練) ? C_WHITE # C_GRAY, 0)
CALL COLORMESSAGE(@"【Study】", (MAPDATA & 場所_勉強) ? C_WHITE # C_GRAY, 0)
CALL COLORMESSAGE(@"【Garden】", (MAPDATA & 場所_菜園) ? C_WHITE # C_GRAY, 0)
CALL COLORMESSAGE(@"【Voice of God】", (MAPDATA & 場所_天の声) ? C_WHITE # C_GRAY, 0)
;qol custom code, maps where "Move In" event can happen (APPEND_SYS.ERB)
CALL COLORMESSAGE(@"【Lodger Support\@ SUMIKOMI_ROOM ? (%GET_PLACENAME(SUMIKOMI_ROOM)%, {SUMIKOMI_ROOM})#\@】", SUMIKOMI_ROOM ? C_WHITE # C_GRAY, 0)
FONTREGULAR

;地点の参照
FOR LOCAL, MINROOM(), MAXROOM
	SIF FISHING_SPOT(LOCAL)
		LOCAL:1 = 1
	SIF IS_LIBRARY(LOCAL, 1)
		LOCAL:2 = 1
	SIF GATHERING_SPOT(LOCAL)
		LOCAL:3 = 1
	SIF PHARMACY_SPOT(LOCAL) == 2
		LOCAL:4 = 1
	SIF STALL_SPOT(LOCAL)
		LOCAL:5 = 1
	SIF PLACE_SENTO(LOCAL)
		LOCAL:6 = 1
	SIF OMIKUJI_SPOT(LOCAL)
		LOCAL:7 = 1
	SIF MORIYA_OMIKUJI(LOCAL)
		LOCAL:8 = 1
	SIF KAIDASHI_SHOP(LOCAL)
		LOCAL:9 = 1
	SIF FLOWER_SHOP(LOCAL)
		LOCAL:10 = 1
	SIF SAKE_SHOP(LOCAL)
		LOCAL:11 = 1
	SIF BOOK_RENTAL(LOCAL)
		LOCAL:12 = 1
	SIF YASAI_SHOP(LOCAL)
		LOCAL:13 = 1
	SIF CASINO_SPOT(LOCAL)
		LOCAL:14 = 1
	SIF HUNTING_SPOT(LOCAL)
		LOCAL:15 = 1
	SIF 伐採可能(LOCAL) ;logging custom code
		LOCAL:19 = 1
NEXT
FOR LOCAL, 1, 20
	IF LOCAL:LOCAL
		;custom code, replace all PRINTs with PRINTBUTTON to take advantage of "not break lines in the middle" quirk it has
		SELECTCASE LOCAL
		CASE 1
			PRINTBUTTON "【Fishing】", ""
		CASE 2
			PRINTBUTTON "【Reading】", ""
		CASE 3
			PRINTBUTTON "【Foraging】", ""
		CASE 4
			PRINTBUTTON "【Mixing】", ""
		CASE 5
			PRINTBUTTON "【Street Stall】", ""
		CASE 6
			PRINTBUTTON "【Public Bath】", ""
		CASE 7
			PRINTBUTTON "【Omikuji】", ""
		CASE 8
			PRINTBUTTON "【Moriya Lottery】", ""
		CASE 9
			PRINTBUTTON "【Grocery Store】", ""
		CASE 10
			PRINTBUTTON "【Florist】", ""
		CASE 11
			PRINTBUTTON "【Liquor Store】", ""
		CASE 12
			PRINTBUTTON "【Bookstore】", ""
		CASE 13
			PRINTBUTTON "【Green Grocer】", ""
		CASE 14
			PRINTBUTTON "【Casino】", ""
		CASE 15
			PRINTBUTTON "【Trapping】", ""
		CASE 19 ;logging custom code
			PRINTBUTTON "【Logging】", ""
		ENDSELECT
	ENDIF
NEXT
PRINTL
PRINT Residents: 
;キャラクター表示
FOR LOCAL, 1, CHARANUM
	SIF CFLAG:LOCAL:出禁 ;addition custom code, hide disabled characters
		CONTINUE
	IF MAP_住人(MAIN_MAP, LOCAL)
		SIF LOCAL:21
			PRINTFORM , 
		RESULT = 0
		TRYCALLFORM M_KOJO_K{LOCAL}
		PRINTFORM \@ LOCAL:20 ? 、 # \@\@ RESULT ? * # \@%CALLNAME:LOCAL%
		LOCAL:21 ++
	ENDIF
NEXT
PRINTL 
;qol tip
PRINTL (Asterisk means characters with dialogue)

;マップ内の採集可能地点を表示する関数
@ShowGatheringSpots(ARG)
#DIM cStart
#DIM cEnd
#DIM SpotID
#DIM Num
#DIM Wordcount
#DIM pCount

cStart = 1 + ARG * 100
cEnd = 99 + ARG * 100
Num = 0
pCount = 0
FOR LOCAL, cStart, cEND
	SpotID = GATHERING_SPOT(LOCAL, 1)
	IF SpotID
		pCount ++
		STRLENFORM %ForagePlaceName(SpotID)%
		Wordcount = RESULT:0
		Num += Wordcount
		IF Num > 110
			PRINTL 
			PRINTFORM 　　　　　　　　
			Num = Wordcount
		ENDIF
		PRINTFORM %ForagePlaceName(SpotID)%
		SIF Num <= 106
			PRINTFORM 　　
	ENDIF
NEXT
SIF !pCount
	PRINT None
PRINTFORML 