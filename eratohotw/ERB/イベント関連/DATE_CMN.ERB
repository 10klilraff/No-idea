;FileName_DATE_CMN.ERB ----------------------------- Rev1.00
;デート先名取得関数
;CALL		USER
;ARG		VOID
;RETURN		デート先名
;COMMENT	
;-----------------------------------------------------------
;Custom code, add 1 to make it return the original japanese
@DATENAME_PLACE(ARG = 0)
#FUNCTIONS
SIF TFLAG:妄想中 == 1
	RETURNF "妄想"
SIF CHK_DATENOW(CFLAG:MASTER:デート中) == 0
	RETURNF ""
SIF ARG
RETURNF STR:(CFLAG:MASTER:デート中 + 5000)
;Custom code
RETURNF STR_TR(CFLAG:MASTER:デート中 + 5000)

;FileName_DATE_CMN.ERB ----------------------------- Rev1.00
;デート地点名取得関数
;CALL		USER
;ARG		VOID
;RETURN		デート地点名
;COMMENT	
;-----------------------------------------------------------
@DATENAME_SPOT()
#FUNCTIONS
;Custom code, replaced function
RETURNF DATENAME_SPOT_TR()
SIF CHK_DATENOW(CFLAG:MASTER:デート中) == 0
	RETURNF ""
SIF TFLAG:デート道中
	RETURNF "道中"
RETURNF NAME_FROM_PLACE(CFLAG:MASTER:現在位置)

;FileName_DATE_CMN.ERB ----------------------------- Rev1.00
;デート地点番号取得関数
;CALL		USER
;ARG		VOID
;RETURN		デート地点NO
;COMMENT	
;-----------------------------------------------------------
@DATE_SPOT()
#FUNCTION
RETURNF CFLAG:MASTER:現在位置 % 100 / 10

;FileName_DATE_CMN.ERB ----------------------------- Rev1.00
;デート先アルバイト可否
;CALL		USER
;ARG		現在位置
;RETURN		アルバイト先種類
;COMMENT	
;-----------------------------------------------------------
@DATE_PARTTIME(ARG)
#FUNCTION
RETURNF ODEKAKEMAP_SETTING(ARG, "アルバイト")

;お金稼ぎが封印中なのでここも封印
SELECTCASE ARG
	;特殊バイト先
	CASE 240,450,520,540,650,830
		RETURNF ARG
	;%デート先%でアルバイトして～…
	CASE 220,230,320,330,460
		RETURNF 2
	;%デート先%で食材集めをして～…
	CASE 350,410,430,440,510,530,630,710,810
		RETURNF 1
ENDSELECT
RETURNF 0

;FileName_DATE_CMN.ERB ----------------------------- Rev1.01
;デート帰りイベント (CFLAG:13)
;CALL		USER
;ARG		キャラNO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@DATE_EVENT(ARG)
#DIM 珠不足
#DIM 経験不足
;qol custom replace
CALL DATE_EVENT_TR(ARG)
RETURN RESULT

VARSET LOCAL
;キャンセルフラグ（外部参照にて使用のこと 例:LOCAL@DATE_EVENT:99 = 1
LOCAL:99 = 0
;パラメータによる発生基礎値生成

LOCAL = MARK:ARG:不埒刻印 * 10 + MARK:ARG:快楽刻印 * 10
IF TALENT:ARG:恋慕
	LOCAL += MIN(ABL:ARG:親密 * 10, 100) + MIN(ABL:ARG:欲望 * 10,100) + MIN(ABL:ARG:奉仕精神 * 10,100)
	LOCAL += MIN(EXP:ARG:デート経験,100) + MIN(EXP:ARG:愛情経験,100)
ELSEIF TALENT:ARG:思慕
	LOCAL += MIN(ABL:ARG:親密 * 10, 50) + MIN(ABL:ARG:欲望 * 10,50) + MIN(ABL:ARG:奉仕精神 * 10,50)
	LOCAL += MIN(EXP:ARG:デート経験,50)
ENDIF
LOCAL += GETPALAMLV(PALAM:ARG:欲情,10) * 5 + GETPALAMLV(PALAM:ARG:好意,10) * 5 + GETPALAMLV(PALAM:ARG:恭順,10) * 5
;60
LOCAL += BASE:ARG:ムード / 50 + (1000 - BASE:ARG:理性) / 30
LOCAL += TALENT:ARG:度胸 * 10 + TALENT:ARG:性的興味 * 10 - TALENT:ARG:自制心 * 10 - TALENT:ARG:無関心 * 10 
DEBUGPRINTFORML 【DATE_EVENT({ARG})】DateEvent_Checksum = {LOCAL}
;別れ際にプレゼント
CALL GIFT(ARG)
IF RESULT == 1
;合意無し（キス成功してない）
ELSEIF !(CFLAG:ARG:1 & 1) && !(CFLAG:ARG:13 & 1)
	;成功判定
	IF (LOCAL > 220 + TALENT:ARG:一線越えない * 30 + ABS(TALENT:ARG:キス未経験 * 20)) && CFLAG:ARG:合意判定 > 180 && (TALENT:ARG:思慕 || TALENT:ARG:恋慕)
		CALL KOJO_MESSAGE_SEND("SP_EVENT",1,ARG,0)
		;キャンセルフラグ
		IF !TCVAR:ARG:キスキャンセル
			CALL ADD_KISS(ARG)
			SETBIT CFLAG:ARG:デート後イベントフラグ , 0
		ENDIF
	ELSEIF !MARK:ARG:反発刻印
		CALL KOJO_MESSAGE_SEND("SP_EVENT",3,ARG,0)
	ENDIF
;反発刻印消去処理
ELSEIF MARK:ARG:反発刻印 && LOCAL > 50
	;ABL:親密LVUP処理取り込み。解読が面倒だったので要所々々取り摘んで…
	FOR LOCAL,0,4
		珠不足 = 0
		経験不足 = 0
		SIF JUEL_DEMAND(9,LOCAL,0,ARG) == -2
			BREAK
		SIF JUEL_DEMAND(9,LOCAL,0,ARG) < 0
			CONTINUE
		;必要な宝珠を探す
		FOR LOCAL:1,0,100
			IF JUEL_DEMAND(9,LOCAL,LOCAL:1,ARG) > 0
				IF JUEL:(ARG):(LOCAL:1) < JUEL_DEMAND(9,LOCAL,LOCAL:1,ARG) / 2
					珠不足 ++
					BREAK
				ENDIF
			ENDIF
		NEXT
		SIF 珠不足
			CONTINUE
		;必要な経験を探す
		FOR LOCAL:1,0,100
			IF EXP_DEMAND(9,LOCAL,LOCAL:1,ARG) > 0
				IF  EXP:(ARG):(LOCAL:1) < EXP_DEMAND(9,LOCAL,LOCAL:1,ARG) / 2
					経験不足 ++
					BREAK
				ENDIF
			ENDIF
		NEXT
		SIF 経験不足
			CONTINUE
		;ここまで来れたら反発刻印消去
		;要するにデートでいい雰囲気＋親密LVUP水準の1/2満たしたら反発消去
		MARK:ARG:反発刻印 --
		CALL KOJO_MESSAGE_SEND("SP_EVENT",3,ARG,(MARK:ARG:反発刻印 + 1))
		PRINTL
		IF MARK:ARG:反発刻印
			PRINTFORMW %CALLNAME:ARG%'s Lv {MARK:ARG:反発刻印} hate mark has been lowered!
		ELSE
			PRINTFORMW %CALLNAME:ARG%'s Lv 1 Hate Mark has been removed!
		ENDIF
		BREAK
	NEXT
;告白していない（MASTERに恋人が居ない）
ELSEIF !(CFLAG:ARG:1 & 4) && !(CFLAG:ARG:13 & 2) &&  !FLAG:告白禁止 && CAN_MAKE_恋人()
	;成功判定
	IF (LOCAL > 530 + TALENT:ARG:一線越えない * 20)
		CALL KOJO_MESSAGE_SEND("SP_EVENT",2,ARG,0)
		PRINTFORML \@THIRD_PERSON?Select how to respond to %CALLNAME:ARG%'s confession#How do you respond to %CALLNAME:ARG%'s confession?\@
		CALL ASK_YN(@"Accept it (Can only have {FLAG:追加恋人枠 + 1} \@FLAG:追加恋人枠?s#\@)","Refuse") 
		IF !RESULT
			SETBIT CFLAG:ARG:既成事実 , 2
			SETBIT CFLAG:MASTER:既成事実, 2
			TALENT:MASTER:恋人 = NO:ARG
			TALENT:ARG:恋人 = NO:ARG
			SETBIT CFLAG:ARG:デート後イベントフラグ , 1
			CALL KOJO_MESSAGE_SEND("SP_EVENT",2,ARG,1)
			PRINTL
			PRINTFORM %PARSE("You")% and %CALLNAME:ARG% are now 
			SETCOLOR C_PINK
			PRINT Lovers
			RESETCOLOR
			PRINTW !
			CALL QoL_Info_Write(ARG, Qol_Dates_GF) ;qol custom code
		ELSE
			CALL KOJO_MESSAGE_SEND("SP_EVENT",2,ARG,2)
		ENDIF
	;贈り物イベント
	ELSEIF GIFT_DATE_CHECK(ARG)
		CALL GIFT_DATE_EVENT(ARG)
	ELSEIF !MARK:ARG:反発刻印
		CALL KOJO_MESSAGE_SEND("SP_EVENT",3,ARG,0)
	ENDIF
;贈り物イベント
ELSEIF GIFT_DATE_CHECK(ARG)
	CALL GIFT_DATE_EVENT(ARG)
ELSE
	CALL KOJO_MESSAGE_SEND("SP_EVENT",3,ARG,0)
ENDIF

@GIFT(ARG)
#DIM 好感度上昇値
#DIM プレゼント評価
;デート前好感度と現好感度を比較
好感度上昇値 = CFLAG:ARG:2 - TFLAG:デート前好感度
IF DAY:2 == 4 && DAY:3 == 24 && ITEM:94 && TFLAG:デート前好感度
	PRINTFORML Present the Christmas Gift?
	CALL ASK_YN
	IF !RESULT
		PRINTFORMW %PARSE("You give")% the present to %CALLNAME:ARG%.
		ITEM:94 = 0
		IF TALENT:ARG:恋人
			PRINTFORMW %CALLNAME:ARG%'s face quickly turns red.
			PRINTFORMW ... %CALLNAME:ARG% kisses %PARSE("you")% on the cheek.
			プレゼント評価 = 10
		ELSEIF TALENT:ARG:恋慕
			PRINTFORMW %CALLNAME:ARG%'s face quickly turns red.
			PRINTFORMW %CALLNAME:ARG% is very happy to receive such a gift.
			プレゼント評価 = 7
		ELSEIF TALENT:ARG:思慕
			PRINTFORML Although it is a bit surprising for %CALLNAME:ARG%, %HE_SHE(ARG)% is pleased to receive the present.
			PRINTFORML Being so straightforward about giving them a present has made %CALLNAME:ARG% more conscious about %HIS_HER(ARG)% feelings for %PARSE("you")%.
			プレゼント評価 = 5
		ELSE
			PRINTFORMW %CALLNAME:ARG% is puzzled from receiving such an expensive gift, though %HE_SHE(ARG)% doesn't seem to be displeased.
			プレゼント評価 = 2
		ENDIF
		CFLAG:ARG:2 += 好感度上昇値 * プレゼント評価 / 5
		CFLAG:ARG:4 += 好感度上昇値 * プレゼント評価 / 10
		RETURN 1
	ELSE
		RETURN 0
	ENDIF
ELSE
	RETURN 0
ENDIF

@WITH_恋人(ARG)
#FUNCTION
;恋人の何人とARGが同じ場所に居るかを返す
VARSET LOCAL
SIF ARG == 0
	ARG = MASTER
FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:現在位置 == CFLAG:ARG:現在位置 && TALENT:LOCAL:恋人
		LOCAL:1 ++
NEXT
RETURNF LOCAL:1

@CAN_MAKE_恋人()
#FUNCTION
;恋人が今何人いるかカウント
VARSET LOCAL
FOR LOCAL, 1, CHARANUM
	SIF TALENT:LOCAL:恋人
		LOCAL:1 ++
NEXT
IF MORELOVER
	RETURNF 1
ELSE
	SIF LOCAL:1 < (1 + FLAG:追加恋人枠)
		RETURNF 1
	RETURNF 0
ENDIF
;===========================================
;キス経験やファーストキス処理などをまとめてやる関数
;===========================================
@ADD_KISS(ARG, ARG:1 = 1, ARG:2 = 0) ;custom code
IF TALENT:MASTER:キス未経験
	CALL SET_FIRSTKISS(MASTER, ARG, ARG:2 == 0 ? 1 # 0) ;custom code, mutual kiss returns 1, so it's set to outside of date (0), on date by default (1)
	CALL AddEXP("愛情経験", ARG, 10) ;custom code
ENDIF
IF TALENT:ARG:キス未経験
	CALL SET_FIRSTKISS(ARG, MASTER, ARG:2 == 0 ? 1 # 0) ;custom code
	CALL AddEXP("愛情経験", ARG, 10) ;custom code
ENDIF
CALL AddEXP("キス経験", ARG, ARG:1) ;custom code
CALL AddEXP("キス経験", MASTER, ARG:1) ;custom code
SETBIT CFLAG:ARG:既成事実 , 0
Qol_Info_Count_Array:ARG:Qol_Count_Kiss ++ ;qol custom code
SIF !SHIRAHU(ARG)
	Qol_Info_Count_Array:ARG:Qol_Count_UncKiss ++ ;qol custom code