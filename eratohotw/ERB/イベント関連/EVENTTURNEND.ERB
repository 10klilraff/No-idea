@EVENTTURNEND
#DIM 養育費

;宴会当日に一日が終わると宴会フラグOFF
;IF DAY >= FLAG:34 && FLAG:宴会開催フラグ
IF TIME + 1440 * DAY >= FLAG:開始時間 + 1440 * FLAG:開始日 && FLAG:宴会開催フラグ
	;会場となった場所は荒れ果てる、拠点外でも対象地点の汚れ増加させるため宴会場ではない
	SIF YOGORE:(FLAG:32) < 300
		YOGORE:(FLAG:32) += 1000
	FLAG:宴会開催フラグ = 0
	FLAG:31 = 0
	FLAG:32 = 0
	TFLAG:宴会場 = 0
	FLAG:33 = 0
	FLAG:34 = 0
	FLAG:35 = 0
	FLAG:36 = 0
	;宴会会場表示 = 
	FLAG:宴会差し入れ = 0
	FOR LOCAL,0,CHARANUM
		CFLAG:LOCAL:350 = 0
		MAXBASE:LOCAL:仕事量 = 0
		BASE:LOCAL:仕事量 = 0
		CFLAG:LOCAL:宴会参加 = 0
		CFLAG:LOCAL:行動 = 0
	NEXT
ENDIF
;霊廟フラグOFF
FLAG:17 = 0
DRAWLINE
PRINTFORML 
;SETCOLOR C_PINK
;FONTBOLD
FOR LOCAL,0,CHARANUM
	;Custom code, first person
	IF LOCAL == MASTER
		IF EXP:LOCAL:キス経験 > 300 && ABL:LOCAL:Ｍ感覚 > 5 && !TALENT:LOCAL:キス魔
			PRINTFORML After exchanging many kisses that found a foothold in %PARSE("your",1)% heart,
			PRINTFORML %PARSE("you")% can't help but feel restless unless %PARSE("you're",1)% kissing with someone...
			;陥落素質取得口上,6,キス魔
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 6)
			PRINTFORMW %PARSE("You")% now %PARSE("have")%【Lewd Lips】.
			TALENT:LOCAL:キス魔 = 1
			CALL QoL_Info_Write(LOCAL, Qol_Dates_LewdLips) ;qol custom code
		ENDIF
		IF EXP:LOCAL:調教自慰経験 > 200 && EXP:LOCAL:自慰経験 > 500 && !TALENT:LOCAL:自慰狂い
			PRINTFORML Knowing all about how to vent %PARSE("your",1)% desires, %PARSE("you")% can no longer think of anything but \@ HAS_PENIS(MASTER) ? jerking off # masturbating\@ at all times...
			;陥落素質取得口上,7,自慰狂い
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 7)
			PRINTFORMW %PARSE("You")% now【%PARSE("Love")% to Masturbate】.
			TALENT:LOCAL:自慰狂い = 1
			CALL QoL_Info_Write(LOCAL, Qol_Dates_Masturb) ;qol custom code
		ENDIF
		IF EXP:LOCAL:子宮口開発経験 > 50 && !TALENT:LOCAL:ポルチオ性感
			PRINTFORML After having had %PARSE("your",1)% cervix repeatedly struck, %PARSE("you have")% reached the point where %PARSE("you feel",1)% pleasure instead of pain.
			;陥落素質取得口上,8,ポルチオ性感
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 8)
			PRINTFORMW %PARSE("You")% now %PARSE("have")% a【Lewd Cervix】.
			TALENT:LOCAL:ポルチオ性感 = 1
			CALL QoL_Info_Write(LOCAL, Qol_Dates_LewdCervix) ;qol custom code
		ENDIF
	;	IF FLAG:おもらし有効 > 0 && EXP:LOCAL:放尿経験 > 1000 * (10 - 2 * TALENT:LOCAL:中毒しやすい) / 10 && !TALENT:LOCAL:おもらし癖
	;		PRINTFORML 絶頂時に放尿し続けてきた%CALLNAME:LOCAL%の身体は、今や放尿するのが癖になってしまう程に緩くなってしまった…
	;		;陥落素質取得口上,9,おもらし癖
	;		CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 9)
	;		PRINTFORMW %CALLNAME:LOCAL%は[おもらし癖]を得た
	;		TALENT:LOCAL:おもらし癖 = 1
	;	ENDIF
		CONTINUE
	ENDIF

	;この3つはあなたも取得可能
	IF EXP:LOCAL:キス経験 > 300 && ABL:LOCAL:Ｍ感覚 > 5 && !TALENT:LOCAL:キス魔
		PRINTFORML After exchanging many kisses that found a foothold in %HIS_HER(LOCAL)% heart,
		PRINTFORML %CALLNAME:LOCAL% can't help but feel restless unless %HE_SHE(LOCAL)%'s kissing with someone...
		
		;陥落素質取得口上,6,キス魔
		CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 6)
		PRINTFORMW %CALLNAME:LOCAL% now has【Lewd Lips】.
		TALENT:LOCAL:キス魔 = 1
		CALL QoL_Info_Write(LOCAL, Qol_Dates_LewdLips) ;qol custom code
	ENDIF
	IF EXP:LOCAL:調教自慰経験 > 200 && EXP:LOCAL:自慰経験 > 500 && !TALENT:LOCAL:自慰狂い
		PRINTFORML Knowing all about how to vent %HIS_HER(LOCAL)% desires, %CALLNAME:LOCAL% can no longer think of anything but \@ HAS_PENIS(LOCAL) ? jerking off # masturbating\@ at all times...
		;陥落素質取得口上,7,自慰狂い
		CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 7)
		PRINTFORMW %CALLNAME:LOCAL% now【Loves to Masturbate】.
		TALENT:LOCAL:自慰狂い = 1
		CALL QoL_Info_Write(LOCAL, Qol_Dates_Masturb) ;qol custom code
	ENDIF
	IF EXP:LOCAL:子宮口開発経験 > 50 && !TALENT:LOCAL:ポルチオ性感
		PRINTFORML After having had their cervix repeatedly struck, %CALLNAME:LOCAL% has reached the point where %HE_SHE(LOCAL)% feels pleasure instead of pain.
		;陥落素質取得口上,8,ポルチオ性感
		CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 8)
		PRINTFORMW %CALLNAME:LOCAL% now has a【Lewd Cervix】.
		TALENT:LOCAL:ポルチオ性感 = 1
		CALL QoL_Info_Write(LOCAL, Qol_Dates_LewdCervix) ;qol custom code
	ENDIF
;	IF FLAG:おもらし有効 > 0 && EXP:LOCAL:放尿経験 > 1000 * (10 - 2 * TALENT:LOCAL:中毒しやすい) / 10 && !TALENT:LOCAL:おもらし癖
;		PRINTFORML 絶頂時に放尿し続けてきた%CALLNAME:LOCAL%の身体は、今や放尿するのが癖になってしまう程に緩くなってしまった…
;		;陥落素質取得口上,9,おもらし癖
;		CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 9)
;		PRINTFORMW %CALLNAME:LOCAL%は[おもらし癖]を得た
;		TALENT:LOCAL:おもらし癖 = 1
;	ENDIF
	;以下はあなたはスキップ
	SIF LOCAL == MASTER
		CONTINUE
	;恋慕取得
	IF 恋慕取得条件(LOCAL)
		;面霊気は特別あつかい
		IF LOCAL == 57
			PRINTFORMW As %CALLNAME:LOCAL% thinks of %PARSE("you")%, %HIS_HER(LOCAL)% face softens up a bit.
			PRINTFORMW A normal smile seems to have sprung up on %CALLNAME:LOCAL%'s face.
			;陥落素質取得口上,1,恋慕
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 1)
			PRINTFORMW %CALLNAME:LOCAL%'s\@ TALENT:LOCAL:思慕 ?【Yearning】 #【Lust】 \@blooms into【Love】!
			FLAG:10 ++
		;愛欲の有無で分岐
		ELSEIF TALENT:LOCAL:愛欲
			PRINTFORMW %CALLNAME:LOCAL% feels joy from having %HIS_HER(LOCAL)% heart and body satisfied by %PARSE("you")%...
			PRINTL The lust-fueled relationship has blossomed into a proper one, and takes on the shape of love.
			;陥落素質取得口上,1,恋慕
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 1)
			PRINTFORMW %CALLNAME:LOCAL%'s【Lust】turns into【Love】!
		ELSE
			PRINTFORML %CALLNAME:LOCAL% seems to enjoy spending time with %PARSE("you")%...
			;陥落素質取得口上,1,恋慕
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 1)
			PRINTFORMW %CALLNAME:LOCAL%'s【Yearning】turns into【Love】!
			FLAG:10 ++
		ENDIF
		SIF !MARK:LOCAL:時姦刻印 && !TALENT:LOCAL:愛欲 && !CFLAG:LOCAL:睡眠姦 && !CFLAG:LOCAL:泥酔姦 
			CFLAG:LOCAL:清い交際 = 1
		TALENT:LOCAL:恋慕 = 1
		TALENT:LOCAL:思慕 = 0
		TALENT:LOCAL:愛欲 = 0
		TALENT:LOCAL:セフレ = 0
		 
		CALL QoL_Info_Write(LOCAL, Qol_Dates_Love) ;qol custom code
		;楽譜入手処理
		PRINTL 
		RESULT = 楽譜入手_陥落(LOCAL)
		SIF RESULT != -1
			PRINTL 
	;思慕取得
	;セフレ／愛人の取得で分岐
	ELSEIF 思慕取得条件(LOCAL)
		IF TALENT:LOCAL:セフレ
			PRINTFORML %CALLNAME:LOCAL% has started showing interest in more than just %PARSE("your")% body.
			;陥落素質取得口上,2,思慕
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 2)
			PRINTFORMW %HE_SHE(LOCAL,1)% has started【Yearning】for %PARSE("you")%!
		ELSE
			PRINTFORML %CALLNAME:LOCAL% has developed an interest in %PARSE("you")%...
			;陥落素質取得口上,2,思慕
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 2)
			PRINTFORMW %HE_SHE(LOCAL,1)% has started【Yearning】for %PARSE("you")%!
		ENDIF
		TALENT:LOCAL:思慕 = 1
		CALL QoL_Info_Write(LOCAL, Qol_Dates_Yearn) ;qol custom code
	;愛欲取得
	;奉仕快楽の分岐がまだないので、取得条件に併合
	ELSEIF 愛欲取得条件(LOCAL)
		;思慕持ち、心より身体を選んだ的な
		IF TALENT:LOCAL:思慕
			PRINTFORML %CALLNAME:LOCAL% cares more about the physical connection with %PARSE("you")% than the one between %PARSE("your",2)% hearts...
			PRINTFORML %HE_SHE(LOCAL,1)% wants to turn visible the invisible bond between two of %PARSE("you",4)% by means of sleeping together. 
			;陥落素質取得口上,3,愛欲
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 3)
			PRINTFORMW %CALLNAME:LOCAL% has started to【Lust】over %PARSE("you")%!
		ELSE
			PRINTFORML %CALLNAME:LOCAL% wants to drown with %PARSE("you")% in desire...
			;陥落素質取得口上,3,愛欲
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 3)
			PRINTFORMW %HE_SHE(LOCAL,1)% has started to【Lust】over %PARSE("you")%!
		ENDIF
			FLAG:10 ++
			TALENT:LOCAL:愛欲 = 1
			TALENT:LOCAL:思慕 = 0
			CALL QoL_Info_Write(LOCAL, Qol_Dates_Lust) ;qol custom code
			 
			;楽譜入手処理
			PRINTL
			RESULT = 楽譜入手_陥落(LOCAL)
			SIF RESULT != -1
				PRINTL 
	;セフレ／愛人取得
	;セフレと愛人の違いは思慕の有無
	ELSEIF セフレ取得条件(LOCAL)
		SIF !TALENT:LOCAL:セフレ
			CALL QoL_Info_Write(LOCAL, Qol_Dates_FBuddy) ;qol custom code
		;思慕取得済＋セフレ未取得
		IF TALENT:LOCAL:思慕 && !TALENT:LOCAL:セフレ
			PRINTFORML %CALLNAME:LOCAL% only has to think about %PARSE("you")% to start drowning in pleasure...
			;陥落素質取得口上,4,愛人
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 4)
			PRINTFORMW %HE_SHE(LOCAL,1)% has become %PARSE("your")%【\@TALENT:LOCAL:2 == 2?Paramour#Mistress\@】!
			TALENT:LOCAL:セフレ = 2
		;思慕取得済＋セフレ取得済
		ELSEIF TALENT:LOCAL:思慕 && TALENT:LOCAL:セフレ == 1
			PRINTFORML %CALLNAME:LOCAL% wants %HIS_HER(LOCAL)% relationship with %PARSE("you")% to be more than just physical...
			;陥落素質取得口上,4,愛人
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 4)
			PRINTFORMW %HE_SHE(LOCAL,1)% has become %PARSE("your")%【\@TALENT:LOCAL:2 == 2?Paramour#Mistress\@】!
			TALENT:LOCAL:セフレ = 2
		;思慕未取得＋セフレ未取得ならまずセフレ取得
		ELSEIF !TALENT:LOCAL:セフレ
			PRINTFORML %CALLNAME:LOCAL% drowns in the pleasure given by %PARSE("you")%...
			;陥落素質取得口上,5,セフレ
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 5)
			PRINTFORMW %HE_SHE(LOCAL,1)% has become %PARSE("your")%【Sex Friend】!
			TALENT:LOCAL:セフレ = 1
		ENDIF
	ENDIF

	IF EXP:LOCAL:演奏経験 >= 1000 && ABL:LOCAL:技巧 > 3 && !TALENT:LOCAL:音楽知識
		PRINTFORMW %CALLNAME:LOCAL% has gained a profound knowledge of music.
		PRINTFORMW %CALLNAME:LOCAL% now has a sense of【Musicality】.
		;PRINTFORMW %CALLNAME:LOCAL%は音楽に造詣深くなった
		;PRINTFORMW %CALLNAME:LOCAL%は[音楽知識]を得た
		TALENT:LOCAL:音楽知識 = 1
		;技能レベルアップ口上,7-1,音楽知識
		CALL KOJO_MESSAGE_SEND("EVENT", 34, LOCAL, 7, 1)
		CALL QoL_Info_Write(LOCAL, Qol_Dates_Musicality) ;qol custom code
	ELSEIF EXP:LOCAL:演奏経験 >=5000 && !TALENT:LOCAL:音感
		PRINTFORMW %CALLNAME:LOCAL%'s sense of pitch has become fairly honed.
		PRINTFORMW %CALLNAME:LOCAL% now has a keen【Sense of Sound】.
		TALENT:LOCAL:音感 = 1
		;技能レベルアップ口上,7-2,相対音感
		CALL KOJO_MESSAGE_SEND("EVENT", 34, LOCAL, 7, 2)
		CALL QoL_Info_Write(LOCAL, Qol_Dates_Sound) ;qol custom code
	ENDIF
	IF CFLAG:LOCAL:不機嫌
		CFLAG:LOCAL:不機嫌 --
		IF !CFLAG:LOCAL:不機嫌
			SIF MARK:LOCAL:反発刻印
				CFLAG:LOCAL:不機嫌 ++
		ENDIF
	ENDIF
	;EXキャラの恋慕時はメンバー追加イベントフラグを立てる
	IF !CFLAG:LOCAL:神社在住
		SIF TALENT:LOCAL:恋慕 && EXP:LOCAL:愛情経験 >= 200
			FLAG:1 = 3
	ENDIF
NEXT

IF EXP:MASTER:異常清掃経験 > (500 + 100 * FLAG:850) && ABL:MASTER:清掃技能 > 3 && !TALENT:MASTER:清掃中毒
	PRINTFORMW %PARSE("You have")% finally noticed a terrible thing.
	PRINTFORMW Whether asleep or awake, while time is stopped, even when fucking beautiful girls...
	PRINTFORMW The question of whether the room is clean or not weighs heavily on %PARSE("your",5)% shoulders.
	PRINTFORMW %PARSE("You")% cannot achieve peace of mind anymore unless %PARSE("your",1)% surroundings are clean enough.
	PRINTFORMW %PARSE("Your")% obsession with cleaning has reached a point where %PARSE("you're",1)% unable to contain it.
	PRINTFORMW %PARSE("You")% now %PARSE("have")% a【Cleaning Addiction】!
	TALENT:MASTER:清掃中毒 = 1
ENDIF
;MASTERを除く全員のプリセット処理
FOR LOCAL,1,CHARANUM
	;気力の回復
	BASE:LOCAL:気力 = MAXBASE:LOCAL:気力

	LOCAL:1 = MAXBASE:LOCAL:体力 / 3

	;回復早い、回復遅い
	LOCAL:1 = LOCAL:1 * 3 / (3 - TALENT:LOCAL:回復速度)
	
	;添い寝してたら回復速度向上
	SIF CFLAG:LOCAL:添い寝中
		LOCAL:1 *= 2
	SIF CFLAG:LOCAL:徹夜
		LOCAL:1 /= 2
	CFLAG:LOCAL:徹夜 = 0
	BASE:LOCAL:体力 = MIN(MAXBASE:LOCAL:体力, BASE:LOCAL:体力 + LOCAL:1)

	;南無三カード発行者が居たら発行フラグOFF
	CFLAG:LOCAL:328 = 0
NEXT
;全員のプリセット処理
FOR LOCAL,1,CHARANUM
	BASE:LOCAL:精力 = MIN(MAXBASE:LOCAL:精力, BASE:LOCAL:精力 + MAXBASE:LOCAL:精力 * BASE:LOCAL:体力 / MAXBASE:LOCAL:体力)
	
	;隠密中のキャラが居たらフラグOFF
	CALL SET_SNEAK(0,LOCAL,1)
	;CFLAG:LOCAL:隠密中 = 0
	
	;イタズラ中のキャラが居たらフラグOFF
	CALL SET_PRANK(0,LOCAL,1)
	;CFLAG:LOCAL:イタズラ = 0
	
NEXT

;宴会の開催判定
SIF FLAG:3 && FLAG:宴会開催フラグ == 0
	CALL ENKAI_SETTING

IF CFLAG:MASTER:育児人数
	養育費 = CFLAG:MASTER:育児人数 * 1000 / (1 + FLAG:甲斐性無)
	PRINTFORML %PARSE("You pay")% \\{養育費} for child support.
	MONEY -= 養育費
ENDIF

CALL RANK_POINT
PRINTW  

FLAG:0 = 0


;モブ子のセーブ
IF TALENT:RANDOM_CHARANUM:行きずり == 1 && GETBIT(TFLAG:一日一回, 9)
	IF FORGET_MOBGIRL
		FORGET_MOBGIRL = 0
		GOTO FORGET_MOB2
	ENDIF
	CALL MOBGIRL_PROFILE2()
	PRINTFORML Concerning %CALLNAME:RANDOM_CHARANUM% %PARSE("you")% met today...
	CALL ASK_YN("Forget them","Remember them")
	IF RESULT
		VARSET TCVAR:RANDOM_CHARANUM:0, 0
		VARSET EX:RANDOM_CHARANUM:0, 0
		VARSET TEQUIP:RANDOM_CHARANUM:0, 0
		VARSET PALAM:RANDOM_CHARANUM:0, 0
		CALL InitializeBeforeTrain(RANDOM_CHARANUM)
		CFLAG:RANDOM_CHARANUM:900 ++
		CALL SAVE_MOBGIRL
		SIF RESULT == -1
			GOTO FORGET_MOB
		PRINTFORMW %PARSE("You think")% about how nice it would be to \@ TALENT:RANDOM_CHARANUM:風俗嬢 ? play with#see\@ %CALLNAME:RANDOM_CHARANUM% again.
	ELSE
		$FORGET_MOB
		PRINTFORMW %PARSE("You")% completely %PARSE("forget")% about %CALLNAME:RANDOM_CHARANUM%'s existence.
	ENDIF
ENDIF
$FORGET_MOB2
IF TALENT:RANDOM_CHARANUM:行きずり == 2 && GETBIT(TFLAG:一日一回, 9)
	VARSET TCVAR:RANDOM_CHARANUM:0, 0
	VARSET EX:RANDOM_CHARANUM:0, 0
	VARSET TEQUIP:RANDOM_CHARANUM:0, 0
	VARSET PALAM:RANDOM_CHARANUM:0, 0
	CALL InitializeBeforeTrain(RANDOM_CHARANUM)
	SAVECHARA TOSTR(CFLAG:RANDOM_CHARANUM:モブ子セーブスロット), CSTR:RANDOM_CHARANUM:モブ子プロフィール１, RANDOM_CHARANUM
	[IF DEBUG]
		PRINTFORMW Information about %CALLNAME:RANDOM_CHARANUM% has been updated.
	[ENDIF]
ENDIF

BEGIN SHOP

@RANK_POINT
VARSET LOCAL
;清潔度
LOCAL:2 = EX:MASTER:今日の掃除回数 / 10
PRINTFORML Garbage cleaned: {EX:MASTER:今日の掃除回数}
PRINTFORM Cleaning bonus: \\{LOCAL:2}
IF DAY:2 == 4 && DAY:3 == 31
	PRINT (x2)  【Spring Cleaning Bonus】
	LOCAL:2 *= 2
ENDIF
PRINTL
;拠点の主の好感度と信頼度が上昇
LOCAL:3 = MIN(10, EX:MASTER:今日の掃除回数 / 2000)
IF LOCAL:3
	PRINTFORML Because of the effort put into cleaning, %CALLNAME:拠点_大家%'s opinion of %PARSE("you")% increases.
	CFLAG:拠点_大家:好感度 += LOCAL:3 * 2
	CFLAG:拠点_大家:信頼度 += LOCAL:3
	IF LOCAL:3 / 3 > 0
		PRINTFORML As a bonus, %PARSE("you")% also %PARSE("receive")% {LOCAL:3 / 3} cheap ingredients.
		ITEM:安物食材 += LOCAL:3 / 3
	ENDIF
ENDIF
PRINTL  
;手伝い報酬
LOCAL:4 = TCVAR:MASTER:手伝った量

SIF LOCAL:4 > 0
	PRINTFORML Assistant bonus \\{LOCAL:4}

MONEY += LOCAL:2 + LOCAL:4

;好感度上昇
IF FLAG:12 && MONEY:2 < CM_LIMIT
	LOCAL:5 = MAX(1,FLAG:12 / 100)
	LOCAL:5 = MIN(LOCAL:5, CM_LIMIT - MONEY:2)
	PRINTFORML Favorability increasing bonus ＋{LOCAL:5} Charisma
	MONEY:2 += LOCAL:5
	PRINTFORML Current Charisma: %三桁区切(MONEY:2)%  Charisma limit: %三桁区切(CM_LIMIT)%
ENDIF
FLAG:12 = 0

;addition custom code
CALL Add_Rumor

PRINTW  

@恋慕取得条件(ARG)
#FUNCTION
SIF TALENT:ARG:恋慕
	RETURNF 0
SIF CFLAG:ARG:好感度 < REQUIRED_FAVOR_恋慕
	RETURNF 0
SIF CFLAG:ARG:信頼度 < REQUIRED_TRUST_恋慕
	RETURNF 0
SIF ABL:ARG:親密 < REQUIRED_AFFECTION_恋慕
	RETURNF 0
SIF MARK:ARG:反発刻印
	RETURNF 0
SIF TALENT:ARG:愛欲 && !FLAG:陥落路線変化
	RETURNF 0
RETURNF 1

@思慕取得条件(ARG)
#FUNCTION
SIF TALENT:ARG:愛欲
	RETURNF 0
SIF TALENT:ARG:恋慕
	RETURNF 0
SIF TALENT:ARG:思慕
	RETURNF 0
SIF CFLAG:ARG:好感度 < REQUIRED_FAVOR_思慕
	RETURNF 0
SIF CFLAG:ARG:信頼度 < REQUIRED_TRUST_思慕
	RETURNF 0
SIF ABL:ARG:親密 < REQUIRED_AFFECTION_思慕
	RETURNF 0
SIF TALENT:ARG:セフレ && !FLAG:陥落路線変化
	RETURNF 0
RETURNF 1

@愛欲取得条件(ARG)
#FUNCTION
SIF TALENT:ARG:恋慕
	RETURNF 0
SIF TALENT:ARG:愛欲
	RETURNF 0
SIF !TALENT:ARG:セフレ
	RETURNF 0
SIF CFLAG:ARG:好感度 < REQUIRED_FAVOR_愛欲
	RETURNF 0
SIF ABL:ARG:欲望 < REQUIRED_DESIRE_愛欲
	RETURNF 0
SIF (EXP:ARG:絶頂経験 - EXP:ARG:無自覚絶頂経験) < REQUIRED_ECSTACY_愛欲
	RETURNF 0
SIF (ABL:ARG:欲望 - ABL:ARG:親密) < 1
	RETURNF 0
RETURNF 1

@セフレ取得条件(ARG)
#FUNCTION
SIF TALENT:ARG:恋人
	RETURNF 0
SIF TALENT:ARG:恋慕
	RETURNF 0
SIF TALENT:ARG:セフレ == 2
	RETURNF 0
SIF CFLAG:ARG:好感度 <= REQUIRED_FAVOR_セフレ
	RETURNF 0
SIF ABL:ARG:欲望 < REQUIRED_DESIRE_セフレ
	RETURNF 0
SIF (EXP:ARG:絶頂経験 - EXP:ARG:無自覚絶頂経験) < REQUIRED_ECSTACY_セフレ
	RETURNF 0
SIF (ABL:ARG:欲望 - ABL:ARG:親密) < 0
	RETURNF 0
RETURNF 1
