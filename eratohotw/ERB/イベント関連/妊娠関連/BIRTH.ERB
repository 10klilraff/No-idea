@BIRTH(ARG)
#DIM CHILDNUM
DRAWLINE
;妊娠消去、出産経験、性別の決定、誕生日決定
TALENT:ARG:妊娠 = 0
EXP:ARG:出産経験 += 1
Qol_Info_Count_Array:ARG:Qol_Count_Birth ++ ;qol custom code
CHILDNUM = EXP:ARG:出産経験 % 10
SIF !EXP:ARG:出産経験 % 10
	CHILDNUM = 10
IF FLAG:子供の性別
	CHILD_SEX:ARG:CHILDNUM = FLAG:子供の性別
ELSE
	CHILD_SEX:ARG:CHILDNUM = RAND:2 + 1
ENDIF
CHILD_BIRTHDAY:ARG:CHILDNUM = DAY
CHILD_BIRTHDATE:ARG:CHILDNUM = (DAY / 124 + (DAY % 124 ? 1 # 0)) * 1000
CHILD_BIRTHDATE:ARG:CHILDNUM += DAY:2 * 100 + DAY:3
CHILD_WEATHER:ARG:CHILDNUM = %GET_WEATHER()%
IF CFLAG:ARG:無自覚妊娠
	CALL KOJO_MESSAGE_SEND("EVENT",25,ARG,4,CHILDNUM)
	CHILD_LOVE:ARG:CHILDNUM = -100
ELSEIF TALENT:ARG:恋慕
	CALL KOJO_MESSAGE_SEND("EVENT",25,ARG,1,CHILDNUM)
	CHILD_LOVE:ARG:CHILDNUM = 300
ELSEIF TALENT:ARG:思慕
	CALL KOJO_MESSAGE_SEND("EVENT",25,ARG,2,CHILDNUM)
	CHILD_LOVE:ARG:CHILDNUM = 200
ELSE
	CALL KOJO_MESSAGE_SEND("EVENT",25,ARG,3,CHILDNUM)
	CHILD_LOVE:ARG:CHILDNUM = 100
ENDIF
;あらかじめ自立先を決めちゃう（自立するときにもっかい決める）
CALL 自立先選定(ARG, CHILDNUM)
PRINTFORMW %CALLNAME:ARG% gives birth to your child safely.
PRINTFORMW \@ CHILD_SEX:ARG:CHILDNUM == 1 ? She resembles her mother %CALLNAME:ARG% # He resembles his father \@ a bit.
PRINTFORMW %CALLNAME:ARG% holds the newborn baby, and gently cradles it as %HE_SHE(ARG)% breastfeeds %HIM_HER_CHILD(ARG,,CHILDNUM)%...
SIF TALENT:ARG:母性 == 0
	PRINTFORMW %CALLNAME:ARG%'s [Maternity] has awoken!
PRINTL ******************************************************************
PRINTFORML %ORDINAL(CHILDNUM)% child　Mother: %NAME:ARG%　Father: %NAME:MASTER%
PRINTFORML 　Gender: \@ CHILD_SEX:ARG:CHILDNUM == 1 ? Female # Male \@
PRINTFORM Birthday: 
PRINTFORML %PRINT_DATE_F(DAY)% 
PRINTFORML  %FESTIVAL_TR()% -%GET_WEATHER_TR()%-
;PRINTFORML 　身長：
;PRINTFORML 　体重：
;PRINTFORML 　種族：
;PRINTFORML 髪の色：
;PRINTFORML 目の色：
PRINTL ******************************************************************


;育児中・母性追加
CALL CHILD_NAME(ARG)
CFLAG:ARG:育児人数 += 1
CFLAG:MASTER:育児人数 += 1
CFLAG:ARG:出産経過日 = 1
CFLAG:ARG:妊娠経過日数 = 0
CFLAG:ARG:無自覚妊娠 = 0
CFLAG:ARG:身に覚え_母 = 0
CFLAG:ARG:身に覚え_父 = 0
CFLAG:ARG:妊娠自覚状態 = 0 
TALENT:ARG:母性 = 1

CFLAG:ARG:産休中 = 0

EXP:ARG:愛情経験 += 10
CFLAG:ARG:子供人数 += 1
CFLAG:MASTER:子供人数 += 1
;Custom Code
IF FLAG:甲斐性無 && !FLAG:LUSTMODE
	PRINTFORML The realization that %PARSE("your",1)% child was just born hits %PARSE("you")%.
	CALL ASK_YN("A sense of responsibility boils up from inside",@"Anxiety for the future %PARSE("fills you",1)% with dread")
	IF !RESULT
		PRINTFORMW %PARSE("You are")% no longer incompetent.
	ELSE
		PRINTFORMW If human nature was that easy to change, there would be no conflicts.
		PRINTFORMW As %PARSE("you are")% as unreliable as ever, %CALLNAME:ARG% is left to take care of raising the child.
	ENDIF
	FLAG:甲斐性無 = RESULT
ENDIF
		

@CHILD_NAME(YOME)
#DIM YOME
#DIM CHILDNUM
#DIMS PROPOSED_NAME, 10
CHILDNUM = EXP:YOME:出産経験 % 10
SIF !EXP:YOME:出産経験 % 10
	CHILDNUM = 10
PROPOSED_NAME:0 = %ランダムキャラ_命名()%
;Custom code, disabled as it's untranslated
;IF CHILD_SEX:YOME:CHILDNUM == 1
;	PROPOSED_NAME:0 = %ランダムキャラ_命名(YOME)%
;ELSE
;	PROPOSED_NAME:0 = %男の子_命名(YOME, CHILDNUM)%
;ENDIF
$LOOP0
PRINTL Let's pick a name for the child.
PRINTFORML 
CALL ASK_M("Input directly",1,"Random list",1,"After the「hot wind that blows through Mexico」naturally, the name「Santana」!",1)

SELECTCASE RESULT
	CASE 0
		$LOOP1
		PRINTL Input the child's name please.
		INPUTS
		IF RESULTS == ""
			GOTO LOOP1
		ELSE
			PROPOSED_NAME:0 = %RESULTS%
			CALL CHK_CHILDNAME_DUPLICATION(PROPOSED_NAME:0)
			PRINTFORML Is %PROPOSED_NAME:0% fine?
			CALL ASK_YN
			IF RESULT
				GOTO LOOP1
			ELSE
				CHILDNAME:YOME:CHILDNUM = %PROPOSED_NAME:0%
			ENDIF
		ENDIF
	CASE 1
		$LOOP2
		FOR LOCAL,1,10
			PROPOSED_NAME:LOCAL = %ランダムキャラ_命名()%
			;Custom code, disabled as it's untranslated
			;IF CHILD_SEX:YOME:CHILDNUM == 1
			;	PROPOSED_NAME:LOCAL = %ランダムキャラ_命名(YOME)%
			;ELSE
			;	PROPOSED_NAME:LOCAL = %男の子_命名(YOME, CHILDNUM)%
			;ENDIF
			PRINTFORML [{LOCAL}] - %PROPOSED_NAME:LOCAL%
		NEXT
		DRAWLINE
		PRINTFORML [99] - New list
		PRINTFORML [100] - Return
		$LOOP3
		INPUT
		SELECTCASE RESULT
			CASE 1 TO 9
				LOCAL = RESULT
				CALL CHK_CHILDNAME_DUPLICATION(PROPOSED_NAME:LOCAL)
				PRINTFORML Is %PROPOSED_NAME:LOCAL% fine?
				CALL ASK_YN
				IF RESULT
					GOTO LOOP0
				ELSE
					CHILDNAME:YOME:CHILDNUM = %PROPOSED_NAME:LOCAL%
				ENDIF
			CASE 99
				GOTO LOOP2
			CASE 100
				GOTO LOOP0
			CASEELSE
				GOTO LOOP3
		ENDSELECT
	CASE 2
		PRINTFORML The angry %CALLNAME:YOME% picks a name for %PARSE("you")%...
		CHILDNAME:YOME:CHILDNUM = %ランダムキャラ_命名()%
		;Custom code, disabled as it's untranslated
		;CHILDNAME:YOME:CHILDNUM = %PROPOSED_NAME:0%
ENDSELECT

PRINTFORML The child's name became %CHILDNAME:YOME:CHILDNUM%.
TALENT:YOME:育児中 = CHILDNUM
;----------------------------------------------------
;そのキャラに特定の年齢の子供がいたら返す関数
;----------------------------------------------------
@CHK_HAVE_CHILD(ARG,ARG:1,ARG:2)
#FUNCTION
#DIM 年齢
SIF !CFLAG:ARG:子供人数 ;bug fix, check number of kids instead of child-caring
	RETURNF 0

FOR LOCAL, 1, 11
	SIF CHILDNAME:ARG:LOCAL == "" ;bug fix, skip if child doesn't exist - name is required for each; also would be better to BREAK, but just to be safe
		CONTINUE
	年齢 = DAY - CHILD_BIRTHDAY:ARG:LOCAL
	SIF INRANGE(年齢,ARG:1,ARG:2-1)
		RETURNF LOCAL
NEXT

RETURNF 0
;----------------------------------------------------
;誕生日の子供がいるか返す関数
;eraTW4.570proto_bugfix_m05での仕様変更
;・REFを使用し、配列を書き換えるF関数にした（引数を全て渡さないとエラーになる）
;・仕様上、配列の想定最大要素数は10*キャラクタ数上限となる（＝RESULTの要素数を突破してエラーが出たので仕様変更）
;・BREAKやCONTINUEを使用して処理を効率化
;----------------------------------------------------
@CHILD_HAPPY_BIRTHDAY(CNT_BIRTHDAY, NAME_CHILD, AGE_CHILD)
#FUNCTION
#DIM CHARA
#DIM CHILDNUM
#DIM  REF CNT_BIRTHDAY
#DIMS REF NAME_CHILD
#DIM  REF AGE_CHILD
FOR CHARA, 1, CHARANUM
	SIF !CFLAG:CHARA:子供人数
		CONTINUE
	FOR CHILDNUM, 1, MIN(11, CFLAG:CHARA:子供人数 + 1)
		;名無し≒未登録なら次のキャラへ
		SIF !STRLENS(CHILDNAME:CHARA:CHILDNUM)
			BREAK
		;誕生日以外はスキップ
		SIF (DAY - CHILD_BIRTHDAY:CHARA:CHILDNUM) % 124
			CONTINUE
		CNT_BIRTHDAY ++
		NAME_CHILD:CNT_BIRTHDAY = %CHILDNAME:CHARA:CHILDNUM%
		AGE_CHILD:CNT_BIRTHDAY = (DAY - CHILD_BIRTHDAY:CHARA:CHILDNUM) / 124
	NEXT
NEXT
RETURNF CNT_BIRTHDAY

;子供の名前被りチェック
@CHK_CHILDNAME_DUPLICATION(ARGS)
#DIM C_ID
FOR C_ID,1,CHARANUM
	;"FOR LOCAL,0,10"から修正
	FOR LOCAL,1,11
		SIF CHILDNAME:C_ID:LOCAL == ARGS
			CALL COLORMESSAGE(@"%CALLNAME:C_ID% already has a child named that.",C_YELLOW,1)
	NEXT
NEXT