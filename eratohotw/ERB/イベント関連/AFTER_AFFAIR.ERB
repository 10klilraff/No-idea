@AFTER_AFFAIR
#DIM SPENT_TIME
SPENT_TIME = (TIME-UFUFU_START_TIME) / 60 
IF SPENT_TIME < 0
	SPENT_TIME += 24
ENDIF
;ブチギレと無理矢理状態とTSP切れと泥酔姦と風呂場プレイと睡眠時間中は表示しない
SIF CFLAG:ブチギレ || TCVAR:無理矢理 || !SHIRAHU(TARGET)
	RETURN 0
	;ほんのちょっとのうふふでは表示されないよう、潤滑、欲情、体力、溜まってる度、その日の絶頂回数に条件をつける（大した意味はない）
SIF !(PALAM:潤滑 >= PALAMLV:3 && PALAM:欲情 >= PALAMLV:3 && BASE:体力 < MAXBASE:体力 * 9 / 10 && CFLAG:溜まってる度 < 300 && EX:Ｃ絶頂 + EX:Ｖ絶頂 + EX:Ａ絶頂 + EX:Ｂ絶頂 + EX:Ｍ絶頂 >= 4)
	RETURN 0

PRINTFORMD %PARSE("Yours")% and \@ GET_TARGETNUM() > 1? the others'# %CALLNAME:TARGET%'s\@ 
;経過時間表示
IF SPENT_TIME
	PRINTFORMD {SPENT_TIME}-hour 
	SIF SPENT_TIME > 5
		PRINTFORMD marathon-lengthed 
ENDIF
;ここまで
IF TFLAG:泡風呂
	PRINTFORMD monetarily compensated rendezvous
ELSEIF TALENT:恋慕 || TALENT:恋人
	PRINTFORMD love-filled rendezvous
ELSEIF TALENT:愛欲
	PRINTFORMD lust-driven debauchery
ELSEIF TALENT:思慕
	PRINTFORMD tryst
ELSEIF TALENT:セフレ == 1
	PRINTFORMD sex-centric tryst
ELSE
	PRINTFORMD quick but thoughtless liaison
ENDIF
PRINTFORMW  comes to an end.

SIF BATHROOM() 
	RETURN 0

;-------------------------------------------------------------------------------------
;以下、精液漏れ・愛液による濡れの描写（精液漏れの描写が優先）

;膣充填率が水準以上もしくはアナル充填率が水準以上
IF SOAKED_OR_NOT("V_SEMINAL") || SOAKED_OR_NOT("A_SEMINAL")
	PRINTFORMD A large quantity of %EXPR_VAR("SPERM")% that's been 
	SELECTCASE RAND:3
	CASE 0
		PRINTFORMD pumped into 
	CASE 1
		PRINTFORMD released into 
	CASEELSE
		PRINTFORMD poured into 
	ENDSELECT
	PRINTFORMD %CALLNAME:TARGET%'s 
	SIF SOAKED_OR_NOT("V_SEMINAL")
		PRINTFORMD %EXPR_VAR("VULVA")% 
	SIF SOAKED_OR_NOT("A_SEMINAL")
		PRINTFORMD \@ SOAKED_OR_NOT("V_SEMINAL") ? %"and "% # \@%EXPR_VAR("ANUS")% 
	SELECTCASE RAND:3
	CASE 0
		PRINTFORMD overflows and leaks out.
	CASE 1
		PRINTFORMD leaks out.
	CASEELSE
		PRINTFORMD oozes out.
	ENDSELECT
	CALL PRINT_BREAK,,"dc"
	IF TALENT:MASTER:濃厚精液
		PRINTFORML But the semi-solid %EXPR_VAR("SPERM")% doesn't drip like a liquid, forming long strings instead, sticking to %CALLNAME:TARGET%'s inner thighs.
	ELSE
		PRINTFORMD %CALLNAME:TARGET% wipes %HIS_HER(TARGET)% %EXPR_VAR("CROTCH")% with a 
		SELECTCASE RAND:3
		CASE 0
			PRINTFORMD tissue
		CASE 1
			PRINTFORMD handkerchief
		CASEELSE
			PRINTFORMD towel
		ENDSELECT
		PRINTFORMD , and before long the %EXPR_VAR("SPERM")% 
		SELECTCASE RAND:4
		CASE 0
			PRINTFORMD trickles down in thick drops.
		CASE 1
			PRINTFORMD trickles down.
		CASE 2
			PRINTFORMD overflows, drawing a line down %HIS_HER(TARGET)% thigh.
		CASEELSE
			PRINTFORMD overflows.
		ENDSELECT
		CALL PRINT_BREAK,,"dc"
	ENDIF
	;--------------------------------------------
	;垂れ口上呼び出し
	;--------------------------------------------
	CALL WEAREDPANTIES_TRANSPARENCY("SPERM")
	CALL KOJO_MESSAGE_SEND("EVENT",15,TARGET,3,1)
;膣・アナル充填率がともに水準未満だが潤滑が必要LV以上
ELSEIF SOAKED_OR_NOT("SECRETIONAL")
	PRINTFORMD %CALLNAME:TARGET% tries to use a 
	SELECTCASE RAND:3
	CASE 0
		PRINTFORMD tissue
	CASE 1
		PRINTFORMD handkerchief
	CASEELSE
		PRINTFORMD towel
	ENDSELECT
	PRINTFORMD  to wipe %HIS_HER(TARGET)% drenched %EXPR_VAR("CROTCH")%, but %HIS_HER(TARGET)% body still lingers in the afterglow and keeps pumping out more and more.
	CALL PRINT_BREAK,,"dc"
	;--------------------------------------------
	;溢れ口上呼び出し
	;--------------------------------------------
	CALL WEAREDPANTIES_TRANSPARENCY("JUICE")
	CALL KOJO_MESSAGE_SEND("EVENT",15,TARGET,3,2)
ENDIF
;-------------------------------------------------------------------------------------------------------
;以下、事後のムードの描写
;複数TARGETがいる場合非表示
IF GET_TARGETNUM() == 1
	PRINTFORMD After %PARSE("you",3)% two finish up %PARSE("your",2)% love affair, 
	IF TALENT:恋慕 || TALENT:恋人
		PRINTFORMDL %PARSE("you",2)% whispered sweet nothings to each other.
	ELSEIF TALENT:愛欲
		PRINTFORMDL %PARSE("you",2)% needed some more time to catch %PARSE("your",2)% breaths after the intense session.
	ELSEIF TALENT:思慕
		PRINTFORMDL %PARSE("you",2)% were a bit embarrassed and giggled at each other, but felt %PARSE("yourselves")% growing closer.
	ELSEIF TALENT:セフレ == 1
		PRINTFORMDL %PARSE("you",2)% huddled together in silence for a while under a calm atmosphere.
	ELSE
		PRINTFORMDL there was an awkward silence in the air between %PARSE("you",4)%.
	ENDIF
	PRINTFORMW
ENDIF

CALL KOJO_MESSAGE_SEND("EVENT",15,TARGET,3,3)

;---------------------------------------------------------------------------------------------------------
;以下、事後に服等を着なおす描写		
;上半身上着、ワンピース、着物、外衣のどれかを着たままだったばあい
IF EQUIP:上半身上着１ || EQUIP:上半身上着２ || EQUIP:ワンピース || EQUIP:着物 || EQUIP:外衣
	PRINTFORMDL %CALLNAME:TARGET% turns away from %PARSE("you")% and fixes %HIS_HER(TARGET)% clothes.
	IF PALAM:欲情 >= PALAMLV:7
		PRINTFORMD %HIS_HER(TARGET,1)% white, sweaty skin flushed pink
	ELSE
		PRINTFORMD %HIS_HER(TARGET,1)% sweaty body
	ENDIF
	PRINTFORMW  is quite appealing to the eye.
;現在上半身が裸でかつ普段着の上半身下着がブラジャーかマイクロブラのばあい
ELSEIF TEQUIP:上半身着衣状況 == 0 && (CFLAG:TARGET:207 == CLOTHES_ID("上半身下着１", "ブラジャー") || CFLAG:TARGET:208 == CLOTHES_ID("上半身下着２", "マイクロブラ"))
	PRINTFORMDL %CALLNAME:TARGET% turns away from %PARSE("you")% and hooks %HIS_HER(TARGET)% bra.
	PRINTFORMW The way %HIS_HER(TARGET)% scapula stands out as %HE_SHE(TARGET)% moves %HIS_HER(TARGET)% arms is beautiful.
ENDIF

CALL KOJO_MESSAGE_SEND("EVENT",15,TARGET,3,4)

;----------------------------------------------------------------------------------------------------------
;以下、残り香の描写

;濃厚な残り香の表示に必要な射精数、噴乳数、放尿数の設定
LOCAL:5 = 5 ;必要射精数
LOCAL:6 = 5 ;必要噴乳数
LOCAL:7 = 1 ;必要放尿数

;残り香を表示するのは屋内のときだけ
IF OPENPLACE(CFLAG:MASTER:現在位置) < 2
	;その日のふたりの射精数、噴乳数、放尿数によって分岐
	IF EX:射精 + EX:MASTER:射精 >= LOCAL:5 || EX:噴乳 + EX:MASTER:噴乳 >= LOCAL:6 || EX:放尿 + EX:MASTER:放尿 >= LOCAL:7
		PRINTFORMD %PARSE("Your",2)% hot breaths and 
		SIF EX:射精 + EX:MASTER:射精 >= LOCAL:5
			PRINTFORMD the unique smell of semen 
		SIF EX:噴乳 + EX:MASTER:噴乳 >= LOCAL:6
			PRINTFORMD \@ EX:射精 + EX:MASTER:射精 >= LOCAL:5 ? %EX:放尿 + EX:MASTER:放尿 >= LOCAL:7?", "#"and "% # \@the sweet smell of breast milk%EX:射精 + EX:MASTER:射精 >= LOCAL:5 && EX:放尿 + EX:MASTER:放尿 >= LOCAL:7 ? "," #""% 
		SIF EX:放尿 + EX:MASTER:放尿 >= LOCAL:7
			PRINTFORMD \@ EX:射精 + EX:MASTER:射精 >= LOCAL:5 || EX:噴乳 + EX:MASTER:噴乳 >= LOCAL:6 ? and%" "% # \@the smell of urine 
		PRINTFORMDL mix together to create an obscene and rich smell.
		PRINTFORMW It clearly indicates what just transpired.
	ELSE
		PRINTFORMW The bittersweet smell of sweat and lovemaking hints at what just transpired.
	ENDIF
ENDIF
;----------------------------------------------------------------------------------------------------------


@WEAREDPANTIES_TRANSPARENCY(ARGS)
#DIM ぱんつ状況
;Custom code, calling replacement instead
CALL WEAREDPANTIES_TRANSPARENCY_TR(ARGS)
RETURN

;パンツを奪ったorもらったor元から穿いてない場合
IF CFLAG:ノーパン || (CFLAG:205 == 0 && CFLAG:206 == 0)
	ぱんつ状況 = 0
;パンツを一旦脱いでいた場合
ELSEIF !EQUIP:下半身下着１ && !EQUIP:下半身下着２
	ぱんつ状況 = 1
ELSE
	ぱんつ状況 = 2
ENDIF


IF !ぱんつ状況
	PRINTFORMDL %CALLNAME:TARGET%には穿くパンツが無い。
	IF ARGS == "SPERM"
		PRINTFORMDL 今日は、ノーパンで%EXPR_VAR("SPERM")%を垂れ流したまま過ごすことになりそうだ。
	ELSE
		PRINTFORMDL 今日は、%EXPR_VAR("CROTCH")%をしとどに濡らした上にノーパンのまま過ごすことになりそうだ。
	ENDIF
;パンツがある場合
ELSE
	SIF ぱんつ状況 == 1
		PRINTFORMDL %CALLNAME:TARGET%は脱いでいた下着を穿きなおした。
	IF ARGS == "SPERM"
		PRINTFORMD 垂れ出てきた%EXPR_VAR("SPERM")%
	ELSE
		PRINTFORMD とめどなく漏れ出る愛液
	ENDIF
	PRINTFORMD が\@ ぱんつ状況 == 1? その# \@下着を
	SELECTCASE RAND:3
	CASE 0
		PRINTFORMD ぐちょぐちょにし
	CASE 1
		PRINTFORMD ぐしょりと湿らせ
	CASEELSE
		PRINTFORMD びっしょりと濡らし
	ENDSELECT
	;パンツが透けうる場合
	IF PANTIES_TRANSPARENCY(TARGET)
		PRINTFORMDL 、秘裂の形が透けて見えるほどだった。
	ELSE
		PRINTFORMDL た。
	ENDIF
ENDIF


;### びしょ濡れ判定用式中関数 ################
;ARG=キャラ番号
@SOAKED_OR_NOT(ARGS,ARG)
#FUNCTION
#DIM 必要膣充填率
#DIM 必要アナル充填率
#DIM 必要潤滑レベル

SIF ARG == 0
	ARG = TARGET

必要膣充填率 = 30
必要アナル充填率 = 30
必要潤滑レベル = PALAMLV:5

IF ARGS == "V_SEMINAL"
	IF 充填率(ARG,1) >= 必要膣充填率
		RETURNF 1
	ELSE
		RETURNF 0
	ENDIF
ELSEIF ARGS == "A_SEMINAL"
	IF 充填率(ARG,2) >= 必要アナル充填率
		RETURNF 1
	ELSE
		RETURNF 0
	ENDIF
ELSEIF ARGS == "SECRETIONAL"
	IF PALAM:ARG:潤滑 >= 必要潤滑レベル
		RETURNF 1
	ELSE
		RETURNF 0
	ENDIF
ENDIF

THROW 不正な文字列引数


;### 表現バリエーション用式中関数 ################
@EXPR_VAR(ARGS)
#FUNCTIONS
VARSET LOCALS

IF ARGS == "SPERM"
	SELECTCASE RAND:4
	CASE 0
		LOCALS = cloudy liquid
	CASE 1
		LOCALS = semen
	CASE 2
		LOCALS = sperm
	CASEELSE
		LOCALS = baby juice
	ENDSELECT
ELSEIF ARGS == "VULVA"
	SELECTCASE RAND:4
	CASE 0
		LOCALS = secret place
	CASE 1
		LOCALS = slit
	CASE 2
		LOCALS = vulva
	CASEELSE
		LOCALS = vagina
	ENDSELECT
ELSEIF ARGS == "ANUS"
	SELECTCASE RAND:3
	CASE 0
		LOCALS = asshole
	CASE 1
		LOCALS = anus
	CASEELSE
		LOCALS = asspussy
	ENDSELECT
ELSEIF ARGS == "CROTCH"
	SELECTCASE RAND:3
	CASE 0
		LOCALS = nether region
	CASE 1
		LOCALS = privates
	CASEELSE
		LOCALS = crotch
	ENDSELECT
ENDIF

RETURNF LOCALS