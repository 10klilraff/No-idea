;────────────────────────────────────
;調教終了の処理
;────────────────────────────────────
@EVENTEND
#LATER
#DIM 変動値
#DIM 会ってない日数
#DIM YANUSHI
PRINTL The day has ended.
PRINTFORML 
;SIF STRLENS(TSTR:1)
;	CALL SELL_VIDEO

;睡眠フラグ
CALL SET_SLEEP(1,MASTER,0)

CFLAG:MASTER:286 = 0
CFLAG:MASTER:287 = 0
CFLAG:MASTER:288 = 0
CFLAG:MASTER:289 = 0
CFLAG:MASTER:290 = 0
CFLAG:MASTER:ノーパン = 0

;おみくじを引けるようにフラグリセット
CLEARBIT FLAG:999, 1
;あなた懐妊処理 
;CALL MasterPregnancy 

;中出しフラグリセット
FOR LOCAL,1,CHARANUM
	CFLAG:LOCAL:286 = 0
	CFLAG:LOCAL:287 = 0
	CFLAG:LOCAL:288 = 0
	CFLAG:LOCAL:289 = 0
	CFLAG:LOCAL:290 = 0
	;願掛けによる感度上昇解除
	SIF GROUPMATCH(TCVAR:LOCAL:願掛け補正,23,31,41,70,80,82,83,101,102,103,104,106)
		TALENT:LOCAL:(TCVAR:LOCAL:願掛け補正) -= 1
	;qol custom code, record previous favorite position
	LOCAL:9 = CFLAG:LOCAL:好きな体位
	;好きな体位決定
	SIF CFLAG:LOCAL:好きな体位 > 10 
		CFLAG:LOCAL:好きな体位 = 0
	FOR COUNT,1,10
		SIF EXP:LOCAL:(COUNT + 100) > EXP:LOCAL:(CFLAG:LOCAL:好きな体位 + 100) + 50
			CFLAG:LOCAL:好きな体位 = COUNT
	NEXT
	;qol custom code
	SIF LOCAL:9 != CFLAG:LOCAL:好きな体位
		CALL QoL_FavPosShow(LOCAL)
	;---------------
	;パンツフラグ
	CFLAG:LOCAL:ノーパン = 0
	;custom code, at the latest at the end of the day, the character realizes their underwear is gone
	CALL PANTY_AWOL(LOCAL,0)
	CFLAG:LOCAL:衣装一時変更 = 0
	SIF TCVAR:LOCAL:本来の服装
		CFLAG:LOCAL:基本服装セット = TCVAR:LOCAL:本来の服装
	SELECTCASE ABL:LOCAL:精液中毒
		CASE 0
			LOCAL:1 = 5
		CASE 1 TO 3
			LOCAL:1 = 7
		CASE 4 TO 7
			LOCAL:1 = 10
		CASEELSE
			LOCAL:1 = 20
	ENDSELECT
	;機嫌
	TALENT:LOCAL:機嫌 = 0
	CALL KIGEN_CHANGE(LOCAL,10)
	SIF TALENT:MASTER:濃厚精液
		LOCAL:1 *= 2
	SETCOLOR 0xEE60EE
	IF EX:LOCAL:膣内精液 > 200 || EX:LOCAL:アナル内精液 > 200
		PRINTFORM The warmth of %PARSE("your")% semen inside %CALLNAME:LOCAL%'s 
		IF EX:LOCAL:膣内精液 > 200 && EX:LOCAL:アナル内精液 > 200
			PRINTL pussy and ass makes them ache for more...
		ELSEIF EX:LOCAL:膣内精液 > 200
			PRINTL pussy makes it ache for more...
		ELSE 
			PRINTL anus makes it ache for more...
		ENDIF 
		IF EX:LOCAL:膣内精液 > 200 
			JUEL:LOCAL:屈服 += LOCAL:1 * (ABL:LOCAL:膣射中毒 + 1) * EX:LOCAL:膣内精液
			JUEL:LOCAL:欲情 += LOCAL:1 * (ABL:LOCAL:膣射中毒 + 1) * EX:LOCAL:膣内精液
		ENDIF
		IF EX:LOCAL:アナル内精液 > 200
			JUEL:LOCAL:屈服 += LOCAL:1 * (ABL:LOCAL:肛射中毒 + 1) * EX:LOCAL:アナル内精液
			JUEL:LOCAL:欲情 += LOCAL:1 * (ABL:LOCAL:肛射中毒 + 1) * EX:LOCAL:アナル内精液
		ENDIF
	ENDIF
	IF (EX:LOCAL:20 + EX:LOCAL:21 + EX:LOCAL:22 + EX:LOCAL:23 + EX:LOCAL:24) > 0
		PRINTFORM %CALLNAME:LOCAL%'s 
		SIF EX:LOCAL:20
			PRINTFORM clitoris
		SIF EX:LOCAL:20 && (EX:LOCAL:21 || EX:LOCAL:22 || EX:LOCAL:23 || EX:LOCAL:24)
			PRINTFORM \@((EX:LOCAL:21 && (EX:LOCAL:22 || EX:LOCAL:23 || EX:LOCAL:24)) || (EX:LOCAL:22 && (EX:LOCAL:21 || EX:LOCAL:23 || EX:LOCAL:24)) || (EX:LOCAL:23 && (EX:LOCAL:21 || EX:LOCAL:22 || EX:LOCAL:24)) || (EX:LOCAL:24 && (EX:LOCAL:21 || EX:LOCAL:22 || EX:LOCAL:23)))?,# %" and"%\@ 
		SIF EX:LOCAL:21
			PRINTFORM vagina
		SIF EX:LOCAL:21 && (EX:LOCAL:22 || EX:LOCAL:23 || EX:LOCAL:24)
			PRINTFORM \@((EX:LOCAL:22 && (EX:LOCAL:23 || EX:LOCAL:24)) || (EX:LOCAL:23 && (EX:LOCAL:22 || EX:LOCAL:24)) || (EX:LOCAL:24 && (EX:LOCAL:22 || EX:LOCAL:23)))?,#%EX:LOCAL:20?", and"#" and"%\@ 
		SIF EX:LOCAL:22
			PRINTFORM anus
		SIF EX:LOCAL:22 && (EX:LOCAL:23 || EX:LOCAL:24)
			PRINTFORM \@(EX:LOCAL:23 && EX:LOCAL:24)?,#%EX:LOCAL:20 || EX:LOCAL:21?", and"#" and"%\@ 
		SIF EX:LOCAL:23
			PRINTFORM chest
		SIF EX:LOCAL:23 && EX:LOCAL:24
			PRINTFORM \@((EX:LOCAL:21 && (EX:LOCAL:22 || EX:LOCAL:23)) ||(EX:LOCAL:22 && (EX:LOCAL:21 || EX:LOCAL:23)) ||(EX:LOCAL:23 && (EX:LOCAL:21 || EX:LOCAL:22)))?, and#%" "%and\@ 
		SIF EX:LOCAL:24
			PRINTFORM lips
		PRINTFORMW  constantly ache for more...
		CFLAG:LOCAL:溜まってる度 += (EX:LOCAL:20 + EX:LOCAL:21 + EX:LOCAL:22 + EX:LOCAL:23 + EX:LOCAL:24) * 100
	ENDIF
	{
	IF (TALENT:LOCAL:150 || TALENT:LOCAL:151) && !TALENT:LOCAL:初潮 && EXP:LOCAL:Ｖ絶頂経験 > MAX(0, 200 - CFLAG:LOCAL:子宝補正)
	 && !RAND:(10 - MIN(9, CFLAG:LOCAL:子宝補正 / 10 + ABL:LOCAL:膣射中毒)) && EX:LOCAL:膣内精液
	 }
		PRINTFORML The copious sexual stimulation %CALLNAME:LOCAL%'s immature body has received caused %HIS_HER(LOCAL)% reproductive system to jump into action...
		PRINTFORMW %CALLNAME:LOCAL% acquires 【First Menstruation】!
		PRINTFORMW %CALLNAME:LOCAL% can now become pregnant.
		TALENT:LOCAL:初潮 = 1
		CALL KOJO_MESSAGE_SEND("EVENT", 34, LOCAL, 32) ;addition custom code
		CALL QoL_Info_Write(LOCAL, Qol_Dates_FirstPeriod) ;qol custom code
	ENDIF
	IF ABL:LOCAL:精液中毒 < 3
		IF CFLAG:LOCAL:累計ぶっかけ + CFLAG:LOCAL:累計精飲 * 3 +CFLAG:LOCAL:累計膣内精液 + CFLAG:LOCAL:累計アナル精液 >= 15000 * (10 - 2 * TALENT:LOCAL:中毒しやすい) / 10
			PRINTFORML After being filled so many times, %CALLNAME:LOCAL%'s body vividly remembers the taste of %PARSE("your")% semen...
			PRINTFORML %CALLNAME:LOCAL%'s unconscious yearning for sperm has grown even stronger...
			PRINTFORMW %CALLNAME:LOCAL%'s Semen Addiction has become 3.
			ABL:LOCAL:精液中毒 = 3
		ELSEIF CFLAG:LOCAL:累計ぶっかけ + CFLAG:LOCAL:累計精飲 * 3 +CFLAG:LOCAL:累計膣内精液 + CFLAG:LOCAL:累計アナル精液 >= 7500 * (10 - 2 * TALENT:LOCAL:中毒しやすい) / 10 && ABL:LOCAL:精液中毒 < 2
			PRINTFORML %CALLNAME:LOCAL%'s body's familiarity with semen continues to grow...
			PRINTFORMW %CALLNAME:LOCAL%'s Semen Addiction has become 2.
			ABL:LOCAL:精液中毒 = 2
		ELSEIF CFLAG:LOCAL:累計ぶっかけ + CFLAG:LOCAL:累計精飲 * 3 +CFLAG:LOCAL:累計膣内精液 + CFLAG:LOCAL:累計アナル精液 >= 3000 * (10 - 2 * TALENT:LOCAL:中毒しやすい) / 10 && !ABL:LOCAL:精液中毒
			PRINTFORML %CALLNAME:LOCAL%'s body began to enjoy the taste of semen...
			PRINTFORMW %CALLNAME:LOCAL%'s Semen Addiction has become 1.
			ABL:LOCAL:精液中毒 = 1
		ENDIF
		CALL KOJO_MESSAGE_SEND("EVENT", 34, LOCAL, 30) ;addition custom code
	ENDIF
	IF CFLAG:LOCAL:累計精飲 > 10000 * (10 - 2 * TALENT:LOCAL:中毒しやすい) / 10 && !TALENT:LOCAL:精愛味覚
		PRINTFORML Having consumed semen so many times, %CALLNAME:LOCAL%'s body has started getting pleasure from the taste itself.
		PRINTFORMW %CALLNAME:LOCAL%【Loves Taste Of Semen】now!
		TALENT:LOCAL:精愛味覚 = 1
		CALL KOJO_MESSAGE_SEND("EVENT", 34, LOCAL, 31) ;addition custom code
		CALL QoL_Info_Write(LOCAL, Qol_Dates_LoveSemen) ;qol custom code
	ENDIF
	IF ABL:LOCAL:精液中毒 >= 3
		IF CFLAG:LOCAL:累計膣内精液 > 3000 * ABL:LOCAL:膣射中毒 * (10 - 2 * TALENT:LOCAL:中毒しやすい) / 10 &&  EX:LOCAL:膣内精液 > 200 && ABL:LOCAL:膣射中毒 < 3
			PRINTFORMW Semen soaks into %CALLNAME:LOCAL%'s pussy...
			PRINTFORMW %CALLNAME:LOCAL%'s vagina has started craving %PARSE("your")% semen\@ ABL:LOCAL:膣射中毒 ? %" "%even more# \@.
			ABL:LOCAL:膣射中毒 ++
			CALL KOJO_MESSAGE_SEND("EVENT", 34, LOCAL, 30, 1) ;addition custom code
		ELSEIF CFLAG:LOCAL:累計アナル精液 > 3000 * ABL:LOCAL:肛射中毒 * (10 - 2 * TALENT:LOCAL:中毒しやすい) / 10 && EX:LOCAL:アナル内精液 > 200 && ABL:LOCAL:肛射中毒 < 3
			PRINTFORMW Semen soaks into %CALLNAME:LOCAL%'s rectum...
			PRINTFORMW %CALLNAME:LOCAL%'s anus has started craving %PARSE("your")% semen\@ ABL:LOCAL:膣射中毒 ? %" "%even more# \@.
			ABL:LOCAL:肛射中毒 ++
			CALL KOJO_MESSAGE_SEND("EVENT", 34, LOCAL, 30, 2) ;addition custom code
		ENDIF
	ENDIF
	RESETCOLOR
	IF FLAG:妊娠スイッチ == 1
		SIF CFLAG:LOCAL:妊娠経過日数 == 0 && !FLAG:避妊 && !TCVAR:LOCAL:ピル && TALENT:LOCAL:行きずり != 2
			CALL GET_CHILD(LOCAL)
	ELSE
	;
	ENDIF
	CALL 出産経過(LOCAL)
	SIF CFLAG:LOCAL:600
		CFLAG:LOCAL:600 = 0
	IF CFLAG:LOCAL:面識
		会ってない日数 = DAY - CFLAG:LOCAL:最後に会った日
		CALL KOJO_MESSAGE_SEND("EVENT", 31, LOCAL, TCVAR:LOCAL:今日会ったか ? 0 # 会ってない日数)
	ENDIF
NEXT

IF CFLAG:MASTER:累計精飲 > 10000 * (10 - 2 * TALENT:MASTER:中毒しやすい) / 10 && !TALENT:MASTER:精愛味覚
	PRINTFORML Having consumed semen so many times, %PARSE("your")% body has started getting pleasure from the taste itself.
	PRINTFORMW %PARSE("You")% 【%PARSE("Love")% the Taste of Semen】now!
	TALENT:MASTER:精愛味覚 = 1
	CALL QoL_Info_Write(MASTER, Qol_Dates_LoveSemen) ;qol custom code
ENDIF
;自分に子宝補正は掛けられないため、暫定で30回分の補正が掛った値で判定する
{
IF (TALENT:PLAYER:150 || TALENT:PLAYER:151) && !TALENT:PLAYER:初潮 && EXP:PLAYER:Ｖ絶頂経験 > MAX(0, 200 - 30)
 && !RAND:(10 - MIN(9, 30 / 10 + ABL:PLAYER:膣射中毒)) && EX:PLAYER:膣内精液
 }
	PRINTFORML The copious sexual stimulation %PARSE("your")% immature body has received caused %PARSE("your",1)% reproductive system to jump into action...
	PRINTFORMW %PARSE("You acquire")% 【First Menstruation】!
	PRINTFORMW %PARSE("You")% can now become pregnant.
	TALENT:PLAYER:初潮 = 1
	CALL QoL_Info_Write(MASTER, Qol_Dates_FirstPeriod) ;qol custom code
ENDIF

FOR LOCAL, MINROOM(), MAXROOM
	YOGORE:LOCAL += RAND:50
	YANUSHI = PRIVATEROOM:(LOCAL % 100)
	IF YANUSHI && YOGORE:LOCAL > (4000 - ABL:YANUSHI:清掃技能 * 500)
		PRINTFORML %CALLNAME:YANUSHI% cleans %HIS_HER(YANUSHI)% room.
		EXP:YANUSHI:清掃経験 ++ 
		YOGORE:LOCAL /= 4
	ENDIF
	;公共の場もしくはあなたが入れない場所でプライベートルームじゃないなら上限を付ける,多少抜けはあるけどだいたいこれでカバー
	IF ROOMDATA:(LOCAL % 100) & 場所_公共の場 || (!MAP_CAN_MOVE(LOCAL, MASTER) && !YANUSHI)
;		PRINTFORMW 上限アリ：%NAME_FROM_PLACE(LOCAL)%（{LOCAL}）
		YOGORE:LOCAL = MIN(YOGORE:LOCAL, 500)
	ENDIF
NEXT
IF FLAG:加護
	FLAG:加護 = 0
	DRAWLINE
	PRINTFORMW The effect of divine protection has expired.
ENDIF
IF FLAG:異常気象
	FLAG:異常気象 = 0
	DRAWLINE
	PRINTFORMW The abnormal weather has calmed down.
ENDIF
IF FLAG:ファッション
	FLAG:ファッション = 0
	DRAWLINE
	PRINTFORMW The fashion boom has settled down.
ENDIF
IF GatheringSeason
	GatheringSeason = 0
	PRINTFORMW The season has returned to normal.
ENDIF
IF TFLAG:出張お掃除
	DRAWLINE
	PRINTFORML The cleaning service came over as requested.
	PRINTFORMW ...
	PRINTFORMW ......
	PRINTFORMW %GET_MAPNAME(MAIN_MAP)% has become clean and beautiful!
	FOR LOCAL,MINROOM(),MAXROOM
		YOGORE:LOCAL /= 4
	NEXT
ENDIF
IF FLAG:市場の神 >= 10
	FLAG:市場の神 = 0
	DRAWLINE
	PRINTL The glitter of the lunar rainbow is ceasing...
	PRINTW The souvenir shop's selection has returned to normal.
ENDIF

[SKIPSTART]
;依頼の期限切れをチェック
FOR LOCAL,1,6
	SIF !IRAI_SLOT:LOCAL
		CONTINUE
	SIF IRAI_DEADLINE:LOCAL > DAY
		CONTINUE
	DRAWLINE
	PRINTFORML %CALLNAME:(IRAI_SLOT_TO_CLIENT(LOCAL))%からの依頼の期限が切れた……
	CALL COLORMESSAGE("【依頼失敗】",C_YELLOW,2,1)
	CFLAG:(IRAI_CLIENT:LOCAL):依頼状況 = 4
NEXT
[SKIPEND]

;デイリー
;SIF CFLAG:MASTER:現在位置 != OMANEKIBEYA() && CFLAG:MASTER:デート中 == MAIN_MAP
SIF CFLAG:MASTER:現在位置 == CFLAG:MASTER:初期位置
	CALL DAILYEVENT
;畑の管理
CALL YASAI

;貸本屋
CALL BOOKLEND

CALL 酒造

CALL MOENY_HENDOU

;罠猟
CALL TRAP_PROGRESS
;虫トラップ
CALL MUSHI_TRAP_PROGRESS

MONEY:1 += MAXBASE:MASTER:TSP - BASE:MASTER:TSP
IF MONEY:1 >= 30000 && MAXBASE:MASTER:TSP == 2500
	DRAWLINE
	PRINTFORML %PARSE("Your")% control over %PARSE("your",1)% time stop ability has reached its pinnacle!
	MAXBASE:MASTER:TSP = 4000
ELSEIF MONEY:1 >= 10000 && MAXBASE:MASTER:TSP == 1500
	DRAWLINE
	PRINTFORML %PARSE("Your")% control over %PARSE("your",1)% time stop ability has become amazing!
	MAXBASE:MASTER:TSP = 2500
ELSEIF MONEY:1 >= 5000 && MAXBASE:MASTER:TSP == 800
	DRAWLINE
	PRINTFORML %PARSE("Your")% control over %PARSE("your",1)% time stop ability has improved considerably!
	MAXBASE:MASTER:TSP = 1500
ELSEIF MONEY:1 >= 1800 && MAXBASE:MASTER:TSP == 500
	DRAWLINE
	PRINTFORML %PARSE("Your")% control over %PARSE("your",1)% time stop ability is now even better than before!
	MAXBASE:MASTER:TSP = 800
ELSEIF MONEY:1 >= 500 && MAXBASE:MASTER:TSP == 200
	DRAWLINE
	PRINTFORML %PARSE("Your")% control over %PARSE("your",1)% time stop ability has become better!
	MAXBASE:MASTER:TSP = 500
ELSEIF MONEY:1 >= 150 && MAXBASE:MASTER:TSP == 100
	DRAWLINE
	PRINTFORML %PARSE("Your")% control over %PARSE("your",1)% time stop ability has become slightly better!
	MAXBASE:MASTER:TSP = 200
ENDIF

BASE:MASTER:TSP = MAXBASE:MASTER:TSP

IF TALENT:MASTER:2 & 2
	IF BASE:MASTER:精力 == MAXBASE:MASTER:精力 && !GETBIT(TFLAG:一日一回,8)
		FLAG:71 ++
		IF FLAG:71 >= 5 && !RAND:3
			DRAWLINE
			PRINTFORML %PARSE("Your")% dick is irritated because it hasn't been used lately!
			PRINTFORML %PARSE("You toss")% and %PARSE("turn")% on the futon...
			CALL COLORMESSAGE("Maximum energy decreased temporarily due to stress",C_YELLOW,2,1)
			変動値 = MIN(MAXBASE:MASTER:気力 * 20 / 100, 500)
			CALL BUFF_BASE(MASTER,BASE_気力,-変動値)
		ELSEIF FLAG:71 >= 3
			TALENT:MASTER:120 = 1
			PRINTFORML By the way, %PARSE("you haven't")% ejaculated lately...
			PRINTFORML %PARSE("You have")% [Thick Semen] now.
		ENDIF
	ELSE
		FLAG:71 = 0
		TALENT:MASTER:120 = 0
	ENDIF
	IF ((nVigCheck > MULTIPLY(MAXBASE:MASTER:精力, 80)) || (BASE:MASTER:精力 < MAXBASE:MASTER:精力 * 2 / 10)) && BASE:MASTER:体力 > 500 && nSexDone > 0 ;addition custom code
		DRAWLINE
		PRINTFORML %PARSE("You")% ejaculated a lot today...
		PRINTFORML %PARSE("You fall")% asleep spent and happy.
		CALL Add_Vig_Check ;addition custom code
		GOTO SKIPMOD ;addition custom code
		CALL COLORMESSAGE("Maximum TSP and energy have temporarily increased",C_YELLOW,2,1)
		変動値 = MIN(MAXBASE:MASTER:気力 / 10, 300)
		CALL BUFF_BASE(MASTER,BASE_気力,変動値)
		変動値 = MIN(MAXBASE:MASTER:TSP / 2, 500)
		CALL BUFF_BASE(MASTER,BASE_TSP,変動値)
	ENDIF
ENDIF
$SKIPMOD ;addition custom code


CALL ONANISM

CALL GIFT_FAVORITE

;寝る前にみんなの日記を…
CALL CHARA_DIARY_AFTERTRAIN

SIF FLAG:酒屋買取強化 > 0
	FLAG:酒屋買取強化 --
IF FLAG:土産屋大安売り >= 1000
	FLAG:土産屋大安売り = FLAG:土産屋大安売り % 1000
ELSEIF FLAG:土産屋大安売り >= 100
	FLAG:土産屋大安売り = 0
ENDIF
IF FLAG:市場の神 == 1
	FLAG:市場の神 = 0
ELSEIF FLAG:市場の神 == 2
	FLAG:市場の神 = 10
ENDIF
;何の珠を得られたか
FOR LOCAL,0,CHARANUM
	SIF CFLAG:LOCAL:出禁
		CONTINUE
	CALL SOURCE_ABLUP(LOCAL)
	CALL JUEL_CHECK(LOCAL)
	CALL DENIAL_CHECK(LOCAL)
	SIF TALENT:LOCAL:行きずり 
		CFLAG:LOCAL:出禁 = 2
NEXT

BEGIN ABLUP



@JUEL_CHECK(ARG)
FOR LOCAL,0,100
	IF PALAM:ARG:LOCAL < PALAMLV:1
		LOCAL:1 = 0
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:1*3
		LOCAL:1 = 1
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:2
		LOCAL:1 = 2
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:2*3
		LOCAL:1 = 10
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:3
		LOCAL:1 = 20
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:3*2
		LOCAL:1 = 100
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:4
		LOCAL:1 = 200
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:5
		LOCAL:1 = 1000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:6
		LOCAL:1 = 2000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:7
		LOCAL:1 = 3000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:8
		LOCAL:1 = 5000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:9
		LOCAL:1 = 8000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:10
		LOCAL:1 = 12000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:11
		LOCAL:1 = 25000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:12
		LOCAL:1 = 40000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:13
		LOCAL:1 = 60000
	ELSEIF PALAM:ARG:LOCAL < PALAMLV:14
		LOCAL:1 = 100000
	ELSE
		LOCAL:1 = 150000
	ENDIF
	;快感系
	IF LOCAL >= 0 && LOCAL < 9
		GOTJUEL:ARG:LOCAL = LOCAL:1 + EX:ARG:LOCAL * 1000
	;その他
	ELSEIF LOCAL < 30
		GOTJUEL:ARG:LOCAL = LOCAL:1
	;否定の珠
	ELSEIF LOCAL == 30 || LOCAL == 31 || LOCAL == 32
		GOTJUEL:ARG:100 += LOCAL:1
	ENDIF
NEXT
JUEL:ARG:100 += GOTJUEL:ARG:100
FOR LOCAL,0,100
	SIF !STRLENS(PALAMNAME:LOCAL)
		CONTINUE
	SIF LOCAL != 9 &&LOCAL != 30 && LOCAL != 31 && LOCAL != 32
		JUEL:ARG:LOCAL += GOTJUEL:ARG:LOCAL
NEXT
@DENIAL_CHECK(ARG)
SIF JUEL:ARG:100 == 0
	RETURN 0
LOCAL = 1
WHILE LOCAL
	SIF !JUEL:ARG:否定 || (!JUEL:ARG:恭順 && !JUEL:ARG:欲情 && !JUEL:ARG:屈服)
		BREAK
	LOCAL:1 = JUEL:ARG:否定 == 1 ? 1 # JUEL:ARG:否定 / 2
	;JUEL:10～12で消費
	LOCAL:2 = RAND:3 + 10
	SIF LOCAL:1 > JUEL:ARG:(LOCAL:2)
		LOCAL:1 = JUEL:ARG:(LOCAL:2)
	JUEL:ARG:否定 -= LOCAL:1
	JUEL:ARG:(LOCAL:2) -= LOCAL:1
WEND
LOCAL = 1
WHILE LOCAL
	SIF !JUEL:ARG:否定 || (!JUEL:ARG:恥情 && !JUEL:ARG:苦痛 && !JUEL:ARG:恐怖 && !JUEL:ARG:好意 && !JUEL:ARG:優越)
		BREAK
	LOCAL:1 = JUEL:ARG:否定 == 1 ? 1 # JUEL:ARG:否定 / 2
	;JUEL:14～16で消費
	LOCAL:2 = RAND:5
	SELECTCASE LOCAL:2
		CASE 0
			LOCAL:2 = 14
		CASE 1
			LOCAL:2 = 15
		CASE 2
			LOCAL:2 = 16
		CASE 3
			LOCAL:2 = 20
		CASE 4
			LOCAL:2 = 21
	ENDSELECT
	SIF LOCAL:1 > JUEL:ARG:(LOCAL:2)
		LOCAL:1 = JUEL:ARG:(LOCAL:2)
	JUEL:ARG:否定 -= LOCAL:1
	JUEL:ARG:(LOCAL:2) -= LOCAL:1
WEND

@ONANISM
#DIM 欲求不満度
#DIM 満足度
#DIM 嫉妬度
#DIM 珠入手倍率
#DIM 部位指定
#DIM 追加Ｖ自慰
#DIM 追加Ａ自慰
#DIM 追加Ｂ自慰
#DIM 自慰タイプ
#DIM 自慰発生閾値
#DIM CONST 文字サイズ = 15
#DIMS 自慰画像
VARSET LOCAL

FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:出禁
		CONTINUE
	部位指定 = 0
	自慰タイプ = 0
	追加Ｖ自慰 = 0
	追加Ａ自慰 = 0
	追加Ｂ自慰 = 0
	自慰発生閾値 = LIMIT(750 - 200 * TALENT:LOCAL:自慰しやすい - 100 * TALENT:LOCAL:快感応答 - 100 * TALENT:LOCAL:自己愛 + 200 * TALENT:LOCAL:自制心,500,999)
	自慰発生閾値 -= 300 * TALENT:LOCAL:自慰狂い
	IF CFLAG:LOCAL:オナバレ
		PRINTFORML %CALLNAME:LOCAL% buries %HIS_HER(LOCAL)% face in a pillow and comforts %HIS_HER(LOCAL)%self...
	ELSEIF CFLAG:LOCAL:溜まってる度 > 自慰発生閾値
		SIF !FLAG:自慰画像非表示
			CALL PrintOnanismImage(LOCAL)
		LOCAL:1 = 0
		IF TALENT:LOCAL:愛欲
			LOCAL:1 = 4
		ELSEIF TALENT:LOCAL:恋慕
			LOCAL:1 = 3
		ELSEIF TALENT:LOCAL:セフレ
			;愛人もここ
			LOCAL:1 = 2
		ELSEIF TALENT:LOCAL:思慕
			LOCAL:1 = 1
		ENDIF
		IF TALENT:LOCAL:自制心 && LOCAL:1 < 5
			LOCAL:1 += 1
		ENDIF
		IF CFLAG:LOCAL:既成事実 & 合意_うふふ && !RAND:(8 - LOCAL:1)
			;custom breakeng, this sentence gets too long
			PRINTFORML %BREAKENG(@"Even though %CALLNAME:LOCAL%'s body keeps aching from the unbearable desire, %HE_SHE(LOCAL)% calms %HIM_HER(LOCAL)%self thinking about having sex with %PARSE("you")% in the near future, forcing %HIM_HER(LOCAL)%self to sleep...")%
			自慰タイプ = 7
			;CALL KOJO_MESSAGE_SEND("EVENT",22,LOCAL,7)
			CFLAG:LOCAL:溜まってる度 = LIMIT(CFLAG:LOCAL:溜まってる度 + 100, 0 ,1000)
			SIF !CFLAG:LOCAL:神社在住 
				CFLAG:LOCAL:翌日来訪 = 1
			PRINTL
		ELSEIF CFLAG:MASTER:現在位置 == CFLAG:LOCAL:初期位置
			PRINTFORML In the middle of the night, %PARSE("you hear")% a noise and %PARSE("wake")% up.
			PRINTFORML ... %CALLNAME:LOCAL% tries to suppress %HIS_HER(LOCAL)% voice while masturbating and holding %PARSE("your")% hand...
			自慰タイプ = 2
		ELSEIF ABL:LOCAL:精液中毒 > 2 && EX:LOCAL:膣内精液 && !RAND:4
			PRINTFORML %CALLNAME:LOCAL% can no longer hold out, and %HE_SHE(LOCAL)% masturbates by rubbing %PARSE("your")% semen into %HIS_HER(LOCAL)% pussy...
			自慰タイプ = 3
		ELSEIF TALENT:LOCAL:無知
			LOCAL:1 = 0
			LOCAL:1 = EXP:LOCAL:自慰経験 - EXP:LOCAL:調教自慰経験
			IF LOCAL:1 >= 120 - (MIN(ABL:LOCAL:親密,6) * 10) && !FLAG:無知自動喪失禁止
				PRINTFORMW %CALLNAME:LOCAL% finally speaks to %HIS_HER(LOCAL)% guardians about it...
				PRINTFORMW %HE_SHE(LOCAL,1)% now knows the truth.
				PRINTFORMW %HE_SHE(LOCAL,1)% loses %HIS_HER(LOCAL)% ignorance...
				TALENT:LOCAL:無知 = 0
				CALL QoL_Info_Write(LOCAL, Qol_Dates_SexKnow) ;qol custom code
			ELSEIF LOCAL:1 >= 80 - (MIN(ABL:LOCAL:親密,4) * 10)
				PRINTFORML %CALLNAME:LOCAL% is immersed in masturbation without understanding what %HE_SHE(LOCAL)%'s even doing...
			ELSEIF LOCAL:1 >= 50 - (MIN(ABL:LOCAL:親密,3) * 10)
				PRINTFORML %CALLNAME:LOCAL% finds %HIM_HER(LOCAL)%self humping a desk corner for stimulation...
			ELSEIF LOCAL:1 >= 30 - (MIN(ABL:LOCAL:親密,2) * 10)
				PRINTFORML %CALLNAME:LOCAL% secretly rubs a book against %HIS_HER(LOCAL)% crotch...
			ELSEIF LOCAL:1 >= 15 - (MIN(ABL:LOCAL:親密,1) * 10)
				PRINTFORML During study, %CALLNAME:LOCAL% rubs %HIS_HER(LOCAL)% pussy with %HIS_HER(LOCAL)% brush...
			ELSE
				PRINTFORML %CALLNAME:LOCAL% rubs %HIS_HER(LOCAL)% thighs together while lying in bed, sighing in anguish...
			ENDIF
			自慰タイプ = 4
		ELSEIF ABL:LOCAL:自慰中毒 >= 3 && (TALENT:LOCAL:思慕 || IS_LOVER(LOCAL) || TALENT:LOCAL:恋人 || TALENT:LOCAL:セフレ) && !RAND:3
			PRINTFORML When %CALLNAME:LOCAL% can no longer hold back, she intensely masturbates while sniffing clothes marked with %PARSE("your")% scent...
			LOCAL:2 = RAND:5
			IF (TALENT:LOCAL:Ｃ感度 == 1 || ABL:LOCAL:Ｃ感覚 >= 10) && LOCAL:2 == 4
				PRINTFORM Furiously rubbing her clit, she squirts as she cums over and over
				部位指定 = 1
			ELSEIF (TALENT:LOCAL:Ｖ感度 == 1 || ABL:LOCAL:Ｖ感覚 >= 10) && LOCAL:2 == 3
				PRINTFORM Fingering herself deeply until her hand is wet with juices, she cums again and again
				部位指定 = 2
				追加Ｖ自慰 = 1
			ELSEIF (TALENT:LOCAL:Ａ感度 == 1 || ABL:LOCAL:Ａ感覚 >= 10) && LOCAL:2 == 2
				PRINTFORM Fingering her anus as well as her pussy, she cums again and again
				部位指定 = 3
				追加Ａ自慰 = 1
			ELSEIF (TALENT:LOCAL:Ｂ感度 == 1 || ABL:LOCAL:Ｂ感覚 >= 10) && LOCAL:2 == 1
				IF TALENT:LOCAL:バストサイズ >= 1
					PRINTFORM Bringing her nipples to her mouth, 
				ELSEIF TALENT:LOCAL:バストサイズ <= -1
					PRINTFORM Stroking her flat chest, 
				ELSE
					PRINTFORM Squeezing her soft breasts, 
				ENDIF
				PRINTFORM she cums again and again
				部位指定 = 4
				追加Ｂ自慰 = 0
			ELSE
				;PRINTFORML しかし、どんなに快感を得ようとも%CALLNAME:MASTER%への想いは治まることはなく、%CALLNAME:LOCAL%は朝まで悶々とした時間を過ごした……
				PRINTFORM Her body trembles in pleasure
				部位指定 = 5
			ENDIF
			IF TALENT:LOCAL:思慕 || TALENT:LOCAL:恋慕 || TALENT:LOCAL:恋人
				PRINTFORML , her thoughts of you continuing to linger...
			ELSE
				PRINTFORML , her heated passion never fading...
			ENDIF
			追加Ｖ自慰 = 1
			自慰タイプ = 5
		ELSEIF ABL:LOCAL:自慰中毒 >= 3 && !RAND:2
			PRINTFORML Unable to satisfy herself with fingers anymore, %CALLNAME:LOCAL% uses a toy that she bought from a Kappa.
			PRINTFORM She indulges herself in masturbation, 
			IF ABL:LOCAL:Ａ感覚 >= 5
				IF TALENT:LOCAL:貞操 == -1 && TALENT:LOCAL:処女 == 0
					PRINTFORM inserting it deep inside her ass and pussy and pistoning it
					追加Ｖ自慰 = 1
				ELSEIF TALENT:LOCAL:貞操 == 1
					PRINTFORM gently and carefully pressing and rubbing it against her ass and pussy
				ELSE
					PRINTFORM playing with her ass and pussy, teasing them
				ENDIF
				部位指定 = 3
				追加Ａ自慰 = 1
			ELSE
				IF TALENT:LOCAL:貞操 == -1 && TALENT:LOCAL:処女 == 0
					PRINTFORM inserting it deep inside her pussy and pistoning it
					追加Ｖ自慰 = 1
				ELSEIF TALENT:LOCAL:貞操 == 1
					PRINTFORM gently and carefully pressing and rubbing it against her pussy
				ELSE
					PRINTFORM playing with her pussy, teasing it
				ENDIF
				部位指定 = 1
				
			ENDIF
			PRINTFORML ...
			自慰タイプ = 6
		ELSE
			PRINTFORML As %CALLNAME:LOCAL% can no longer keep %HIS_HER(LOCAL)% aching body in check, %HE_SHE(LOCAL)% begins to play with %HIS_HER(LOCAL)% fingers...
			自慰タイプ = 1
		ENDIF
		CALL KOJO_MESSAGE_SEND("EVENT",22,LOCAL,自慰タイプ,部位指定)
		SIF 自慰タイプ == 7
			CONTINUE
		EXP:LOCAL:自慰経験 += ABL:LOCAL:自慰中毒 * 2 + 1
		CALL COLORMESSAGE(@"Masturbation Exp＋{ABL:LOCAL:自慰中毒 * 2 + 1}",C_PINK,1)
		SELECTCASE ABL:LOCAL:自慰中毒
			CASE 0 TO 2
				珠入手倍率 = 5
			CASE 3 TO 5
				珠入手倍率 = 3
			CASEELSE
				珠入手倍率 = 1
		ENDSELECT
		;快C
		CALL JUEL_UP_SPECIAL(LOCAL,"快Ｃ","Ｃ感覚",珠入手倍率,1)
		SIF 追加Ｖ自慰
			CALL JUEL_UP_SPECIAL(LOCAL,"快Ｖ","Ｖ感覚",珠入手倍率,1)
		SIF 追加Ａ自慰
			CALL JUEL_UP_SPECIAL(LOCAL,"快Ａ","Ａ感覚",珠入手倍率,1)
		SIF 追加Ｂ自慰
			CALL JUEL_UP_SPECIAL(LOCAL,"快Ｂ","Ｂ感覚",珠入手倍率,1)
		;恥情
		CALL JUEL_UP_SPECIAL(LOCAL,"恥情","自慰中毒",珠入手倍率 * 2,1)
		;欲情
		CALL JUEL_UP_SPECIAL(LOCAL,"欲情","欲望",珠入手倍率,1)
		CFLAG:LOCAL:溜まってる度 /= 4
		PRINTW 
	ENDIF
	;まず欲望で基礎値を決める
	SELECTCASE ABL:LOCAL:欲望
		CASE 0 TO 2
			欲求不満度 = 10
		CASE 3 TO 5
			欲求不満度 = 20
		CASE 6 TO 10
			欲求不満度 = 30
		CASE 10 TO 15
			欲求不満度 = 50
		CASEELSE
			欲求不満度 = 80
	ENDSELECT

	欲求不満度 += ABL:LOCAL:自慰中毒 * 7 + (ABL:LOCAL:膣射中毒 + ABL:LOCAL:肛射中毒) * 3
	欲求不満度 = 欲求不満素質補正(LOCAL,欲求不満度)
	SELECTCASE 欲求不満度
		CASE IS > 400
			欲求不満度 = (欲求不満度 - 400) / 10 + 400
		CASE IS > 200
			欲求不満度 = (欲求不満度 - 200) / 4 + 200
		CASE IS > 100
			欲求不満度 = (欲求不満度 - 100) / 2 + 100
	ENDSELECT
	SIF 自慰タイプ
		欲求不満度 = 0
	SIF TCVAR:LOCAL:今日会ったか
		欲求不満度 /= 2
	SIF TALENT:LOCAL:妊娠
		欲求不満度 /= 5
	SIF TALENT:LOCAL:育児中
		欲求不満度 /= 2
	;溜まってる度は0~1000の間に
	CFLAG:LOCAL:溜まってる度 = LIMIT(CFLAG:LOCAL:溜まってる度 + 欲求不満度, 0, 1000)
	DEBUGPRINTFORML %CALLNAME:LOCAL%の満足度：{満足度}　欲求不満上昇値：{欲求不満度}　溜まってる度：{CFLAG:LOCAL:溜まってる度}
NEXT

@DAILYEVENT
;デイリー最大数は イベント関連\デイリーイベント\DAILY_VARS.ERH で指定
#DIM EVENT_DAY,DAILY_EV_MAX

CALL FISHER_YATES_SHAFFLE(30)
ARRAYCOPY "SHAFFLE_ARRAY" , "EVENT_DAY"

DRAWLINE
FOR LOCAL,0,DAILY_EV_MAX
	RESULT = 0
	TRYCALLFORM DAILY_CONDITION_EV{EVENT_DAY:LOCAL}
	IF RESULT > 0
		DEBUGPRINTFORML デイリーイベント{EVENT_DAY:LOCAL}が発生したよ
		DRAWLINE
		BREAK
	ENDIF
	SIF RESULT == -1
		DEBUGPRINTFORML デイリーイベント{EVENT_DAY:LOCAL}は発生条件を満たさなかったんだぜ
NEXT
;addition custom code, make market event guaranteed (way too rare otherwise, has to compete with 30+ other events)
IF FLAG:市場の神 == 1 ;if didn't trigger
	TRYCALLFORM DAILY_CONDITION_EV33
	DRAWLINE
ENDIF
;-----------------------------------------------------------------------
;PRINTFORMW 
PRINTFORML 


@欲求不満素質補正(ARG,ARG:1)
#FUNCTION
LOCAL = ARG:1
SIF TALENT:ARG:自制心
	LOCAL /= 2
SIF TALENT:ARG:自己愛 < 0
	LOCAL /= 2
SIF TALENT:ARG:自己愛 > 0
	LOCAL = LOCAL * 5 / 4
SIF TALENT:ARG:幼稚 && !TALENT:ARG:初潮
	LOCAL /= 4
;Custom code
IF !FLAG:STABLEBYPASS
	SIF TALENT:ARG:妊娠
		LOCAL /= 4
	SIF TALENT:ARG:育児中
		LOCAL /= 2
ENDIF
SIF CFLAG:ARG:オナバレ
	LOCAL /= 4
RETURNF LOCAL