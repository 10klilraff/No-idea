@DAILY_CONDITION_EV12
VARSET LOCAL
IF RAND:15 > 0
	RETURN -1
ELSE
	CALL FISHER_YATES_SHAFFLE(CHARANUM - 1)
	FOR LOCAL:10, 0, CHARANUM - 1
		LOCAL = SHAFFLE_ARRAY:(LOCAL:10) + 1
		IF CFLAG:LOCAL:信頼度 >= 100 && CFLAG:LOCAL:神社在住
			LOCAL:1 = LOCAL
			BREAK
		ENDIF
	NEXT
	IF !LOCAL:1
		RETURN -1
	ELSE
		CALL DAILY_EV12(LOCAL:1)
		RETURN 1
	ENDIF
ENDIF

@DAILY_EV12(ARG)
#DIM 正解
#DIM 経験値
#DIM 制限時間
#DIM 倍率
#DIM FEINT
#DIM イカサマ
VARSET LOCAL
イカサマ = 0
制限時間 = 2000 - (ABL:ARG:戦闘能力 - ABL:MASTER:戦闘能力) * 100
CALL KOJO_MESSAGE_SEND("DAILY", 12, ARG, 1)
CALL ASK_YN("Accept","Decline")
IF RESULT
	CALL KOJO_MESSAGE_SEND("DAILY", 12, ARG, 2)
ELSE
	正解 = RAND:4
	経験値 = MAX(ABL:ARG:戦闘能力 * 5, 5)
	SELECTCASE RAND:20
		CASE IS <= 11
		;普通に
			LOCALS:0 = ↑
			LOCALS:1 = →
			LOCALS:2 = ←
			LOCALS:3 = ↓
			倍率 = 100
		CASE 12 TO 15
		;ローマ字
			LOCALS:0 = UE
			LOCALS:1 = MIGI
			LOCALS:2 = HIDARI
			LOCALS:3 = SHITA
			倍率 = 160
		;英語
		CASE 16,17,18
			LOCALS:0 = UP
			LOCALS:1 = RIGHT
			LOCALS:2 = LEFT
			LOCALS:3 = DOWN
			倍率 = 120
		;スペイン語
		CASE 19
			LOCALS:0 = Arriba
			LOCALS:1 = Derecha
			LOCALS:2 = Izquierda
			LOCALS:3 = Abajo
			倍率 = 300
	ENDSELECT
	;戦闘能力が高いとフェイントかけてくる
	IF ABL:ARG:戦闘能力 > 3 && !RAND:3
		FEINT = 1
		制限時間 += 400
		倍率 += 80
	ELSE
		FEINT = 0
	ENDIF
	CALL FISHER_YATES_SHAFFLE(4)
	PRINTFORMW Avoid %CALLNAME:ARG%'s attack, dodge in the opposite direction (Click to start)
	CALL KOJO_MESSAGE_SEND("DAILY", 12, ARG, 3)
	PRINTFORM %CALLNAME:ARG% attacks from 
	SETCOLOR C_YELLOW
	IF FEINT
		PRINTFORM %LOCALS:(RAND:4)%
		RESETCOLOR
		PRINTFORM  but it's actually from %LOCALS:(3 - 正解)%
	ELSE
		PRINTFORM %LOCALS:(3 - 正解)%
		RESETCOLOR
	ENDIF
	PRINTFORML !
	$LOOP_IKASAMA
	FOR LOCAL:10,0,4
		LOCAL = SHAFFLE_ARRAY:(LOCAL:10)
		PRINTFORML [{LOCAL}] - Dodge %LOCALS:LOCAL%
	NEXT
	SIF !イカサマ && BASE:MASTER:TSP > 500
		PRINTFORML [{4}] - Time Stop
	TINPUT 制限時間, -1, 1
	IF RESULT == 正解
		PRINTFORML %PARSE("You graze")% the attack!
		IF !イカサマ
			PRINTFORMW It was valuable to both of %PARSE("you",4)%.
		ELSE
			PRINTFORMW Cheating means it wasn't really that good a practice...
		ENDIF
		CALL KOJO_MESSAGE_SEND("DAILY", 12, ARG, 4, イカサマ)
		経験値 = 経験値 * 倍率 / 100
		;custom code
		CALL AddEXP("戦闘経験", MASTER, 経験値)
		CALL AddEXP("戦闘経験", ARG, 経験値 * 2)
		CALL CHANGE_CFLAG(2,ARG,経験値 * 2)
		CALL CHANGE_CFLAG(4,ARG,経験値 * 2)
	ELSEIF RESULT == 4 && !イカサマ && BASE:MASTER:TSP > 500
		イカサマ = 1
		倍率 /= 5
		PRINTFORMW %PARSE("You use")% time stop to see through the attack!
		GOTO LOOP_IKASAMA
	ELSE
		PRINTFORML Missed it!
		PRINTFORM %PARSE("You experience")% 
		SELECTCASE ABL:ARG:戦闘能力
			CASE IS <= 1
				PRINTFORM a little bit
			CASE 2,3
				PRINTFORM a bit
			CASEELSE
				PRINTFORM an absurd amount
		ENDSELECT
		PRINTFORMW  of pain.
		CALL KOJO_MESSAGE_SEND("DAILY", 12, ARG, 5)
		;custom code
		CALL RECOVER(MASTER, -(400 * (1 + ABL:ARG:戦闘能力)), "体力")
		CALL AddEXP("戦闘経験", ARG, 経験値)
		CALL CHANGE_CFLAG(2,ARG,経験値 / 2)
		CALL CHANGE_CFLAG(4,ARG,経験値 / 2)
	ENDIF
ENDIF
PRINTW 



@DAILY_MESSAGE_12(ARG, ARG:1, ARG:2, ARG:3, ARG:4, ARGS:1, ARGS:2)
SELECTCASE ARG:1
CASE 1
	PRINTFORMDL %CALLNAME:ARG% asks for help in practicing a new technique.
	SIF ABL:ARG:戦闘能力 >= 4
			PRINTFORMDL Since it's them, screwing up will probably hurt considerably...
CASE 2
	PRINTFORMDL %CALLNAME:ARG% looks disappointed...
ENDSELECT