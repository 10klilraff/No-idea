;FileName_情事発覚.ERB --------------------------------- Rev1.00
;情事の発覚
;CALL		USER
;ARG		0:入室キャラNO, 1:発覚状況(0:同室, 1:覗き)
;RETURN		0:同室, 1:立ち去る
;COMMENT	
;-----------------------------------------------------------
@AFFAIR_DISCLOSURE(ARG, NOZOKI)
#DIM PREV_TARGET
#DIM IRAI_ABLE
#DIM NOZOKI
#DIM FORCE_STOP
#DIM FORCE_GETOUT
#DIM WITNESSED
#DIM HAPPENING_TYPE

VARSET LOCAL
FORCE_STOP = 0
FORCE_GETOUT = 0
HAPPENING_TYPE = 0
WITNESSED = TFLAG:現在のTARGET
SIF CFLAG:MASTER:イタズラ
	WITNESSED = TARGET
IF WITNESSED && (CFLAG:(WITNESSED):うふふ || CFLAG:(WITNESSED):イタズラ) && !GETBIT(FLAG:潜伏,0)
	DRAWLINE
	IF CFLAG:(WITNESSED):イタズラ
		PRINTFORMW %CALLNAME:ARG% catches %PARSE("you")% in bed with %CALLNAME:(WITNESSED)%!
		IF !CFLAG:ARG:弱み握り && CFLAG:(WITNESSED):イタズラ == 2
			PRINTFORMW %CALLNAME:ARG% can use this to blackmail %PARSE("you")%.
		ELSE
			PRINTFORMW %CALLNAME:ARG% looks disgusted.
		ENDIF
		CALL TOUCH_SET(0,0,0,1)
		DRAWLINE
		FORCE_STOP = 1
		CFLAG:ARG:弱み握り += 10
	ELSEIF TFLAG:102 == 3 && TFLAG:抱きつきモード
		PRINTFORMW %CALLNAME:ARG% catches %PARSE("you")% flirting with %CALLNAME:(TFLAG:現在のTARGET)%!
		PRINTFORMW %CALLNAME:(TFLAG:現在のTARGET)% looks regretful as %HE_SHE(TFLAG:現在のTARGET)% separates %PARSE("from you",1)%...
		CALL UWAKI_RECORD(ARG,0,TFLAG:現在のTARGET)
		TFLAG:抱きつきモード = 0
		TFLAG:102 = 1
		CALL 押し倒され終了
	ELSE
		IF NOZOKI
			PRINTFORMW From【%NAME_FROM_PLACE(CFLAG:ARG:現在位置)%】%CALLNAME:ARG% comes upon the liaison between %PARSE("you")% and %CALLNAME:(TFLAG:現在のTARGET)%!
		ELSE
			PRINTFORMW %CALLNAME:ARG% comes upon the liaison between %PARSE("you")% and %CALLNAME:(TFLAG:現在のTARGET)%!
		ENDIF
		;bug fix custom code for secluded spot FLAG:潜伏
		IF RESULT:10 == 99
			CALL UWAKI_RECORD(ARG,0,TFLAG:現在のTARGET)
		ELSE
			CALL UWAKI_RECORD(ARG)
		ENDIF
		;恋人同士（もしくは恋人を含む）場合
		IF TALENT:MASTER:恋人 && (TALENT:(TFLAG:現在のTARGET):恋人 || WITH_恋人())
			;出くわした
			IF !NOZOKI
				[SKIPSTART]
				;恋慕かある程度仲が良い
				IF (CFLAG:ARG:6 > 1 || TALENT:ARG:恋慕)
					PRINTFORMW %CALLNAME:ARG%は気まずそうにしている…
					PRINTL [0] まとめて押し倒す
					PRINTL [1] やめておく
					$INPUT_EMBRACE
					INPUT
					IF RESULT == 0
						IF !NOZOKI
							PRINTFORMW %CALLNAME:MASTER%が手招きすると%CALLNAME:ARG%は嬉しそうに体を摺り寄せてきた
							CFLAG:ARG:うふふ = CFLAG:(WITNESSED):うふふ
							CFLAG:ARG:同室 = 1
							;見た人
							CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,4)
							;見られた人（最も地位の高いTARGETで代表）
							CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,4)
						ENDIF
					ELSEIF RESULT == 1
						PRINTFORMW %CALLNAME:ARG%は%CALLNAME:MASTER%と目が合うとそそくさとその場を去った…
						IF !NOZOKI
							;見た人
							CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,3)
							;見られた人（最も地位の高いTARGETで代表）
							CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,3)
						ENDIF
						IF CFLAG:ARG:現在位置 == CFLAG:ARG:前ターン位置
							CALL GETOUT(ARG)
						ELSE
							CFLAG:ARG:現在位置 = CFLAG:ARG:前ターン位置
						ENDIF
						;キャラを動かしたのでTARGET再設定
						CALL TARGETSET_CHACK
						CFLAG:ARG:同室 = 0
						RETURN 1
					ELSE
						CLEARLINE 1
						GOTO INPUT_EMBRACE
					ENDIF
				;参戦するほど仲は良くない
				ELSE
				[SKIPEND]
				;入ってきた方が恋人だった場合、割り込んでくる
;				IF TALENT:MASTER:恋人 && TALENT:MASTER:恋人 == TALENT:ARG:恋人
				IF TALENT:ARG:恋人
					PRINTFORMW %CALLNAME:ARG% is unable to resist and joins in.
					CFLAG:ARG:うふふ = CFLAG:(WITNESSED):うふふ
					;恋人が他の女といちゃついてたら、理性吹っ飛んでもおかしくないし、テンションマックスって意味でムードが上がってもいいんじゃないかなぁと愚考する次第
					;BASE:ARG:ムード = MAXBASE:ARG:ムード
					;BASE:ARG:理性 = 0
					;上記がコメントアウトされてるけど、それが原因のバグが出てるっぽいので追記
					;まあ、マックス＆0にするのはさすがにやり過ぎだった気がしないでもない
					SIF BASE:ARG:ムード < 300
						BASE:ARG:ムード = 300
					SIF BASE:ARG:理性 > 300
						BASE:ARG:理性 = 300
					;見た人
					CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,2)
					CALL UWAKI_RECORD(ARG,1,RESULT:10 == 99 ? WITNESSED # 0) ;bug fix
					;見られた人（最も地位の高いTARGETで代表）
					;CALL KOJO_EVENT(4,WITNESSED,ARG)
					CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,2)
					CALL UWAKI_RECORD(WITNESSED,1,RESULT:10 == 99 ? ARG # 0) ;bug fix
					DRAWLINE
					RETURN 0
				;してる方が恋人だった場合、そそくさと立ち去る（恋人が混じっている多人数プレイかつTARGETが恋人でない場合も立ち去る
				ELSE
					PRINTFORMW When %PARSE("your")% eyes meet %HE_SHE(ARG)% runs away in a hurry...
					IF !NOZOKI
						;見た人
						CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,3)
						;見られた人（最も地位の高いTARGETで代表）
						CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,3)
					ENDIF
					IF CFLAG:ARG:現在位置 == CFLAG:ARG:前ターン位置
						CALL GETOUT(ARG)
					ELSE
						CFLAG:ARG:現在位置 = CFLAG:ARG:前ターン位置
					ENDIF
					;キャラを動かしたのでTARGET再設定
					CALL TARGETSET_CHACK
					CFLAG:ARG:同室 = 0
					RETURN 1
				ENDIF
			;覗きみた
			ELSE
				PRINTFORMW %CALLNAME:ARG% hides %HIS_HER(ARG)% blushing face before running off in a hurry...
				IF !NOZOKI
					;見た人
					CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,3)
					;見られた人（最も地位の高いTARGETで代表）
					CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,3)
				ENDIF
				IF CFLAG:ARG:現在位置 == CFLAG:ARG:前ターン位置
					CALL GETOUT(ARG)
				ELSE
					CFLAG:ARG:現在位置 = CFLAG:ARG:前ターン位置
				ENDIF
				;キャラを動かしたのでTARGET再設定… まぁ、覗きなんで大丈夫なはず。
				CALL TARGETSET_CHACK
				CFLAG:ARG:同室 = 0
				RETURN 1
			ENDIF
		;ここから恋人が含まれていない場合
		ELSEIF (TALENT:ARG:淫乱 || TALENT:ARG:ヤリマン)
			PRINTFORMW %PARSE("You call")% %CALLNAME:ARG% over and %HE_SHE(ARG)% gladly comes over.
			CFLAG:ARG:うふふ = CFLAG:(WITNESSED):うふふ
			CFLAG:ARG:同室 = 1
			;見た人
			CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,4)
			;見られた人（最も地位の高いTARGETで代表）
			CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,4)
		ELSEIF TALENT:ARG:無知 && !(CFLAG:ARG:既成事実 & 合意_うふふ)
		;無知かつうふふ未経験
			IF NOZOKI
				PRINTFORMW From %NAME_FROM_PLACE(CFLAG:ARG:現在位置)% %CALLNAME:ARG% looks at %PARSE("your",2)% liaison \@ TALENT:ARG:羞恥心 > 0 ? despite hiding %HIS_HER(ARG)% blushing face# full of curiosity\@...
			ELSE
				PRINTFORML %CALLNAME:ARG% looks at %PARSE("your",2)% liaison \@ TALENT:ARG:羞恥心 > 0 ? despite hiding %HIS_HER(ARG)% blushing face# full of curiosity\@...
				PRINTFORMW %PARSE("You drive")% off the confused %CALLNAME:ARG%.
				CALL GETOUT(ARG)
				CALL TARGETSET_CHACK
				CFLAG:ARG:同室 = 0
			ENDIF
		;そのキャラの私室でヤってた場合
		ELSEIF CFLAG:ARG:初期位置 == CFLAG:MASTER:現在位置
			PRINTFORML %CALLNAME:ARG% criticizes %PARSE("you")% for doing such things in %HIS_HER(ARG)% room before %PARSE("kicking you",1)% out.
			PRINTFORMW %HE_SHE(ARG,1)% can use this to %PARSE("blackmail you",1)%.
			CALL GET_ANGRY(ARG)
			FORCE_STOP = 1
			FORCE_GETOUT = 1
			CFLAG:ARG:弱み握り += 20
			DRAWLINE
		ELSEIF CFLAG:ARG:行動 == 5 && CFLAG:MASTER:現在位置 == CFLAG:ARG:職場
			PRINTFORML %CALLNAME:ARG% criticizes %PARSE("you")% for doing such things in %HIS_HER(ARG)% workplace before %PARSE("kicking you",1)% out.
			PRINTFORMW %HE_SHE(ARG,1)% can use this to %PARSE("blackmail you",1)%.
			CALL GET_ANGRY(ARG)
			FORCE_STOP = 1
			FORCE_GETOUT = 1
			CFLAG:ARG:弱み握り += 20
			DRAWLINE
		ELSEIF CFLAG:ARG:態度 < 3 && PRIVATEROOM:(CFLAG:MASTER:現在位置 % 100) && PRIVATEROOM:(CFLAG:MASTER:現在位置 % 100) != TARGET
			SIF !CFLAG:ARG:弱み握り
				PRINTFORMW %CALLNAME:ARG% can use this to blackmail %PARSE("you")%.
			IF !NOZOKI
				;見た人
				;CALL KOJO_EVENT(1,ARG,WITNESSED)
				CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,1)
				;見られた人（最も地位の高いTARGETで代表）
				;CALL KOJO_EVENT(2,WITNESSED,ARG)
				CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,1)
			ENDIF
			CALL TOUCH_SET(0,0,0,1)
			DRAWLINE
			FORCE_STOP = 1
			CFLAG:ARG:弱み握り += 10
			;謹慎処分とか弱味とかの処理を入れる予定
		ELSE
			IF !NOZOKI
				;態度だけだと既成事実とか見てないことに気づく、ので合意がある場合に迫ってくるように
				IF TCVAR:MASTER:馴れ合い >= 3 && GETBIT(CFLAG:ARG:既成事実 , 1)
					PRINTFORMW %CALLNAME:ARG% is unable to resist and joins in.
					CFLAG:ARG:うふふ = CFLAG:(WITNESSED):うふふ
					;一応、押し倒し可能なムードに
					;理性は押し倒しに関係してないけど、理性的なら混ざろうとしないだろうと
					BASE:ARG:ムード = MAX(BASE:ARG:ムード, 300)
					BASE:ARG:理性 = MIN(BASE:ARG:理性, 300)
					;見た人
					CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,2)
					CALL UWAKI_RECORD(ARG,1,RESULT:10 == 99 ? WITNESSED # 0) ;bug fix
					;見られた人（最も地位の高いTARGETで代表）
					CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,2)
					CALL UWAKI_RECORD(WITNESSED,1,RESULT:10 == 99 ? ARG # 0) ;bug fix
					DRAWLINE
					RETURN 0
				ELSE
					PRINTFORMW %CALLNAME:ARG% \@TALENT:ARG:恋慕 ?looks disappointed  #turns away embarrassed  \@ and leaves in a hurry...
					;見た人
					CALL KOJO_MESSAGE_SEND("EVENT",6,ARG,WITNESSED,3)
					;見られた人（最も地位の高いTARGETで代表）
					CALL KOJO_MESSAGE_SEND("EVENT",7,WITNESSED,ARG,3)
					IF CFLAG:ARG:現在位置 == CFLAG:ARG:前ターン位置
						CALL GETOUT(ARG)
					ELSE
						CFLAG:ARG:現在位置 = CFLAG:ARG:前ターン位置
					ENDIF
					CALL TARGETSET_CHACK
					CFLAG:ARG:同室 = 0
					IF TALENT:ARG:恋慕
						TALENT:ARG:機嫌 = MIN(TALENT:ARG:機嫌, 0)
						BASE:ARG:怒り += MAXBASE:ARG:怒り / 2
						CFLAG:ARG:情事目撃 = TARGET
					ENDIF
					RETURN 1
				ENDIF
			;覗かれた場合
			ELSE
				PRINTFORMW %CALLNAME:ARG% \@TALENT:ARG:恋慕 ?is disappointed #turns away embarrassed \@...
				IF TALENT:ARG:恋慕 && !CFLAG:ARG:情事目撃
					IF TCVAR:ARG:馴れ合い < 3
						TALENT:ARG:機嫌 = MIN(TALENT:ARG:機嫌, 0)
						BASE:ARG:怒り += MAXBASE:ARG:怒り / 4
					ENDIF
					CFLAG:ARG:情事目撃 = TARGET
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF
IF FORCE_STOP
	SIF CFLAG:MASTER:イタズラ
		CALL SET_PRANK(0,TARGET,2)
	FOR LOCAL,0,CHARANUM
		CALL ENDUFUFU(LOCAL)
		SIF LOCAL == WITNESSED || LOCAL == ARG
			CONTINUE
		CALL GETOUT(LOCAL)
	NEXT
	TFLAG:106 = 0
	RETURN 0
ENDIF
;通常遭遇
;experimental bug fix: modified below, TIME != TIME:3 removed because girls won't greet you right after waking up with tsp movement enabled (time doesn't change therefore this gets ignored)
IF !CFLAG:ARG:睡眠 && (!NOZOKI || CFLAG:ARG:現在位置 == CFLAG:MASTER:現在位置) && !FLAG:時間停止 && !CHK_DATENOW(CFLAG:ARG:デート中) && !GETBIT(FLAG:潜伏,0)
	;デート中に偶然遭遇
	IF CHK_DATENOW(CFLAG:MASTER:デート中)
		IF !CFLAG:(NO:ARG):面識 && TALENT:ARG:行きずり
			PRINTFORMW %PARSE("You call")% out to %CALLNAME:ARG%.
		ELSE
			IF !TCVAR:ARG:今日会ったか
				PRINTFORMW %PARSE("You come")% across %CALLNAME:ARG%.
			ELSE
				PRINTFORMW %PARSE("You come")% across %CALLNAME:ARG% again.
			ENDIF
		ENDIF
		IF CFLAG:(NO:ARG):面識 == 0
			CALL SYOTAIMEN(ARG)
		ELSE
			FOR LOCAL:4,1,CHARANUM
				IF CHK_DATENOW(CFLAG:(LOCAL:4):デート中)
					IF !TCVAR:ARG:今日会ったか
						CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,9,LOCAL:4)
					ENDIF
					CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,7,LOCAL:4) 
					BREAK
				ELSEIF LOCAL:4 == CHARANUM - 1
					IF !TCVAR:ARG:今日会ったか
						CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,9,0)
					ENDIF
					CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,6,0)
				ENDIF
			NEXT
		ENDIF
	;さっきから居るのは分かっていたか否か
	ELSEIF CFLAG:ARG:前ターン位置 == CFLAG:ARG:現在位置
		PRINTFORM %CALLNAME:ARG% \@CAN_MOVE(CFLAG:MASTER:前ターン位置, CFLAG:ARG:現在位置) == 2 ? is # is\@ 
		PRINTFORMW %AT_IN_ON(CFLAG:ARG:現在位置 > 100 ? CFLAG:ARG:現在位置 + 8000 # CFLAG:ARG:現在位置)%.
		SIF !TCVAR:ARG:今日会ったか
			PRINTFORML Upon seeing %CALLNAME:ARG%, %PARSE("you realize you")% %PARSE("haven't")% greeted %HIM_HER(ARG)% today.
		HAPPENING_TYPE = 1
	;風呂に入ってきた場合
	ELSEIF CAN_MOVE(CFLAG:MASTER:前ターン位置, CFLAG:ARG:前ターン位置) < 2 && CFLAG:ARG:前ターン位置 != CFLAG:MASTER:前ターン位置 && BATHCHECK(CFLAG:MASTER:現在位置)
		PRINTFORMW %CALLNAME:ARG% enters the bathroom...
		IF 風呂追い出し条件(ARG)
			IF CFLAG:ARG:弱み握り && HETEROSEX(MASTER,ARG)
				PRINTFORMW %CALLNAME:ARG% drives %PARSE("you")% out...
				CFLAG:MASTER:現在位置 = KICKOUT()
				HAPPENING_TYPE = 5
				CALL TARGETSET_CHACK
				CFLAG:ARG:同室 = 0
			ELSE
				PRINTFORMW %PARSE("Your",2)% eyes meet for a brief moment before %CALLNAME:ARG% runs off...
				CFLAG:ARG:現在位置 = CFLAG:ARG:前ターン位置
				CALL TARGETSET_CHACK
				CFLAG:ARG:同室 = 0
				HAPPENING_TYPE = 4
			ENDIF
		ELSE
			PRINTFORMW Seems like %CALLNAME:ARG% wants to join %PARSE("you")%.
			SIF !TCVAR:ARG:今日会ったか
				PRINTFORML %PARSE("You realize you")% %PARSE("haven't")% greeted %HIM_HER(ARG)% today.
			HAPPENING_TYPE = 3
		ENDIF
	;トイレに入ってきた場合
	ELSEIF CAN_MOVE(CFLAG:MASTER:前ターン位置, CFLAG:ARG:前ターン位置) < 2 && CFLAG:ARG:前ターン位置 != CFLAG:MASTER:前ターン位置 && IN_TOILET(CFLAG:MASTER:現在位置)
		PRINTW Someone knocks on the toilet door.
		$TOILET_INPUT_LOOP
		PRINTL [0]Tell them it's occupied　[1]Let them in
		INPUT
		IF RESULT == 1
			PRINTFORM %CALLNAME:ARG% enters the 
			PRINTFORMS NAME_FROM_PLACE(CFLAG:ARG:現在位置)
			PRINTFORMW .
			HAPPENING_TYPE = 2
		ELSEIF RESULT == 0
			PRINTW The footsteps grows quiet.
			CFLAG:ARG:現在位置 = CFLAG:ARG:前ターン位置
			CALL TARGETSET_CHACK
			CFLAG:ARG:同室 = 0
			HAPPENING_TYPE = -1
		ELSE
			GOTO TOILET_INPUT_LOOP
		ENDIF
	;ELSEIF CAN_MOVE(CFLAG:MASTER:前ターン位置, CFLAG:ARG:前ターン位置) < 2 && CFLAG:ARG:前ターン位置 != CFLAG:MASTER:前ターン位置
	;↑の条件では来訪時に初対面口上が出ない場合があるので変更
	ELSEIF CFLAG:ARG:前ターン位置 != CFLAG:MASTER:前ターン位置
		IF CFLAG:ARG:前ターン位置 != MAXROOM && CFLAG:ARG:前ターン位置 != OMANEKIBEYA()
			PRINTFORMW %CALLNAME:ARG% comes to %NAME_FROM_PLACE(CFLAG:ARG:現在位置)%.
		ENDIF
		SIF !TCVAR:ARG:今日会ったか
			PRINTFORML %PARSE("You realize you")% %PARSE("haven't")% greeted %HIM_HER(ARG)% today.
		HAPPENING_TYPE = 2
	ENDIF
	IF !FLAG:70 && CFLAG:ARG:現在位置 == CFLAG:MASTER:現在位置
		IF !NOZOKI && HAPPENING_TYPE >= 0
		;初対面
			IF CFLAG:(NO:ARG):面識 == 0
				CALL SYOTAIMEN(ARG)
			ELSEIF CFLAG:ARG:情事目撃 && CFLAG:(CFLAG:ARG:情事目撃):現在位置 != CFLAG:ARG:現在位置
				PRINTFORMW %CALLNAME:ARG% confronts %PARSE("you")% about what %PARSE("you",1)% did with %CALLNAME:(CFLAG:ARG:情事目撃)% earlier.
				;CALL KOJO_EVENT(0, ARG, 2)
				CALL KOJO_MESSAGE_SEND("EVENT",8,ARG,1,0)
				CFLAG:ARG:情事目撃 = 0
			ELSEIF CFLAG:ARG:情事目撃 && CFLAG:(CFLAG:ARG:情事目撃):現在位置 == CFLAG:ARG:現在位置
				PRINTFORMW %CALLNAME:ARG% scolds %PARSE("you")% and %CALLNAME:(CFLAG:ARG:情事目撃)% for what %PARSE("you",2)% did earlier...
				CALL KOJO_MESSAGE_SEND("EVENT",8,ARG,2,0)
				CFLAG:ARG:情事目撃 = 0
			ELSE
				IF !TCVAR:ARG:今日会ったか && CFLAG:ARG:同室
					CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,8,0)
				ENDIF
				;通常遭遇口上
				CALL KOJO_MESSAGE_SEND("EVENT",1,ARG,HAPPENING_TYPE,0)
			ENDIF
		ENDIF
		CALL FirstMeetToday(ARG)
	ENDIF
ENDIF
IF WORKING(ARG, 1) ;qol custom code, show if they're working when you see them
	CALL 仕事情景描写(ARG)
	SIF FLAG:70
		PRINTFORMW ...At least, that's what it looks like in Time Stop.
	PRINTFORML
ENDIF

@OSASOI(ARG)
#DIM 目的地
#DIM 自宅地域
#DIM 現在地域

自宅地域 = CFLAG:ARG:自宅位置 / 100
現在地域 = GET_MAPID(CFLAG:MASTER:現在位置)
;仕事がある日は誘わない 
SIF !CHARA_HOLIDAY(ARG)
	RETURN
SIF GROUPMATCH(TIME:5, 5, 9)
	RETURN
SIF !AT_HOME(MASTER)
	RETURN
IF (TALENT:ARG:恋慕 || TALENT:ARG:思慕) && CFLAG:ARG:宴会参加 && TFLAG:宴会場 / 100 != MAIN_MAP && TFLAG:宴会場 / 100 != 現在地域 && (FLAG:開始日 - DAY) == 0
	目的地 = TFLAG:宴会場 / 100
	PRINTFORML %CALLNAME:ARG% invites %PARSE("you")% to the banquet over at %GET_MAPNAME(目的地)%.
	GOTO INVITE_ENKAI
ENDIF
IF TALENT:ARG:恋慕 && !RAND:5 && !CFLAG:ARG:宴会参加
	IF CFLAG:ARG:溜まってる度 >= (700 + TALENT:ARG:羞恥心 * 150) && !CFLAG:ARG:神社在住 && !CFLAG:ARG:自宅位置
		PRINTFORML %CALLNAME:ARG% invites %PARSE("you")% to %HIS_HER(ARG)% room.
		CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,3,自宅地域)
		CALL ASK_YN("Go","Do not go")
		IF !RESULT
			PRINTFORML %PARSE("You accept")% the invitation, and %PARSE("head")% towards %CALLNAME:ARG%'s room...
			CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,4,自宅地域)
			PRINTFORMW +{TIME_REQUIRED(現在地域, 自宅地域)}分
			TIME += TIME_REQUIRED(現在地域, 自宅地域)
			CFLAG:ARG:デート中 = 自宅地域
			CFLAG:MASTER:デート中 = 自宅地域
			FLAG:デート相手 = ARG
			CFLAG:MASTER:同行 = 60
			CFLAG:ARG:同行 = 60
			TFLAG:デート前好感度 = CFLAG:ARG:2
			TARGET = ARG
			CALL OMANEKI_ENTER(ARG)
			CALL TARGETSET_CHACK
			;お誘いに乗ったか断ったかをRETURNで返すようにしました
			RETURN 1
		ELSE
			PRINTFORMW %CALLNAME:ARG% looks a bit let down.
			CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,5,自宅地域)
			RETURN 0
		ENDIF
	ELSEIF !RAND:4 && CFLAG:ARG:よく行く地域 >= 0 && CFLAG:ARG:よく行く地域 != MAIN_MAP
		目的地 = CFLAG:ARG:よく行く地域
	ELSE
		目的地 = RAND:10
		SIF 目的地 == MAIN_MAP
			RETURN
	ENDIF
	;要求移動時間が60以上だと不発
	IF TIME_REQUIRED(現在地域, 目的地) >= 60
		RETURN 0
	ENDIF
	PRINTFORML %CALLNAME:ARG% invites %PARSE("you")% to %GET_MAPNAME(目的地)%.
	$INVITE_ENKAI
	CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,0,目的地)
	CALL ASK_YN("Go","Do not go")
	IF !RESULT
		CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,1,目的地)
		CFLAG:MASTER:同行 = 60
		CFLAG:ARG:同行 = 60
		TFLAG:デート前好感度 = CFLAG:ARG:2
		CALL SET_DATE(目的地, ARG, TIME_REQUIRED(CFLAG:MASTER:デート中, 目的地) - 1000)
		CALL TARGETSET_CHACK
		RETURN 1
	ELSE
		PRINTFORMW %CALLNAME:ARG% looks a bit disappointed.
		CALL KOJO_MESSAGE_SEND("EVENT",27,ARG,2,目的地)
		RETURN 0
	ENDIF
ENDIF

RETURN 0



@UWAKI_RECORD(ARG,公認,浮気相手)
#DIM 浮気相手
#DIM 公認

SIF UWAKI_KNOWN:ARG:0 == ""
	UWAKI_KNOWN:ARG:0 = /
SIF UWAKI_ACCEPTED:ARG:0 == ""
	UWAKI_ACCEPTED:ARG:0 = /


IF 浮気相手
	LOCALS = %CALLNAME:浮気相手%
	SIF !STRCOUNT(UWAKI_KNOWN:ARG:0,LOCALS)
		UWAKI_KNOWN:ARG:0 = %UWAKI_KNOWN:ARG:0%%LOCALS%/
	SIF !STRCOUNT(UWAKI_ACCEPTED:ARG:0,LOCALS) && 公認
		UWAKI_ACCEPTED:ARG:0 = %UWAKI_ACCEPTED:ARG:0%%LOCALS%/
ELSE
	;第二引数が0の場合、あなたと同じ場所にいるキャラ全員記録
	FOR LOCAL,1,CHARANUM
		SIF LOCAL == ARG
			CONTINUE
		SIF CFLAG:LOCAL:現在位置 != CFLAG:MASTER:現在位置
			CONTINUE
		LOCALS = %CALLNAME:LOCAL%
		SIF !STRCOUNT(UWAKI_KNOWN:ARG:0,LOCALS)
			UWAKI_KNOWN:ARG:0 = %UWAKI_KNOWN:ARG:0%%LOCALS%/
		SIF !STRCOUNT(UWAKI_ACCEPTED:ARG:0,LOCALS) && 公認
			UWAKI_ACCEPTED:ARG:0 = %UWAKI_ACCEPTED:ARG:0%%LOCALS%/
	NEXT
ENDIF

@GET_UWAKI_HISTORY(ARG,浮気相手)
;公認だと2を返す
#FUNCTION
#DIM 浮気相手
LOCALS = /%CALLNAME:浮気相手%/

IF STRCOUNT(UWAKI_ACCEPTED:ARG:0,LOCALS)
	RETURNF 2
ELSEIF STRCOUNT(UWAKI_KNOWN:ARG:0,LOCALS)
	RETURNF 1
ELSE
	RETURNF 0
ENDIF

@SYOTAIMEN(ARG)
SIF CFLAG:ARG:面識
	RETURN
IF TALENT:ARG:行きずり == 1
	IF TALENT:MASTER:魅惑
		PRINTFORMW %CALLNAME:ARG% finds %PARSE("you")%【Captivating】.
		CALL COLORMESSAGE(@"Favorability and Intimacy+",C_YELLOW,2,1)
		ABL:ARG:親密 += 2
		CFLAG:ARG:好感度 += 300
	ENDIF
	IF TALENT:MASTER:謎の魅力
		PRINTFORMW %CALLNAME:ARG% feels %HIS_HER(ARG)% heart begin to race as %HE_SHE(ARG)% notices %PARSE("your")%【Mysterious Charm】.
		CALL COLORMESSAGE(@"Favorability and Intimacy+",C_YELLOW,2,1)
		ABL:ARG:親密 += 2
		CFLAG:ARG:好感度 += 300
	ENDIF
ENDIF
IF !FLAG:なりきり
	SETCOLOR 0xc53d43
	SIF NO:ARG != 拠点_大家
		PRINTFORMW %CALLNAME:ARG% meets %PARSE("you")% for the first time and offers greetings.
	RESETCOLOR
	CALL PRINT_FACE, ARG ;qol custom code
	CALL KOJO_MESSAGE_SEND("ENCOUNTER",0,ARG,1,0)
ENDIF
CFLAG:ARG:面識 = 1
CALL QoL_Info_Write(ARG, Qol_Dates_Meet) ;qol custom code

@FirstMeetToday(ARG)
#DIM PREV_TARGET
#DIM IRAI_ABLE
SIF CFLAG:ARG:現在位置 != CFLAG:MASTER:現在位置
	RETURN
SIF TCVAR:ARG:今日会ったか
	RETURN

TCVAR:ARG:今日会ったか = 1
CFLAG:ARG:最後に会った日 = DAY
SIF TCVAR:ARG:アナザー普段着
	PRINTFORMW %CALLNAME:ARG% is wearing different clothes than usual today.
IF CFLAG:ARG:オナバレ
	;陥落素質あり、【快感に素直】の時、わりと素直な反応に
	IF CFLAG:ARG:オナバレ == 1
		IF (IS_LOVER(ARG) || TALENT:ARG:セフレ) && TALENT:ARG:快感応答 == 1
			PRINTFORMW Recalling how %HE_SHE(ARG)% got caught masturbating the other day, %CALLNAME:ARG% %TEXTR(@"doesn't look very bothered/looks at %PARSE("you")% as if %HE_SHE(ARG)% has something to say/tries to stay calm")%...
		ELSE
			PRINTFORMW %CALLNAME:ARG% feels embarrassed after being caught masturbating the other day...
		ENDIF
	ELSEIF CFLAG:ARG:オナバレ == 2
		IF (IS_LOVER(ARG) || TALENT:ARG:セフレ) && TALENT:ARG:快感応答 == 1
			PRINTFORMW Recalling how %HE_SHE(ARG)% was caught masturbating and what %HE_SHE(ARG)% agreed to, %CALLNAME:ARG% %TEXTR(@"casts %PARSE("you")% a sidelong glance/flashes %PARSE("you")% a wide grin/smiles in reminiscence")%...
		ELSE
			PRINTFORMW %CALLNAME:ARG% feels embarrassed after being caught masturbating the other day and what %HE_SHE(ARG)% agreed to...
		ENDIF
	ELSEIF CFLAG:ARG:オナバレ == 3
		IF (IS_LOVER(ARG) || TALENT:ARG:セフレ) && TALENT:ARG:快感応答 == 1
			PRINTFORMW Remembering how %HE_SHE(ARG)% was caught masturbating and the subsequent climax show, %CALLNAME:ARG% %TEXTR(@"gives %PARSE("you")% a wink/gives %PARSE("you")% a fleeting glance/looks a bit awkward")%...
		ELSE
			PRINTFORML %CALLNAME:ARG% is so embarrassed about being caught masturbating the other day %HE_SHE(ARG)% can barely look %PARSE("you")% in the eye.
			PRINTFORMW Especially when %HE_SHE(ARG)% remembers the show %HE_SHE(ARG)% put on...
		ENDIF
	ENDIF
	CFLAG:ARG:オナバレ = 0
ELSEIF TALENT:ARG:機嫌 == 1
	PRINTFORMW Did something nice happen? Looks like %CALLNAME:ARG% is in a good mood today.
ELSEIF TALENT:ARG:機嫌 == -1
	PRINTFORMW Looks like %CALLNAME:ARG% is in a bad mood today...
ENDIF
IF CFLAG:ARG:溜まってる度 >= 500 && TALENT:MASTER:劣情察知
	SETCOLOR 255,100,255
	FONTBOLD
	PRINTFORMW Ping!
	PRINTFORMW %PARSE("You sense")% that %CALLNAME:ARG% has an unbearable sexual desire...!
	RESETCOLOR
	FONTREGULAR
ENDIF
CALL SEASONAL(ARG)

PREV_TARGET = TARGET
TARGET = ARG
CALL CAN_IRAI_START
IRAI_ABLE = RESULT
IF IRAI_ABLE && IRAI_CHARA_TO_PROGRESS(ARG) == "依頼未受託" && !FLAG:依頼スルー
	;CALL先の判定でTARGET==CLIENT前提の処理が多々あるので一時変更、そのまま戻ってくるので退避しとく
	CALL IRAI_START(ARG)
	TARGET = PREV_TARGET
ELSEIF IRAI_ABLE && GROUPMATCH(IRAI_CHARA_TO_PROGRESS(ARG), "期限切れ", "報告忘れ")
	PRINTFORML %CALLNAME:ARG% is angry with %PARSE("you")% for not completing the request.
	;TFLAG:104の使い方がよくわからん
	;TFLAG:104 = ARG
	CALL IRAI_REPORT(ARG)
	TARGET = PREV_TARGET
ELSEIF TALENT:ARG:機嫌 >= 0 && BASE:MASTER:気力 > 0 && BASE:ARG:気力 > 0 && !CFLAG:MASTER:同行 && NEMUKE() < 720 && !睡眠時間(ARG)
	CALL OSASOI(ARG)
	SIF !RESULT
		TARGET = PREV_TARGET
ELSE
	TARGET = PREV_TARGET
ENDIF