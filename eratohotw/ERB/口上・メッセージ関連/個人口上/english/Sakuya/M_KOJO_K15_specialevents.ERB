;==================================================
;FIRST GREETING OF THE DAY
;SPECIAL GREETINGS for holidays
;ARG 0 = Player enter, 1 = Sakuya enter
;==================================================
@K15_FIRST_GREETING_OF_DAY_LINES(ARG)
	;-------------------------------------------------
	;new year
	IF DAY:2 == 1 && DAY:3 == 1
		IF K15_LOVER_LOVE()
			PRINTFORML 「Happy new year, %CALLNAME:MASTER%.」
			PRINTFORMDL She scooches up to you, tempting you to embrace her.
			PRINTFORML 「Thank you very much for everything.」
			PRINTFORMW 「Please treat me kindly this year as well.」
		ELSEIF K15_HATE()
			PRINTFORML 「I hope you will learn your lesson this year.」
			PRINTFORMDW She smirks at you with her arms crossed.
		ELSEIF TALENT:恋慕 || K15_PLAYER_LOVER_REMILIA()
			PRINTFORML 「Happy new year, %CALLNAME:MASTER%.」
			PRINTFORMDL She gives you a very polite bow.
			PRINTFORMW 「Hopefully it will be a wonderful year for you.」
		ELSE
			PRINTFORML 「Happy new year, %CALLNAME:MASTER%.」
			PRINTFORMDW She gives you a small bow.
		ENDIF
	;-------------------------------------------------
	;mothers day	
	ELSEIF DAY:2 == 1 && DAY:3 == 11
		;already has an child
		IF EXP:出産経験
			IF K15_LOVER_LOVE()
				PRINTFORML 「%GREETING_K15()%, %CALLNAME:MASTER%, today is Mother's day.」
				PRINTFORMDL She comes closer and embraces you tightly.
				PRINTFORMW 「Thank you for everything, will you spend the day with me and our child\@EXP:出産経験>1?ren#\@?」
			ELSEIF TALENT:恋慕
				PRINTFORML 「%GREETING_K15()%, %CALLNAME:MASTER%, today is Mother's day.」
				PRINTFORMDL She comes closer and holds your hand while smiling warmly.
				PRINTFORMW 「Will you spend the day with me and our child\@EXP:出産経験>1?ren#\@?」
			ELSE
				PRINTFORML 「%GREETING_K15()%, %CALLNAME:MASTER%, today is Mother's day apparently.」
				PRINTFORML 「Will you spend some time with the child\@EXP:出産経験>1?ren#\@ today?」
							;worried because pregnant despite not in love or in a proper relation
				PRINTFORMDW She looks a bit worried for some reason.
			ENDIF
		;should be pregnant for the first time
		ELSEIF TALENT:妊娠
			IF K15_LOVER_LOVE()
				PRINTFORML 「%GREETING_K15()%, %CALLNAME:MASTER%.」
				PRINTFORML 「Today is Mother's day, I'm happy to bear your child.」
				PRINTFORMDW She scooches up to you and guides your hands to feel her pregnant belly.
			ELSEIF TALENT:恋慕
				PRINTFORML 「%GREETING_K15()%, %CALLNAME:MASTER%.」
				PRINTFORML 「Today is Mother's day, I can't believe I'm going to be one soon.」
				PRINTFORMDW She smiles while gently rubbing her pregnant belly.
			ELSEIF K15_HATE()
				PRINTFORMDW %CALLNAME:15% glares at you while she holds her pregnant belly.
			ELSE
				PRINTFORML 「%GREETING_K15()%, %CALLNAME:MASTER%.」
				PRINTFORML 「Today is Mother's day, I can't believe I'm going to be one soon.」
							;worried because pregnant despite not in love or in a proper relation
				PRINTFORMDW She looks a bit worried for some reason.
			ENDIF
		;no child
		ELSE
			IF K15_LOVER_LOVE()
				PRINTFORML 「%GREETING_K15()%, %CALLNAME:MASTER%. Today is Mother's day apparently.」
				PRINTFORMDW She fiddles with her dress in a shy manner while blushing.
			ELSEIF K15_HATE()
				PRINTFORMDW She ignores you.
			ELSEIF TALENT:恋慕
				PRINTFORMW 「%GREETING_K15()%, %CALLNAME:MASTER%. Today is Mother's day apparently.」
			ELSE
				PRINTFORMW 「%GREETING_K15()%, %CALLNAME:MASTER%.」
			ENDIF
		ENDIF
	;-------------------------------------------------
	;white day
	ELSEIF DAY:2 == 1 && DAY:3 == 14
		IF K15_HATE()
			PRINTFORMDW She ignores you.
		ELSEIF TALENT:恋慕
			PRINTFORMDL %CALLNAME:15% appears right in front of you without saying anything and closes her eyes.
			PRINTFORML
			PRINTFORMDL [0] Kiss her cheek.
			PRINTFORMDL [1] Kiss her.
			PRINTFORMDL [2] Grope her.
			
			TINPUT 5000,4,0
			SELECTCASE RESULT
				CASE 0
					PRINTFORMDW You give her a kiss on her cheek.
					PRINTFORML 「%CALLNAME:MASTER%～ ehehe♪」
					PRINTFORML 「That kiss really soothed me.」
					PRINTFORMDW %CALLNAME:15% laughs in a cute way and smiles warmly.
					PRINTFORMW 「Today is white day, will you spend the day with me?」
					
						;mood gain
					TALENT:機嫌 = 1
					EXP:15:愛情経験 += 5
				CASE 1
					PRINTFORMDW You kiss her soft inviting lips.
					
					;first kiss both
					IF TALENT:MASTER:キス未経験
						;logic
						CALL SET_FIRSTKISS(MASTER, 15, 1)
						EXP:15:愛情経験 += 10
						;reset color
						CALL M_KOJO_ENG01_COLOR_K15
					ENDIF
					
					;first kiss Sakuya
					IF TALENT:15:キス未経験
						;logic
						CALL MUTUAL_KISS
						;reset color
						CALL M_KOJO_ENG01_COLOR_K15
						
						PRINTFORML （My first kiss...）
						PRINTFORML 「%CALLNAME:MASTER%～ You finally kissed me.」
						PRINTFORML 「You've given me all I wanted for this day... I love you～」
						PRINTFORMDW %CALLNAME:15% stares at you full with love and teary eyes.
					ELSE
						PRINTFORML 「I love you, %CALLNAME:MASTER%.」
						PRINTFORMDW She gives you a warm smile.
						PRINTFORMW 「Today is white day, can you spend the day with me?」
					ENDIF
					
					;brighten mood
					SIF TALENT:機嫌 == -1
						TALENT:機嫌 = 0
					
						         ;lust + 5 + intimacy
					CFLAG:溜まってる度 += (5 + ABL:欲望)*5
				CASE 2
					PRINTFORMDW You grope her breasts.
					PRINTFORML 「Mou～ %CALLNAME:MASTER%%UNICODE(0x2665)%... 」
					PRINTFORMDL She blushes heavily and looks at you with a bossy look.
					PRINTFORMW 「That is impolite. Today is white day, did you forget?」
							  ;lust + 5 + desire
					CFLAG:溜まってる度 += (5 + ABL:親密)*5
				CASEELSE
					PRINTFORML 「Mou... %CALLNAME:MASTER%.」
					PRINTFORMDW She looks disappointed in you.
					PRINTFORMW 「Today is white day, did you forget?」
					
					;worsen mood
					SIF TALENT:機嫌 == 1
						TALENT:機嫌 = 0
			ENDSELECT
		ELSE
			PRINTFORML 「%GREETING_K15()%, %CALLNAME:MASTER%.」
			SIF K15_LIKES_PLAYER()
				PRINTFORMDW She blushes when she looks at you for some reason.
		ENDIF
	;-------------------------------------------------
	;valentine
	ELSEIF DAY:2 == 4 && DAY:3 == 14
		;does hate mark 3 even get chocolates?
		SELECTCASE CFLAG:好感度
			CASE 1000 TO 3000
				PRINTFORML 「%GREETING_K15()%, %CALLNAME:MASTER%.」
				PRINTFORMW The head maid is smiling happily while carrying a basket of some very high quality chocolates. You suspect that she prepared them herself.
				PRINTFORML 「Ah, these are for the Mistress. It's a special day today where you give the ones you like some chocolates.」
				PRINTFORMDW You have a feeling that she misunderstands the meaning of valentine's day.
				PRINTFORMW ...
				PRINTFORML 「My apologies, I almost forgot. This one is for you.」
				PRINTFORMW She hands you a very small gift from the basket while smiling.
			CASE 3001 TO 5000
				PRINTFORML 「%GREETING_K15()%, %CALLNAME:MASTER%.」
				PRINTFORMW The head maid is smiling while carrying a basket of some very high quality chocolates. You suspect that she prepared them herself.
				PRINTFORMW 「Ah, these are for the Mistress. It's a special day today where you give the ones you like some chocolates.」
				PRINTFORML 「This one is for you.」
				PRINTFORMW She hands you small gift from the basket while blushing.
			CASE IS > 5000
				IF EXP:15:愛情経験 > 100
					PRINTFORML 「%GREETING_K15()%, %CALLNAME:MASTER%.」
					PRINTFORMDL You see her blushing and smiling at you.
					PRINTFORMW 「Today is valentines day, please accept my present.」
					PRINTFORMDL She hands you a perfectly wrapped gift containing some very well-made chocolates.
					PRINTFORMW 「I hope you enjoy it, I made it myself.」
				ELSE
					PRINTFORML 「%GREETING_K15()%, %CALLNAME:MASTER%.」
					PRINTFORMDW You see her blushing and looking at you.
					PRINTFORML 「Today is a special day where you gift the ones you like with some chocolates, I want you to have this.」
					PRINTFORMDW She hands you a perfectly wrapped gift.
					PRINTFORMW 「Please enjoy it, I made it myself.」
				ENDIF
			CASEELSE
				PRINTFORML 「%GREETING_K15()%♪」
				PRINTFORMW The head maid is smiling happily while carrying a basket of some very high quality chocolates. You suspect that she prepared them herself.
				PRINTFORML 「Ah, these are for the Mistress. It's a special day today where you give the ones you like some chocolates.」
				PRINTFORMDW You have a feeling that she misunderstands the meaning of valentine's day.
		ENDSELECT
	;-------------------------------------------------
	;halloween 
	ELSEIF DAY:2 == 3 && DAY:3 == 30
		IF K15_HATE()
			PRINTFORMW %CALLNAME:15% glares at you.
		ELSE
			PRINTFORML 「Happy Halloween, %CALLNAME:MASTER%.」
			
			IF K15_PLAYER_IN_SDM()
				PRINTFORML 「The mansion looks so charming with all the decorations, don't you agree?」
			ENDIF
			;pause
			PRINTFORMW
			
		ENDIF
	;-------------------------------------------------
	;christmas
	ELSEIF DAY:2 == 4 && DAY:3 == 25
		IF K15_HATE() 
			PRINTFORML 「How much coal did you get this year, I wonder.」
			PRINTFORMW %CALLNAME:15% glares at you.
		ELSE
			PRINTFORML 「Merry Christmas, %CALLNAME:MASTER%.」
			
			IF K15_PLAYER_IN_SDM()
				PRINTFORMW 「The mansion looks so charming with the decorations, don't you agree?」
			ENDIF
			
			IF TALENT:恋人 && EXP:15:愛情経験 > 100
				PRINTFORMDL She hands you a present and gives you a kiss on your cheek.
				PRINTFORMW 「I wanted to thank you for the time we spent.」
				PRINTFORMDW As you open the box, you find some delicious homemade chocolates.
				ITEM:123 ++
			ENDIF
		ENDIF
	;-------------------------------------------------
	;banquet reminder
	ELSEIF FLAG:宴会開催フラグ && IN_HOME(FLAG:32)
		IF K15_HATE() 
			PRINTFORMW 「The Mistress is holding a banquet this evening, I will be watching your behaviour closely if you decide to attend.」
		ELSEIF K15_LOVER_LOVE()	
			PRINTFORML 「%GREETING_K15()%, %CALLNAME:MASTER%.」
			PRINTFORMW 「Mistress %CALLNAME:16% is holding a banquet this evening, hope you can attend it.」
		ELSEIF K15_PLAYER_LOVER_REMILIA()
			PRINTFORML 「%GREETING_K15()%, %CALLNAME:MASTER%.」
			PRINTFORMDL She gives you a very polite bow.
			PRINTFORMW 「The Mistress is holding a banquet this evening, hope you can attend it.」
		ELSEIF K15_PLAYER_IS_SDM_RESIDENT()
			PRINTFORML 「%GREETING_K15()%, Mistress %CALLNAME:16% is holding a banquet this evening.」
			PRINTFORMW 「I will be expecting you to attend it and help out.」
		ELSE
			PRINTFORML 「%GREETING_K15()%, Mistress %CALLNAME:16% is holding a banquet this evening.」
			PRINTFORMW 「Feel free to attend.」
		ENDIF
	;-------------------------------------------------
	;normal day
	ELSE
		IF K15_HATE() 
			PRINTFORMW 「%GREETING_K15()%, you're still alive?」
		ELSE
			CALL K15_GREETING_LINES(ARG)
		ENDIF
	ENDIF
	
RETURN 1

;==================================================
;GREETINGS
;ARG 0 = Player enter, 1 = Sakuya enter
;==================================================
@K15_GREETING_LINES(ARG)
	
	;greeting to others
	IF ARG
		;Remilia
		IF K15_SAME_LOCATION_AS(16)
			;-------------------------------------------------
			;remilia sleep
			IF CFLAG:16:睡眠
				;Remilia is lover
				IF K15_PLAYER_LOVER_REMILIA()
					PRINTFORMDL She gives you a graceful bow.
					PRINTFORMW 「%GREETING_K15()%, %CALLNAME:PLAYER%. I see that the Mistress fell asleep.」
				;Sakuya loves player
				ELSEIF TALENT:恋慕
					PRINTFORMDL She gives you a loving smile.
					PRINTFORMW 「%GREETING_K15()%, %CALLNAME:PLAYER%. I see that the Mistress fell asleep.」
				;Remilia loves player
				ELSEIF TALENT:16:恋慕
					PRINTFORMDL She gives you a slightly bossy look.
					PRINTFORMW 「%CALLNAME:PLAYER%. Please be careful to not to disturb the Mistress sleep.」
				ELSE
					PRINTFORMDL She gives you an annoyed look.
					PRINTFORMW 「%CALLNAME:PLAYER%, the Mistress is not to be disturbed during her sleep.」
				ENDIF
			;-------------------------------------------------
			ELSE
				;loves you as lover
				IF K15_LOVER_LOVE()
					PRINTFORMDL She gives you and %CALLNAME:16% a graceful bow when she entered the room.
					PRINTFORMW 「%GREETING_K15()%, %CALLNAME:PLAYER%, Mistress.」
				;Remilia is lover
				ELSEIF K15_PLAYER_LOVER_REMILIA()
					PRINTFORMDL She gives %CALLNAME:16% and you a graceful bow when she entered the room.
					PRINTFORMW 「%GREETING_K15()%, Mistress, %CALLNAME:PLAYER%.」
				;Sakuya loves you
				ELSEIF TALENT:恋慕
					PRINTFORMDL She gives %CALLNAME:16% and you a graceful bow when she entered the room.
					PRINTFORMW 「%GREETING_K15()%, Mistress, %CALLNAME:PLAYER%.」
				ELSE
					PRINTFORMDL She gives %CALLNAME:16% a graceful bow when she entered the room... and you a quick look.
					PRINTFORMW 「%GREETING_K15()%, Mistress.」
				ENDIF
			ENDIF
			;-------------------------------------------------
			RETURN 1
		ENDIF
	ENDIF
	
	;private rooms
	IF K15_BED_ROOM() > 0
		IF TALENT:恋慕
			;-------------------------------------------------
			;Sakuya is very horny
			IF K15_LUST(900)
				PRINTFORMDL She strikes a shy pose when she notices you in the room. 
				PRINTFORMW 「%CALLNAME:MASTER%, I've been feeling a bit lonely lately%UNICODE(0x2665)%～」
			;-------------------------------------------------
			;Sakuya is horny
			ELSEIF K15_LUST(500)
				PRINTFORMDL She gives you loving look when she noticed you \@ARG?in #entering \@ the room while blushing a bit. 
				PRINTFORML 「%CALLNAME:MASTER%, is there anything I can provide you with?」
				PRINTFORML 「Some tea? A massage? Or maybe?...」
				PRINTFORMDL She gives you a cheeky smile while holding her hand in front of her mouth.
			;-------------------------------------------------
			;not horny
			ELSE
				PRINTFORMDL She gives you a loving look when she noticed you \@ARG?in #entering \@ the room. 
				PRINTFORMW 「%CALLNAME:MASTER%, is there anything I can do for you?」
			ENDIF
			;-------------------------------------------------
		ELSEIF K15_PLAYER_LOVER_REMILIA()
			PRINTFORMDL She gives you a polite bow.
			PRINTFORMW 「%CALLNAME:MASTER%, Is there anything I can do for you?」
		ELSE
			;-------------------------------------------------
			IF K15_BED_ROOM() == 1
				PRINTFORMW 「%CALLNAME:MASTER%, what \@ARG?are you doing#brings you to\@ my room?」
			;-------------------------------------------------
			ELSEIF K15_BED_ROOM() == 2
				PRINTFORML 「Excuse me for the intrusion.」
				PRINTFORMDW She gives you a polite bow.
			ENDIF
			;-------------------------------------------------
		ENDIF
	;remilia's room
	ELSEIF CFLAG:現在位置 == 326
		IF ARG
			;note Remilia shouldn't be here
			PRINTFORMW 「Excuse me.」
			IF K15_PLAYER_LOVER_REMILIA()
				PRINTFORMDL She gives you a polite bow when she notices you in %CALLNAME:16%'s room.
				PRINTFORMW 「Is there anything I can do for you?」
			;-------------------------------------------------
			ELSEIF TALENT:恋慕
				PRINTFORMDW She gives you a loving look when she notices you in %CALLNAME:16%'s room.
			;------------------------------------------------- 
			ELSE
				PRINTFORMDW She gives you a bossy look when she notices you in %CALLNAME:16%'s room.
			ENDIF
			;------------------------------------------------- 
		ELSE
			IF K15_PLAYER_LOVER_REMILIA()
				PRINTFORMDL She gives you a polite bow when she notices you entering %CALLNAME:16%'s room.
				PRINTFORMW 「Is there anything I can do for you?」
			;-------------------------------------------------
			ELSEIF TALENT:恋慕
				PRINTFORMDL She gives you a smile when she notices you entering %CALLNAME:16%'s room.
				PRINTFORMW 「%GREETING_K15()%, %CALLNAME:MASTER%.」
			ELSE
				PRINTFORMDL She gives you a bossy look when she notices you entering %CALLNAME:16%'s room.
				PRINTFORMW 「%CALLNAME:MASTER%, please do not disturb the Mistress.」
			ENDIF
		ENDIF
	;garden
	ELSEIF ( CFLAG:現在位置 == 302 || CFLAG:現在位置 == 306 ) && !ARG
		SELECTCASE TIME
				 ;6  to 22
			CASE 360 TO 1320
				PRINTFORMDW You hear her quietly humming some melodies, while taking care of the flowers.
			CASEELSE
				CALL K15_GREETING_LINES_ACTION(ARG)
				CALL K15_GREETING_LINES_DIALOG(ARG)
		ENDSELECT
	ELSE
		CALL K15_GREETING_LINES_ACTION(ARG)
		CALL K15_GREETING_LINES_DIALOG(ARG)
	ENDIF
RETURN 1

;==================================================
;GENERIC GREETINGS
;ARG 0 = Player enter, 1 = Sakuya enter
;==================================================
@K15_GREETING_LINES_ACTION(ARG)
	;-------------------------------------------------
	;love
	IF TALENT:恋慕
		IF K15_LUST(900)
			PRINTFORMDW She smiles and blushes heavily when she notices you.
		ELSEIF K15_LUST(500)
			PRINTFORMDW She smiles and blushes when she notices you.
		ELSE
			PRINTFORMDW She smiles warmly when she notices you.
		ENDIF
	;-------------------------------------------------
	;Remilia's lover
	ELSEIF K15_PLAYER_LOVER_REMILIA()
		IF K15_LUST(750) && K15_LIKES_PLAYER()
			PRINTFORMDW She gives a polite bow when she notices you, while blushing a bit.
		ELSE
			PRINTFORMDW She gives a polite bow when she notices you.
		ENDIF
	;-------------------------------------------------
	;like
	ELSEIF K15_LIKES_PLAYER()
		IF K15_LUST(750)
			PRINTFORMDW You notice her blushing a bit as she notices you.
		ELSE
			PRINTFORMDW She smiles a bit when she noticed you.
		ENDIF
	ELSE
		IF K15_LUST(750)
			PRINTFORMDW You notice her blushing a bit.
		ENDIF
	ENDIF
	;-------------------------------------------------
RETURN 1

;==================================================
;GENERIC GREETINGS
;ARG 0 = Player enter, 1 = Sakuya enter
;==================================================
@K15_GREETING_LINES_DIALOG(ARG)
	IF TALENT:恋慕
		IF !TCVAR:15:今日会ったか
			PRINTDATAW 
				DATAFORM 「%GREETING_K15()%. Did you sleep well tonight, %CALLNAME:MASTER%?」
				DATAFORM 「%GREETING_K15()%, %CALLNAME:MASTER%. Did you sleep well tonight?」
				DATAFORM 「Ara, %GREETING_K15()% %CALLNAME:MASTER%. Would you like me to do anything for you?」
				DATAFORM 「%GREETING_K15()%. Did you rest properly, %CALLNAME:MASTER%?」
			ENDDATA
		ELSEIF RAND:5 == 0
			PRINTDATAW 
				DATAFORM 「%CALLNAME:MASTER%, if there is anything you want me to do, please ask me.」
				DATAFORM 「%CALLNAME:MASTER%, is there anything I can do for you?」
				DATAFORM 「%CALLNAME:MASTER%, I was looking for you.」
				DATAFORM 「I missed you, %CALLNAME:MASTER%.」
				DATAFORM 「%GREETING_K15()%, %CALLNAME:MASTER%.」
				DATAFORM 「Ara, %GREETING_K15()%.」
			ENDDATA
		ENDIF
	ELSEIF K15_PLAYER_LOVER_REMILIA()
		IF !TCVAR:15:今日会ったか
			PRINTDATAW 
				DATAFORM 「%GREETING_K15()%. Did you sleep well tonight, %CALLNAME:MASTER%?」
				DATAFORM 「%GREETING_K15()%, %CALLNAME:MASTER%. How was your sleep?」
				DATAFORM 「%GREETING_K15()%. %GREETING_K15()%. is there anything I can do for you?」
				DATAFORM 「%GREETING_K15()%, %CALLNAME:MASTER%.」
			ENDDATA
		ELSEIF RAND:4 == 0
			PRINTDATAW 
				DATAFORM 「%CALLNAME:MASTER%, if there is anything you want, please ask.」
				DATAFORM 「%CALLNAME:MASTER%, is there anything I can do for you?」
				DATAFORM 「%CALLNAME:MASTER%, I was just looking for you.」
				DATAFORM 「%GREETING_K15()%, %CALLNAME:MASTER%.」
			ENDDATA
		ENDIF
	ELSE
		;first greeting
		IF !TCVAR:15:今日会ったか
			PRINTDATAW 
				DATAFORM 「%GREETING_K15()%, %CALLNAME:MASTER%.」
				DATAFORM 「Ara, %GREETING_K15()%.」
				DATAFORM 「Ara, %GREETING_K15()%, %CALLNAME:MASTER%.」
			ENDDATA
		;tasks
		ELSEIF K15_PLAYER_IS_SDM_RESIDENT() && RAND:4 == 0
			PRINTDATAW 
				DATAFORM 「%CALLNAME:MASTER%, have you done your tasks for today?」
				DATAFORM 「%CALLNAME:MASTER%, are you doing your job properly?」
				DATAFORM 「%GREETING_K15()%, %CALLNAME:MASTER%.」
				DATAFORM 「Ara, %GREETING_K15()%.」
				DATAFORM 「Ara, %GREETING_K15()%, %CALLNAME:MASTER%.」
			ENDDATA
		;doesn't really care about non servant
		ELSEIF RAND:16 == 0
			PRINTDATAW 
				DATAFORM 「%GREETING_K15()%.」
				DATAFORM 「%GREETING_K15()%, %CALLNAME:MASTER%.」
				DATAFORM 「Ara, %GREETING_K15()%.」
			ENDDATA
		ENDIF
	ENDIF
	
RETURN 1


;==================================================
;first kiss
;ARG 0 = by player, 1 = by sakuya
;note: does not include after date kiss
;==================================================
@K15_FIRSTKISS_COMMENT(ARG)
	IF ARG
		IF K15_LOVER_LOVE()
			PRINTFORMDW %CALLNAME:15% slowly moves towards you. You feel her warm breath slowly becoming warmer and more frequent.
			PRINTFORML 「%CALLNAME:MASTER%%UNICODE(0x2665)%～」
			PRINTFORMDW She kisses you very deeply and you can feel her racing heart beat.
			PRINTFORML 「I have dreamed of this moment for so long...」
			PRINTFORMDW %CALLNAME:15% stares at you full with love with teary eyes.
		ELSEIF K15_HATE()
			PRINTFORMDL %CALLNAME:15% looks a bit angry at you after she finished kissing you.
			PRINTFORMW 「You better treasure my first kiss. I won't forgive you if you don't.」
		ELSEIF TALENT:恋慕
			PRINTFORML 「%CALLNAME:MASTER%♪～」
			PRINTFORMDL She kisses you passionately.
			PRINTFORMW 「I love you, %CALLNAME:MASTER%♪～ I'm so happy I gave you my first kiss.」
		ELSEIF K15_LIKES_PLAYER()
			PRINTFORML 「%CALLNAME:MASTER%～」
			PRINTFORML 「Please treasure my first kiss.」
			PRINTFORMDW %CALLNAME:15% takes a small moment to touch her lips to savor the moment.
		ELSE
			PRINTFORML 「Please treasure that kiss, it was a very valuable one.」
			PRINTFORMDW %CALLNAME:15% blushes a bit after kissing.
		ENDIF
	ELSE
		IF K15_LOVER_LOVE()
			PRINTFORML （My first kiss...）
			PRINTFORML 「%CALLNAME:MASTER%～ You finally kissed me.」
			PRINTFORML 「I have dreamed of this moment for so long... I love you so much～」
			PRINTFORMDW %CALLNAME:15% stares at you full with love with teary eyes.
		ELSEIF K15_HATE()
			PRINTFORML （My first kiss stolen just like that by someone I despise...）
			PRINTFORMW 「...」
		ELSEIF TALENT:恋慕
			PRINTFORML （My first kiss...）
			PRINTFORML 「%CALLNAME:MASTER%, you finally kissed me～」
			PRINTFORML 「Thank you, I was kind of worrying you would never kiss me...」
			PRINTFORMDW %CALLNAME:15% smiles while she touches her lips savoring the moment you kissed her.
		ELSEIF K15_LIKES_PLAYER()
			PRINTFORML （My first kiss...）
			PRINTFORML 「%CALLNAME:MASTER%!?～」
			PRINTFORMDL %CALLNAME:15% blushes heavily and breaks away from being kissed.
			PRINTFORML 「Oh, I'm sorry. I didn't expect you would kiss me so suddenly.」
			PRINTFORMDL %CALLNAME:15% smiles shyly while blushing and touches her lips savoring the moment you kissed her.
			PRINTFORMW 「%CALLNAME:MASTER%, please remember my first kiss.」
		ELSE
			PRINTFORML （My first kiss...）
			PRINTFORML 「%CALLNAME:MASTER%, it's impolite to steal a maiden's first kiss so suddenly.」
			PRINTFORMDW %CALLNAME:15% blushes a bit.
		ENDIF
	ENDIF
RETURN 1


;==================================================
;Special events
;==================================================
@K15_SAKUYA_SPECIAL_EVENTS(ARG)
	
	;Sakuya knows about player timestop
	IF CFLAG:K15C_TOTAL_STEAL_PANTIES_WITNESSES >= 6 && CFLAG:時間停止バレ == 0 && RAND:10 == 0
		CALL K15_SPEVENT_PLAYER_TIMESTOP_KNOWLEDGE()
		RETURN 1
	ENDIF
	
RETURN 1


;==================================================
;Sakuya knows about player timestop
;creator note: I'm not happy with this event, feels like fan fiction tier.
;==================================================
@K15_SPEVENT_PLAYER_TIMESTOP_KNOWLEDGE(ARG)
	PRINTFORMDL
	PRINTFORMDW %CALLNAME:15% has a pretty serious look on her face.
	PRINTFORMDL
	PRINTFORMW 「There is something I want to ask you.」
	PRINTFORMDW 
	
	IF MARK:反発刻印 == 3
		PRINTFORMDW Her expression quickly turned into anger.
		PRINTFORMW 「Why do I have to share my kind of power with someone as despicable as you!?」
		CALL K15_KNIFEATTACK_SPELLCARD()
	ELSEIF TALENT:恋慕
		PRINTFORMW 「To think we have the same power...」
		PRINTFORMDW %CALLNAME:15% whispers from behind after vanishing suddenly and shows you a panty.
		PRINTFORMDW You somehow recognize it as one of hers you attempted to take from her one day.
		PRINTFORMDW She embraces you and you know that she is more happy than angry about finding out your secret.
	ELSE
		PRINTFORMDW Her expression quickly turned into anger and points her knife against you.
		PRINTFORMW 「Why do you have the same power as me?」
		PRINTFORMW 「Yet your use of it... it disgusts me...」
		PRINTFORMW 「You are a major threat to the Mistress.」
		PRINTFORMW 「As of now, you are my rival and I will not hesitate to protect her at all cost.」
		
		;considers you a rival, since she cares too much about her role as head maid.
		MARK:反発刻印 = 3
		MARK:反発取得履歴 = 3
		
		TFLAG:反発刻印取得 = MARK:反発刻印
	ENDIF
	
	;time stop knowledge
	CFLAG:時間停止バレ = 1
	
	PRINTFORMDL
	PRINTFORMDW %CALLNAME:15% now knows about your timestop.
RETURN 1
