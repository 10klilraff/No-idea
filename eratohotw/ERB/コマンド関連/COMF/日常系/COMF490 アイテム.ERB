;-------------------------------------------------
;アイテム画面
;-------------------------------------------------
@COM490
#DIM 対象
#DIM 全員
#DIM CLIENT_ID
#DIM 虚無への回帰
#DIM 使用アイテム
#DIMS 食品特徴
VARSET LOCAL
VARSET LOCALS
対象 = MASTER
虚無への回帰 = 0
PRINTL Pick an item to use.
$LOOP
;qol custom code
GOTO QOL
DRAWLINE
FOR LOCAL,120, 699
	IF 対象 != MASTER
		IF 使用可能アイテム(LOCAL) == 2 && ITEM:LOCAL
			CALL PRINT_ITEM_EXPLANATION(LOCAL)
		ENDIF
	ELSE
		IF 使用可能アイテム(LOCAL) && ITEM:LOCAL
			CALL PRINT_ITEM_EXPLANATION(LOCAL)
		ENDIF
	ENDIF
NEXT
PRINTL
DRAWLINE
SIF ITEM:昆虫採集セット
	PRINTFORML [901] Check Bug Cage
PRINTFORML [900] Use Collector's Gap automatically (Currently: \@ FLAG:Cスキマ自動仕様 ? ON# OFF\@)
IF TARGET && SHIRAHU(TARGET)
	SIF 対象 != MASTER
		SETCOLOR C_YELLOW
	PRINTFORML [998] Use on %CALLNAME:TARGET%
	RESETCOLOR
ENDIF

PRINTL [999] Return
INPUT
$QOL
CALL QoL_490_ITEMMENU(対象)
使用アイテム = RESULT
;アイテム以外の処理
SELECTCASE RESULT
	CASE 999
		RETURN -1
	CASE 900
		FLAG:Cスキマ自動仕様 = !FLAG:Cスキマ自動仕様
		PRINTFORMW Automatic usage has been switched \@ FLAG:Cスキマ自動仕様 ? ON# OFF\@.
		GOTO LOOP
	CASE 901
		;[虫捕りをする]へ転送
		CALL SHOW_MUSHI_CAGE
		GOTO LOOP
	CASE 998
		IF 対象 != MASTER
			対象 = MASTER
		ELSEIF SHIRAHU(TARGET) && TARGET
			対象 = TARGET
		ENDIF
		GOTO LOOP
ENDSELECT

;使用可能アイテム以外の入力、またはアイテム未所持の場合は再入力
IF 使用可能アイテム(使用アイテム) == 0 || ITEM:使用アイテム <= 0
	GOTO LOOP
ENDIF
;相手使用時にMASTERのみアイテムの選択は再入力
IF 対象 != MASTER && 使用可能アイテム(使用アイテム) == 1
	GOTO LOOP
ENDIF
;IF DRUG(使用アイテム,対象)
;	PRINTW That's enough drugs for today.
;	GOTO LOOP
;ENDIF
IF ITEM_SWEETS(使用アイテム) && !TIME_PROGRESS(TCVAR:対象:デザート空腹時刻)
	PRINTW Not hungry enough for a snack.
	GOTO LOOP
ENDIF
TFLAG:193 = 使用アイテム
TFLAG:194 = 対象 == MASTER ? MASTER # 1

;----------------------------------
;addition custom code, for modded item usage
CALL Add_UseItem(使用アイテム, 対象)
IF RESULT > 0
	SIF RESULT == 3
		GOTO LOOP
	LOCAL:1 = 1
	SIF RESULT == 2
		虚無への回帰 = 1
	GOTO DONE
ENDIF
;----------------------------------

SELECTCASE 使用アイテム
	CASE 120
		PRINTFORML %PARSE("You eat")% the %ITEMNAME_TR(使用アイテム)%.
		SIF 対象 == MASTER
			PRINTL ... It tasted pretty cheap.
		食品特徴 = %ITEMNAME:使用アイテム%/甘い/ジャンキー/サクサク/
		CALL RECOVER(対象, ITEM_TASTE(対象, 50, 食品特徴), "体力", "Recover via chocolate")
		CALL RECOVER(対象, ITEM_TASTE(対象, 50, 食品特徴), "気力", "Recover via chocolate")
		CALL 満腹度上昇(対象, "デザート", 60)
		LOCAL:1 ++
	CASE 121
		IF 対象 == MASTER
			PRINTFORML %PARSE("You eat")% the %ITEMNAME_TR(使用アイテム)%.
			食品特徴 = %ITEMNAME:使用アイテム%/甘い/
			CALL RECOVER(MASTER, ITEM_TASTE(MASTER, 300, 食品特徴), "体力", "Recover via chocolate")
			CALL RECOVER(MASTER, ITEM_TASTE(MASTER, 300, 食品特徴), "気力", "Recover via chocolate")
			CALL 満腹度上昇(対象, "デザート")
			LOCAL:1 ++
		ENDIF
	CASE 122
		IF 対象 == MASTER
			PRINTFORML %PARSE("You eat")% the %ITEMNAME_TR(使用アイテム)%.
			PRINTFORML Delicious!
			食品特徴 = %ITEMNAME:使用アイテム%/甘い/上品/豪華/高級食材/
			CALL RECOVER(MASTER, ITEM_TASTE(MASTER, 500, 食品特徴), "体力", "Recover via chocolate")
			CALL RECOVER(MASTER, ITEM_TASTE(MASTER, 500, 食品特徴), "気力", "Recover via chocolate")
			CALL 満腹度上昇(対象, "デザート")
			LOCAL:1 ++
		ENDIF
	CASE 123
		IF 対象 == MASTER
			PRINTFORML %PARSE("You eat")% the %ITEMNAME_TR(使用アイテム)%.
			PRINTFORML %PARSE("You",1)% can really feel the love!
			食品特徴 = %ITEMNAME:使用アイテム%/愛情/甘い/
			CALL RECOVER(MASTER, ITEM_TASTE(MASTER, 1000, 食品特徴), "体力", "Recover via chocolate")
			CALL RECOVER(MASTER, ITEM_TASTE(MASTER, 1000, 食品特徴), "気力", "Recover via chocolate")
			CALL 満腹度上昇(対象, "デザート")
			LOCAL:1 ++
		ENDIF
	CASE 212
		PRINTFORML \@ 対象 == MASTER ? %PARSE("You eat")% # %CALLNAME:対象% eats \@ the %ITEMNAME_TR(使用アイテム)%.
		LOCAL:1 ++
		;端午の節句は夏の季語だそうなので春物じゃなかった
		食品特徴 = %ITEMNAME:使用アイテム%/甘い/
		CALL RECOVER(対象, ITEM_TASTE(対象, 100, 食品特徴), "体力", "Recover via rice cakes")
		CALL RECOVER(対象, ITEM_TASTE(対象,   0, 食品特徴), "気力", "Recover via rice cakes")
		CALL 満腹度上昇(対象, "デザート")
	CASE 213
		SIF TARGET && SHIRAHU(TARGET)
			PRINTFORM %PARSE("You")% and %CALLNAME:TARGET% eat
		PRINTDATA
			DATAFORM 『Ice cream』
			DATAFORM 『Frozen mandarin orange』
			DATAFORM 『Double stick ice popsicle』
			DATAFORM 『Double stick ice cream』
			DATAFORM 『Chuupet』
			DATAFORM 『Papico ice cream』
			DATAFORM 『Condensed milk ice cream bar』
			DATAFORM 『Red bean ice cream bar』
			DATAFORM 『Watermelon popsicle』
			DATAFORM 『Orange ice pops』
			DATAFORM 『Black Thunder ice cream』
			DATAFORM 『Yukimi Daifuku』
		ENDDATA
		PRINTFORML together.
		食品特徴 = %ITEMNAME:使用アイテム%/冷たい/甘い/
		CALL RECOVER(MASTER, ITEM_TASTE(MASTER, 150, 食品特徴), "気力", "Recover via ice cream")
		CALL 満腹度上昇(MASTER, "デザート")
		IF  TARGET && SHIRAHU(TARGET)
			CALL RECOVER(TARGET, ITEM_TASTE(TARGET, 150, 食品特徴), "気力", "Recover via ice cream")
			CALL 満腹度上昇(TARGET, "デザート")
		ENDIF
		LOCAL:1 ++
	CASE 215
		PRINTFORML Used %ITEMNAME_TR(使用アイテム)%
		SETBIT TCVAR:MASTER:特別なお茶, 0
		LOCAL:1 ++
	CASE 352
		PRINTFORML \@ 対象 == MASTER ? %PARSE("You eat")% # %CALLNAME:対象% eats \@ the %ITEMNAME_TR(使用アイテム)%.
		食品特徴 = %ITEMNAME:使用アイテム%/野菜/果物/甘い/
		CALL RECOVER(対象, ITEM_TASTE(対象, 50, 食品特徴), "気力", "Recover via strawberry")
		LOCAL:1 ++
	CASE 364
		食品特徴 = %ITEMNAME:使用アイテム%/野菜/果物/甘い/
		IF FLAG:70 == 1
			PRINTFORML %PARSE("You eat")% the %ITEMNAME_TR(使用アイテム)%.
			CALL RECOVER(MASTER, ITEM_TASTE(対象, 100, 食品特徴), "気力", "Recover via watermelon")
		ELSE
			PRINTFORML \@ TARGET != MASTER ? Everyone eats # %PARSE("You eat")% \@ %ITEMNAME_TR(使用アイテム)%.
			FOR 全員, 0, CHARANUM
				SIF CFLAG:全員:現在位置 != CFLAG:MASTER:現在位置
					CONTINUE
				SIF !SHIRAHU(全員)
					CONTINUE
				CALL RECOVER(全員, ITEM_TASTE(対象, 100, 食品特徴), "気力", "Recover via watermelon")
			NEXT
		ENDIF
		LOCAL:1 ++
	;CASE 400
	;	PRINTFORML %CALLNAME:MASTER%は%ITEMNAME:使用アイテム%を開いた
	;	CALL COM401
	;	LOCAL:1 ++
	CASE 500
		PRINTFORML %PARSE("You drink")% %ARTICLE(ITEMNAME_TR(使用アイテム))%.
		PRINTL Disgusting! Another one!
		食品特徴 = %ITEMNAME:使用アイテム%/苦い/苦い/苦い/ヘルシー/野菜/葉物/野趣/
		CALL RECOVER(対象,100, "体力", "Recover via green juice")
		CALL RECOVER(対象, ITEM_TASTE(対象, 200, 食品特徴), "気力", "Recover via green juice")
		LOCAL:1 ++
	CASE 501
		PRINTFORML \@ 対象 == MASTER ? %PARSE("You drink")% # %CALLNAME:対象% drinks \@ %ARTICLE(ITEMNAME_TR(使用アイテム))%.
		CALL RECOVER(対象, 薬の知識(200), "体力", "Recover via energy drink")
		CALL RECOVER(対象, 薬の知識(100), "気力", "Recover via energy drink")
		CFLAG:対象:眠気覚まし = 薬の知識(20)
		TCVAR:対象:お薬 = 1
		LOCAL:1 ++
	CASE 502
		PRINTFORML \@ 対象 == MASTER ? %PARSE("You drink")% # %CALLNAME:対象% drinks \@ %ARTICLE(ITEMNAME_TR(使用アイテム))%.
		CALL RECOVER(対象, 薬の知識(300), "精力", "Recover via Vigora")
		TCVAR:対象:お薬 = 1
		LOCAL:1 ++
	CASE 503
		PRINTFORML \@ 対象 == MASTER ? %PARSE("You drink")% # %CALLNAME:対象% drinks \@ %ARTICLE(ITEMNAME_TR(使用アイテム))%.
		PRINTL Do it in one shot, MANTAN！
		CALL RECOVER(対象, 薬の知識(500), "体力", "Recover via Gin Gin Ｚ")
		CALL RECOVER(対象, 薬の知識(300), "気力", "Recover via Gin Gin Ｚ")
		CFLAG:対象:眠気覚まし = 薬の知識(60)
		TCVAR:対象:お薬 = 1
		LOCAL:1 ++
	;CASE 504
	;		IF FLAG:70
	;			PRINTFORML 時間停止を解除してから使おう
	;			GOTO LOOP
	;		ENDIF
	;		IF CFLAG:MASTER:現在位置 != CFLAG:MASTER:初期位置
	;			PRINTFORML これは自室で飲もう
	;			GOTO LOOP
	;		ENDIF
	;		PRINTFORML ストロング虚無を口にした途端、虚無の力で思考はぼやけ、すべてがどうでもよくなってきた
	;		PRINTFORML %CALLNAME:MASTER%は缶を空けながら一日を無為に過ごした…
	;		虚無への回帰 = 1
	CASE 505	;ギンギラギンＸ
		PRINTFORML \@ 対象 == MASTER ? %PARSE("You drink")% # %CALLNAME:対象% drinks \@ %ARTICLE(ITEMNAME_TR(使用アイテム))%.
		PRINTL I feel like I can take on the entire world...!
		CALL RECOVER(対象, 薬の知識(800), "体力", "Recover via Gin Gin Ｘ")
		CALL RECOVER(対象, 薬の知識(600), "気力", "Recover via Gin Gin Ｘ")
		CFLAG:対象:眠気覚まし = 薬の知識(180)
		CFLAG:対象:徹夜 = 1
		TCVAR:対象:お薬 = 1
		LOCAL:1 ++
	CASE 506	;精力剤『天元凸破』
		PRINTFORML \@ 対象 == MASTER ? %PARSE("You drink")% # %CALLNAME:対象% drinks \@ %ARTICLE(ITEMNAME_TR(使用アイテム))%.
		PRINTL Your ××× will pierce the heavens!
		CALL RECOVER(対象, 薬の知識(400), "体力", "Recover via Medical Drug『Tengen Toppa』")
		CALL RECOVER(対象, 薬の知識(300), "気力", "Recover via Medical Drug『Tengen Toppa』")
		CALL RECOVER(対象, 薬の知識(800), "精力", "Recover via Medical Drug『Tengen Toppa』")
		TCVAR:対象:お薬 = 1
		LOCAL:1 ++
	CASE 507
		PRINTFORML \@ 対象 == MASTER ? %PARSE("You drink")% # %CALLNAME:対象% drinks \@ %ARTICLE(ITEMNAME_TR(使用アイテム))%.
		CALL RECOVER(対象, 薬の知識(150), "気力", "Recover via potion")
		CALL RECOVER(対象, 薬の知識(150), "TSP", "Recover via potion")
		TCVAR:対象:お薬 = 1
		LOCAL:1 ++
	CASE 508
		PRINTFORML \@ 対象 == MASTER ? %PARSE("You drink")% # %CALLNAME:対象% drinks \@ %ARTICLE(ITEMNAME_TR(使用アイテム))%.
		PRINTFORML Magic is surging within... 
		CALL RECOVER(対象, 薬の知識(300), "気力", "Recover via magic potion")
		CALL RECOVER(対象, 薬の知識(600), "TSP", "Recover via magic potion")
		TCVAR:対象:お薬 = 1
		LOCAL:1 ++
	CASE 509
		PRINTFORML \@ 対象 == MASTER ? %PARSE("You drink")% # %CALLNAME:対象% drinks \@ %ARTICLE(ITEMNAME_TR(使用アイテム))%.
		CALL RECOVER(対象, 薬の知識(200), "気力", "Recover via no-sleep potion")
		CFLAG:対象:眠気覚まし = 薬の知識(240)
		CFLAG:対象:徹夜 = 1
		TCVAR:対象:お薬 = 1
		LOCAL:1 ++
	CASE 510
		PRINTFORML %PARSE("You drink")% %ARTICLE(ITEMNAME_TR(使用アイテム))%.
		PRINTL Liver metabolism has increased, it's now harder to get drunk.
		TFLAG:酔い止め = 1
		LOCAL:1 ++
	CASE 511	;エリクサー
		PRINTFORML \@ 対象 == MASTER ? %PARSE("You drink")% # %CALLNAME:対象% drinks \@ %ARTICLE(ITEMNAME_TR(使用アイテム))%.
		PRINTL All exhausted and tiredness is just blown away...!
		CALL RECOVER(対象, MAXBASE:対象:体力 < 9999 ? 9999 # MAXBASE:対象:体力, "体力", "Recover via elixir")
		CALL RECOVER(対象, MAXBASE:対象:気力 < 9999 ? 9999 # MAXBASE:対象:気力, "気力", "Recover via elixir")
		CALL RECOVER(対象, MAXBASE:対象:精力 < 9999 ? 9999 # MAXBASE:対象:精力, "精力", "Recover via elixir")
		CALL RECOVER(対象, MAXBASE:対象:TSP  < 9999 ? 9999 # MAXBASE:対象:TSP,  "TSP",  "Recover via elixir")
		CFLAG:対象:眠気覚まし = 300
		TCVAR:対象:お薬 = 1
		LOCAL:1 ++
	CASE 512	;仙丹
		PRINTFORML \@ 対象 == MASTER ? %PARSE("You drink")% # %CALLNAME:対象% drinks \@ %ARTICLE(ITEMNAME_TR(使用アイテム))%.
		PRINTL Unusual power overwhelming...!
		CALL BUFF_BASE(対象, BASE_体力, MAX(MAXBASE:対象:体力 / 5, 500))
		CALL BUFF_BASE(対象, BASE_気力, MAX(MAXBASE:対象:気力 / 5, 500))
		CALL BUFF_BASE(対象, BASE_精力, MAX(MAXBASE:対象:精力 / 5, 500))
		LOCAL:1 ++
	CASE 514	;仙香玉兎
		IF TFLAG:仙香玉兎 > 0
			PRINTFORMW Already used.
			GOTO LOOP
		ELSE
			PRINTFORML %PARSE("You put")% the %ITEMNAME_TR(使用アイテム)% in the incense burner.
			PRINTL The bewitching incense begins drifting around in the air...
			TFLAG:仙香玉兎 = 240
			LOCAL:1 ++
		ENDIF
	CASE 515	;国士無双の薬　３回目まではバフ効果
		PRINTFORML \@ 対象 == MASTER ? %PARSE("You drink")% # %CALLNAME:対象% drinks \@ the %ITEMNAME_TR(使用アイテム)%.
		IF TCVAR:対象:国士無双の薬 < 3
			PRINTL Power overwhelming......!
			CALL BUFF_BASE(対象, BASE_体力, MAX(MAXBASE:対象:体力 / 15, 150))
			CALL BUFF_BASE(対象, BASE_精力, MAX(MAXBASE:対象:精力 / 10, 150))
			TCVAR:対象:国士無双の薬 ++
			LOCAL:1 ++
		ELSE
			;四回目で爆発、デバフに
			SETCOLOR C_RED
			PRINTFORMW ！！
			PRINTFORML ... An explosion afro... Must've drunk too much...
			RESETCOLOR
			CALL BUFF_BASE(対象, BASE_体力, MIN((-1 * (MAXBASE:対象:体力 / 5)), -450))
			CALL BUFF_BASE(対象, BASE_精力, MIN((-1 * (MAXBASE:対象: 精力 / 3)), -450))
			TCVAR:対象:国士無双の薬 = 0
			LOCAL:1 ++
		ENDIF
	CASE 516
		IF TFLAG:GG設置位置 == CFLAG:MASTER:現在位置
			PRINTFORMW %TEXTR(@"「I did it.」（Resident at %GET_MAPNAME(MAIN_MAP)% - %CALLNAME:MASTER%.）/This variety is designed to be safe, but if you feel unwell please distance yourself and get some rest.")%
		ELSEIF TFLAG:GG設置位置
			PRINTFORML Gassing Garden can only be used once.
			PRINTFORMW Continue using the one already deployed.
			GOTO LOOP
		ELSEIF INROOM(CFLAG:MASTER:現在位置)
			PRINTFORMW Outdoors only.
			GOTO LOOP
		;目撃者がいると駄目
		ELSEIF (TARGET > 0 || DATE_HITOGOMI(CFLAG:MASTER:現在位置) > 0) && !FLAG:70
			PRINTFORMW There's a lot of people here... Try a stealthier approach.
			GOTO LOOP
		ELSE
			PRINTFORML %PARSE("You sprinkle")% the magic seeds.
			PRINTFORML The plant grows and blossoms in the blink of an eye, filling the area with a lust-evoking perfume...
			TFLAG:GG設置位置 = CFLAG:MASTER:現在位置
			LOCAL:1 ++
		ENDIF
	CASE 517	;ポイズンボディ
		IF TFLAG:ポイズンボディ > 0
			PRINTFORMW Taking any more than this will result in liver damage.
			GOTO LOOP
		ELSEIF HAS_PENIS(対象)
			PRINTFORML You take one dose of %ITEMNAME_TR(使用アイテム)%.
			PRINTFORML A strong swirling sensation washes over you, as you feel your crotch starting to heat up...
			PRINTFORML （Estrus effect has been added to your semen. While the effect is active, ejaculation incurs extra exhaustion.）
			PRINTFORML You get assailed by a sudden fit of headache, dizziness, nausea and joint pains. 
			PRINTFORML But you manage to endure it with a strong resolve that some things are more important than health.
			TFLAG:ポイズンボディ = 240
			LOCAL:1 ++
		ELSE
			PRINTFORMW This medicine apparently only works on men.
			GOTO LOOP
		ENDIF
	CASE 520
		IF !TFLAG:キスマーク
			PRINTFORMW There's no point in using it now.
			GOTO LOOP
		ELSE
			PRINTFORML %PARSE("You wrap")% %ITEMNAME_TR(使用アイテム)% around the kiss mark to hide it.
			TFLAG:包帯 = 1
			LOCAL:1 ++
		ENDIF
	CASE 521
		IF FLAG:70
			PRINTFORMW Use it after time has resumed.
			GOTO LOOP
		ENDIF
		PRINTFORML %PARSE("You open")% the fortune letter.
		PRINTFORML The Fortune-Teller comes out.
		PRINTFORMW 
		IF CFLAG:[[霊夢]]:現在位置 == CFLAG:MASTER:現在位置
			PRINTFORML Suddenly Reimu swings at Fortune-Teller head with her purification rod.
			PRINTFORML The pitiful Fortune-Teller is split in two.
			FOR LOCAL,1,CHARANUM
				IF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置
					BASE:LOCAL:理性 = MAXBASE:LOCAL:理性
					BASE:LOCAL:ムード = 0
				ENDIF
			NEXT
			LOCAL:1 ++
		ELSE
			PRINTFORML 「Divination opens up the path before you.」
			$EKISYA
			CALL ASK_M("Today's fortune", !GETBIT(FLAG:999, 1), "Find a person in need", 1, "Nail clippers", BASE:MASTER:TSP >= 20, "Dispel the Stone of Mirada (100 Charisma)", MONEY:2 >= 100, "Thumbs up", 1, "Give an offering", 1)
			SELECTCASE RESULT
				CASE 0
					SELECTCASE RAND:10
						CASE 0
							TFLAG:幸運補正 = 3
							PRINTFORML 「It's fantastic, why not try the lottery?」
						CASE 1 TO 3
							TFLAG:幸運補正 = 1
							PRINTFORML 「It's good, a lot of fortune has accumulated.」
						CASE 4,5
							PRINTFORML 「Neither good nor bad.」
						CASE 6 TO 8
							TFLAG:幸運補正 = -1
							PRINTFORML 「Not too good, don't be reckless.」
						CASE 9
							TFLAG:幸運補正 = -3
							PRINTFORML 「... This is bad, keep quiet about it.」
				ENDSELECT
				CASE 1
					CLIENT_ID = SEARCH_UNIQUE_COM_人助け()
					IF !CLIENT_ID
						PRINTFORML 「Hm... Can't see anyone in particular.」
						PRINTFORML 「Since there's nothing else, I'm currently being targeted by the shrine maiden.」
						PRINTFORML There doesn't seem to be anyone.
					ELSE
						PRINTFORML 「%CALLNAME:CLIENT_ID% seems in need of help」
					ENDIF
				CASE 2
					PRINTFORMW When %PARSE("you see")% the Fortune-Teller %PARSE("you",1)% suddenly %PARSE("feel")% like bullying him.
					PRINTFORMW ... What's with those nails?
					PRINTFORML %PARSE("You don't")% know if it's because he's no longer human, but nails that long look weird.
					PRINTFORML Isn't it inconvenient? Won't he accidentally scratch people?
					PRINTFORML %PARSE("You")% always %PARSE("check")% %PARSE("your",1)% nails every day so %PARSE("you",1)% can touch delicate parts safely.
					PRINTFORML If he were to accidentally scratch someone it would hurt considerably.
					PRINTFORMW First of all, dirt accumulates under fingernails.
					PRINTFORMW Unable to withstand it, %PARSE("you stop")% time and cut the Fortune-Teller's nails.
					PRINTFORMW %PARSE("You are")% filled with a great sense of accomplishment...
					PRINTFORML On the other hand, Fortune-Teller is suddenly confused at having his nails suddenly shortened...
					CALL RECOVER_PERMIL(対象,40,"気力")
					BASE:MASTER:TSP -= 20
				CASE 3
					PRINTFORML Who should be dispelled?
					FOR LOCAL,1,CHARANUM
						SIF CFLAG:LOCAL:体目当て
							PRINTFORMLC [{LOCAL}] %CALLNAME:LOCAL%
					NEXT
					PRINTFORML 
					DRAWLINE
					PRINTFORML [999] Never mind
					INPUT
					IF RESULT == 999
						GOTO EKISYA
					ELSEIF CFLAG:RESULT:体目当て
						PRINTFORML 「I think such tools are no good.」
						PRINTFORMW The Fortune-Teller does some complicated ritual, and the Stone of Mirada loses its shine before crumbling.
						TIME += 25
						CFLAG:RESULT:体目当て = 0
						MONEY:2 -= 100
					ENDIF
				CASE 4
					PRINTFORML %PARSE("You meet")% the Fortune-Tellers gaze and give him a thumbs up.
					PRINTFORML He hesitates for a while, but then returns the thumbs up in full force.
					PRINTFORMW A strong friendship was forged.
					GOTO EKISYA
				CASE 5
					PRINTFORML %PARSE("You stop")% time, take off the Fortune-Tellers clothes, and offer his underwear to god.
					CALL BUFF_BASE(MASTER,BASE_TSP,100,1)
					PRINTFORMW The Fortune-Teller loses all composure from suddenly being naked.
			ENDSELECT
			PRINTFORMW 
			DRAWLINE
			$EKISYA_DIVIDE
			PRINTFORML Reimu suddenly appears, and she swings at the Fortune-Teller's head with her purification rod.
			PRINTFORML The pitiful Fortune-Teller is split in two.
			PRINTFORMW Reimu leaves.
			FOR LOCAL,1,CHARANUM
				IF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置
					BASE:LOCAL:理性 = MAXBASE:LOCAL:理性
					BASE:LOCAL:ムード = 0
				ENDIF
			NEXT
			LOCAL:1 ++
		ENDIF
	CASE 530
		CALL SET_MIRADA
		IF RESULT == -1
			RETURN -1
		ELSE
			LOCAL:1 ++
		ENDIF
	CASE 550	;ヤツメ串
		IF !TIME_PROGRESS(TCVAR:対象:空腹時刻)
			PRINTFORMW %CALLNAME:対象% is not hungry enough.
			GOTO LOOP
		ELSE
			PRINTFORML \@ 対象 == MASTER ? %PARSE("You eat")% # %CALLNAME:対象% eats \@ the %ITEMNAME_TR(使用アイテム)%.
			CALL 満腹度上昇(対象,"軽食")
			食品特徴 = %ITEMNAME:使用アイテム%/魚料理/おつまみ/
			CALL RECOVER_PERMIL(対象, ITEM_TASTE(対象, 40, 食品特徴), "体力", 0, "Recover via lamprey skewer")
			CALL RECOVER_PERMIL(対象, ITEM_TASTE(対象, 80, 食品特徴), "気力", 0, "Recover via lamprey skewer")
			CALL RECOVER_PERMIL(対象, ITEM_TASTE(対象, 50, 食品特徴), "精力", 0, "Recover via lamprey skewer")
			LOCAL:1 ++
		ENDIF
	CASE 626	;キイチゴ
		PRINTFORML \@ 対象 == MASTER ? %PARSE("You eat")% # %CALLNAME:対象% eats \@ the %ITEMNAME_TR(使用アイテム)%.
		食品特徴 = %ITEMNAME:使用アイテム%/果物/甘い/すっぱい/野趣/
		CALL RECOVER(対象, ITEM_TASTE(対象, 50, 食品特徴), "気力", "Recover via raspberry")
		LOCAL:1 ++
	CASE 628	;アケビ
		PRINTFORML \@ 対象 == MASTER ? %PARSE("You eat")% # %CALLNAME:対象% eats \@ the %ITEMNAME_TR(使用アイテム)%.
		食品特徴 = %ITEMNAME:使用アイテム%/果物/甘い/野趣/
		CALL RECOVER(対象,80, "体力", "Recover via akebia")
		CALL RECOVER(対象, ITEM_TASTE(対象, 80, 食品特徴), "気力", "Recover via akebia")
		LOCAL:1 ++
	CASE 630	;仙桃
		PRINTFORML \@ 対象 == MASTER ? %PARSE("You eat")% # %CALLNAME:対象% eats \@ the %ITEMNAME_TR(使用アイテム)%.
		PRINTL Power overwhelming...!
		CALL BUFF_BASE(対象, BASE_体力, MAX(MAXBASE:対象:体力 / 10, 500))
		CALL BUFF_BASE(対象, BASE_気力, MAX(MAXBASE:対象:気力 / 10, 500))
		CALL BUFF_BASE(対象, BASE_精力, MAX(MAXBASE:対象:精力 / 10, 500))
		LOCAL:1 ++
	CASEELSE
		GOTO LOOP
ENDSELECT
$DONE ;addition custom code, move here if modded item was used
IF LOCAL:1
	IF !FLAG:70
		TIME += 5
	ELSE
		BASE:MASTER:TSP -= 5
	ENDIF
	ITEM:使用アイテム -= 1
	SIF 虚無への回帰
		CALL OYASUMI
	RETURN 0
ENDIF


@SET_MIRADA
PRINTFORML Please select a target.
PRINTFORML Yellow means it's already set, and pink that they have already fallen for some condition.
SIF FLAG:並び替え
	CALL CHARASORT_EX
FOR LOCAL:1, 1, EX_CHARANUM + 1
	IF FLAG:並び替え
		LOCAL = RESULT:(LOCAL:1)
		SIF LOCAL < 0
			CONTINUE
	ELSE
		LOCAL = LOCAL:1
		SIF LOCAL >= CHARANUM
			BREAK
	ENDIF
	;SIF TALENT:LOCAL:行きずり
	;	CONTINUE
	SIF CFLAG:LOCAL:出禁
		CONTINUE
	SIF CFLAG:LOCAL:体目当て
		SETCOLOR C_YELLOW
	SIF TALENT:LOCAL:セフレ || TALENT:LOCAL:恋慕
		SETCOLOR C_PINK
	PRINTFORMLC [{LOCAL, 3}] %CALLNAME:LOCAL%
	RESETCOLOR
NEXT
PRINTFORML 
DRAWLINE
PRINTFORML [999] Never mind
INPUT
SIF !INRANGE(RESULT,1,CHARANUM)
	RETURN -1
SIF CFLAG:RESULT:体目当て
	RETURN -1
LOCAL = 0 ;custom code
IF TALENT:RESULT:セフレ || TALENT:RESULT:恋慕
	TCVAR:RESULT:発情 = 1
	LOCAL = 1 ;custom code
ELSE
	CFLAG:RESULT:体目当て = 1
ENDIF
PRINTFORMW Thinking about %CALLNAME:RESULT% while holding the Stone of Mirada tightly, the stone begins shining a little.
;custom code
IF LOCAL
	SETCOLOR C_PINK
	PRINTFORML Somewhere, %CALLNAME:RESULT% pants with flushed cheeks...
	PRINTFORMW Looks like the Stone of Mirada had quite the extreme effect on %HIS_HER(RESULT)% body as %HE_SHE(RESULT,1)% becomes extremely aroused.
	RESETCOLOR
ENDIF
RETURN 1

@使用可能アイテム(ARG)
;1だとMASTERのみ、2だとあなた以外にも使える
#FUNCTION

SIF CFLAG:MASTER:うふふ && !DRUG(ARG)
	RETURNF 0
SELECTCASE ARG
	CASE 121 TO 123, 215, 364, 500, 510, 514, 515, 516, 517, 520, 521, 530
		RETURNF 1
	CASE 120, 212, 213, 352, 501, 503, 505 TO 509, 511, 512, 550, 626, 628, 630
		RETURNF 2
	CASE 502
		SIF HAS_PENIS(TARGET)
			RETURNF 2
		RETURNF 1
	CASEELSE
		RETURNF Add_UsableItem(ARG) ;addition custom code, return modded values for new items, otherwise 0
ENDSELECT

@DRUG(ARG,ARG:1)
#FUNCTION
SIF GROUPMATCH(ARG, 501, 502, 503, 505, 506, 507, 508, 509, 511)
	RETURNF 1

@ITEM_SWEETS(ARG)
#FUNCTION
SIF GROUPMATCH(ARG, 120, 121, 123, 124, 212, 213)
	RETURNF 1

@PRINT_ITEM_EXPLANATION(ARG)
PRINTFORM [{ARG}] %ITEMNAME_TR(ARG), 18, LEFT%
SIF ARG > 99
	PRINT   
SIF ITEM:ARG < 10
	PRINT  
PRINTFORM 　Quantity: {ITEM:ARG}  
SETCOLOR C_L_GRAY
	PRINTFORML %ITEM_EXPLANATION(ARG)%
RESETCOLOR

@薬の知識(ARG)
#FUNCTION
#DIM 回復量
#DIM 強化回数
回復量 = ARG
;回復量 = 基準値 * (1.1 ^ (調合Lv + 薬に関係しそうな知識))
強化回数 = TALENT:MASTER:調合Lv + TALENT:MASTER:禁断の知識 + TALENT:MASTER:調合知識
強化回数 = MIN(強化回数, 10) ;bug fix failsafe
WHILE 強化回数 > 0
	TIMES 回復量, 1.1
	強化回数 --
WEND
RETURNF 回復量

;味覚による回復補正
;満腹度が上がらない食品は気力のみ補正
@ITEM_TASTE(CHARA, 基準値, 食品特徴)
#FUNCTION
#DIM CHARA
#DIM 基準値
#DIMS 食品特徴
#DIMS TASTE_CHARA_GOOD
#DIMS TASTE_CHARA_BAD
#DIM 回復量
TASTE_CHARA_GOOD = %GET_STR(, "キャラデータ", CHARA, "料理：好きな味")%
TASTE_CHARA_BAD = %GET_STR(, "キャラデータ", CHARA, "料理：嫌いな味")%

回復量 = 基準値 + (STR_MULTI_COUNT(TASTE_CHARA_GOOD, 食品特徴) - STR_MULTI_COUNT(TASTE_CHARA_BAD, 食品特徴)) * 10

;旬など季節に応じた補正
SELECTCASE GET_MONTH()
	CASE "春の月"
		IF STRCOUNT(食品特徴, "春物/") && !STRCOUNT(TASTE_CHARA_BAD, "春物/")
			TIMES 回復量, 1.3
		ENDIF
	CASE "夏の月"
		;夏はアイスが2.0倍（嫌いじゃなければ）
		IF STRCOUNT(食品特徴, "アイス/") && !STRCOUNT(TASTE_CHARA_BAD, "アイス/")
			TIMES 回復量, 2.0
		ELSEIF STRCOUNT(食品特徴, "スイカ/")
			TIMES 回復量, 1.3
		ENDIF
	CASE "秋の月"
		IF STRCOUNT(食品特徴, "アケビ/")
			TIMES 回復量, 1.3
		ENDIF
	CASE "冬の月"
		;冬はアイスが0.5倍（好きじゃなければ）
		IF STRCOUNT(食品特徴, "アイス/") && !STRCOUNT(TASTE_CHARA_GOOD, "アイス/")
			TIMES 回復量, 0.5
		ENDIF
ENDSELECT

RETURNF 回復量

;アイテム
@COM_ABLE490
;移動実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(490)
	RETURN RESULT
RETURN 1
