;-------------------------------------------------
;る～ことと話す
;日常系コマンド、レベル1
;-------------------------------------------------
@COM420
PRINTL 「May I help you?」
PRINTL [0] - I'm looking for someone
PRINTL [1] - I want to do some cleaning
PRINTL [2] - About git
PRINTL [100] - Return
INPUT
IF RESULT == 0
	PRINTL 「Who are you looking for?」
	CALL HITOSAGASI()
ELSEIF RESULT == 1
CALL OHEYA_CHECK
ELSEIF RESULT == 2
	PRINTDATA
		DATAFORM 「With git you can merge any patch in one swift move.」
		DATAFORM 「Using git you can track any changes easily.」
		DATAFORM 「It also retains all the previous code neatly.」
	ENDDATA
	PRINTW Sounds like something amazing indeed.
	RETURN -1
ELSEIF RESULT == 100
	RETURN -1
ELSE
	RETURN -1
ENDIF
INPUTS
RESULT = TOINT(RESULTS)
IF RESULT == 0
	REPLACE RESULTS, CALLNAME:MASTER, "%CALLNAME:MASTER%"
	FOR LOCAL, 1, 19
		;Custom code
		SIF STR_TR(LOCAL) == RESULTS
			RESULT = LOCAL
	NEXT
ENDIF
SIF RESULT == 0
	RETURN -1
SIF IN_HOME(RESULT) == 1
	CALL SKIP_MOVE(CFLAG:MASTER:現在位置, RESULT)
RETURN -1

@OHEYA_CHECK
VARSET LOCAL
FOR LOCAL, MINROOM(), MAXROOM
	LOCALS = %NAME_FROM_PLACE(LOCAL)%
	SIF LOCALS == ""
		CONTINUE
	SIF CFLAG:MASTER:現在位置 == LOCAL
		SETCOLOR C_GREEN
;	PRINTBUTTON "["+LOCALS+"]", LOCAL
;	RESETCOLOR
;	PRINTFORM %" "*(22-STRLENS(LOCALS))%
	PRINTFORM [{LOCAL, 3}]%LOCALS, 42, LEFT%
	LOCALS = %SEIKETU(LOCAL), 29, RIGHT%
	PRINTFORM %LOCALS%　　
	RESETCOLOR
	LOCAL:1 ++
	SIF LOCAL:1 % 2 == 0
		PRINTL
NEXT
PRINTFORML 
DRAWLINE
;custom code
PRINTBUTTON "[Return]", 0

@SEIKETU(ARG)
#FUNCTIONS
IF OUTROOF(ARG) && DAY:2 == 4
	SELECTCASE YOGORE:ARG
		CASE IS < 100
			SETCOLOR C_GREEN
			RETURNF "Snow Level: Refreshing"
		CASE IS < 500
			SETCOLOR C_M_GREEN
			RETURNF "Snow Level: Low"
		CASE IS < 1000
			SETCOLOR C_P_GREEN
			RETURNF "Snow Level: Somewhat Low"
		CASE IS < 2000
			SETCOLOR 0xFFFFFF
			RETURNF "Snow Level: Medium"
		CASE IS < 3500
			SETCOLOR 0xFFFFFF
			RETURNF "Snow Level: Somewhat High"
		CASE IS < 5000
			SETCOLOR 0xFFFFFF
			RETURNF "Snow Level: High"
		CASEELSE
			SETCOLOR 0xFFFFFF
			RETURNF "Snow Level: Snow Mountain"
	ENDSELECT
ELSE
	SELECTCASE YOGORE:ARG
		CASE IS < 100
			SETCOLOR C_GREEN
			RETURNF "Cleanliness: Spotless"
		CASE IS < 500
			SETCOLOR C_M_GREEN
			RETURNF "Cleanliness: High"
		CASE IS < 1000
			SETCOLOR C_P_GREEN
			RETURNF "Cleanliness: Somewhat High"
		CASE IS < 2000
			SETCOLOR C_YELLOW
			RETURNF "Cleanliness: Medium"
		CASE IS < 3500
			SETCOLOR C_V_PINK
			RETURNF "Cleanliness: Somewhat Low"
		CASE IS < 5000
			SETCOLOR C_RED
			RETURNF "Cleanliness: Low"
		CASEELSE
			SETCOLOR C_RED
			RETURNF "Cleanliness: Garbage Mountain"
	ENDSELECT
ENDIF
;Custom Code
@HITOSAGASI(ARG, ARG:2=0)
CALL HITOSAGASI_2(1)
CALL HITOSAGASI_2(2)
;custom code
PRINTBUTTON "[Return]", 0
;Qol custom code
IF ARG:2 == 1
	PRINTL
	PRINTBUTTON "[Check Other Areas]", -1
	PRINTL
ENDIF

;ARG=何回目の表示か(1or2)
@HITOSAGASI_2(ARG)
SIF FLAG:並び替え
	CALL CHARASORT_EX
FOR LOCAL:1, 1, EX_CHARANUM
	IF FLAG:並び替え
		LOCAL = RESULT:(LOCAL:1)
		SIF LOCAL < 0
			CONTINUE
	ELSE
		LOCAL = LOCAL:1
		SIF LOCAL >= CHARANUM
			BREAK
	ENDIF
	SIF AT_HOME(LOCAL) == 0
		CONTINUE
	SIF CFLAG:LOCAL:現在位置 < MINROOM()
		CONTINUE
	SIF CFLAG:LOCAL:現在位置 == MAXROOM && CFLAG:LOCAL:睡眠
		CONTINUE
	IF ARG == 1
		SIF !CFLAG:LOCAL:神社在住
			CONTINUE
		SETCOLOR C_S_BLUE
	ELSEIF ARG == 2
		SIF CFLAG:LOCAL:神社在住
			CONTINUE
	ENDIF
	PRINTFORM %CALLNAME:LOCAL, 15, LEFT%
	RESETCOLOR
	LOCALS = %NAME_FROM_PLACE(CFLAG:LOCAL:現在位置)%
;	SIF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:初期位置
;		LOCALS = %CALLNAME:MASTER%私室
;	PRINTBUTTON "["+LOCALS+"]", CFLAG:LOCAL:現在位置
;	PRINTFORM %" "*(22-STRLENS(LOCALS))%
	PRINTFORM [{CFLAG:LOCAL:現在位置, 3}]%LOCALS, 24, LEFT%
	IF LOCALS == ""
		PRINTFORML In an unknown position ({CFLAG:LOCAL:現在位置})
	ELSEIF CFLAG:LOCAL:睡眠
		PRINTL Asleep
	ELSEIF CFLAG:LOCAL:行動 == 5
		IF CFLAG:LOCAL:現在位置 == TFLAG:宴会場 && TIME_PROGRESS(TIME:1) && (CFLAG:LOCAL:職種 == 49 || CFLAG:LOCAL:職種 == 51)
			PRINTL Attending banquet
		ELSE
			PRINTL Working
		ENDIF
	ELSE
		PRINTL
	ENDIF
NEXT
;Qol Custom code
IF ARG:2 == 1
	PRINTL
	PRINTBUTTON "[Check Other Areas]", -1
	PRINTL
ENDIF
PRINTL

;-------------------------------------------------
;実行判定
;-------------------------------------------------
;る～ことと話す
@COM_ABLE420
;自宅外は不可
SIF AT_HOME(MASTER) == 0
	RETURN 0
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;移動実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(420)
	RETURN RESULT
SELECTCASE MAIN_MAP
	CASE 0
		;る～ことが寝ていたらダメ
		SIF CFLAG:2:睡眠
			RETURN 0
		;る～ことの居る所
		SIF CFLAG:MASTER:現在位置 != CFLAG:2:現在位置
			RETURN 0
	CASE 3
		SIF CFLAG:MASTER:現在位置 != 323
			RETURN 0
	CASE 9
		SIF CFLAG:MASTER:現在位置 != 923
			RETURN 0
	CASEELSE
		RETURN 0
ENDSELECT

SIF CFLAG:うふふ == 2
	RETURN 0
RETURN 1

