;-------------------------------------------------
;ホワイトデー
;-------------------------------------------------
@COM491

#DIM CAN_COOK
#DIM 好感度上昇量
#DIM 信頼度上昇量
#DIM WD_BUFF
#DIMS WD_GIFT
#DIMS WD_GIFT_TR ;custom code

好感度上昇量 = 0
信頼度上昇量 = 0
CAN_COOK = 0
WD_BUFF = 0

;調理可能か
CALL COM_ABLE413
CAN_COOK = RESULT
IF FESTIVAL() == "ホワイトデー" && INRANGE(CFLAG:TARGET:バレンタイン,1,4) && SHIRAHU(TARGET)
	CALL ASK_M("Return Gift Recipients",1,"Cook Return Gift Sweets (Req. 5 Sweetening Material)",ITEM:甘味材 >= 5,@"Give the return gift to %CALLNAME:TARGET%",1)
ELSE
	CALL ASK_M("Return Gift Recipients",1,"Cook Return Gift Sweets",1)
ENDIF
SELECTCASE RESULT
	CASE 0
		CALL Valentine_List
		WAIT
		RETURN -1
	CASE 1
		IF !CAN_COOK
			PRINTFORMW You must move to a place where you can cook first.
			RETURN -1
		ENDIF
		CALL WD_COOK
		RETURN RESULT
	CASE 2
		PRINTFORML What do you want to gift in return?（Received Gift：%CAPITALIZE(Valentine_Chocolate(TARGET))%）
		{
		CALL ASK_M("Cancel",1,"Black Thunder",ITEM:ブラックサンダー,"Rotor Chocolate",ITEM:ローターチョコ,"White Day Chocolate",ITEM:WDチョコレート,
		"Homemade Chocolate Cookies",ITEM:手作りチョコクッキー,"White Day Custard",ITEM:WDプディング,
		"Handmade Custard",ITEM:手作りプディング,@"Decorated Cake\@ CFLAG:TARGET:バレンタイン != 4 ? %" "%(Only in return for Handmade Cookies)#\@",ITEM:デコレーションケーキ && CFLAG:TARGET:バレンタイン == 4)
		}
		SELECTCASE RESULT
			CASE 0
				RETURN -1
			CASE 1
				WD_GIFT = ブラックサンダー
				;custom code: from here on out - WD_GIFT_TR used to return translated gift name
				WD_GIFT_TR = Black Thunder
				SIF CFLAG:バレンタイン > 1 ;gifting the cheap throwaway store-bought chocolate in return to a proper gift reaction?
					PRINTFORMW %CALLNAME:TARGET% seems utterly baffled...
				好感度上昇量 = 30
				信頼度上昇量 = 30
			CASE 2
				WD_GIFT = ローターチョコ
				WD_GIFT_TR = Rotor Chocolate
				好感度上昇量 = 50
				信頼度上昇量 = 50
				SOURCE:快Ｃ += 3000
				SOURCE:反感 += 300
			CASE 3
				WD_GIFT = WDチョコレート
				WD_GIFT_TR = White Day Chocolate
				好感度上昇量 = 100
				信頼度上昇量 = 100
				WD_BUFF = 100
			CASE 4
				WD_GIFT = 手作りチョコクッキー
				WD_GIFT_TR = Homemade Chocolate Cookies
				好感度上昇量 = 150
				信頼度上昇量 = 150
				EXP:愛情経験 += 1
				WD_BUFF = 100
			CASE 5
				WD_GIFT = WDプディング
				WD_GIFT_TR = White Day Custard
				好感度上昇量 = 200
				信頼度上昇量 = 200
				EXP:愛情経験 += 3
				WD_BUFF = 100
			CASE 6
				WD_GIFT = 手作りプディング
				WD_GIFT_TR = Handmade Custard
				好感度上昇量 = 300
				信頼度上昇量 = 300
				EXP:愛情経験 += 5
				WD_BUFF = 200
			CASE 7
				WD_GIFT = デコレーションケーキ
				WD_GIFT_TR = Decorated Cake
				好感度上昇量 = 1000
				信頼度上昇量 = 1000
				EXP:愛情経験 += 30
				WD_BUFF = 1000
		ENDSELECT
		TFLAG:193 = RESULT
		SIF RESULT >= 5
			TALENT:機嫌 = 1
		PRINTFORMW You present the %WD_GIFT_TR% to %CALLNAME:TARGET%!
		;ITEM:WD_GIFT --
		;custom code
		CALL LOST_ITEM(WD_GIFT, 1, 1)
		CALL CHANGE_CFLAG(2,TARGET,好感度上昇量)
		CALL CHANGE_CFLAG(4,TARGET,信頼度上昇量)
		IF WD_BUFF
			CALL BUFF_BASE(TARGET,BASE_体力, WD_BUFF, 1)
			CALL BUFF_BASE(TARGET,BASE_気力, WD_BUFF, 1)
		ENDIF
		;custom code, add source (based on gift shop's, slightly reduced because of strong gifts)
		SOURCE:歓楽 = 800 + 800 * TFLAG:193 + 400 * 好感度係数(TARGET)
		SOURCE:受動 = 600 + 800 * TFLAG:193 + 400 * ABL:従順
		SOURCE:征服 = 600 + 500 * TFLAG:193 + 900 * ABL:サドっ気
		;-----------------------------
		CFLAG:TARGET:バレンタイン += 100
		TIME += 10
		RETURN 1
ENDSELECT


@Valentine_List
FOR LOCAL,1,CHARANUM
	SIF !CFLAG:LOCAL:バレンタイン
		CONTINUE
	SELECTCASE CFLAG:LOCAL:バレンタイン % 100
		CASE 2
			SETCOLOR C_GREEN
		CASE 3
			SETCOLOR C_YELLOW
		CASE 4
			SETCOLOR C_PINK
	ENDSELECT
	;返礼済みなら打ち消し線
	SIF CFLAG:LOCAL:バレンタイン > 100
		FONTSTYLE 4
	PRINTFORMLC [{LOCAL}] %CALLNAME:LOCAL%
	FONTREGULAR
	RESETCOLOR
NEXT
PRINTFORML 
DRAWLINE
SETCOLOR C_PINK
PRINTFORM Handmade　
SETCOLOR C_YELLOW
PRINTFORM Premium　
SETCOLOR C_GREEN
PRINTFORM Ordinary　
RESETCOLOR
;original uses 義理 which would be "out of obligation/courtesy", but it's too confusing
PRINTFORML Black Thunder

@WD_COOK
PRINTFORML What to make? （Every item requires at least 5 Sweetening Material (Own: x{ITEM:甘味材})）
{
CALL ASK_M("Cancel",1, "Chocolate Cookies×10＋α：Can be made in large quantity. Perfect for handouts.",1,
@"Custard×5＋α：Well-balanced choice.（Req: Honey (Own: x{ITEM:はちみつ})）",ITEM:はちみつ,
@"Decorated Cake：For those who matter most.（Req: 3 Strawberry (Own: x{ITEM:イチゴ}), Cooking Skill 4 (Now:{ABL:MASTER:料理技能})）",ITEM:イチゴ >= 3 && ABL:MASTER:料理技能 >= 4,
@"Rotor Chocolate×5：Edible mystery chocolate with 'rotory' filling.（Req: Rotor (Own: x{ITEM:ローター})）",ITEM:ローター)
}
SELECTCASE RESULT
	CASE 0
		RETURN -1
	CASE 1
		CALL GET_ITEM("手作りチョコクッキー", 10 + ABL:MASTER:料理技能, 2)
		;ITEM:甘味材 -= 5
		;custom code
		CALL LOST_ITEM("甘味材", 5, 2)
	CASE 2
		CALL GET_ITEM("手作りプディング", 5 + ABL:MASTER:料理技能 / 3, 2)
		;ITEM:甘味材 -= 5
		;ITEM:はちみつ -= 1
		;custom code
		CALL LOST_ITEM("甘味材", 5, 1)
		CALL LOST_ITEM("はちみつ", 1, 1)
	CASE 3
		;ITEM:甘味材 -= 5
		;ITEM:イチゴ -= 3
		CALL GET_ITEM("デコレーションケーキ", 1, 1)
		;custom code
		CALL LOST_ITEM("甘味材", 5, 1)
		CALL LOST_ITEM("イチゴ", 3, 1)
	CASE 4
		CALL GET_ITEM("ローターチョコ", 5, 1)
		;ITEM:甘味材 -= 5
		;ITEM:ローター -= 1
		;custom code
		CALL LOST_ITEM("甘味材", 5, 1)
		CALL LOST_ITEM("ローター", 1, 1)
ENDSELECT
TIME += 60
EXP:MASTER:料理経験 += 3
DOWNBASE:MASTER:気力 = 300
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE491
;チョコレートを贈る実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(491)
	RETURN RESULT
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;添い寝中
SIF CFLAG:添い寝中
	RETURN 0
;イタズラ中
SIF CFLAG:MASTER:イタズラ
	RETURN 0
SIF CFLAG:うふふ == 2
	RETURN 0
SIF !PREPARE_WD()
	RETURN 0
IF FESTIVAL() != "ホワイトデー"
	SIF !KITCHEN(CFLAG:MASTER:現在位置)
		RETURN 0
ENDIF

RETURN 1

@PREPARE_WD()
#FUNCTION
SIF DAY:2 != 1
	RETURNF 0
SIF DAY:3 > 14
	RETURNF 0
RETURNF 1

;デバッグ用
@WD_DEBUG
#FUNCTION ;custom code
ITEM:甘味材 = 99
ITEM:イチゴ = 99
ITEM:はちみつ = 99
DAY:2 = 1
DAY:3 = 13
FOR LOCAL,1,CHARANUM
	CFLAG:LOCAL:バレンタイン = 1 + RAND:4
NEXT