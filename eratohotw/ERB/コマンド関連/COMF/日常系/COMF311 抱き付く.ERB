;-------------------------------------------------
;抱き付く
;日常系コマンド　レベル2
;-------------------------------------------------
@COM311
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 10
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF
ELSE
	;Custom Code
	IF FLAG:甲斐性無 && !FLAG:LUSTMODE && !CFLAG:うふふ
		DRAWLINE
		PRINTFORMW %PARSE("You don't")% have enough courage to do that.
		RETURN -1
	ENDIF
	TFLAG:理性削り = 1
	SELECTCASE TFLAG:192
		;通常
		CASE 0
			LOCAL = RAND:110
			LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
			SIF LOCAL:1 > 109
				LOCAL:1 = 109
			IF LOCAL <= 9
				;10％で大成功
				TFLAG:193 = 1
				IF CHK_DATENOW(CFLAG:TARGET:デート中)
					EXP:MASTER:デート経験 ++
					EXP:TARGET:デート経験 ++
				ENDIF
				;com stuff custom code, reliance
				;信頼
				TFLAG:98 = 5
			ELSEIF LOCAL >= LOCAL:1
				;10～1％で失敗
				TFLAG:193 = -1
				;信頼
				TFLAG:98 = -2
			ELSE
				;残りは成功
				TFLAG:193 = 0
				;信頼
				TFLAG:98 = 3
			ENDIF
		;強制成功or大成功
		CASE 1
			IF RAND:110 <= 9
				;10％で大成功
				TFLAG:193 = 1
				IF CHK_DATENOW(CFLAG:TARGET:デート中)
					EXP:MASTER:デート経験 ++
					EXP:TARGET:デート経験 ++
				ENDIF
				;信頼
				TFLAG:98 = 5
			ELSE
				;残りは成功
				TFLAG:193 = 0
				;信頼
				TFLAG:98 = 3
			ENDIF
		;強制失敗
		CASE -1
			TFLAG:193 = -1
			TFLAG:98 = -2
		;コマンド終了
		CASE -2
			TFLAG:193 = -1
			RETURN 0
	ENDSELECT
	;先制発生時は失敗しない
	SIF TFLAG:102 == 3 && TFLAG:192 != -1
		TFLAG:193 = MAX(TFLAG:193,0)
	IF CFLAG:TARGET:添い寝中
		DOWNBASE:MASTER:気力 += 50
	ELSE
		DOWNBASE:気力 += 100
		DOWNBASE:MASTER:気力 += 50
	ENDIF

	;固定で獲得するソース
	SOURCE:歓楽 = 120
	SOURCE:情愛 = 160
	SOURCE:露出 = 100
	SOURCE:反感 = 250

	;ABL:親密をみる
	IF ABL:親密 <= 1
		SOURCE:情愛 += (ABL:親密 * 30)
	ELSEIF ABL:親密 <= 3
		SOURCE:情愛 += 200 + (ABL:親密 * 30)
	ELSEIF ABL:親密 <= 5
		SOURCE:情愛 += 400 + (ABL:親密 * 30)
	ELSEIF ABL:親密 <= 8
		SOURCE:情愛 += 600 + (ABL:親密 * 40)
		SOURCE:露出 += 200 + (ABL:欲望 * 15)
	ELSEIF ABL:親密 <= 11
		SOURCE:情愛 += 800 + (ABL:親密 * 40)
		SOURCE:露出 += 300 + (ABL:欲望 * 15)
	ELSE
		SOURCE:情愛 += 1800 + (ABL:親密 * 20)
		SOURCE:露出 += 700 + (ABL:欲望 * 10)
	ENDIF

	;好感度をみる
	IF CFLAG:2 <= 1000
		;com stuff custom code + below - buff to make embrace more potent
		SOURCE:情愛 += CFLAG:2 / 2
	ELSEIF CFLAG:2 <= 5000
		SOURCE:恭順 = CFLAG:2 / 5
		SOURCE:情愛 += 200 + (CFLAG:2 - 500) / 5
		SOURCE:歓楽 += 200 + (CFLAG:2 - 500) / 4
	ELSEIF CFLAG:2 <= 10000
		SOURCE:恭順 = 100 + CFLAG:2 / 20
		SOURCE:情愛 += 400 + (CFLAG:2 - 500) / 5
		SOURCE:歓楽 += 400 + (CFLAG:2 - 500) / 4
	ELSE
		SOURCE:恭順 = 350 + CFLAG:2 / 100
		SOURCE:情愛 += 1200 + (CFLAG:2 - 5000) / 7
		SOURCE:歓楽 += 1200 + (CFLAG:2 - 5000) / 4
	ENDIF
	SIF SOURCE:恭順 < 0
		SOURCE:恭順 = 0

	LOCAL = 0
	;好感度が低いと好感度低下
	IF CFLAG:2 <= 50
		LOCAL -= 3
	ELSEIF CFLAG:2 <= 100
		LOCAL -= 2
	ELSEIF CFLAG:2 <= 250
		LOCAL -= 1
	ENDIF


	SOURCE:受動 = 120 + 240 * ABL:従順
	SOURCE:征服 = 120 + 240 * ABL:サドっ気

	;好感度変化
	TFLAG:97 += LOCAL

	IF TFLAG:193 == -1
		TIMES SOURCE:歓楽 , 0.10
		TIMES SOURCE:情愛 , 0.50
		TIMES SOURCE:征服 , 0.50
		TIMES SOURCE:受動 , 0.50
		TIMES SOURCE:露出 , 0.40
	ELSEIF TFLAG:193 == 0
		TIMES SOURCE:歓楽 , 1.00
		TIMES SOURCE:情愛 , 1.00
		TIMES SOURCE:征服 , 1.00
		TIMES SOURCE:受動 , 1.00
		TIMES SOURCE:露出 , 0.90
		TFLAG:ムードボーナス = 30
	ELSE
		TIMES SOURCE:歓楽 , 2.00
		TIMES SOURCE:情愛 , 2.00
		TIMES SOURCE:征服 , 2.00
		TIMES SOURCE:受動 , 2.00
		TIMES SOURCE:露出 , 1.80
		TFLAG:ムードボーナス = 50
	ENDIF
	TIME += !FLAG:TIME_COMPRESSION ? 10 # 5

	IF CHK_DATENOW(CFLAG:TARGET:デート中)
		EXP:MASTER:デート経験 ++
		EXP:TARGET:デート経験 ++
	ENDIF

	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 1
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF
	;com stuff custom code
	TCVAR:Hug_Timeout = 1440 * DAY + TIME
ENDIF
Qol_Info_Count_Array:TARGET:Qol_Count_Hug ++ ;qol custom code
SIF !SHIRAHU(TARGET)
	Qol_Info_Count_Array:TARGET:Qol_Count_UncHug ++ ;qol custom code
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE311
;抱き付く実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(311)
	RETURN RESULT
;時間停止中はスキップ
IF !FLAG:70
	;馴れ合い強度1
	SIF TCVAR:MASTER:馴れ合い < 1 && !CFLAG:添い寝中
		RETURN 0
	;睡眠中
	SIF CFLAG:睡眠 && !CFLAG:添い寝中
		RETURN 0
	;仕事中
	SIF CFLAG:行動 == 5
		RETURN 0
	;com stuff custom code 1時間間隔
	SIF TIME_PROGRESS(TCVAR:Hug_Timeout) < 60 && !CFLAG:イタズラ && !CFLAG:添い寝中
		RETURN 0
ENDIF
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
;腕使用中
SIF TFLAG:61
	RETURN 0
SIF GETBIT (TCVAR:26,1)
	RETURN 0
;シックスナイン体勢
SIF TEQUIP:シックスナイン
	RETURN 0
RETURN 1

