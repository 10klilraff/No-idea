;-------------------------------------------------
;天の声を聞く
;日常系コマンド、レベル1
;-------------------------------------------------
@COM422
PRINTL [0] - Find someone
PRINTL [1] - Find dirty rooms
PRINTL [2] - Find someone who's outside
PRINTFORML [3] - Change【Follow】Target（Currently: \@ STALKED ? %CALLNAME:STALKED% # None\@）
PRINTL [100] - Return
INPUT
IF RESULT == 0
	CALL HITOSAGASI()
ELSEIF RESULT == 1
	CALL OHEYA_CHECK
ELSEIF RESULT == 2
	CALL BROAD_HITOSAGASI
	TIME += 3
	RETURN 1
ELSEIF RESULT == 3 ;custom code
	CALL STALKER_CHANGE
	RETURN 1
ELSEIF RESULT == 100
	RETURN -1
ENDIF
INPUTS
RESULT = TOINT(RESULTS)
IF RESULT == 0
	REPLACE RESULTS, CALLNAME:MASTER, "%CALLNAME:MASTER%"
	FOR LOCAL, 1, 19
		;Custom code
		SIF STR_TR(LOCAL) == RESULTS
			RESULT = LOCAL
	NEXT
ENDIF

IF (RESULT >= 26 && RESULT <= 35) && ABL:MASTER:教養 < 1 && !INRANGE(CFLAG:MASTER:初期位置,26,35)
	PRINTFORMW %PARSE("You are")% too inexperienced to find it...
ELSE
	IF IN_HOME(RESULT) == 1
		SIF AT_HOME(MASTER) == 0
			GOTO MURIDESU
		CALL SKIP_MOVE(CFLAG:MASTER:現在位置, RESULT)
	ENDIF
ENDIF

TIME += 10
RETURN 1
$MURIDESU
PRINTW Can't go from here...
TIME += 10
RETURN 1

@BROAD_HITOSAGASI
$KAKUNIN
FOR LOCAL,0,11
	SIF LOCAL == MAIN_MAP
		CONTINUE
	LOCAL:1 = 5000 + LOCAL
	;Custom code
	PRINTFORMLC %@"[{LOCAL,2}] - %STR_TR(LOCAL:1)%", 41, LEFT%
	CALL 広域チェック(LOCAL,1)
	CALL 広域チェック(LOCAL,2)
	SIF !FLAG:画像表示
		PRINTL
	PRINTL
NEXT
DRAWLINE
PRINT [100] - Exit
$INPUT_LOOP
INPUT
IF RESULT == 100 || RESULT == MAIN_MAP
	RETURN -1
ELSEIF RESULT < 0
	CLEARLINE 1
	GOTO INPUT_LOOP
ELSEIF RESULT >= 11
	PRINTW Not created yet
	CLEARLINE 2
	GOTO INPUT_LOOP
ENDIF
CALL 広域チェック(RESULT,2)
PRINTL
CALL ASK_YN("Check other areas","Leave")
SIF !RESULT
	GOTO KAKUNIN

@広域チェック(ARG,ARG:1)
#DIM DYNAMIC nRoster, 200
VARSET LOCAL

LOCAL:9 = 0
FOR LOCAL,1,CHARANUM
	SIF AT_HOME(LOCAL) == 1
		CONTINUE
	SIF CFLAG:LOCAL:現在位置 == MAXROOM && CFLAG:LOCAL:睡眠
		CONTINUE
	FOR LOCAL:1,10,100,10
		IF CFLAG:LOCAL:現在位置 == ARG * 100 + LOCAL:1
			;その地域にいるキャラを表示
			IF ARG:1 == 2
				;custom code
				IF FLAG:画像表示
					nRoster:(LOCAL:9++) = LOCAL
				ELSE
					SIF CFLAG:LOCAL:行動 == 5
						SETCOLOR 255, 100, 0
					PRINTFORM %CALLNAME:LOCAL,15,LEFT%
					SIF ++(LOCAL:9) % 6 == 0
						PRINTL
					RESETCOLOR
				ENDIF
			ENDIF
			LOCAL:2 ++
		ENDIF
	NEXT
NEXT
SIF FLAG:画像表示
	CALL PRINT_ROSTER(nRoster, "broad", 40, 16)
;その地域にいる人数
IF ARG:1 == 1
	IF LOCAL:2
		PRINTFORML %@"{LOCAL:2}",2% \@LOCAL:2 == 1?person#people\@
	ELSE
		PRINTL Nobody here
	ENDIF
ENDIF

;天の声を聞く
@COM_ABLE422
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(421)
	RETURN RESULT
;賽銭箱
SIF !天の声可(CFLAG:MASTER:現在位置)
	RETURN 0
SIF CFLAG:うふふ == 2
	RETURN 0
RETURN 1
