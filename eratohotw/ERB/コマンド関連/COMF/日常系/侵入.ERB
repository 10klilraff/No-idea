@部屋に忍び込む(ARG, 直前位置)
#DIM 直前位置
;ARG=行先
FOR LOCAL, 1, CHARANUM
	IF CFLAG:LOCAL:311 == ARG
		IF CFLAG:LOCAL:ブチギレ
			IF FLAG:70 == 1
				;時間停止中,現状死に分岐
				PRINTFORML %PARSE("You enter")% the room, opening the door to find an extremely angry %CALLNAME:LOCAL%.
				PRINTFORMW After seeing such a indescribable display, %PARSE("you give",1)% up on sneaking in...
			ELSE
				PRINTFORML %PARSE("You open")% the door and %PARSE("sneak")% into the sleeping %CALLNAME:LOCAL%'s room.
				PRINTFORMW But %PARSE("you",1)% immediately %PARSE("come")% face to face with %CALLNAME:(LOCAL)%, staring daggers %PARSE("at you",1)%.
				;部屋へ通しても寝ない仕組みが作れたら解放
				;IF TALENT:LOCAL:恋慕
					;CALL KOJO_MESSAGE_SEND("EVENT",12,LOCAL,2,2)
					;PRINTFORMW %CALLNAME:LOCAL%はしぶしぶ%CALLNAME:MASTER%を部屋へ通した…
					;移動成立 でも忍び込みは立たない
					;CFLAG:MASTER:現在位置 = (TFLAG:195)
				;ELSE
					CALL KOJO_MESSAGE_SEND("EVENT",12,LOCAL,2,1)
					PRINTFORMW %PARSE("You give")% up on sneaking in...
				;ENDIF
			ENDIF
			;侵入成功フラグクリア
			CLEARBIT CFLAG:LOCAL:42,8
			LOCK:(CFLAG:LOCAL:現在位置) = 1
			CFLAG:MASTER:現在位置 = 直前位置
		ELSE
			IF (!RAND:3 || !CFLAG:LOCAL:睡眠) && !FLAG:70 && !CFLAG:LOCAL:出禁 && !TCVAR:LOCAL:睡眠薬強度 && EXP:LOCAL:睡眠姦経験 < 300
				;PRINT 物音に気づいた
				PRINTFORML %CALLNAME:LOCAL% hears a sound.
				
				CALL CHARA_AWAKE(LOCAL,1)
				IF !CFLAG:LOCAL:睡眠 && CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置 && !CFLAG:LOCAL:隠密中
					IF TALENT:LOCAL:恋慕
						IF CFLAG:LOCAL:衰弱
							PRINTFORMW %CALLNAME:LOCAL% checks and finds out it's %PARSE("you")% before yawning and going back to sleep...
							CALL KOJO_MESSAGE_SEND("EVENT",12,LOCAL,1,3)
							CALL SET_DERAY(99,LOCAL)
							CALL CHARA_SLEEP(LOCAL,0)
						ELSE
							PRINTFORMW %CALLNAME:LOCAL% sighs in relief as %HE_SHE(TARGET)% finds out it's %PARSE("you")%...
							CALL KOJO_MESSAGE_SEND("EVENT",12,LOCAL,1,2)
						ENDIF
						;移動成立
						CFLAG:MASTER:現在位置 = ARG
					ELSE
						IF CFLAG:LOCAL:衰弱
							PRINTFORMW %CALLNAME:LOCAL% throws %PARSE("you")% out before going back to sleep...
							CALL KOJO_MESSAGE_SEND("EVENT",12,LOCAL,1,5)
							CALL SET_DERAY(99,LOCAL)
							CALL CHARA_SLEEP(LOCAL,0)
						ELSE
							PRINTFORMW %CALLNAME:LOCAL% throws %PARSE("you")% out...
							CALL KOJO_MESSAGE_SEND("EVENT",12,LOCAL,1,4)
						ENDIF
						;侵入成功フラグクリア
						CLEARBIT CFLAG:LOCAL:42,8
						LOCK:(CFLAG:LOCAL:現在位置) = 1
						CFLAG:MASTER:現在位置 = 直前位置
					ENDIF
				ENDIF
			ELSE
				PRINTFORML %PARSE("You sneak")% into the sleeping %CALLNAME:LOCAL%'s room.
				CALL KOJO_MESSAGE_SEND("EVENT",12,LOCAL,1,1)
				;移動成立
				CFLAG:MASTER:現在位置 = ARG
				;侵入成功フラグセット
				SETBIT CFLAG:LOCAL:42,8
			ENDIF
		ENDIF
	ENDIF
NEXT
SIF CFLAG:MASTER:現在位置 != ARG
	RETURN -1

@トイレ侵入(ARG,ARG:1)
#DIM 入れません
入れません = 0
IF FLAG:70 == 1
	FOR LOCAL, 1, CHARANUM
		IF CFLAG:LOCAL:現在位置 == ARG
			PRINTFORMW It seems %CALLNAME:LOCAL% is already here.
			BREAK
		ENDIF
	NEXT
	CFLAG:MASTER:現在位置 = ARG
	;遠距離移動フラグ消去
	SIF ARG:1
		NEXTCOM = -1
ELSE
	FOR LOCAL, 1, CHARANUM
		IF CFLAG:LOCAL:現在位置 == (TFLAG:195)
			PRINTFORMW 入ってま～す
			入れません = 1
		ENDIF
	NEXT
	IF 入れません == 1
		入れません = 0
		IF ARG:1
			NEXTCOM = -1
			DEBUGPRINTL 【COM400】RETURNPOINT E
			RETURN 0
		ELSE
			RETURN -1
		ENDIF
	ENDIF
	CFLAG:MASTER:現在位置 = ARG
	;遠距離移動フラグ消去
	SIF ARG:1
		NEXTCOM = -1
ENDIF

@虫かご侵入(ARG,ARG:1)
#DIM 入れません
#DIM 虫かご
入れません = 0
FOR LOCAL, 1, CHARANUM
	SIF CFLAG:LOCAL:同行 && TALENT:LOCAL:100 >= -4
		入れません = 1
NEXT
SIF TALENT:MASTER:100 >= -4
	入れません = 2
IF 入れません == 1
	PRINTFORMW Bringing them along is impossible...
	RETURN -1
ELSEIF 入れません
	 PRINTDATAW
	DATAFORM It's an insect cage.
	DATAFORM There's no way to fit inside!
	DATAFORM Only inchlings can enter.
	ENDDATA
	IF ARG:1
		NEXTCOM = -1
		DEBUGPRINTL 【COM400】RETURNPOINT D
		RETURN 0
	ELSE
		虫かご += 1
		IF 虫かご >= 10 && !FLAG:70
			PRINTFORMW %PARSE("You become")% so fixated on getting inside %PARSE("you become",1)% an inchling.
			;半永続する
			TALENT:MASTER:100 = -5
			虫かご = 0
		ENDIF
		RETURN -1
	ENDIF
ENDIF
CFLAG:MASTER:現在位置 = ARG
SIF ARG:1
	NEXTCOM = -1
