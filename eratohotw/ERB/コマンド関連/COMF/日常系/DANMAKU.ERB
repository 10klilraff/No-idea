@DANMAKU_OPPONENT_STATUS(ARG, ARG:1)
;特殊能力表示
LOCAL:1 = 0
FOR LOCAL, 0, 10
	SIF GETBIT(CFLAG:ARG:弾幕特能, LOCAL)
		LOCAL:1 ++
NEXT
IF LOCAL:1 == 10
	CALL DANMAKU_SA_OUTPUT(ARG, LOCAL:1)
ELSEIF LOCAL:1 == 0
	PRINTFORML No Danmaku special abilities.
ELSE
	FOR LOCAL, 0, 10
		CALL DANMAKU_SA_OUTPUT(ARG, LOCAL)
	NEXT
ENDIF

SIF ARG:1
	RETURN
;戦闘能力の差でメッセージ分岐
SELECTCASE ABL:ARG:戦闘能力 - ABL:MASTER:戦闘能力
	CASE -6 TO -4
		PRINTFORML Even with a blindfold and standing still %PARSE("you")% can win.
	CASE -4 TO -2
		PRINTFORML Even with a blindfold %PARSE("you")% can win.
	CASE -1
		PRINTFORML Losing is unlikely.
	CASE 0
		PRINTFORML Might be possible to win.
	CASE 1
		PRINTFORML Winning might not be possible.
	CASE 2
		PRINTFORML The opponent is very strong.
	CASE 3
		PRINTFORML The opponent seems to be at least twice as strong as %PARSE("you")%.
	CASE 4
		PRINTFORML Unless a miracle occurs %PARSE("you")% will die.
	CASE 5,6
		PRINTFORML The opponent is a giant, %PARSE("you are")% an ant under their foot.
	CASEELSE
		
ENDSELECT

@DANMAKU_SA_OUTPUT(OPPONENT, ARG:0)
#DIM OPPONENT

SIF ARG:0 < 10
	LOCAL = GETBIT(CFLAG:OPPONENT:弾幕特能, ARG:0)
	;Custom code, breaking lines
	IF ARG:0 == 10
		SETCOLOR C_RED
		PRINTFORML 【100 Eternities】: 
		PRINTFORML      All effects at once. Ultimate.
	ELSEIF LOCAL && ARG:0 == 0
		SETCOLOR C_P_BLUE
		PRINTFORML 【Spell It All Out】: 
		PRINTFORML      %CALLNAME:MASTER% Dice Number Down
	ELSEIF LOCAL && ARG:0 == 1
		SETCOLOR C_P_BLUE
		PRINTFORML 【Lukewarm Sincerity】: 
		PRINTFORML      %CALLNAME:MASTER% Dice Value Down
	ELSEIF LOCAL && ARG:0 == 2
		SETCOLOR C_RED
		PRINTFORML 【High-Spirited】: 
		PRINTFORML      %CALLNAME:OPPONENT% Dice Number Up
	ELSEIF LOCAL && ARG:0 == 3
		SETCOLOR C_RED
		PRINTFORML 【Open-minded】: 
		PRINTFORML      %CALLNAME:OPPONENT% Dice Value Up
	ELSEIF LOCAL && ARG:0 == 4
		SETCOLOR C_P_PURPLE
		PRINTFORML 【Anomalies, Strength, Disorder, and Spirits】: 
		PRINTFORML      If %CALLNAME:MASTER% beats %CALLNAME:OPPONENT% by just a small, fixed amount their life will not go down.
	ELSEIF LOCAL && ARG:0 == 5
		SETCOLOR C_P_PURPLE
		PRINTFORML 【Deep Knowledge Of Life】: 
		PRINTFORML      %CALLNAME:OPPONENT% begins with 4 lives
	ELSEIF LOCAL && ARG:0 == 6
		SETCOLOR C_P_PURPLE
		PRINTFORML 【Merciless】: 
		PRINTFORML      %CALLNAME:MASTER% gets one less bomb after losing a life
	ELSEIF LOCAL && ARG:0 == 7
		SETCOLOR C_P_PURPLE
		PRINTFORML 【All-In】: 
		PRINTFORML      %CALLNAME:OPPONENT% will use bombs at a certain probability
	ELSEIF LOCAL && ARG:0 == 8
		SETCOLOR C_RED
		PRINTFORML 【Stubborn】: 
		PRINTFORML      %CALLNAME:OPPONENT% Dice Value Correction Up
	ELSEIF LOCAL && ARG:0 == 9
		SETCOLOR C_P_BLUE
		PRINTFORML 【Melancholic】: 
		PRINTFORML      %CALLNAME:MASTER% Dice Value Correction Down
	ENDIF
RESETCOLOR


;-------------------------------------------------
;戦闘中の処理
;-------------------------------------------------
@DANMAKU_BATTLE(OPPONENT, ENEMY = -1)
#DIM プレイヤー残機
#DIM 相手残機
#DIM 残りHP
#DIM 残りボム回数
#DIM ターン数
#DIM プレイヤーダイス面
#DIM 相手ダイス面
#DIM プレイヤーダイス数
#DIM 相手ダイス数
#DIM プレイヤーダイス値
#DIM 相手ダイス値
#DIM プレイヤー修正値
#DIM 相手修正値
#DIM 相手強さ
#DIM 霊力充填フラグ
#DIM 慢心フラグ
#DIM OPPONENT
#DIM ENEMY
#DIM BombPower
#DIM B_Min
#DIM B_Max
#DIM 持ち込みアイテム
#DIM DamageThreshold
#DIM Pichun
#DIM Charge
;addition custom code
#DIM nInitLives
#DIM nInitBombs
;--------------------
プレイヤー残機 = 3
相手残機 = 3
相手強さ = ABL:OPPONENT:戦闘能力
残りHP = MAXBASE:MASTER:体力
残りボム回数 = 2
ターン数 = 1
霊力充填フラグ = 0
慢心フラグ = 0
Pichun = 0
Charge = 0
B_min = ABL:MASTER:戦闘能力 * 7 * (ABL:MASTER:戦闘能力 / 2 + 1)
B_Max = ABL:MASTER:戦闘能力 * 11 * (ABL:MASTER:戦闘能力 / 2 + 1) + 6
SIF TALENT:OPPONENT:プライド > 0 && ABL:MASTER:戦闘能力 < 相手強さ && 相手強さ > 2 && TALENT:OPPONENT:行きずり == 1
	慢心フラグ = 1
;【洽覧深識】の効果
SIF GETBIT(CFLAG:OPPONENT:弾幕特能, 5)
	相手残機 ++
;【エクステンド】の効果
SIF TALENT:MASTER:エクステンド == 1
	プレイヤー残機 ++
;【ルナシューター】の効果
SIF TALENT:MASTER:ルナシューター == 1
	残りボム回数 ++
;弾幕勝負補助アイテムの持ち込み
CALL DANMAKU_ITEM_USE
持ち込みアイテム = RESULT

;addition custom code
nInitLives = プレイヤー残機
nInitBombs = 残りボム回数
RESULT:9 = 0
RESULT:10 = 0
;--------------------

;プレイヤーのダイス数は2.5n + 期待値2 + 期待値1
;ダイス面は(n+1)*期待値10*期待値7*n/2

WHILE プレイヤー残機 > 0 && 相手残機 > 0
	;ダイス数計算
	プレイヤーダイス数 = (ABL:MASTER:戦闘能力 + RAND:5) * 2 + (ABL:MASTER:戦闘能力 + RAND:3) / 2
	相手ダイス数 = (相手強さ + RAND:4 + 1) * 2 + 相手強さ / 2 * RAND:3
	SELECTCASE 相手残機
		CASE IS > 2
			IF 慢心フラグ && 相手残機 > 2
				CALL COLORMESSAGE(@"%CALLNAME:OPPONENT% looks down on %PARSE("you")%.",C_YELLOW,1)
				相手ダイス数 -= 相手強さ / 2
			ENDIF
		CASE 2
			相手ダイス数 += 相手強さ / 2
		CASE 1
			相手ダイス数 += 相手強さ / 2 + 1
	ENDSELECT

	;ダイス面計算
	プレイヤーダイス面 = (ABL:MASTER:戦闘能力 + 1) * (6 + RAND:8) + RAND:(15 + TFLAG:幸運補正) * ABL:MASTER:戦闘能力 / 2
	相手ダイス面 = (相手強さ + 1) * (9 + RAND:7) + RAND:10 - (相手残機 - 1) * 相手強さ
	;修正値計算
	IF (相手強さ - ABL:MASTER:戦闘能力) < 0
		プレイヤー修正値 = (ABL:MASTER:戦闘能力 - 相手強さ + 3) * (-RAND:4 - 8) * ABL:MASTER:戦闘能力 / 2
		相手修正値 = (ABL:MASTER:戦闘能力 - 相手強さ + 3) * (RAND:4 + 8) * 相手強さ / 2
	ELSEIF (相手強さ - ABL:MASTER:戦闘能力) > 0
		プレイヤー修正値 = (相手強さ - ABL:MASTER:戦闘能力 + 3) * (RAND:4 + 8) * ABL:MASTER:戦闘能力 / 2
		相手修正値 = (相手強さ - ABL:MASTER:戦闘能力 + 3) * (-RAND:4 - 8) * 相手強さ / 2
	ELSE
		プレイヤー修正値 = 0
		相手修正値 = 0
	ENDIF
	
	;イベント用
	プレイヤー修正値 -= (HANDICAP_FIXED + TFLAG:GipsLV)
	プレイヤー修正値 -= RAND:(HANDICAP_RAND + TFLAG:GipsLV * ABL:MASTER:戦闘能力 + 1)
	;【言笑自若】の効果
	SIF GETBIT(CFLAG:OPPONENT:弾幕特能, 0)
		プレイヤーダイス数 -= (相手強さ / 2 + RAND:3 + 1) * RAND:2
	;【温柔敦厚】の効果
	SIF GETBIT(CFLAG:OPPONENT:弾幕特能, 1)
		プレイヤーダイス面 -= (相手強さ / 2 + 1) * RAND:4 + RAND:5
	;【勇気凛々】の効果
	SIF GETBIT(CFLAG:OPPONENT:弾幕特能, 2)
		相手ダイス数 += (相手強さ / 2 + RAND:3 + 2) * RAND:2
	;【豪放磊落】の効果
	SIF GETBIT(CFLAG:OPPONENT:弾幕特能, 3)
		相手ダイス面 += 相手強さ * 2 + 6 / (RAND:3 + 1)
	;【狷介孤高】の効果
	SIF GETBIT(CFLAG:OPPONENT:弾幕特能, 8)
		相手修正値 += 相手強さ * (RAND:5 + 5) + RAND:5
	;【幽愁暗恨】の効果
	SIF GETBIT(CFLAG:OPPONENT:弾幕特能, 9)
		プレイヤー修正値 -= 相手強さ * (RAND:4 + 4) + RAND:9
		
	;【霊力充填】の効果
	IF 霊力充填フラグ
		Charge = 相手強さ / 2 + 1
		プレイヤーダイス数 += Charge
		CALL COLORMESSAGE(@"【Full Spirit Power】activates, dice number increased by {Charge}!",C_RED,1)
		Charge = (相手強さ / 2 + 1) * 2
		プレイヤーダイス面 += Charge
		CALL COLORMESSAGE(@"【Full Spirit Power】activates, dice value increased by {Charge}!",C_RED,1)
		Charge = 相手強さ * (RAND:3 + 2)
		プレイヤー修正値 += Charge
		CALL COLORMESSAGE(@"【Full Spirit Power】activates, correction value increased by {Charge}!",C_RED,1)
		霊力充填フラグ = 0
	ENDIF

	;【国士無双の薬】使用中　残りHP増大・ダイス面に補正
	SIF TCVAR:MASTER:国士無双の薬
		プレイヤーダイス面 += MAX(RAND:(MAX((TCVAR:MASTER:国士無双の薬), 1) * 2), 1)

	;戦闘バフの効果（プレイヤーと対戦相手両方）
	SIF TCVAR:MASTER:エンチャント
		プレイヤー修正値 += ABL:MASTER:戦闘能力 * RAND:3 + RAND:3
	SIF TCVAR:OPPONENT:エンチャント
		相手修正値 += 相手強さ * RAND:3 + RAND:3
	SIF TCVAR:MASTER:加護
		相手修正値 -= ABL:MASTER:戦闘能力 * RAND:3 + RAND:3
	SIF TCVAR:OPPONENT:加護
		プレイヤー修正値 -= 相手強さ * RAND:3 + RAND:3


	;弾幕勝負補助アイテムの効果
	CALL DANMAKU_ITEM_EFFECT(持ち込みアイテム)
	プレイヤーダイス面 += RESULT:0
	プレイヤーダイス数 += RESULT:1
	;念のためリセット
	VARSET RESULT
	プレイヤーダイス数 = MAX(プレイヤーダイス数, 1)
	相手ダイス数 = MAX(相手ダイス数, 1)
	プレイヤーダイス面 = MAX(プレイヤーダイス面, 1)
	相手ダイス面 = MAX(相手ダイス面, 1)
		
	PRINTL 
	PRINTFORML -----------------------------------------------------
	PRINTFORML TURN:{ターン数}
	PRINTFORML -----------------------------------------------------
	PRINTL 
	
	CALL DICE_PRINT(プレイヤーダイス数, プレイヤーダイス面, プレイヤー修正値, 相手ダイス数, 相手ダイス面, 相手修正値, "確定前")
	PRINTFORMW The dice of fate have been cast!
	;イベント用
	IF (HANDICAP_FIXED || HANDICAP_RAND)
		CALL KOJO_MESSAGE_SEND("DANMAKU", 相手残機, OPPONENT, ENEMY, -1, "ハンデ")
	ENDIF
	;ダイス値計算
	プレイヤーダイス値 = DICE_VALUE(プレイヤーダイス数, プレイヤーダイス面, プレイヤー修正値)
	相手ダイス値 = DICE_VALUE(相手ダイス数, 相手ダイス面, 相手修正値)
	LOCAL = 相手ダイス値 - プレイヤーダイス値
	
	;持ち込みアイテム【バレットゴーレム】の効果　基本ダイス差を0.8倍に
	SIF LOCAL && 持ち込みアイテム == 854
		TIMES LOCAL, 0.8
	
	CALL DICE_PRINT(プレイヤーダイス値, 0, 0, 相手ダイス数, 相手ダイス面, 相手修正値, "確定後")
	$BEFORE_BATTLE
	PRINTL 
	PRINTFORML Remaining HP: {残りHP}    Bombs: {残りボム回数}   Bomb Power: Min:{B_Min}, Max:{B_Max}   Lives: {プレイヤー残機}
	
	IF 持ち込みアイテム
		SETCOLOR 0x666666
		PRINTFORML   （Carried item: %ITEMNAME_TR(持ち込みアイテム)% - %DANMAKU_ITEM_EXPLANATION(持ち込みアイテム)%.）
	ENDIF

	IF TCVAR:MASTER:国士無双の薬
		SETCOLOR 0x666666
		PRINTFORML   （Ongoing effect: Peerless Patriot's Elixir　　... Dice value slightly improved.）
	ENDIF
	
	IF TCVAR:MASTER:エンチャント
		SETCOLOR 0x666666
		PRINTFORML   （Buff: Enchantment　　... Dice value correction Up）
	ENDIF
	IF TCVAR:MASTER:加護
		SETCOLOR 0x666666
		PRINTFORML   （Buff: Divine Protection　　... Opponent dice value correction Down）
	ENDIF
	IF TCVAR:OPPONENT:エンチャント
		SETCOLOR 0xc86666
		PRINTFORML   （Opponent buff: Enchantment　　... Opponent dice value correction Up）
	ENDIF
	IF TCVAR:OPPONENT:加護
		SETCOLOR 0xc86666
		PRINTFORML   （Opponent buff: Divine Protection　　... Dice value correction Down）
	ENDIF
	RESETCOLOR
	
	CALL ASK_M("Focus on dodging", 1, "Attack", 1)
	SELECTCASE RESULT
		CASE 0
			;【霊力充填】の効果
			IF TALENT:MASTER:霊力充填 == 1
				霊力充填フラグ = 1
				CALL COLORMESSAGE(@"%CALLNAME:MASTER%'s【Full Sprit Power】Activates!",C_RED,2)
			ENDIF
			PRINTFORML %PARSE("You focus")% on dodging the Danmaku...
			PRINTFORMW ・・・
			PRINTL 
			CALL DICE_PRINT(プレイヤーダイス値, 相手ダイス値, 0, 0, 0, 0, "勝負")
			PRINTW 
			;【バレットゴーレム】の効果  回避選択時はさらに0.9倍（トータル0.72倍程度に）
			IF LOCAL && 持ち込みアイテム == 855
				TIMES LOCAL, 0.9
				PRINTFORML The Golem shields against the Danmaku...!
			ENDIF
			$UseEpitaph
			;【グレイズ上手】の効果
			IF TALENT:MASTER:グレイズ上手 == 1
				IF ABL:MASTER:戦闘能力 < 3
					LOCAL = (LOCAL * 2) / (ABL:MASTER:戦闘能力 + 2)
				ELSE
					LOCAL = (LOCAL * 3) / (ABL:MASTER:戦闘能力 + 3)
				ENDIF
			ENDIF
			;addition custom code
			CALL Add_SeedOfUnknownForm("danmaku", OPPONENT, LOCAL)
			SIF RESULT > 0
				LOCAL = RESULT
			;--------------------
			DamageThreshold = MAX(ABL:MASTER:戦闘能力 / 2, 1)
			IF LOCAL <= 20 * DamageThreshold
				PRINTFORML %PARSE("You take")% almost no damage.
				SIF LOCAL > 0
					残りHP -= LOCAL * 4
			ELSEIF LOCAL <= 50 * DamageThreshold
				PRINTFORML %PARSE("You take")% little damage.
				残りHP -= LOCAL * 5
			ELSEIF LOCAL <= 150 * DamageThreshold
				PRINTFORML %PARSE("You take")% a lot of damage.
				残りHP -= LOCAL * 6
			ELSE
				PRINTFORML %PARSE("You take")% considerable amount of damage.
				残りHP = 残りHP - MAXBASE:MASTER:体力 / 2 + 1
			ENDIF
			PRINTFORMW Remaining HP: {残りHP}
			IF 残りHP <= 0
				Pichun = 1
			ENDIF
		CASE 1
			;【乾坤一擲】の効果
			IF GETBIT(CFLAG:OPPONENT:弾幕特能, 7)
				IF RAND:100 + 1 < 10 + 相手強さ * 2
					LOCAL:1 = 相手強さ * (7 + RAND:5) * (相手強さ / 2 + 1) + RAND:7
					相手ダイス値 += LOCAL:1
					SETCOLOR C_RED
						PRINTFORML %CALLNAME:OPPONENT%'s【All-In】activates!
						PRINTFORMW %CALLNAME:OPPONENT%'s Dice Value increases by +{LOCAL:1}!
					RESETCOLOR
					;口上呼び出し
					CALL KOJO_MESSAGE_SEND("DANMAKU", 相手残機, OPPONENT, ENEMY, -1, "乾坤一擲")
					LOCAL = 相手ダイス値 - プレイヤーダイス値
				ENDIF
			ENDIF
			IF 残りボム回数 >= 1
				PRINTFORML Use a bomb?
				CALL ASK_M("Use it", 1, "Do not use it", 1)
				SELECTCASE RESULT
					CASE 0
						CALL UseBomb
						BombPower = RESULT
						プレイヤーダイス値 += BombPower
						PRINTFORMW Dice value increased to {プレイヤーダイス値}!
						残りボム回数 --
						LOCAL = 相手ダイス値 - プレイヤーダイス値
					CASE 1
						
				ENDSELECT
			ENDIF
			PRINTFORML %PARSE("You decide")% to fight!
			PRINTFORMW ・・・
			PRINTL 
			CALL DICE_PRINT(プレイヤーダイス値, 相手ダイス値, 0, 0, 0, 0, "勝負")
			IF (持ち込みアイテム == 870 ||持ち込みアイテム == 871) && LOCAL > 0 && BASE:MASTER:TSP >= 500
				;qol edit, don't waste tsp when you think you don't have to
				PRINTFORML Use TSP to dodge?
				CALL ASK_YN
				SIF RESULT
					GOTO SKIP_TSP
				SETBGCOLOR 1, 24, 60
				CALL COLORMESSAGE(TEXTR("I see it! Your movements! No matter what you do, I can say 『I've seen through it』!/......... I can read the 『Trajectory』of your lines.........『The trajectory of your movements in the future』...!"), 0xFF1493, 2, 1)
				PRINTFORML %CALLNAME:MASTER% predicted the shots with 『Epitaph』!
				CALL RECOVER(MASTER, -500, "TSP")
				CALL COLORMESSAGE(@"TSP Remaining:{BASE:MASTER:TSP}", C_RED, 2)
				CALL ASK_M("Focus on avoiding",1,"『KING CRIMSON』！(TSP1500)",持ち込みアイテム == 871 && BASE:MASTER:TSP >= 1500)
				SELECTCASE RESULT
					CASE 0
						PRINTFORML %CALLNAME:MASTER% switched to evasion!
						RESETBGCOLOR
						GOTO UseEpitaph
					CASE 1
						CLEARLINE 1
						PRINTL 
						SETCOLOR 0xFF1493
						LOCALS = Within『King Crimson』's ability, this world's time disappears, and no one remembers the movements they made within this segment of time!/『The clouds in the sky don't realize they've broken apart!』 『A flame that's extinguished doesn't even realize the moment it went out!』/Only the『Results』remain!! All that remains in this world are the『Results』!!!/In the world where time has been eliminated, all『Movement』becomes pointless!!!
						PRINTFORML %TEXTR(LOCALS)%
						PRINTFORMW This is my『King Crimson』's ability! I『Erased Time』and leapt past it......!!
						RESETCOLOR
						RESETBGCOLOR
						CALL RECOVER(MASTER, -1500, "TSP")
						PRINTFORML The Danmaku scatters behind %CALLNAME:MASTER%――
						IF RAND:3 + 4 < 相手強さ
							PRINTFORMW %BREAKENG(@"The subsequent counterattack from an impossible angle by %CALLNAME:MASTER% was evaded by %CALLNAME:OPPONENT%, who sensed it by the number of blood drops %HE_SHE(OPPONENT)% dripped!")%
							LOCAL = 0
							
							;addition custom code
							CALL KOJO_MESSAGE_SEND("DANMAKU", 相手残機, OPPONENT, ENEMY, -1, "エピタフ失敗")
						ELSE
							PRINTFORMW %BREAKENG(@"No sooner than %HIS_HER(OPPONENT)% eyes %SPLIT_G("widen in astonishment:shoot up in surprise")%, %CALLNAME:OPPONENT% is met with an inexplicable counterattack from the blind spot!")%
							LOCAL = -100
							
							;addition custom code
							CALL KOJO_MESSAGE_SEND("DANMAKU", 相手残機, OPPONENT, ENEMY, -1, "エピタフ成功")
						ENDIF
				ENDSELECT
				$SKIP_TSP
			ENDIF
			PRINTW 
			IF LOCAL > 0
				IF 残りボム回数 >= 1
					PRINTFORML %PARSE("You are")% cornered and %PARSE("decide")% to use a bomb!
					CALL UseBomb
					BombPower = RESULT

					SETCOLOR C_RED
					PRINTFORM Difference in dice value: {LOCAL} 
					IF LOCAL <= BombPower
						PRINTFORML ≦ {BombPower}
						PRINTFORMW %PARSE("Your")% bomb cancels %CALLNAME:OPPONENT%'s danmaku!
						LOCAL = 0
					ELSE
						PRINTFORMW ＞ {BombPower}
					ENDIF
					RESETCOLOR
					残りボム回数 --
				ENDIF
			ELSEIF LOCAL < 0
				;【怪力乱神】の効果
				IF GETBIT(CFLAG:OPPONENT:弾幕特能, 4) && (-LOCAL) < 相手強さ * 10
					SETCOLOR C_RED
						PRINTFORML %CALLNAME:OPPONENT%'s【Anomalies, Strength, Disorder, and Spirits】activates!
						PRINTFORMW The danmaku %PARSE("you")% fired at %CALLNAME:OPPONENT% disappear just before connecting!
						;口上呼び出し
						CALL KOJO_MESSAGE_SEND("DANMAKU", 相手残機, OPPONENT, ENEMY, -1, "怪力乱神")
						LOCAL = 0
					RESETCOLOR
				ENDIF
			ENDIF
			IF LOCAL < 0
				CALL COLORMESSAGE(@"A hit!",C_YELLOW,1)
				相手残機 --
				;口上呼び出し
				CALL KOJO_MESSAGE_SEND("DANMAKU", 相手残機, OPPONENT, ENEMY, -1, "被弾")
				PRINTFORMW Remaining opponent lives: {相手残機}
			ELSEIF LOCAL == 0
				PRINTFORMW Neither side can land a blow, so the round restarts.
			ELSE
				Pichun = 1
			ENDIF
		CASE 2

	ENDSELECT
	IF Pichun
		PRINTFORML ＼Pichuuun／
		PRINTFORML %PARSE("You were")% hit...
		RESULT:8 = (プレイヤー残機-1) ;addition custom code
		CALL KOJO_MESSAGE_SEND("DANMAKU", 相手残機, OPPONENT, ENEMY, -1, "Pichun") ;addition custom code
		プレイヤー残機 --
		残りHP = MAXBASE:MASTER:体力
		残りボム回数 = 2
		PRINTFORMW Remaining lives: {プレイヤー残機}
		;【ルナシューター】の効果
		SIF TALENT:MASTER:ルナシューター == 1
			残りボム回数 ++
		;【残忍酷薄】の効果
		IF GETBIT(CFLAG:OPPONENT:弾幕特能, 6) && プレイヤー残機 > 0
			CALL COLORMESSAGE(@"%CALLNAME:OPPONENT%'s【Merciless】activates!",C_RED,2)
			;【三粒の天滴】の効果
			IF ITEM:三粒の天滴 && !RAND:2
				PRINTFORMW The Frog God's blessing disables【Merciless】!
			ELSE
				;口上呼び出し
				CALL KOJO_MESSAGE_SEND("DANMAKU", 相手残機, OPPONENT, ENEMY, -1, "残忍酷薄")
				残りボム回数 --
			ENDIF
		ENDIF
	ENDIF
	Pichun = 0
	ターン数 ++
WEND

PRINTL 
IF プレイヤー残機 <= 0
	PRINTFORMW %PARSE("You")% lost...
	CALL COLORMESSAGE(@"Loser!",C_RED,2,1)
ELSEIF 相手残機 <= 0
	PRINTFORML %PARSE("You")% won against %CALLNAME:OPPONENT%!
	CALL COLORMESSAGE(@"Life prevails!",C_GREEN,2,1)
	CALL DANMAKU_GIPS_REWARD
	;addition custom code
	SIF nInitLives == プレイヤー残機
		RESULT:9 = 1
	SIF nInitBombs == 残りボム回数
		RESULT:10 = 1
	;--------------------
ENDIF
TFLAG:GipsLV = 0
PRINTL 

;楽譜入手処理
IF (RAND:(100) < 30)
	RESULT = 楽譜入手_弾幕勝負(OPPONENT)
	SIF RESULT != -1
		PRINTL
ENDIF

RETURN 相手残機


@DICE_PRINT(ARG:0, ARG:1, ARG:2, ARG:3, ARG:4, ARG:5, ARGS)
IF ARGS == "確定前"
	PRINTFORM %CALLNAME:MASTER%: 
	SETCOLOR C_GREEN
	PRINTFORM {ARG:0}d{ARG:1}
	IF ARG:2 > 0
		SETCOLOR C_RED
		PRINTFORM +{ARG:2}
	ELSEIF ARG:2 < 0
		SETCOLOR C_BLUE
		PRINTFORM {ARG:2}
	ENDIF
	RESETCOLOR
	PRINTFORM (Min:{ARG:0 + ARG:2}, Max:{ARG:0 * ARG:1 + ARG:2})  VS  
	SETCOLOR C_GREEN
	PRINTFORM {ARG:3}d{ARG:4}
	IF ARG:5 > 0
		SETCOLOR C_RED
		PRINTFORM +{ARG:5}
	ELSEIF ARG:5 < 0
		SETCOLOR C_BLUE
		PRINTFORM {ARG:5}
	ENDIF
	RESETCOLOR
	PRINTFORM (Min:{ARG:3 + ARG:5}, Max:{ARG:3 * ARG:4 + ARG:5}) 
	PRINTFORML :%CALLNAME:TARGET%
;ARG:1,2は使わない
ELSEIF ARGS == "確定後"
	PRINTFORM %CALLNAME:MASTER%: 
	SETCOLOR C_GREEN
	PRINTFORM {ARG:0}  
	RESETCOLOR
	PRINTFORM VS  
	SETCOLOR C_GREEN
	PRINTFORM {ARG:3}d{ARG:4}
	IF ARG:5 > 0
		SETCOLOR C_RED
		PRINTFORM +{ARG:5}
	ELSEIF ARG:5 < 0
		SETCOLOR C_BLUE
		PRINTFORM {ARG:5}
	ENDIF
	RESETCOLOR
	PRINTFORM (Min:{ARG:3 + ARG:5}, Max:{ARG:3 * ARG:4 + ARG:5}) 
	PRINTFORML :%CALLNAME:TARGET%
;ARG:2,3,4,5は使わない
ELSEIF ARGS == "勝負"
	PRINTFORM %CALLNAME:MASTER%: 
	SETCOLOR C_GREEN
	PRINTFORM {ARG:0}  
	RESETCOLOR
	PRINTFORM VS  
	SETCOLOR C_GREEN
	PRINTFORM {ARG:1} 
	RESETCOLOR
	PRINTFORML :%CALLNAME:TARGET%
ENDIF


@DICE_VALUE(ARG:0, ARG:1, ARG:2)
#FUNCTION
;初期化しないとダメ
LOCAL:1 = 0
FOR LOCAL, 0, ARG:0
	LOCAL:1 += RAND:(ARG:1) + 1
NEXT

RETURNF LOCAL:1 + ARG:2


@SHOW_DANMAKU_RULE
PRINTFORML The basic idea is to roll dice and get the highest number.
PRINTFORMW Both sides start with 3 lives, with the Combat ability determining how good the dice will be.
PRINTFORMW At the start of each round the player can choose to dodge or attack (the player can only see their own dice value here).
PRINTFORMW If the player avoids, HP will go down instead of lives.
PRINTFORMW If the player's HP reaches 0, a life is lost. Once the player runs out of lives the game ends.
PRINTFORMW If the player chooses to attack and they get a lower dice value then the opponent, the player loses a life.
PRINTFORMW Only by rolling higher than the opponent can their lives go down.
;残機0ならまだプレイできるじゃんとか言わない
PRINTFORMW The player wins if the opponent's lives go down to 0 before the player's.
PRINTFORMW Further, the player can also use bombs to increase their dice value, use it in critical situations.
PRINTFORMW When the player loses a die roll, a bomb is automatically used.
PRINTFORMW Once the player loses a life the bombs are set to 2. Losing a life before using up all bombs is a waste.


@UseBomb
#DIM BombPower
BombPower = ABL:MASTER:戦闘能力 * (7 + RAND:5) * (ABL:MASTER:戦闘能力 / 2 + 1) + RAND:7

CALL COLORMESSAGE(@"Bomb power: {BombPower}",C_YELLOW,1)
;【ルナシューター】の効果
IF TALENT:MASTER:ルナシューター == 1
	BombPower = BombPower * (RAND:3 + 2)
	CALL COLORMESSAGE(@"Due to【Luna Shooter】, the bomb power is increased to {BombPower}!",C_YELLOW,2)
ENDIF
;【三粒の天滴】の効果
IF ITEM:三粒の天滴
	;LOCAL = MAX(RAND:750, 400)
	LOCAL = BombPower / 10
	BombPower += LOCAL
	CALL COLORMESSAGE(@"Suddenly, countless frogs start pouring out from your pockets!",C_YELLOW,1)
	;bug fix custom code - show proper increase
	CALL COLORMESSAGE(@"Bomb power increased to {BombPower}!",C_YELLOW,1)
ENDIF
RETURN BombPower

@DANMAKU_ITEM_USE
#DIM 持ち込みアイテム
#DIMS 効果テキスト
持ち込みアイテム = 0
DRAWLINE
PRINTFORML Select which Danmaku assistance item to use.
DRAWLINE
FOR LOCAL,850, 900
	SIF !DANMAKU_ITEM_AVAILABLE(LOCAL)
		CONTINUE
	PRINTFORML [{LOCAL}] - %ITEMNAME_TR(LOCAL)% 
NEXT
DRAWLINE
PRINTFORML [999] - Cancel
INPUT
IF !DANMAKU_ITEM_AVAILABLE(RESULT)
	RETURN 0
ENDIF

持ち込みアイテム = RESULT
PRINTFORML Use %ITEMNAME_TR(持ち込みアイテム)%?
PRINTFORML %DANMAKU_ITEM_EXPLANATION(持ち込みアイテム)%.
CALL ASK_YN
IF !RESULT
	;先に進む
ELSE
	RESTART
ENDIF
ITEM:持ち込みアイテム -= 1
RETURN 持ち込みアイテム

@DANMAKU_ITEM_EXPLANATION(ARG)
#FUNCTIONS
#DIMS 効果テキスト
SELECTCASE ARG
	CASE 850
		効果テキスト = Improves dice rolls a bit
	CASE 851
		効果テキスト = Increases the number of dice a bit
	CASE 852
		効果テキスト = Improves dice rolls
	CASE 853
		効果テキスト = Increases the number of dice
	CASE 854
		効果テキスト = Dice will roll better, but fewer dice
	CASE 855
		効果テキスト = Slightly reduces damage taken
	CASE 870
		効果テキスト = Even after failing at dice, dodge at the cost of 500 TSP
	CASE 871
		効果テキスト = Cheat ability, invalidates bullets and reduced the opponent's life at the cost of TSP
	CASEELSE
		効果テキスト = 
ENDSELECT
RETURNF 効果テキスト

@DANMAKU_ITEM_EFFECT(ARG)
#DIM Dnum
#DIM Dface
Dnum = 0
Dface = 0
SELECTCASE ARG
	CASE 850	;パワフルドラッグ
		Dface = MAX(RAND:((ABL:MASTER:戦闘能力) + 3), 2)
	CASE 851	;アイシクルボム
		Dnum = MAX(RAND:((ABL:MASTER:戦闘能力) + 1), 1)
	CASE 852	;太乙の火種
		Dface = MAX(RAND:((ABL:MASTER:戦闘能力 + 1) * 2), 4)
	CASE 853	;魅惑のエサ
		Dnum = MAX(RAND:((ABL:MASTER:戦闘能力 + 1) * 2), 3)
	CASE 854	;賢者の石
		Dface = MAX(RAND:((ABL:MASTER:戦闘能力 + 1) * 3), 6)
		Dnum = MAX(RAND:((ABL:MASTER:戦闘能力 + 1) * 2), 5)
	CASE 855	;バレットゴーレム
	CASE 870	;エピタフ
	CASE 871	;キンクリ
ENDSELECT
RETURN Dface, Dnum

@DANMAKU_ITEM_AVAILABLE(ARG)
#FUNCTION
SIF !INRANGE(ARG, 850, 900)
	RETURNF 0
SIF DANMAKU_ITEM_EXPLANATION(ARG) == ""
	RETURNF 0
SIF !ITEM:ARG
	RETURNF 0
RETURNF 1

@DANMAKU_GipsEquip(ARGS)
IF ARGS == "椛口上" || ITEM:ルナシューター養成ギプス
	PRINTFORML Having equipped the Lunar Shooter Cast, %PARSE("you fiddle")% with the buckle.
	CALL ASK_M("Never Mind", 1, "Easy",1, "Normal", 1, "Hard", 1, "Lunatic" , 1, "『Danger・Manual Entry』", 1 )
ELSE
	PRINTFORML Having equipped the Shooter Cast, %PARSE("you fiddle")% with the buckle.
	CALL ASK_M("Never Mind", 1, "Easy",1, "Normal", 1, "Hard", 1)
ENDIF
SELECTCASE RESULT
	CASE 0
		RETURN
	CASE 1
		TFLAG:GipsLV = 1
	CASE 2
		TFLAG:GipsLV = 3
	CASE 3
		TFLAG:GipsLV = 5
	CASE 4
		TFLAG:GipsLV = 10
	CASE 5
		PRINTFORML Please select desired load weight.
		PRINTL Reference load weights.　Easy:1　Normal:3　Hard:5　Lunatic:10
		PRINTL [0] [1] [3] [5] [10] [15] [30] [50] [100]
		INPUT
		TFLAG:GipsLV = MAX(RESULT, 0)
ENDSELECT

@DANMAKU_GIPS_REWARD
#DIM Reward
SIF TFLAG:GipsLV <= 0
	RETURN
PRINTFORML Due to the Shooter Cast, %PARSE("you gain")% additional experience.
Reward = MIN(15, TFLAG:GipsLV)
;SELECTCASE GipsLV
;	CASE IS >= 10
;		Reward = MIN(15, GipsLV)
;	CASE IS >= 5
;		Reward = 4
;	CASE IS >= 3
;		Reward = 2
;	CASEELSE
;		Reward = 1
;ENDSELECT
IF ABL:MASTER:戦闘能力 <= ABL:TARGET:戦闘能力
	IF TFLAG:GipsLV >= 10
		Reward *= 3
		CALL COLORMESSAGE(@"x3 Experience because the opponent was equal or higher level.",C_YELLOW,1)
	ELSE
		Reward *= 2
		CALL COLORMESSAGE(@"x2 Experience because the opponent was equal or higher level.",C_YELLOW,1)
	ENDIF
ENDIF
CALL AddEXP("戦闘経験" , MASTER, Reward)