;-------------------------------------------------
;女の子を物色
;日常系コマンド、レベル1
;-------------------------------------------------

@COM444
#DIMS YUKIZURI_RACE  ;生成するMOB娘の種類

IF FLAG:デート相手
	PRINTFORMW There is already an earth-shatteringly adorable girl nearby.
	RETURN -1
ENDIF

;com stuff custom code
IF CFLAG:RANDOM_CHARANUM:現在位置 > 0 && CFLAG:RANDOM_CHARANUM:現在位置 != SUKIMA()
	PRINTFORML Currently existing random girl will be removed.
	PRINTFORML Proceed?
	CALL ASK_YN
	SIF RESULT
		RETURN -1
ENDIF
;-------------------------

;潜伏モード中は不可
IF GETBIT(FLAG:潜伏,0)
	PRINTFORMW Now is not the time to be looking for that.
	RETURN -1
ENDIF

LOCAL = 0 ;com stuff custom code

$BUSSYOKU
;com custom code, kick the player out
IF !INRANGE(TIME,420,1140)
	PRINTFORML It's getting too late...
	GOTO END
ENDIF
IF BASE:MASTER:TSP < 50
	PRINTFORML Your Time Stop ability is about to run out...
	GOTO END
ENDIF
;------------------------------------

IF !FLAG:70
	TIME += 3
ELSE
	BASE:MASTER:TSP -= 10
ENDIF
PRINTFORML %PARSE("You are")% looking for a cute girl.
PRINTFORMW ... How about that one?
;Bugfix, moving this as canceling here messes things up
;SETBIT TFLAG:一日一回, 9
SELECTCASE CAN_NANPA(CFLAG:MASTER:現在位置)
	CASE 1
		YUKIZURI_RACE '= TEXTR("町娘/人妻")
	CASE 2
		YUKIZURI_RACE = 妖精
	CASE 3
		YUKIZURI_RACE = メイド妖精
	CASE 4
		;３日に一度販売員が出没
		IF ((GETTIME()/1000000000) % 100) % 3 == 1 && !RAND:7
			YUKIZURI_RACE = 販売員
		ELSE
			YUKIZURI_RACE = イナバ
		ENDIF
	CASE 5
		YUKIZURI_RACE = ゾンビフェアリー
	CASE 6
		YUKIZURI_RACE '= TEXTR("化け狐/化け猫")
	CASE 7
		YUKIZURI_RACE = 河童
	CASE 8
		YUKIZURI_RACE = 白狼天狗
	CASE 9
		IF !RAND:10
			YUKIZURI_RACE = 鬼
		ELSE
			YUKIZURI_RACE = ゾンビフェアリー
		ENDIF
	CASE 10
		YUKIZURI_RACE = 玉兎
	CASE 11
		YUKIZURI_RACE '= TEXTR("化け狸/茨たぬき")
ENDSELECT

;PRINTFORMW %YUKIZURI_RACE%


;PRINTFORMW %YUKIZURI_RACE%

;com stuff custom code
CALL Com_MobRaceSelect(YUKIZURI_RACE, LOCAL)
LOCALS '= RESULTS
SIF RESULTS == "none"
	LOCAL = -1
;--------------------------------------------
	
CALL MAKE_YUKIZURI(RANDOM_CHARANUM, YUKIZURI_RACE)
TEQUIP:RANDOM_CHARANUM:上半身着衣状況 = 1
$CHOICE
;Custom code
CALL CHARA_STATE_TR(RANDOM_CHARANUM)
IF CSTR:RANDOM_CHARANUM:処女喪失履歴 != ""
	DRAWLINE
	CALL COLORMESSAGE(@"Virginity loss history: %CSTR:RANDOM_CHARANUM:処女喪失履歴%",C_PINK,1)
	PRINTFORML 
ENDIF
;DEBUGPRINTFORML %CSTR:RANDOM_CHARANUM:モブ子画像文字列_服_通常%
CALL ASK_M("This one's good",1,"Look for another one",1,"Find the one from just now", TFLAG:直前モブ子マップ == CFLAG:MASTER:現在位置 /100,"Find one from earlier",1,"Stop the hunt",1, @"Current Race: %CAP_PROCESS(LOCALS, 5)%", LOCAL != -1) ;com stuff custom code, specify race

;com stuff custom code
IF LOCAL != -1 && RESULT == 5
	LOCAL = ROUND_INCREMENT(LOCAL, 0, 2)
	GOTO BUSSYOKU
ENDIF
;-----------------------------------------
IF RESULT == 4
	$END
	CFLAG:RANDOM_CHARANUM:現在位置 = SUKIMA()
	CFLAG:RANDOM_CHARANUM:着替え実行 = 0
	CFLAG:RANDOM_CHARANUM:出禁 = 2
	CFLAG:RANDOM_CHARANUM:デート中 = -1 ;com stuff, disable dating flag
	SIF TARGET == RANDOM_CHARANUM
		TARGET = 0
	RETURN -1
ENDIF
IF RESULT == 1
	TFLAG:直前モブ子マップ = CFLAG:MASTER:現在位置 /100
	SAVECHARA "TEMP_MOB", "直前に会った子", RANDOM_CHARANUM
	GOTO BUSSYOKU
ELSEIF RESULT == 3
	CALL LOAD_MOBGIRL(0)
	IF RESULT == -1
		;念のためまっさらにしておく
		CALL モブ子画像リセット(RANDOM_CHARANUM)
		DELCHARA RANDOM_CHARANUM
		ADDCHARA RANDOM_CHARANUM
		;com stuff, disable dating flag
		CFLAG:RANDOM_CHARANUM:出禁 = 2
		CFLAG:RANDOM_CHARANUM:デート中 = -1
		RETURN -1
	ENDIF
	CFLAG:RANDOM_CHARANUM:出禁 = 0
ELSEIF RESULT == 2
	CALL モブ子画像リセット(RANDOM_CHARANUM)
	DELCHARA RANDOM_CHARANUM
	LOADCHARA "TEMP_MOB"
	TFLAG:直前モブ子マップ = 0
	GOTO CHOICE
ENDIF
CFLAG:RANDOM_CHARANUM:現在位置 = CFLAG:MASTER:現在位置
CFLAG:RANDOM_CHARANUM:デート中 = MAIN_MAP

;com stuff
;IF CFLAG:MASTER:現在位置 / 100 == MAIN_MAP
	;拠点内で時間停止せずに物色した場合は、同行扱い
	;IF !FLAG:70
		CFLAG:RANDOM_CHARANUM:同行 = 180
		CFLAG:MASTER:同行 = 180
		SIF FLAG:70
			PRINTFORMW You resume the time, asking %CALLNAME:RANDOM_CHARANUM% on a date.
		FLAG:70 = 0
	;ENDIF
	CFLAG:RANDOM_CHARANUM:拠点来訪 = 1
	CFLAG:RANDOM_CHARANUM:遭遇位置 = 0
	;CFLAG:RANDOM_CHARANUM:自宅位置 = 0
	CFLAG:RANDOM_CHARANUM:来訪時間 = CSVCFLAG(RANDOM_CHARANUM,352)
	CFLAG:RANDOM_CHARANUM:帰宅時間 = CSVCFLAG(RANDOM_CHARANUM,353)
	CFLAG:RANDOM_CHARANUM:就寝時間 = CSVCFLAG(RANDOM_CHARANUM,354)
	CFLAG:RANDOM_CHARANUM:起床時間 = CSVCFLAG(RANDOM_CHARANUM,355)
;com stuff "fix" <- this causes crashes when taking girls outside, supposedly it should've been set elsewhere, but it never does
; ELSE
	; ;引っ越ししたりして定義しなおす場合もあるので、毎回確認
	; CFLAG:RANDOM_CHARANUM:拠点来訪 = 0
	; CFLAG:RANDOM_CHARANUM:遭遇位置 = CFLAG:MASTER:現在位置
	; ;CFLAG:RANDOM_CHARANUM:自宅位置 = CFLAG:MASTER:現在位置
	; CFLAG:RANDOM_CHARANUM:来訪時間 = 0
	; CFLAG:RANDOM_CHARANUM:帰宅時間 = 0
	; CFLAG:RANDOM_CHARANUM:就寝時間 = 0
	; CFLAG:RANDOM_CHARANUM:起床時間 = 0
;ENDIF
CFLAG:RANDOM_CHARANUM:ディレイ = 30
TARGET = RANDOM_CHARANUM
;Bugfix, moved the assignment of flag here
SETBIT TFLAG:一日一回, 9


CALL Com_Date_Meet(RANDOM_CHARANUM) ;com custom code, handle first time meeting/greeting
SIF CFLAG:MASTER:現在位置 / 100 != MAIN_MAP
	CALL Com_DateRando(RANDOM_CHARANUM) ;com custom code, ask whether to date on the spot or bring her with you
RETURN 1


@COM_ABLE444
;一括管理
SIF GLOBAL_COMABLE(622)
	RETURN RESULT
SIF !CAN_NANPA(CFLAG:MASTER:現在位置)
	RETURN 0
SIF !INRANGE(TIME,420,1140)
	RETURN 0
SIF CFLAG:MASTER:睡眠 || CFLAG:MASTER:イタズラ || CFLAG:うふふ ;com custom code
	RETURN 0
;SIF !CFLAG:RANDOM_CHARANUM:出禁
;	RETURN 0
RETURN 1

@CAN_NANPA(ARG)
#FUNCTION

SIF ARG == GET_MAP_REPLACEMENT(202)
	RETURNF 1
SIF ARG == GET_MAP_REPLACEMENT(438)
	RETURNF 2
SIF ARG == GET_MAP_REPLACEMENT(309)
	RETURNF 3
SIF ARG == GET_MAP_REPLACEMENT(415)
	RETURNF 4
SIF ARG == GET_MAP_REPLACEMENT(416)
	RETURNF 4
SIF ARG == GET_MAP_REPLACEMENT(431)
	RETURNF 4
SIF ARG == GET_MAP_REPLACEMENT(434)
	RETURNF 4
SIF ARG == GET_MAP_REPLACEMENT(927)
	RETURNF 5
SIF ARG == GET_MAP_REPLACEMENT(928)
	RETURNF 5
SIF ARG == GET_MAP_REPLACEMENT(601)
	RETURNF 6
SIF ARG == GET_MAP_REPLACEMENT(704)
	RETURNF 7
SIF ARG == GET_MAP_REPLACEMENT(806)
	RETURNF 8
SIF ARG == GET_MAP_REPLACEMENT(938)
	RETURNF 9
SIF ARG == GET_MAP_REPLACEMENT(1003)
	RETURNF 10
SIF ARG == GET_MAP_REPLACEMENT(540)
	RETURNF 11

RETURNF 0


@PRINT_SAVED_GIRL_LIST
DRAWLINE
FOR LOCAL,0,MAXMOB
	IF FIND_CHARADATA(TOSTR(LOCAL))
		CHKCHARADATA RESULTS:0
		SIF MOBGIRL_PLACE:LOCAL == CFLAG:MASTER:現在位置 / 100
			SETCOLOR C_P_GREEN
		SIF MOBGIRL_PLACE:LOCAL == 90
			SETCOLOR C_PINK
		PRINTBUTTON @"[%RESULTS:0%]", LOCAL
		PRINTFORML 
	ELSE
		PRINTBUTTON "[None]", LOCAL
		PRINTFORML 
	ENDIF
	RESETCOLOR
NEXT
DRAWLINE
PRINTBUTTON "[Return]", 99

@LOAD_MOBGIRL(ARG)
CALL PRINT_SAVED_GIRL_LIST
INPUT
LOCAL = RESULT
SIF !INRANGE(LOCAL, 0, MAXMOB -1)
	RETURN -1
IF !ARG && MOBGIRL_PLACE:LOCAL != CFLAG:MASTER:現在位置 / 100
	IF MOBGIRL_PLACE:LOCAL == 90
		PRINTFORMW 店の外では会ってくれない…
	ELSE
		PRINTFORML {MOBGIRL_PLACE:LOCAL}
		PRINTFORMW They're not here.
	ENDIF
	RETURN -1
ELSEIF ARG && MOBGIRL_PLACE:LOCAL != 90
	PRINTFORMW 「はぁ！？　しょんなメスっこ、うちにゃーいねーぎゃ！」
	PRINTFORML 
	RETURN -1
ENDIF
IF FIND_CHARADATA(TOSTR(LOCAL))
	CALL モブ子画像リセット(RANDOM_CHARANUM)
	DELCHARA RANDOM_CHARANUM
	LOADCHARA TOSTR(LOCAL)
	PRINTFORML %PARSE("You find")% %CALLNAME:RANDOM_CHARANUM%.
	IF MOBGIRL_FP:LOCAL != ""
		モブ子一人称 = %MOBGIRL_FP:LOCAL%
		モブ子二人称 = %MOBGIRL_SP:LOCAL%
	ENDIF
	IF CSTR:RANDOM_CHARANUM:モブ子画像文字列_服_通常 == ""
		PRINTFORML Appearance not set. Please select a set.
		PRINTBUTTON "[City Girl] ", "町娘"
		PRINTBUTTON "[Married Woman] ", "人妻"
		PRINTBUTTON "[Fairy] ", "妖精"
		PRINTBUTTON "[Fairy Maid] ", "メイド妖精"
		PRINTBUTTON "[Zombie Fairy] ", "ゾンビフェアリー"
		PRINTBUTTON "[Kappa] ", "河童"
		PRINTBUTTON "[White Wolf Tengu] ", "白狼天狗"
		PRINTBUTTON "[Rabbit] ", "イナバ"
		PRINTBUTTON "[Moon Rabbit] ", "玉兎"
		PRINTBUTTON "[Oni] ", "鬼"
		PRINTBUTTON "[Tanuki] ", "化け狸"
		PRINTBUTTON "[Kitsune] ", "化け狐"
		PRINTBUTTON "[Bakeneko] ", "化け猫"
		PRINTBUTTON "[Random] ", "ランダム"
		INPUTS
		CALL SET_MOB_IMAGE_STR(RANDOM_CHARANUM, RESULTS)
	ENDIF
	;過去に合成されて裸画像の笑顔に[装飾_照れ]が乗ってない場合に追記
	SIF STRFIND(CSTR:RANDOM_CHARANUM:モブ子画像文字列_裸_笑顔, "装飾_照れ") == -1
		CSTR:RANDOM_CHARANUM:モブ子画像文字列_裸_笑顔 += "[装飾9:装飾_照れ][乗色:朱色]"

	;過去（ver4.760以前）に保存されて種族のTALENT設定が適合しない場合に修正を行う
	SELECTCASE CAN_NANPA(CFLAG:MASTER:現在位置)
		;里
		CASE 1
			;以前の194 人間が現varでは194 幽霊になっている
			IF TALENT:RANDOM_CHARANUM:幽霊
				TALENT:RANDOM_CHARANUM:幽霊 = 0
				TALENT:RANDOM_CHARANUM:人間 = 1
			ENDIF
		;霧の湖、紅魔館、および地底の妖精たち
		CASE 2, 3, 5
			;以前の190 妖精が現verでは190 人間になっている
			IF TALENT:RANDOM_CHARANUM:人間
				TALENT:RANDOM_CHARANUM:人間 = 0
				TALENT:RANDOM_CHARANUM:妖精 = 1
			ENDIF
		;竹林
		CASE 4
			;イナバ…妖獣が新設された
			SIF TALENT:RANDOM_CHARANUM:妖怪 != 6
				TALENT:RANDOM_CHARANUM:妖怪 = 6
		;冥界
		CASE 6
			;化け狸、狐、猫…妖獣が新設された
			SIF TALENT:RANDOM_CHARANUM:妖怪 != 6
				TALENT:RANDOM_CHARANUM:妖怪 = 6
		;妖怪の山山麓
		CASE 7
			;河童…河童が新設された
			SIF TALENT:RANDOM_CHARANUM:妖怪 != 4
				TALENT:RANDOM_CHARANUM:妖怪 = 4
		;妖怪の山山頂
		CASE 8
			;白狼天狗…以前の197 天狗が現verでは197 追加種族（巫女）になっている
			IF TALENT:RANDOM_CHARANUM:追加種族
				TALENT:RANDOM_CHARANUM:追加種族 = 0
				TALENT:RANDOM_CHARANUM:妖怪 = 5
			ENDIF
		;地底　鬼か妖精
		CASE 9
			;鬼…以前の195 鬼が現verでは195　付喪神になっている
			IF TALENT:RANDOM_CHARANUM:付喪神
				TALENT:RANDOM_CHARANUM:付喪神 = 0
				TALENT:RANDOM_CHARANUM:妖怪 = 2
			;妖精は同上
			ELSEIF TALENT:RANDOM_CHARANUM:人間
				TALENT:RANDOM_CHARANUM:人間 = 0
				TALENT:RANDOM_CHARANUM:妖精 = 1
			ENDIF
		;月　一応分けておく
		CASE 10
			;玉兎…妖獣が新設された
			SIF TALENT:RANDOM_CHARANUM:妖怪 != 6
				TALENT:RANDOM_CHARANUM:妖怪 = 6
	ENDSELECT

	IF FLAG:70	;時間停止中と風俗嬢は反応なし
	ELSEIF TALENT:RANDOM_CHARANUM:風俗嬢
	ELSEIF CFLAG:RANDOM_CHARANUM:負け犬 && (TALENT:RANDOM_CHARANUM:セフレ || TALENT:RANDOM_CHARANUM:思慕)
		PRINTFORMW %CALLNAME:RANDOM_CHARANUM% looks frustrated upon seeing %PARSE("you")%, but %HE_SHE(RANDOM_CHARANUM)% does squirm somewhat expectantly...
	ELSEIF CFLAG:RANDOM_CHARANUM:負け犬 && !TALENT:RANDOM_CHARANUM:恋慕 && !TALENT:RANDOM_CHARANUM:思慕 && !TALENT:RANDOM_CHARANUM:愛欲
		PRINTFORMW Upon seeing %PARSE("you")% again, %CALLNAME:RANDOM_CHARANUM%'s face stiffens.
	ELSEIF TALENT:RANDOM_CHARANUM:恋慕
		PRINTFORMW %CALLNAME:RANDOM_CHARANUM% happily runs over to %PARSE("you")% as soon as she sees %PARSE("you",5)%.
	ELSEIF TALENT:RANDOM_CHARANUM:愛欲
		PRINTFORMW %CALLNAME:RANDOM_CHARANUM% runs over to %PARSE("you")% as soon as she sees %PARSE("you",5)%, leaning against %PARSE("your",1)% arm...
	ELSEIF ABL:RANDOM_CHARANUM:親密 >= 5
		PRINTFORMW Upon seeing %PARSE("you")% again, %CALLNAME:RANDOM_CHARANUM% waves her hand.
	ENDIF
	CALL Qol_Info_Reset(RANDOM_CHARANUM) ;qol custom code
ELSE
	RETURN -1
ENDIF

@SAVE_MOBGIRL
#DIM CHOSEN_MOB
#DIMS MOBNAME

$LOOP
CALL PRINT_SAVED_GIRL_LIST
INPUT
SIF RESULT == 99
	RETURN -1
SIF !INRANGE(RESULT, 0, MAXMOB - 1)
	GOTO LOOP

CHOSEN_MOB = RESULT
;Custom code
LOCAL:150 = 0
IF FIND_CHARADATA(TOSTR(CHOSEN_MOB))
	LOCAL:150 = 1
	CHKCHARADATA RESULTS:0
	STRFIND RESULTS:0, "：" 
	SUBSTRING RESULTS:0, 0, RESULT
	MOBNAME = %RESULTS:0%
	PRINTFORML Forget %MOBNAME% and memorize %CALLNAME:RANDOM_CHARANUM% instead?
	CALL ASK_YN
	IF !RESULT
		PRINTFORM %PARSE("You")% heartlessly %PARSE("forget")% all about %MOBNAME% on the spot, 
	ELSE
		GOTO LOOP
	ENDIF
ENDIF
MOBGIRL_PLACE:CHOSEN_MOB = CFLAG:RANDOM_CHARANUM:よく行く地域
SIF TALENT:RANDOM_CHARANUM:風俗嬢
	MOBGIRL_PLACE:CHOSEN_MOB = 90
TALENT:RANDOM_CHARANUM:行きずり = 2
CFLAG:RANDOM_CHARANUM:モブ子セーブスロット = CHOSEN_MOB
MOBGIRL_FP:CHOSEN_MOB = %モブ子一人称%
MOBGIRL_SP:CHOSEN_MOB = %モブ子二人称%
SAVECHARA TOSTR(CHOSEN_MOB), CSTR:RANDOM_CHARANUM:モブ子プロフィール１, RANDOM_CHARANUM
IF LOCAL:150
	PRINTFORMW and %PARSE("memorize")% %CALLNAME:RANDOM_CHARANUM% instead\@TALENT:RANDOM_CHARANUM:風俗嬢?, marking her as a favorite#\@.
ELSE
	PRINTFORMW %PARSE("You memorize")% %CALLNAME:RANDOM_CHARANUM%\@TALENT:RANDOM_CHARANUM:風俗嬢?, marking her as a favorite#\@.
ENDIF


;-------------------------------------------------
;モブ子プロフィール２を更新する
;負け犬でなければ、その日の過ごし方の様子でプロフィール２を追記
;負け犬の場合は、陥落状態に合わせて更新
;-------------------------------------------------
@MOBGIRL_PROFILE2()
#DIM 回数

回数 = EXP:RANDOM_CHARANUM:Ｖ性交経験 + EXP:RANDOM_CHARANUM:Ａ性交経験

;負け犬でない場合は、性的関係の進展でテキストを構成
IF CFLAG:RANDOM_CHARANUM:負け犬 == 0 
	;出会って日が浅い
	;custom code, moved below
	; IF DAY:0 - PRINTDATEF_TO_DAY(CSTR:RANDOM_CHARANUM:モブ子プロフィール１) <= 30
		; CSTR:RANDOM_CHARANUM:モブ子プロフィール２ = 出会ったばかりの%CALLNAME:MASTER%
	; ELSE
		; CSTR:RANDOM_CHARANUM:モブ子プロフィール２ = %CALLNAME:MASTER%
	; ENDIF
	;にゅーぷらん
	IF TALENT:RANDOM_CHARANUM:風俗嬢
		SIF GROUPMATCH(TALENT:RANDOM_CHARANUM:風俗嬢, 1, 2) && CFLAG:RANDOM_CHARANUM:1001 == 3
			TALENT:RANDOM_CHARANUM:風俗嬢 += 2
		IF CFLAG:RANDOM_CHARANUM:1001  > 6
			LOCALS = She's grown so attached to %PARSE("you")% that she can no longer tell if %PARSE("you're",2)% having a serious relationship or whether she's still just doing her job.
		ELSEIF CFLAG:RANDOM_CHARANUM:1001  > 3
			LOCALS = She cares for %PARSE("you")% more than just a regular customer, and takes pleasure in being able to serve %PARSE("you",5)%.
		ELSE
			LOCALS = She sees %PARSE("you")% as a valued customer and is happy to receive repeat appointments.
		ENDIF
	;生解禁
	ELSEIF CFLAG:RANDOM_CHARANUM:生解禁
		IF TALENT:RANDOM_CHARANUM:人妻 == 1
			LOCALS = She has become so engrossed in this unhealthy relationship with %PARSE("you")% that she disregards her husband and family against her better judgment.
		ELSE
			LOCALS = She instinctively accepts %PARSE("you")% as a woman and even feels that she is ready to conceive a child should %PARSE("you",1)% creampie her.
		ENDIF
	;合意ックス
	ELSEIF GETBIT(CFLAG:RANDOM_CHARANUM:既成事実 , 1)
		IF TALENT:RANDOM_CHARANUM:人妻 == 1
			LOCALS = She seems to be using %PARSE("you")% as an outlet for her pent-up desires.
		ELSE
			LOCALS = Falling prey to %PARSE("your")% charms, she let %PARSE("you",5)% have %PARSE("your",1)% way with her body.
		ENDIF
	;睡眠姦(一応)
	ELSEIF EXP:RANDOM_CHARANUM:睡眠姦経験
		LOCALS = Falling for %PARSE("your")% schemes, she had %PARSE("you",5)% take advantage of her body while she was asleep.
	;合意なしックス
	ELSEIF CFLAG:RANDOM_CHARANUM:ヤラせちゃった
		IF TALENT:RANDOM_CHARANUM:人妻 == 1
			LOCALS = She can't seem to resist %PARSE("your")% charms that her husband seems to lack.
		ELSE
			LOCALS = One thing led to another, and %PARSE("you")% had a wild affair with her.
		ENDIF
	;不埒刻印
	ELSEIF MARK:RANDOM_CHARANUM:不埒刻印
		IF TALENT:RANDOM_CHARANUM:人妻 == 1
			LOCALS = She's sexually attracted to %PARSE("you")% even though she knows she shouldn't be.
		ELSE
			LOCALS = She may be starting to take a sexual interest in %PARSE("you")%.
		ENDIF
	;時姦刻印
	ELSEIF MARK:RANDOM_CHARANUM:時姦刻印
		LOCALS = Having caught %PARSE("your")% attention, her body received a thorough development with the help of %PARSE("your",1)% time stop abilities.
	;反発刻印 custom code, added
	ELSEIF MARK:RANDOM_CHARANUM:反発刻印
		LOCALS = She seems somewhat disappointed in %PARSE("you")% and doesn't want to see %PARSE("you",5)% anymore.
	ELSE
		LOCALS = %PARSE("You seem")% to be making a good impression on her.
	ENDIF
	;custom code - add qol date, original function doesn't work with translated date of meeting
	SIF DAY:0 - Qol_Info_Dates_Array:RANDOM_CHARANUM:Qol_Dates_Meet <= 30
		LOCALS = After just meeting %PARSE("you")% \@ DAY:0 - Qol_Info_Dates_Array:RANDOM_CHARANUM:Qol_Dates_Meet == 0 ? today # recently\@, %TOLOWER(LOCALS)%
	CSTR:RANDOM_CHARANUM:モブ子プロフィール２ += LOCALS

;負け犬の場合は、陥落強度に応じてテキストを構成
ELSE
	IF TALENT:RANDOM_CHARANUM:恋慕
		CSTR:RANDOM_CHARANUM:モブ子プロフィール２ = She no longer cares whether she wins or loses in Danmaku, instead she is now deeply in love with %PARSE("you")%.
	ELSEIF TALENT:RANDOM_CHARANUM:愛欲
		IF GETBIT(TALENT:MASTER:性別, 1)
			IF 回数 >= 75
				CSTR:RANDOM_CHARANUM:モブ子プロフィール２ = She has completely surrendered to %PARSE("your")% cock and is eagerly awaiting being fucked again.
			ELSE
				CSTR:RANDOM_CHARANUM:モブ子プロフィール２ = She has completely surrendered to %PARSE("you")% and is eagerly awaiting to be made love to again.
			ENDIF
		ELSE
			CSTR:RANDOM_CHARANUM:モブ子プロフィール２ = Gone is the bravado of the past, and now both her body and her soul have been dyed in %PARSE("your")% colors.
		ENDIF
	ELSEIF TALENT:RANDOM_CHARANUM:思慕
		CSTR:RANDOM_CHARANUM:モブ子プロフィール２ = She's starting to develop an interest in %PARSE("you")%, not only for %PARSE("your",1)% strength, but also for who %PARSE("you",1)% %PARSE("are")%.
	ELSEIF TALENT:RANDOM_CHARANUM:セフレ
		CSTR:RANDOM_CHARANUM:モブ子プロフィール２ = %PARSE("You've")% made her squeal so much, she has fallen into a state of wanton lust.
	ELSEIF MARK:RANDOM_CHARANUM:快楽刻印
		CSTR:RANDOM_CHARANUM:モブ子プロフィール２ = She may have been in denial, but her body was honestly responding to the pleasure.
	ENDIF
ENDIF

;-------------------------------------------------
;PRINT_DATE_Fの●年目 ■の★日の文字列を
;DAY:0（総日数）と同じ日数に読み替える関数
;ARGS CSTR:RANDOM_CHARANUM:モブ子プロフィール１を
;代入して、出会った日付を割り出す使用前提
;-------------------------------------------------
@PRINTDATEF_TO_DAY(ARGS)
#FUNCTION
#DIM YEAR
#DIM MOUTH
#DIMS 季
#DIM DAYS
#DIMS カット
#DIM TOTAL

SPLIT ARGS, "：", LOCALS
カット = %LOCALS:1%
SPLIT カット, "年目 ", LOCALS
カット = %LOCALS:1%
YEAR = TOINT(LOCALS:0)
SPLIT カット, "の月の", LOCALS
季 = %LOCALS:0%
カット = %LOCALS:1%
SPLIT カット, "日", LOCALS
DAYS = TOINT(LOCALS:0)
SELECTCASE 季
	CASE "春"
		MOUTH = 0
	CASE "夏"
		MOUTH = 31
	CASE "秋"
		MOUTH = 62
	CASE "冬"
		MOUTH = 93
ENDSELECT
TOTAL = (YEAR - 1) * 124 + MOUTH + DAYS
RETURNF TOTAL

