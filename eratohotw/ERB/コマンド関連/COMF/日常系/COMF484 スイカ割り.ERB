;スイカ割り
@COM484
#DIM CHARA
#DIM 同室人数
#DIM 参加可能人数

;雨天中止
IF GROUPMATCH(天候, 4, 5, 6, 7, 8, 10, 11, 12, 13) || GROUPMATCH(FLAG:異常気象, 9, 10)
	IF OUTROOF(CFLAG:MASTER:現在位置)
		PRINTFORMW With the weather this bad, let's consider doing it later...
		RETURN -1
	ENDIF
;料理がある
ELSEIF DISH_NAME != ""
	PRINTFORMW Probably should do something with the food on you first...
	RETURN -1
ENDIF

;参加可能人数チェック
参加可能人数 = 0
同室人数 = GET_TARGETNUM()
FOR LOCAL, 1, 同室人数 + 1
	CHARA = TARGET:LOCAL
	SIF !SHIRAHU(CHARA)
		CONTINUE
	SIF CFLAG:CHARA:行動 == 5
		CONTINUE
	SIF BASE:CHARA:気力 < 500 || BASE:CHARA:体力 < 500
		CONTINUE
	参加可能人数 ++
NEXT
;TARGETは実行判定で可能なはずなのでエラーチェック
SIF 参加可能人数 == 0
	THROW
SUIKA_PLAYERNUM = 0
CALL SUIKA_CHARASELECT(同室人数, 参加可能人数)
;キャンセル
SIF RESULT == -1
	RETURN -1

;武器選択
CALL SUIKA_WEAPONSELECT

;開幕地の文
DRAWLINE
IF GROUPMATCH(SUIKA_WEAPON, "打ち出の小槌", "制御棒") ;miracle mallet, control rod
	PRINTFORML You put on the blindfold and begin spinning on the spot wholeheartedly...
ELSE
	PRINTFORML You put on the blindfold and focus intently on spinning around the %SUIKA_WEAPON_TR(SUIKA_WEAPON)% thrust upon the ground...
ENDIF
;セットアップ
CALL SUIKA_SETUP
WAIT
;TARGETが参加していない場合はTARGETを変更しておく
IF FLAG:デート相手
	;参加しなくても最優先,そんなひどいことする？
	TARGET = FLAG:デート相手
ELSEIF !MATCH(SUIKA_PLAYER, TARGET)
	;一人目をターゲットに変更
	TARGET = SUIKA_PLAYER:1
ENDIF

TARGET = SUIKA_PLAYER:1
;ゲーム開始
CALL SUIKA_SMASHING
TFLAG:193 = RESULT

;ソース獲得
DOWNBASE:MASTER:体力 = 200
DOWNBASE:MASTER:気力 = 300
FOR LOCAL, 1, SUIKA_PLAYERNUM + 1
	CHARA = SUIKA_PLAYER:LOCAL
	DOWNBASE:CHARA:気力 += 100
	DOWNBASE:CHARA:体力 += 50
	SELECTCASE TFLAG:193
	CASE 0
		SOURCE:CHARA:歓楽 = 1500 + ABL:CHARA:親密 * 100
		SOURCE:CHARA:受動 = 100 + 50 * ABL:CHARA:従順
		SOURCE:CHARA:征服 = 200 + 80 * ABL:CHARA:サドっ気
	CASE 1, 2
		SOURCE:CHARA:歓楽 = 2500 + ABL:CHARA:親密 * 100
		SOURCE:CHARA:受動 = 200 + 60 * ABL:CHARA:従順
		SOURCE:CHARA:征服 = 400 + 120 * ABL:CHARA:サドっ気
	CASE -1
		IF SUIKA_LIAR:LOCAL || CHARA == [[正邪]]
			SOURCE:CHARA:歓楽 = 1500 + ABL:CHARA:親密 * 100
			SOURCE:CHARA:征服 = 400 + 120 * ABL:CHARA:サドっ気
		ELSE
			SOURCE:CHARA:歓楽 = 800 + ABL:CHARA:親密 * 100
			SOURCE:CHARA:受動 = 100 + 50 * ABL:CHARA:従順
			SOURCE:CHARA:征服 = 50 + 20 * ABL:CHARA:サドっ気
		ENDIF
	ENDSELECT
	IF TFLAG:193 >= 0
		SIF CHARA == TARGET
			TFLAG:信頼度管理 = 1 + TFLAG:193
		IF TFLAG:193 == 2
			TIMES SOURCE:CHARA:歓楽, 1.20
			TIMES SOURCE:CHARA:受動, 1.20
			TIMES SOURCE:CHARA:征服, 1.20
		ENDIF
	ENDIF
	;口上用に役職情報を渡しておく
	SIF CHARA == TARGET && (SUIKA_LIAR:LOCAL || CHARA == [[正邪]])
		TFLAG:194 = 1
	;◆口上
	CALL KOJO_MESSAGE_SEND("SUIKA", 0, CHARA, SUIKA_LIAR:LOCAL, 0, "結果")
	IF RESULTS != ""
		PRINTL 
		PRINTFORML %CALLNAME:CHARA%
		PRINTFORMW 「%RESULTS%」
		RESETCOLOR
	ENDIF
	;なるべく動かない
	TCVAR:CHARA:なるべく動かない = 1
NEXT

;料理生成
SIF TFLAG:193 >= 0
	CALL 料理生成("カットスイカ")
	
;結果記録
IF TFLAG:193 > 0
	SUIKA_GREAT ++
ELSEIF TFLAG:193 == 0
	SUIKA_SUCCESS ++
ENDIF

;時間経過
TIME += 60

;フラグをリセットしてあなたのフラグを立てる
VARSET SUIKA_PLAYER
CVARSET TCVAR, GETNUM(TCVAR, "スイカ割り参加"), 0
TCVAR:MASTER:スイカ割り参加 = 1

RETURN 1

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;スイカ割り
@COM_ABLE484
;時間停止中
SIF FLAG:時間停止
	RETURN 0
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(484)
	RETURN RESULT
;スイカがない
SIF !ITEM:スイカ
	RETURN 0
;アイマスクがない
SIF !ITEM:アイマスク
	RETURN 0
;夏じゃない
SIF GET_MONTH() != "夏の月"
	RETURN 0
;とりあえず訓練できる場所
SIF !戦闘訓練可(CFLAG:MASTER:現在位置)
	RETURN 0
;一日一回まで
SIF TCVAR:MASTER:スイカ割り参加
	RETURN 0
;対象がいない
SIF TARGET <= 0
	RETURN 0
;意識がない
SIF !SHIRAHU(TARGET)
	RETURN 0
;添い寝中
SIF CFLAG:MASTER:添い寝中
	RETURN 0
;うふふ中
SIF CFLAG:MASTER:うふふ
	RETURN 0
;仕事中
SIF CFLAG:行動 == 5
	RETURN 0
;体力・気力不足
SIF BASE:MASTER:気力 < 500 || BASE:MASTER:体力 < 500
	RETURN 0
;体力・気力不足
SIF BASE:TARGET:気力 < 500 || BASE:TARGET:体力 < 500
	RETURN 0
RETURN 1

;-------------------------------------------------
;スイカ割り：キャラクターセレクト関数
;ARG 同室人数
;-------------------------------------------------
@SUIKA_CHARASELECT(ARG, 参加可能人数)
#DIM CHARA
#DIM 参加可能人数
#DIM LINE
#DIM 参加要求
VARSET LOCAL
LINE = 7
DRAWLINE
;全員参加指示
SIF 参加要求
	SUIKA_PLAYERNUM = MIN(参加可能人数, SUIKA_MAXPLAYER)
PRINTFORML ●Select Participants（%TOFULL(TOSTR(SUIKA_PLAYERNUM))%／%TOFULL(TOSTR(SUIKA_MAXPLAYER))%）Bluffers：{SUIKA_LIARNUM()}
FOR LOCAL, 1, ARG + 1
	CHARA = TARGET:LOCAL
	SIF !SHIRAHU(CHARA)
		CONTINUE
	SIF CFLAG:CHARA:行動 == 5
		CONTINUE
	SIF BASE:CHARA:気力 < 500 || BASE:CHARA:体力 < 500
		CONTINUE
	;全員参加指示
	IF 参加要求
		TCVAR:CHARA:スイカ割り参加 = LOCAL:1 + 1
		SUIKA_PLAYER:(LOCAL:1 + 1) = CHARA
	ENDIF
	SIF TCVAR:CHARA:スイカ割り参加
		SETCOLOR C_AQUA
	PRINTFORM [{CHARA, 3}] %CALLNAME:CHARA, 18, LEFT%
	LOCAL:1 ++
	IF LOCAL:1 % 3 == 0
		PRINTL 
		LINE ++
	ENDIF
	RESETCOLOR
	SIF 参加要求 && LOCAL:1 == SUIKA_MAXPLAYER
		参加要求 = 0
NEXT
参加要求 = 0
IF LOCAL:1 % 3
	PRINTL 
	LINE ++
ENDIF
IF 参加可能人数 > SUIKA_MAXPLAYER
	PRINTL [902] Allow top 8 to participate
ELSE
	PRINTL [901] Include everyone
ENDIF
DRAWLINE
PRINTL [900] Apply　　　　　　　[903] Rules
PRINTL [999] Cancel
INPUT

;やめる
IF RESULT == 999
	;参加フラグをリセットする
	CVARSET TCVAR, GETNUM(TCVAR, "スイカ割り参加"), 0
	VARSET SUIKA_PLAYER
	RETURN -1
;決定
ELSEIF RESULT == 900
	IF INRANGE(SUIKA_PLAYERNUM, 1, SUIKA_MAXPLAYER)
		RETURN 1
	ELSE
		PRINTFORMW Choose 1～%TOFULL(TOSTR(SUIKA_MAXPLAYER))% %PRINT_PLUR("participant", SUIKA_MAXPLAYER)% to start the game!
		LINE ++
	ENDIF
;全員参加,上から８人を参加
ELSEIF RESULT == 901 || RESULT == 902
	;一旦リセット
	CVARSET TCVAR, GETNUM(TCVAR, "スイカ割り参加"), 0
	SUIKA_PLAYERNUM = 0
	VARSET SUIKA_PLAYER
	参加要求 = 1
;ルール説明
ELSEIF RESULT == 903
	CALL SUIKA_TUTORIAL
	RESTART
;キャラ選択
ELSEIF INRANGE(RESULT, 1, CHARANUM) && MATCH(TARGET, RESULT)
	;参加エントリー
	IF !TCVAR:RESULT:スイカ割り参加
		;上限
		IF SUIKA_PLAYERNUM >= SUIKA_MAXPLAYER
			PRINTW Max amount of participants reached!
			LINE ++
		ELSE
			FOR LOCAL, 1, SUIKA_MAXPLAYER + 1
				;空きに入れる
				IF !SUIKA_PLAYER:LOCAL
					SUIKA_PLAYER:LOCAL = RESULT
					TCVAR:RESULT:スイカ割り参加 = LOCAL
					BREAK
				ENDIF
			NEXT
			SUIKA_PLAYERNUM ++
		ENDIF
	;参加キャンセル
	ELSE
		FOR LOCAL, TCVAR:RESULT:スイカ割り参加, SUIKA_MAXPLAYER + 1
			;参加者はキャンセルする
			IF SUIKA_PLAYER:LOCAL == RESULT
				SUIKA_PLAYER:LOCAL = 0
			;番号が後の人がいるなら登録番号を一つ下げる
			ELSEIF SUIKA_PLAYER:LOCAL
				TCVAR:(SUIKA_PLAYER:LOCAL):スイカ割り参加 --
			ENDIF
		NEXT
		;その後シフトする
		FOR LOCAL, TCVAR:RESULT:スイカ割り参加, SUIKA_MAXPLAYER + 1
			SIF !SUIKA_PLAYER:LOCAL
				SWAP SUIKA_PLAYER:LOCAL, SUIKA_PLAYER:(LOCAL + 1)
		NEXT
		;参加状態を戻す
		TCVAR:RESULT:スイカ割り参加 = 0
		SUIKA_PLAYERNUM --
	ENDIF
ENDIF
CLEARLINE LINE
RESTART

;-------------------------------------------------
;スイカ割り：チュートリアル
;-------------------------------------------------
@SUIKA_TUTORIAL
#DIM MAXPAGE
#DIM PAGE
MAXPAGE = 4
SIF !PAGE
	PAGE = 1
LOCAL:9 = LINECOUNT ; custom code
DRAWLINE
PRINTFORML ●Rules({PAGE} / {MAXPAGE})
SELECTCASE PAGE
CASE 1
	PRINTFORML %BREAKENG(@"With a blindfold over your eyes and a striking weapon in your hands, you are tasked with splitting the Watermelon in half located somewhere in the playing field, while relying on guidance of Gensokyo girls.")%
	PRINTL 
	PRINTFORML However, there is a certain amount of Bluffers among the participants, depending on their numbers.
	PRINTFORML A character who is considered to be a Bluffer will give you false hints, preventing you from reaching your objective.
CASE 2
	PRINTFORML Since you can basically only strike once, proceed with caution as you try to figure out who is a Bluffer and which clues are true.
	PRINTL 
	PRINTL 
	PRINTFORML Trying to move out of bounds of the playing field will result in you bumping into a participant.
	PRINTFORML In that case, no movement is allowed.
CASE 3
	PRINTFORML If you position yourself perfectly when striking a Watermelon, it's considered a great success.
	PRINTFORML A close call is considered to be a regular success.
	PRINTL 
	PRINTFORML Some characters will give instructions such as "Right there!" and so on as you get closer.
	PRINTFORML If you are aiming for great success, you may want to consider those more carefully.
CASE 4
	PRINTFORML When the number of participants is odd (3 or more), the percentage of Bluffers increases and the game becomes more difficult.
	PRINTFORML If you find it too challenging, play with an even number of people.
	PRINTL 
	PRINTFORML If you can successfully split a Watermelon, you will receive a <Cut Watermelon> as a reward.
	PRINTFORML Everybody is encouraged to eat together with all the participants.
ENDSELECT
PRINTL 
PRINTFORML
;custom code
PRINTBUTTON @"%"Prev page",15,LEFT%", 2
PRINTFORM (Page {PAGE,2}/{MAXPAGE,2})%" "*6%
PRINTBUTTON @"%"Next page",15,LEFT%", 1
PRINTFORML
;-------------------
PRINTFORML [0] Return
INPUT
; IF RESULT == 1 && PAGE < MAXPAGE
	; PAGE ++
; ELSEIF RESULT == 2 && PAGE > 1
	; PAGE --
IF GROUPMATCH(RESULT, 1, 2) ;custom code
	PAGE = PAGE_HANDLE(RESULT, 2, PAGE, 1, MAXPAGE)
ELSE
	PAGE = 0
	RETURN
ENDIF
SIF LINECOUNT - LOCAL:9 > 0 ;custom code
	CLEARLINE LINECOUNT - LOCAL:9
RESTART

;-------------------------------------------------
;スイカ割り：武器選択関数
;-------------------------------------------------
@SUIKA_WEAPONSELECT
#DIM nTab = 16 ;custom code
DRAWLINE
PRINTFORML ●Choose the weapon you want to use.
PRINTFORML [1] - %SUIKA_WEAPON_TR("マイほうき"), nTab, LEFT%　The same broom you carry with you at all times. You're allowed to make one mistake with it.
PRINTFORML [2] - %SUIKA_WEAPON_TR("お祓い棒"), nTab, LEFT%　A strange sacred tool with a slight homing property. It always faces in the direction of the Watermelon at the start.
PRINTFORML [3] - %SUIKA_WEAPON_TR("木刀"), nTab, LEFT%　A simple weapon with excellent cutting power. A clean cut will increase the resources acquired. For advanced users.
PRINTFORML [4] - %SUIKA_WEAPON_TR("打ち出の小槌"), nTab, LEFT%　A mallet imbued with wishes. It splits the Watermelon cleanly even upon grazing it.
PRINTFORML [5] - %SUIKA_WEAPON_TR("制御棒"), nTab, LEFT%　A heavy rod that absorbs nuclear energy. Attracts the Watermelon at the start, reducing the distance.
INPUT
IF INRANGE(RESULT, 1, 5)
	SELECTCASE RESULT
	CASE 1
		SUIKA_WEAPON = マイほうき
	CASE 2
		SUIKA_WEAPON = お祓い棒
	CASE 3
		SUIKA_WEAPON = 木刀
	CASE 4
		SUIKA_WEAPON = 打ち出の小槌
	CASE 5
		SUIKA_WEAPON = 制御棒
	ENDSELECT
ELSE
	CLEARLINE 8
	RESTART
ENDIF

;-------------------------------------------------
;スイカ割り：ウソつきの人数
;-------------------------------------------------
@SUIKA_LIARNUM
#FUNCTION
RETURNF MAX((SUIKA_PLAYERNUM - 1) / 2, 0)

;-------------------------------------------------
;スイカ割り：二点間の距離
;ARG:1,座標１ ARG:2,座標２ ARG:3,0=合計距離,1=X軸距離,2=Y軸距離
;-------------------------------------------------
@SUIKA_DISTANCE(ARG:1, ARG:2, ARG:3)
#FUNCTION
LOCAL:1 = ARG:1 / 10
LOCAL:2 = ARG:2 / 10
LOCAL:3 = ARG:1 % 10
LOCAL:4 = ARG:2 % 10
LOCAL:5 = ABS(LOCAL:1 - LOCAL:2)
LOCAL:6 = ABS(LOCAL:3 - LOCAL:4)
IF ARG:3 == 1
	RETURNF LOCAL:5
ELSEIF ARG:3 == 2
	RETURNF LOCAL:6
ELSE
	RETURNF LOCAL:5 + LOCAL:6
ENDIF

;-------------------------------------------------
;スイカ割り：打撃する位置（正面位置）
;-------------------------------------------------
@SUIKA_AIMING
#FUNCTION
SELECTCASE SUIKA_DIRECTION 
CASE 2
	RETURNF SUIKA_MASTER_P - 1
CASE 4
	RETURNF SUIKA_MASTER_P - 10
CASE 6
	RETURNF SUIKA_MASTER_P + 10
CASE 8
	RETURNF SUIKA_MASTER_P + 1
ENDSELECT

;-------------------------------------------------
;スイカ割り：背面位置
;-------------------------------------------------
@SUIKA_BACK
#FUNCTION
SELECTCASE SUIKA_DIRECTION 
CASE 2
	RETURNF SUIKA_MASTER_P + 1
CASE 4
	RETURNF SUIKA_MASTER_P + 10
CASE 6
	RETURNF SUIKA_MASTER_P - 10
CASE 8
	RETURNF SUIKA_MASTER_P - 1
ENDSELECT

;-------------------------------------------------
;スイカ割り：位置が場外に出ている判定
;ARG,座標
;-------------------------------------------------
@SUIKA_OVER(ARG)
#FUNCTION
SIF ARG >= 100 || ARG % 10 == 0 || ARG / 10 == 0
	RETURNF 1

;-------------------------------------------------
;スイカ割り：スイカのある方向（客観方向）
;ARG,0=軸指定なし,1=X軸,2=Y軸
;-------------------------------------------------
@SUIKA_RADAR(ARG)
#FUNCTION
IF !ARG
	LOCAL = RAND:2 + 1
ELSE
	LOCAL = ARG
ENDIF

IF LOCAL == 1
	IF SUIKA_MASTER_P / 10 - SUIKA_SUIKA_P / 10 > 0
		RETURNF 4
	ELSEIF SUIKA_MASTER_P / 10 - SUIKA_SUIKA_P / 10 < 0
		RETURNF 6
	ELSE
		RETURNF 5
	ENDIF
ELSEIF LOCAL == 2
	IF SUIKA_MASTER_P % 10 - SUIKA_SUIKA_P % 10 > 0
		RETURNF 2
	ELSEIF SUIKA_MASTER_P % 10 - SUIKA_SUIKA_P % 10 < 0
		RETURNF 8
	ELSE
		RETURNF 5
	ENDIF
ENDIF

;-------------------------------------------------
;スイカ割り：セットアップ関数
;-------------------------------------------------
@SUIKA_SETUP
#DIM 最大距離
;挑戦回数＋１
SUIKA_CHALLENGE ++
;得点リセット
SUIKA_SCORE = 0
;スイカを失う
CALL LOST_ITEM("スイカ", 1, 1)
;お手つき許容回数
IF SUIKA_WEAPON == "マイほうき"
	SUIKA_OTETSUKI = 1
ELSE
	SUIKA_OTETSUKI = 0
ENDIF
;スイカの位置
SUIKA_SUIKA_P = (RAND:9 + 1) * 10 + RAND:9 + 1
;あなたの位置,スイカの位置と距離が6以上12以内,（制御棒なら距離8以内）
SUIKA_MASTER_P = 0
IF SUIKA_WEAPON == "制御棒"
	PRINTFORML 【Control Rod Effect】Whoosh-whoosh-whoosh...　The control rod pulls the Watermelon in!
	最大距離 = 8
ELSE
	最大距離 = 16
ENDIF
WHILE !SUIKA_MASTER_P || !INRANGE(SUIKA_DISTANCE(SUIKA_SUIKA_P, SUIKA_MASTER_P), 6, 最大距離)
	SUIKA_MASTER_P = (RAND:9 + 1) * 10 + RAND:9 + 1
WEND
;あなたの向き
SUIKA_DIRECTION = 5
IF SUIKA_WEAPON == "お祓い棒"
	WHILE SUIKA_DIRECTION % 2 != 0
		SUIKA_DIRECTION = SUIKA_RADAR()
	WEND
	PRINTFORML 【Purification Rod Effect】Drawn by the Purification Rod, your body turns in the direction of the Watermelon!
ELSE
	SUIKA_DIRECTION = RAND:4 * 2 + 2
ENDIF
;ウソつき役の設定
VARSET SUIKA_LIAR
SIF !SUIKA_LIARNUM()
	RETURN
CALL FISHER_YATES_SHAFFLE(SUIKA_PLAYERNUM)
FOR LOCAL:1, 0, SUIKA_LIARNUM()
	LOCAL = SHAFFLE_ARRAY:(LOCAL:1) + 1
	SUIKA_LIAR:LOCAL = 1
NEXT
[IF_DEBUG]
	DRAWLINE
	PRINTFORML SUIKA_SUIKA_P:{SUIKA_SUIKA_P}
	PRINTFORML SUIKA_MASTER_P:{SUIKA_MASTER_P}
	PRINTFORML SUIKA_DISTANCE:{SUIKA_DISTANCE(SUIKA_SUIKA_P, SUIKA_MASTER_P)}, {SUIKA_DISTANCE(SUIKA_SUIKA_P, SUIKA_MASTER_P, 1)}, {SUIKA_DISTANCE(SUIKA_SUIKA_P, SUIKA_MASTER_P, 2)}
	PRINTFORML SUIKA_DIRECTION:{SUIKA_DIRECTION}
	PRINTFORML SUIKA_AIMING:{SUIKA_AIMING()}
	FOR LOCAL, 1, SUIKA_PLAYERNUM + 1
		PRINTFORML SUIKA_PLAYER:{LOCAL} %CALLNAME:(SUIKA_PLAYER:LOCAL)% \@ SUIKA_LIAR:LOCAL ? ● # ○ \@
	NEXT
	PRINTFORML SUIKA_WEAPON:%SUIKA_WEAPON%
[ENDIF]

;-------------------------------------------------
;スイカ割り：ゲーム部分関数
;-------------------------------------------------
@SUIKA_SMASHING
#DIM CHARA
#DIM LINENUM
#DIM LINENUM_PLUS
CALL FISHER_YATES_SHAFFLE(8)

LINENUM_PLUS = 0
DRAWLINE
FOR LINENUM, 1, 9
	IF LINENUM % 2 == 1
		LOCAL = SHAFFLE_ARRAY:(LINENUM - 1) + 1
		CHARA = SUIKA_PLAYER:LOCAL
		RESETCOLOR ;custom code
		CALL SET_KOJO_COLOR(CHARA)
		PRINTPLAINFORM % CHARA ? CALLNAME:CHARA # "" , 46, LEFT%
	ELSE
		LOCAL = SHAFFLE_ARRAY:(LINENUM - 2) + 1
		CHARA = SUIKA_PLAYER:LOCAL
		RESETCOLOR ;custom code
		CALL SET_KOJO_COLOR(CHARA)
		CALL SUIKA_MESSAGE(CHARA, LOCAL, 1)
	ENDIF
	RESETCOLOR ;custom code
	SELECTCASE LINENUM
	CASE 2
		PRINTFORM 　　　　 [8] Step Forward　　　　　
	CASE 4
		PRINTFORM 　[4] Turn Left　　[6] Turn Right　
	CASE 6
		PRINTFORM 　　　 ＜＜[5] Strike!＞＞ 　　　　
	CASE 8
		PRINTFORM 　　　　　 [2] Step Back   　　　　
	CASEELSE
		PRINTFORM %" " * 32%
	ENDSELECT
	IF LINENUM % 2 == 1
		LOCAL = SHAFFLE_ARRAY:(LINENUM) + 1
		CHARA = SUIKA_PLAYER:LOCAL
		RESETCOLOR ;custom code
		CALL SET_KOJO_COLOR(CHARA)
		PRINTPLAINFORM % CHARA ? CALLNAME:CHARA # "" , 46, RIGHT%
	ELSE
		LOCAL = SHAFFLE_ARRAY:(LINENUM - 1) + 1
		CHARA = SUIKA_PLAYER:LOCAL
		RESETCOLOR ;custom code
		CALL SET_KOJO_COLOR(CHARA)
		CALL SUIKA_MESSAGE(CHARA, LOCAL, 2)
	ENDIF
	PRINTL 
NEXT
RESETCOLOR ;custom code
INPUT
DRAWLINE
SELECTCASE RESULT
CASE 5
	PRINTL 
	PRINTFORMW You strike with %SUIKA_WEAPON_TR(SUIKA_WEAPON)% in front of you with all your might!
	PRINTL 
	LINENUM_PLUS += 3
	IF SUIKA_DISTANCE(SUIKA_AIMING(), SUIKA_SUIKA_P) == 0
		IF SUIKA_WEAPON == "木刀"
			CALL COLORMESSAGE("SLAAAASH!!", C_ORANGE, 2, 3)
			RETURN 2
		ELSE
			CALL COLORMESSAGE("SMAAAASH!!", C_V_PINK, 2, 3)
			RETURN 1
		ENDIF
	ELSEIF SUIKA_DISTANCE(SUIKA_AIMING(), SUIKA_SUIKA_P) == 1
		IF SUIKA_WEAPON == "打ち出の小槌"
			CALL COLORMESSAGE("LUCKY STRIKE!", C_YELLOW, 2, 3)
			RETURN 1
		ELSE
			CALL COLORMESSAGE("HIT!", C_YELLOW, 2, 2)
			RETURN 0
		ENDIF
	ELSE
		CALL COLORMESSAGE("MISS!", C_CREAM, 2, 2)
		IF SUIKA_OTETSUKI
			PRINTL 
			PRINTFORMW ...But the rules allow it since you're using the Broom.
			SUIKA_OTETSUKI --
			LINENUM_PLUS += 4
		ELSE
			RETURN -1
		ENDIF
	ENDIF
CASE 4
	PRINTFORML .........
	PRINTFORMW You turn left.
	SELECTCASE SUIKA_DIRECTION
	CASE 2
		SUIKA_DIRECTION = 6
	CASE 4
		SUIKA_DIRECTION = 2
	CASE 6
		SUIKA_DIRECTION = 8
	CASE 8
		SUIKA_DIRECTION = 4
	ENDSELECT
	LINENUM_PLUS += 2
CASE 6
	PRINTFORML .........
	PRINTFORMW You turn right.
	SELECTCASE SUIKA_DIRECTION
	CASE 2
		SUIKA_DIRECTION = 4
	CASE 4
		SUIKA_DIRECTION = 8
	CASE 6
		SUIKA_DIRECTION = 2
	CASE 8
		SUIKA_DIRECTION = 6
	ENDSELECT
	LINENUM_PLUS += 2
CASE 2
	PRINTFORML .........
	IF SUIKA_OVER(SUIKA_BACK())
		CALL SUIKA_COLLISION
		LINENUM_PLUS += RESULT
	ELSE
		PRINTFORMW You step backward.
		SUIKA_MASTER_P = SUIKA_BACK()
		LINENUM_PLUS += 2
	ENDIF
CASEELSE
	PRINTFORML .........
	IF SUIKA_OVER(SUIKA_AIMING())
		CALL SUIKA_COLLISION
		LINENUM_PLUS += RESULT
	ELSE
		PRINTFORMW You step forward.
		SUIKA_MASTER_P = SUIKA_AIMING()
		LINENUM_PLUS += 2
	ENDIF
ENDSELECT
CLEARLINE 11 + LINENUM_PLUS
RESTART

;-------------------------------------------------
;スイカ割り：衝突メッセージ
;-------------------------------------------------
@SUIKA_COLLISION
#DIM LINE
LINE = 4
LOCAL = SUIKA_PLAYER:(RAND:SUIKA_PLAYERNUM + 1)
PRINTFORMW Thud!!!
PRINTL 
;◆口上
CALL KOJO_MESSAGE_SEND("SUIKA", 0, LOCAL, 0, 0, "衝突")
IF RESULTS != ""
	PRINTFORMW 「%RESULTS%」
	PRINTL 
	LINE += 2
ENDIF
RESETCOLOR
IF LOCAL == [[萃香]]
	PRINTFORMW That's not a Watermelon [suika], that's Suika [the drunk]!
ELSE
	PRINTFORMW You bump into %CALLNAME:LOCAL%...
ENDIF

RETURN LINE

;-------------------------------------------------
;スイカ割り：台詞関数１,どんな台詞を言うかの判定
;ARG,プレイヤー番号
;ARG:1,表示位置 1=LEFT,2=RIGHT
;-------------------------------------------------
@SUIKA_MESSAGE(CHARA, ARG, ARG:1)
#DIM CHARA
#DIM 客観方向
#DIMS 主観方向
#DIM ウソつき
#DIMS 正直な指示
#DIMS 指示
;非表示
IF !CHARA
	IF ARG:1 == 1
		PRINTPLAINFORM %"" , 46, LEFT%
	ELSE
		PRINTPLAINFORM %"" , 46, RIGHT%
	ENDIF
	RETURN
ENDIF

;完全一致
IF SUIKA_DISTANCE(SUIKA_AIMING(), SUIKA_SUIKA_P) == 0
	正直な指示 = そこだ！
;1マスずれ（2分の1）
ELSEIF SUIKA_DISTANCE(SUIKA_AIMING(), SUIKA_SUIKA_P) == 1 && !RAND:2
	正直な指示 = そこだ！
;重なっている（1マスずれの漏れ）
ELSEIF SUIKA_DISTANCE(SUIKA_MASTER_P, SUIKA_SUIKA_P) == 0
	正直な指示 = 少し後ろ
;それ以外
ELSE
	;近さ判定
	;LOCAL:1,距離について言う判定
	;LOCAL:2,「少し」を付けて言う判定
	IF SUIKA_DISTANCE(SUIKA_AIMING(), SUIKA_SUIKA_P) == 1
		LOCAL:1 = !RAND:2
		LOCAL:2 = 1
	ELSEIF SUIKA_DISTANCE(SUIKA_AIMING(), SUIKA_SUIKA_P) == 2
		LOCAL:1 = !RAND:2
		LOCAL:2 = !RAND:2
	ELSEIF SUIKA_DISTANCE(SUIKA_AIMING(), SUIKA_SUIKA_P) == 3
		LOCAL:1 = !RAND:3
		LOCAL:2 = !RAND:4
	ELSE
		LOCAL:1 = !RAND:4
		LOCAL:2 = 0
	ENDIF
	;距離のヒント
	IF LOCAL:1
		IF SUIKA_DISTANCE(SUIKA_AIMING(), SUIKA_SUIKA_P) <= 3
			正直な指示 = 近い
		ELSEIF SUIKA_DISTANCE(SUIKA_AIMING(), SUIKA_SUIKA_P) >= 8
			正直な指示 = かなり遠い
		ELSE
			正直な指示 = 遠い
		ENDIF
	;方向のヒント
	ELSE
		客観方向 = 5
		WHILE 客観方向 % 2 != 0
			客観方向 = SUIKA_RADAR()
		WEND
		IF 客観方向 == SUIKA_DIRECTION
			主観方向 = 前
		ELSE
			SELECTCASE SUIKA_DIRECTION
			CASE 2
				SELECTCASE 客観方向
				CASE 4
					主観方向 = 右
				CASE 6
					主観方向 = 左
				CASE 8
					主観方向 = 後ろ
				ENDSELECT
			CASE 4
				SELECTCASE 客観方向
				CASE 2
					主観方向 = 左
				CASE 6
					主観方向 = 後ろ
				CASE 8
					主観方向 = 右
				ENDSELECT
			CASE 6
				SELECTCASE 客観方向
				CASE 2
					主観方向 = 右
				CASE 4
					主観方向 = 後ろ
				CASE 8
					主観方向 = 左
				ENDSELECT
			CASE 8
				SELECTCASE 客観方向
				CASE 2
					主観方向 = 後ろ
				CASE 4
					主観方向 = 左
				CASE 6
					主観方向 = 右
				ENDSELECT
			ENDSELECT
		ENDIF
		SIF LOCAL:2
			主観方向 = 少し%主観方向%
		正直な指示 = %主観方向%
	ENDIF
ENDIF
;正邪は役職を無視して嘘をつく
IF SUIKA_LIAR:ARG || CHARA == [[正邪]]
	ウソつき = 1
ELSE
	ウソつき = 0
ENDIF
IF ウソつき
	SELECTCASE 正直な指示
	CASE "そこだ！"
		指示 =%TEXTR("少し後ろ/少し前/少し右/少し左")%
	CASE "近い"
		指示 = 遠い
	CASE "遠い"
		指示 = 近い
	CASE "かなり遠い"
		指示 = 遠い
	CASE "前"
		指示 = 後ろ
	CASE "少し前"
		指示 = %TEXTR("少し後ろ/そこだ！")%
	CASE "後ろ"
		指示 = 前
	CASE "少し後ろ"
		指示 = %TEXTR("少し前/そこだ！")%
	CASE "右"
		指示 = 左
	CASE "少し右"
		指示 = %TEXTR("少し左/そこだ！")%
	CASE "左"
		指示 = 右
	CASE "少し左"
		指示 = %TEXTR("少し右/そこだ！")%
	ENDSELECT
ELSE
	指示 = %正直な指示%
ENDIF
;◆口上
CALL KOJO_MESSAGE_SEND("SUIKA", 0, CHARA, 0, 0, 指示)
IF RESULTS != ""
	IF ARG:1 == 1
		PRINTPLAINFORM %"「" + RESULTS + "」", 46, LEFT%
	ELSE
		PRINTPLAINFORM %"「" + RESULTS + "」", 46, RIGHT%
	ENDIF
ELSE
	IF ARG:1 == 1
		PRINTPLAINFORM %SUIKA_MESSAGE_2(CHARA, ARG, 指示), 46, LEFT%
	ELSE
		PRINTPLAINFORM %SUIKA_MESSAGE_2(CHARA, ARG, 指示), 46, RIGHT%
	ENDIF
ENDIF

;-------------------------------------------------
;スイカ割り：台詞関数２,実際に喋る台詞
;ARG,プレイヤー番号
;-------------------------------------------------
@SUIKA_MESSAGE_2(CHARA, ARG, 指示)
#FUNCTIONS
#DIM CHARA
#DIMS 指示
;汎用台詞
SELECTCASE 指示
CASE "そこだ！"
	SELECTCASE CFLAG:CHARA:口調
	;～です系
	CASE 1
		RESULTS = %TEXTR("That's the spot!/Give it your all!/You're in the clear!")%
	;～だよ系
	CASE 2
		RESULTS = %TEXTR("There!/Now!/Now's the time!")%
	;～だぞ系
	CASE 3
		RESULTS = %TEXTR("Right there!/Do it now!/Smash!")%
	;～のじゃ系
	CASE 4
		RESULTS = %TEXTR("That'd be the spot, I say!/Strike it while it's hot, dear!/Split it!")%
	;無口系
	CASE 5
		RESULTS = %TEXTR("There...!/Now...!/That's it...!")%
	;～のよ系
	CASEELSE
		RESULTS = %TEXTR("That's it!/Strike now!/Hit it!")%
	ENDSELECT
CASE "近い"
	SELECTCASE CFLAG:CHARA:口調
	CASE 1
		RESULTS = %TEXTR("It's quite close./We're getting close./Almost there.")%
	CASE 2
		RESULTS = %TEXTR("Close! It's so close!/You're getting close!/Nearly there!")%
	CASE 3
		RESULTS = %TEXTR("Gotta be very close!/It's coming right up!/Just a bit more!")%
	CASE 4
		RESULTS = %TEXTR("Yer closin' in!/It's gonna turn up soon!/It's just a tap away!")%
	CASE 5
		RESULTS = %TEXTR("...It's pretty close./...Getting there./...A little more.")%
	CASEELSE
		RESULTS = %TEXTR("You're nearing it./It's coming up./A bit closer.")%
	ENDSELECT
CASE "遠い"
	SELECTCASE CFLAG:CHARA:口調
	CASE 1
		RESULTS = %TEXTR("Still far away./Keep going.")%
	CASE 2
		RESULTS = %TEXTR("It's still a long way!/Nope, still more to come～")%
	CASE 3
		RESULTS = %TEXTR("Gotta keep going, it ain't there yet!/Still got ways to go!")%
	CASE 4
		RESULTS = %TEXTR("Keep goin', ya ain't there yet./Slow and steady wins the race, keep goin'!")%
	CASE 5
		RESULTS = %TEXTR("...Far./...Still far away.")%
	CASEELSE
		RESULTS = %TEXTR("You're far from it yet./Not quite.")%
	ENDSELECT
CASE "かなり遠い"
	SELECTCASE CFLAG:CHARA:口調
	CASE 1
		RESULTS = %TEXTR("You are still a long way off./Nowhere near, I'm afraid./It's a long way away.")%
	CASE 2
		RESULTS = %TEXTR("It's totally far away～!/Still far, far away～!")%
	CASE 3
		RESULTS = %TEXTR("Pretty dang far, if I gotta say./You're a long way off!")%
	CASE 4
		RESULTS = %TEXTR("It's far off in the distance, I say!/Yer not anywhere near!")%
	CASE 5
		RESULTS = %TEXTR("...Quite far./...Not close at all.")%
	CASEELSE
		RESULTS = %TEXTR("It's pretty far./That's a long way from here.")%
	ENDSELECT
CASE "前"
	SELECTCASE CFLAG:CHARA:口調
	CASE 1
		RESULTS = %TEXTR("It's ahead./Keep going, please./You're in the right direction.")%
	CASE 2
		RESULTS = %TEXTR("It's in front of you!/That's it, keep going!/Go on, go on!")%
	CASE 3
		RESULTS = %TEXTR("Yup, it's in front!/Directly ahead!/You got the right direction!")%
	CASE 4
		RESULTS = %TEXTR("In front of ya./Keep goin' that way./C'mon, go on!")%
	CASE 5
		RESULTS = %TEXTR("...Onwards./...Keep going that way./...That's the direction.")%
	CASEELSE
		RESULTS = %TEXTR("It's in front of you./Keep walking there./It's that way where you're headed.")%
	ENDSELECT
CASE "少し前"
	SELECTCASE CFLAG:CHARA:口調
	CASE 1
		RESULTS = %TEXTR("A little bit forward./A little ahead./One step forward is all it takes...")%
	CASE 2
		RESULTS = %TEXTR("Just a bit forward!/Just move forward a little!/One more step!")%
	CASE 3
		RESULTS = %TEXTR("Just keep going, it's right there!/Just a bit more forward!/Step forward just a bit!")%
	CASE 4
		RESULTS = %TEXTR("Slightly forward, I say./Just a tad forward./One step ahead.")%
	CASE 5
		RESULTS = %TEXTR("...Slightly ahead./...A bit more forward./...One more step.")%
	CASEELSE
		RESULTS = %TEXTR("Move a bit more forward./Just keep going forward, slowly./You might need to make just one more step.")%
	ENDSELECT
CASE "後ろ"
	SELECTCASE CFLAG:CHARA:口調
	CASE 1
		RESULTS = %TEXTR("It's behind you./You're facing away./It's the wrong side.")%
	CASE 2
		RESULTS = %TEXTR("Turn around!/Reverse, reverse!/Not that way～!")%
	CASE 3
		RESULTS = %TEXTR("It's right behind ya./Face backwards!/It ain't there!")%
	CASE 4
		RESULTS = %TEXTR("Behind ya!/Yer looking in the wrong direction!/Not there, it ain't!")%
	CASE 5
		RESULTS = %TEXTR("...Behind./...Turn around./...Wrong way.")%
	CASEELSE
		RESULTS = %TEXTR("Behind you./It's the other way./You won't find it there.")%
	ENDSELECT
CASE "少し後ろ"
	SELECTCASE CFLAG:CHARA:口調
	CASE 1
		RESULTS = %TEXTR("It's right behind you./Slightly back./Maybe step backwards a little...")%
	CASE 2
		RESULTS = %TEXTR("It's right behind you!/Turn around and it's right there!/A step away but backwards!")%
	CASE 3
		RESULTS = %TEXTR("Straight behind ya!/A lil' further back!/Take a step back!")%
	CASE 4
		RESULTS = %TEXTR("Slightly backwards, I say./A little further back./Just gotta turn back a little.")%
	CASE 5
		RESULTS = %TEXTR("...Slightly behind./...A little back./...Take a step back.")%
	CASEELSE
		RESULTS = %TEXTR("Must be right behind you./A little further back./A step back is all it takes.")%
	ENDSELECT
CASE "右"
	SELECTCASE CFLAG:CHARA:口調
	CASE 1
		RESULTS = %TEXTR("To the right./It's on the right～/Turn right please!")%
	CASE 2
		RESULTS = %TEXTR("Right!/It's on the right, on the right!/Go right!")%
	CASE 3
		RESULTS = %TEXTR("Yeah, gotta go right./Right./Go right.")%
	CASE 4
		RESULTS = %TEXTR("Yup, right./Move right./Right side!")%
	CASE 5
		RESULTS = %TEXTR("Right./...Right./...It's on the right.")%
	CASEELSE
		RESULTS = %TEXTR("Go right./To the right side./It's on the right from you.")%
	ENDSELECT
CASE "少し右"
	SELECTCASE CFLAG:CHARA:口調
	CASE 1
		RESULTS = %TEXTR("Slightly to the right./A little to the right, I think!")%
	CASE 2
		RESULTS = %TEXTR("Just a little to the right!/Go right, just a bit!")%
	CASE 3
		RESULTS = %TEXTR("Turn right a little./Gotta go right, c'mon.")%
	CASE 4
		RESULTS = %TEXTR("Still on the right side./Give it a lil' spin to the right./A tad to the right, yup.")%
	CASE 5
		RESULTS = %TEXTR("...A bit to the right./...Slightly right.")%
	CASEELSE
		RESULTS = %TEXTR("Somewhat to the right./A little to the right, I suppose.")%
	ENDSELECT
CASE "左"
	SELECTCASE CFLAG:CHARA:口調
	CASE 1
		RESULTS = %TEXTR("To the left./It's on the left～/Turn left please!")%
	CASE 2
		RESULTS = %TEXTR("Left!/It's on the left, on the left!/Go left!")%
	CASE 3
		RESULTS = %TEXTR("Yeah, gotta go left./Left./Go left.")%
	CASE 4
		RESULTS = %TEXTR("Yup, left./Move left./Left side!")%
	CASE 5
		RESULTS = %TEXTR("Left./...Left./...It's on the left.")%
	CASEELSE
		RESULTS = %TEXTR("Go left./To the left side./It's on the left from you.")%
	ENDSELECT
CASE "少し左"
	SELECTCASE CFLAG:CHARA:口調
	CASE 1
		RESULTS = %TEXTR("Slightly to the left./A little to the left, I think!")%
	CASE 2
		RESULTS = %TEXTR("Just a little to the left!/Go left, just a bit!")%
	CASE 3
		RESULTS = %TEXTR("Turn left a little./Gotta go left, c'mon.")%
	CASE 4
		RESULTS = %TEXTR("Still on the left side./Give it a lil' spin to the left./A tad to the left, yup.")%
	CASE 5
		RESULTS = %TEXTR("...A bit to the left./...Slightly left.")%
	CASEELSE
		RESULTS = %TEXTR("Somewhat to the left./A little to the left, I suppose.")%
	ENDSELECT
ENDSELECT
RETURNF "「" + RESULTS + "」"


