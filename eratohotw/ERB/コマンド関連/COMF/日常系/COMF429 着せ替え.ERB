;-------------------------------------------------
;着せ替え
;日常系コマンド、レベル1
;-------------------------------------------------
@COM429
#DIM C_ID
#DIM ID_着せ替え対象, キャラクタ数上限
#DIM 着せ替え可能人数
#DIM L_ID ;使用リストのID
CALL MAKE_CHARA_LIST("着せ替え可能キャラリスト", L_ID, ID_着せ替え対象, 着せ替え可能人数)
IF 着せ替え可能人数 <= 1
	PRINTFORMW %PARSE("You decide")% to change %PARSE("your",1)% clothes.
	CALL 服装着せ替え(MASTER)
	CALL CLOTHES_SETTING_TRAIN(MASTER)
	RETURN -1
ENDIF

;着替え対象の選択
;リスト作成時に可否判定やってるのでここでは必要ないです
PRINTFORML Here one's clothes can be changed
PRINTFORML Whose clothes should be changed?
PRINTFORML 
CALL SIMPLE_CHARA_LIST(ID_着せ替え対象, 着せ替え可能人数)
;キャンセルした
SIF RESULT == 999
	RETURN -1

;RESULTはすぐ腐るので
C_ID = ID_着せ替え対象:(RESULT)
SIF FLAG:時間停止 == 1 && !(C_ID == MASTER)
	CFLAG:C_ID:時間停止着せ替え = 1
CALL 服装着せ替え(C_ID)
CALL CLOTHES_SETTING_TRAIN(C_ID)
RETURN 0




;CLOTHES_SETTING_TRAINに靴脱ぎ処理を移動させたので
;ここでCLOTHES_SETTING_TRAINすると靴を脱いでしまい、靴設定の保存ができません
;呼び出し元でCLOTHES_SETTING_TRAINすること
@服装着せ替え(C_ID)
#DIM C_ID
CALL SHOW_CLOTHES(C_ID, "現在の衣装")
PRINTFORML Changing %CALLNAME:C_ID%'s clothes
PRINTFORML 
PRINTFORML [6] - Check clothes
PRINTFORML [5] - Favorites
SIF C_ID != MASTER ;custom code
PRINTFORML [4] - Change sleepwear
PRINTFORML [3] - Change casual wear
PRINTFORML [2] - Change clothes temporarily
PRINTFORML [1] - Underwear
PRINTFORML 
PRINTFORML [0] - Return
DO
	INPUT
	SELECTCASE RESULT
	CASE 6
		CALL SHOW_CHARA_CLOTHES(C_ID, 0)
	CASE 5
		CALL SHOW_CLOTHES(C_ID, "おきにいり衣装")
		PRINTFORML Changing %CALLNAME:C_ID%'s favorite clothes
		PRINTFORML 
		PRINTFORML [4] - Confirm current clothes as favorite
		PRINTFORML [3] - Change costume
		PRINTFORML [2] - Imitate another character's casual wear
		PRINTFORML [1] - Reset favorite
		PRINTFORML 
		PRINTFORML [0] - Never mind
		DO
			INPUT
			SELECTCASE RESULT
			CASE 4
				CALL DECIDE_CLOTHES(C_ID, "おきにいり衣装", "現在の衣装")
			CASE 3
				CALL COSPLAY_SET(C_ID, "おきにいり衣装")
			CASE 2
				CALL COSPLAY_CHARA(C_ID, "おきにいり衣装")
			CASE 1
				PRINTFORML Really reset clothes?
				PRINTFORML 
				PRINTFORML [1] - Reset
				PRINTFORML [0] - Don't
				DO
					INPUT
					SELECTCASE RESULT
					CASE 1
						CALL CTRL_CLOTHES_SET(C_ID, "おきにいり衣装のリセット")
					CASE 0
					CASEELSE
						REUSELASTLINE Invalid input
						CONTINUE
					ENDSELECT
					RESTART
				LOOP 1
			CASE 0
				RESTART
			CASEELSE
			ENDSELECT
			RESTART
		LOOP 1
	CASE 4
		SIF C_ID == MASTER
			RESTART
		CALL SHOW_CLOTHES(C_ID, "今日のパジャマ")
		PRINTFORML Changing %CALLNAME:C_ID%'s sleepwear
		PRINTFORML 
		PRINTFORML [5] - Have them sleep in whatever
		PRINTFORML [4] - Have them sleep in their favorite costume
		PRINTFORML [3] - Set their sleepwear
		PRINTFORML [2] - Have them sleep naked
		PRINTFORML [1] - Have them sleep in just underwear
		PRINTFORML 
		PRINTFORML [0] - Return
		DO
			INPUT
			SELECTCASE RESULT
			CASE 5
				CFLAG:C_ID:パジャマ指定 = 0
			CASE 4
				SIF CFLAG:C_ID:おきにいり衣装
					CALL CTRL_CLOTHES_SET(C_ID, "パジャマの変更_おきにいり衣装")
			CASE 3
				CALL COSPLAY_SELECT(C_ID, "パジャマ", "パジャマセット")
			CASE 2
				CALL CTRL_CLOTHES_SET(C_ID, "パジャマの変更_全裸")
			CASE 1
				CALL CTRL_CLOTHES_SET(C_ID, "パジャマの変更_パンイチ")
			CASE 0
			CASEELSE
				REUSELASTLINE Invalid input
				CONTINUE
			ENDSELECT
			RESTART
		LOOP 1
	CASE 3
		CALL SHOW_CLOTHES(C_ID, "今日の衣装")
		PRINTFORML Changing %CALLNAME:C_ID%'s casual wear
		PRINTFORML 
		PRINTFORML [4] - Confirm current clothes (will override favourites)
		PRINTFORML [3] - Change costume
		PRINTFORML [2] - Imitate another character's casual wear
		PRINTFORML [1] - Let them decide
		PRINTFORML 
		PRINTFORML [0] - Return
		DO
			INPUT
			SELECTCASE RESULT
			CASE 4
				CALL DECIDE_CLOTHES(C_ID, "普段着", "現在の衣装")
			CASE 3
				CALL COSPLAY_SET(C_ID, "普段着")
			CASE 2
				CALL COSPLAY_CHARA(C_ID, "普段着")
			CASE 1
				PRINTFORMW They will now wear whatever they feel like
				CFLAG:C_ID:基本服装セット = 0
			CASE 0
			ENDSELECT
			RESTART
		LOOP 1
	CASE 2
		;#;PRINTFORML CFLAG:C_ID:衣装一時変更{CFLAG:C_ID:衣装一時変更}
		CALL SHOW_CLOTHES(C_ID, "現在の衣装")
		PRINTFORML Changing %CALLNAME:C_ID%'s current clothes
		PRINTFORML 
		PRINTFORML [4] - Favorites
		PRINTFORML [3] - Change costume
		PRINTFORML [2] - Imitate another character's casual wear
		PRINTFORML [1] - Restore to daily default
		PRINTFORML 
		PRINTFORML [0] - Quit
		DO
			INPUT
			SELECTCASE RESULT
			CASE 4
				SIF !CFLAG:C_ID:おきにいり衣装
					RESTART
				CALL DECIDE_CLOTHES(C_ID, "現在の衣装", "おきにいり衣装")
			CASE 3
				CALL COSPLAY_SET(C_ID, "現在の衣装")
			CASE 2
				CALL COSPLAY_CHARA(C_ID, "現在の衣装")
			CASE 1
				PRINTFORMW %CALLNAME:C_ID% put on today's clothing
				CALL CTRL_CLOTHES_SET(C_ID, "現在衣装の変更_普段着")
			CASE 0
			ENDSELECT
			RESTART
		LOOP 1
	CASE 1
		PRINTFORML %CALLNAME:C_ID%'s underwear
		;Custom code
		PRINTFORML Current underwear: %CLOTHES_TR(GET_STR(C_ID, "下半身下着_ずらし可能", EQUIP:C_ID:下半身下着２, "名前"),1)%
		SELECTCASE CFLAG:C_ID:指定パンツ
		CASE 0
			PRINTFORML 　Current set underwear: N/A
		CASE 100
			PRINTFORML 　Current set underwear: Nothing
		CASEELSE
			;Custom code
			PRINTFORML 　Current set underwear: %CLOTHES_TR(GET_STR(C_ID, "下半身下着_ずらし可能", CFLAG:C_ID:指定パンツ, "名前"),1)%
		ENDSELECT
		PRINTFORML 
		PRINTFORML 
		PRINTFORML [2] - Set these as favorites
		PRINTFORML [1] - Reset favorite underwear
		PRINTFORML 
		PRINTFORML [0] - Return
		DO
			INPUT
			SELECTCASE RESULT
			CASE 2
				PRINTFORMW Current underwear set as favorite
				CFLAG:C_ID:指定パンツ = EQUIP:C_ID:下半身下着２
				SIF !CFLAG:C_ID:指定パンツ
					CFLAG:C_ID:指定パンツ = 100
			CASE 1
				PRINTFORMW Favorite underwear is now N/A
				CFLAG:C_ID:指定パンツ = 0
			CASE 0
			ENDSELECT
			RESTART
		LOOP 1
	CASE 0
		RETURN
	CASEELSE
		REUSELASTLINE Invalid input
		CONTINUE
	ENDSELECT
	RESTART
LOOP 1



;模倣させる対象キャラの選択
@COSPLAY_CHARA(C_ID, TYPE)
#DIM  C_ID
#DIMS TYPE
#DIM  COS_CHARA
;Custom code
PRINTFORML Changing %CALLNAME:C_ID%'s %CLOTHES_TYPE_TR(TYPE)%
PRINTFORML Whose style should be imitated?
CALL SHOW_CHARA_LIST("普段着模倣可能キャラリスト", "体力気力")
COS_CHARA = RESULT
IF GROUPMATCH(COS_CHARA, 0, 999)
	PRINTFORMW Changing clothes was canceled
	RETURN
ELSEIF COS_CHARA == C_ID
	SELECTCASE TYPE
	CASE "おきにいり衣装"
		PRINTFORMW Changing clothes was canceled
	CASE "普段着"
		PRINTFORMW They will now wear whatever they feel like
		CFLAG:C_ID:基本服装セット = 0
	CASE "現在の衣装"
		PRINTFORMW They will wear their everyday clothes
		CALL CTRL_CLOTHES_SET(C_ID, "現在衣装の変更_普段着")
	ENDSELECT
	RETURN
ENDIF
CALL DECIDE_CLOTHES(C_ID, TYPE, "キャラ普段着", COS_CHARA)
SIF RESULT
	RESTART


;衣装セットのカテゴリ選択
@COSPLAY_SET(C_ID, TYPE)
#DIM  C_ID
#DIM  SET_ID
#DIMS TYPE
#DIMS CATEGORY
CALL SHOW_CLOTHES(C_ID, TYPE)
;Custom code
PRINTFORML Changing %CALLNAME:C_ID%'s %CLOTHES_TYPE_TR(TYPE)%
PRINTFORML Select costume type
PRINTFORML 
PRINTFORML [3] - Normal costumes
PRINTFORML [2] - Cosplay costumes
PRINTFORML [1] - Special character costumes
PRINTFORML 
PRINTFORML [0] - Return
DO
	INPUT
	SELECTCASE RESULT
	CASE 3
		CATEGORY '= "基本衣装セット"
	CASE 2
		CATEGORY '= "コスプレ衣装セット"
	CASE 1
		CATEGORY '= "キャラ特殊衣装セット"
	CASE 0
		RETURN
	CASEELSE
		REUSELASTLINE Invalid input
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1 
CALL COSPLAY_SELECT(C_ID, TYPE, CATEGORY)



;個々の衣装セットの選択
@COSPLAY_SELECT(C_ID, TYPE, CATEGORY)
#DIM  C_ID
#DIM  SET_ID
#DIMS TYPE
#DIMS CATEGORY
;Custom code
PRINTFORML Changing %CALLNAME:C_ID%'s %CLOTHES_TYPE_TR(TYPE)% (%CLOTHES_CAT_TR(CATEGORY)%)
PRINTFORML Please select which costume to use
PRINTFORML 
;Custom code
CALL CLASS_LIST_TR("衣装セット", CATEGORY)
PRINTFORML 
PRINTFORML [0] - Return
DO
	INPUT
	SET_ID = RESULT
	SELECTCASE SET_ID
	CASE 0
		RETURN
	CASEELSE
		SIF GET_INT(0, "衣装セット" , SET_ID, CATEGORY)
			BREAK
		REUSELASTLINE Invalid input
		CONTINUE
	ENDSELECT
LOOP 1 
CALL DECIDE_CLOTHES(C_ID, TYPE, "衣装セット", SET_ID)
SIF RESULT
	RESTART



;衣装決定の最終確認
@DECIDE_CLOTHES(C_ID, COS_SAVE, COS_LOAD, SET_ID)
#DIM  C_ID     ;設定するキャラ
#DIMS COS_SAVE ;衣装の保存先
#DIMS COS_LOAD ;衣装の参照元
#DIM  SET_ID   ;参照衣装等のID
;#; PRINTFORML C_ID{C_ID}, COS_SAVE%COS_SAVE%, COS_LOAD%COS_LOAD%, SET_ID{SET_ID}
PRINTFORMW Confirm choice
CALL SHOW_CLOTHES(C_ID, COS_LOAD, SET_ID)
PRINTFORML 
PRINTFORML Is this costume acceptable?
PRINTFORML 
PRINTFORML [1] - Confirm
PRINTFORML [0] - Return
PRINTFORML 
PRINTFORML [9] - Quit
DO
	INPUT
	SELECTCASE RESULT
	CASE 1
		BREAK
	CASE 0
		RETURN 1
	CASE 9
		RETURN
	CASEELSE
		REUSELASTLINE Invalid input
		CONTINUE
	ENDSELECT
LOOP 1


SELECTCASE COS_LOAD
CASE "現在の衣装"
	SELECTCASE COS_SAVE
	CASE "普段着"
		PRINTFORMW Changed into everyday wear.
		CFLAG:C_ID:基本服装セット = 100
		CALL CTRL_CLOTHES_SET(C_ID, "現在衣装の保存_おきにいり")
	CASE "おきにいり衣装"
		PRINTFORMW Currently equipped clothes set as favorite.
		CALL CTRL_CLOTHES_SET(C_ID, "現在衣装の保存_おきにいり")
	ENDSELECT
	
CASE "衣装セット"
	PRINTFORMW %CALLNAME:C_ID%'s %CLOTHES_TYPE_TR(COS_SAVE)% has been set to %LISTING_TR(GET_STR(C_ID, "衣装セット", SET_ID, "名前"))%.
	SELECTCASE COS_SAVE
	CASE "現在の衣装"
		CALL CTRL_CLOTHES_SET(C_ID, "現在衣装の変更_衣装セット", SET_ID)
	CASE "普段着"
		CALL CTRL_CLOTHES_SET(C_ID, "普段着の変更_衣装セット", SET_ID)
	CASE "おきにいり衣装"
		CALL CTRL_CLOTHES_SET(C_ID, "おきにいり衣装の変更_衣装セット", SET_ID)
	CASE "パジャマ"
		CALL CTRL_CLOTHES_SET(C_ID, "パジャマの変更_衣装セット", SET_ID)
	ENDSELECT

CASE "キャラ普段着"
	PRINTFORMW %CALLNAME:C_ID% has been set to imitate %CALLNAME:SET_ID%.
	SELECTCASE COS_SAVE
	CASE "現在の衣装"
		CALL CTRL_CLOTHES_SET(C_ID, "現在衣装の変更_キャラ衣装", SET_ID, "普段着")
	CASE "普段着"
		CALL CTRL_CLOTHES_SET(C_ID, "普段着の変更_キャラ衣装", SET_ID, "普段着")
	CASE "おきにいり衣装"
		CALL CTRL_CLOTHES_SET(C_ID, "おきにいり衣装の変更_キャラ衣装", SET_ID, "普段着")
	ENDSELECT

ENDSELECT



;着せ替え
@COM_ABLE429
;移動実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(429)
	RETURN RESULT
;私室設定
SIF (CFLAG:MASTER:現在位置 != CFLAG:MASTER:311) && (CFLAG:現在位置 != CFLAG:311)
	RETURN 0
SIF !GETBIT(CFLAG:TARGET:既成事実, 1) && TARGET
	RETURN 0
SIF CFLAG:うふふ
	RETURN 0
RETURN 1
