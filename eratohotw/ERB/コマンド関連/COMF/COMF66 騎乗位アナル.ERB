;-------------------------------------------------
;騎乗位アナル
;セックス系コマンド、レベル2
;-------------------------------------------------
@COM66
;前回の調教が3Pのとき3Pへ
IF PREVCOM == 71
	CALL COM_ABLE71
	SIF RESULT
		JUMP COM71
ENDIF
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = TARGET

;奉仕快楽フラグ
TCVAR:108 = 1

CSTR:PLAYER:10 += @"{TARGET}/"
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:PLAYER:2 = 2
TCVAR:PLAYER:童貞喪失 = 1
TCVAR:Ａ喪失発生 = 1
;強度
TCVAR:PLAYER:4 = 2200

CSTR:TARGET:10 += @"{PLAYER}/"
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手 12=Ａ責め 13=Ｖ責め）
TCVAR:TARGET:2 = 12
;強度
TCVAR:TARGET:4 = 1300

DOWNBASE:PLAYER:体力 += 20
DOWNBASE:PLAYER:気力 += 40

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 200
	SOURCE:PLAYER:快Ｃ = 400

	SOURCE:快Ａ = 500
	SOURCE:苦痛 = 400

ELSE

	DOWNBASE:体力 += 90
	DOWNBASE:気力 += 50
	SOURCE:PLAYER:快Ｃ = 400

	SOURCE:快Ａ = 500
	SOURCE:情愛 = 250
	SOURCE:性行動 = 250
	SOURCE:達成 = 250
	SOURCE:苦痛 = 800
	SOURCE:露出 = 120
	SOURCE:逸脱 = 700
	SOURCE:反感 = 900
	;com stuff custom code, if they're horny af then we reduce hatred, it's bs that they get hate mark even though they'd ride the shit out of you in reverse mode
	SIF CFLAG:溜まってる度 >= 500 || ABL:欲望 >= 5
		TIMES SOURCE:反感, 0.50

	;上のほうで計算した汚れデータ
	SOURCE:不潔 = Y*10 + 60

	;-------------------------------------------------
	;特殊ソース処理
	;-------------------------------------------------
	;抑圧、抵抗持ちには、抑鬱ソース追加
	SIF TALENT:自己愛 < 0 || TALENT:抵抗
		SOURCE:鬱屈 += 900
ENDIF
;Aへの苦痛は先に処理しておく
CALL PAIN_CHECK_A(TARGET)

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;奴隷のＡ⇔調教者のＰの汚れが移動
CALL STAIN_アナルＰ挿入

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;A経験
EXP:Ａ経験 ++

;性交経験

EXP:PLAYER:挿入経験 ++
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF FLAG:70 == 1
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 7
	ENDIF
		EXP:PLAYER:時姦経験 += 1
ELSE
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 7
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 7
	ENDIF
	EXP:Ａ性交経験 ++
	;愛情経験
	SIF !ASSIPLAY
		EXP:愛情経験 += TALENT:恋慕
ENDIF
Qol_Info_Count_Array:TARGET:Qol_Count_ASex ++ ;qol custom code
SIF FLAG:70 == 1 || CFLAG:イタズラ
	Qol_Info_Count_Array:TARGET:Qol_Count_UncASex ++ ;qol custom code
RETURN 1

@CAN_COM66(ARG)
IF FLAG:70 == 1
	SKIPDISP ARG
	PRINTL Anal Cowgirl Rape
ELSE
	SKIPDISP ARG
	PRINTL Anal Cowgirl
	;-------------------------------------------------
	;実行判定処理
	;-------------------------------------------------
	A = 0
	S = 0

	;すべての命令に共通の要素を考慮
	;(従順が高いと命令に従いやすいなど)
	CALL COM_ORDER

	;ABL:欲望
	IF ABL:欲望
		SIF S
			PRINT  + 
		A += ABL:欲望 * 3
		PRINT Desire
		PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 3,')
		S = 1
	ENDIF
	;ABL:Ａ感覚
	IF ABL:Ａ感覚
		SIF S
			PRINT  + 
		A += ABL:Ａ感覚 * 2
		PRINT A Sens
		PRINTV 'LV,ABL:Ａ感覚,'(,ABL:Ａ感覚 * 2,')
		S = 1
	ENDIF
	;ABL:奉仕精神
	IF ABL:奉仕精神
		SIF S
			PRINT  + 
		A += ABL:奉仕精神 * 4
		PRINT Servitude
		PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 4,')
		S = 1
	ENDIF
	IF MARK:快楽刻印
		SIF S
			PRINT  + 
		A += MARK:快楽刻印 * 3
		PRINT Pleasure Mark
		PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 3,')
		S = 1
	ENDIF

	;PALAM:潤滑（の不足）
	IF PALAM:潤滑 < PALAMLV:3
		PRINT  - 
		A -= 5
		PRINT Low Lubrication
		PRINTV '(,5,')
		S = 1
	ENDIF
	;PALAM:欲情
	IF GETPALAMLV(PALAM:欲情,5)
		SIF S
			PRINT  + 
		A += GETPALAMLV(PALAM:欲情,5) * 3
		PRINT Desire
		PRINTV 'LV,GETPALAMLV(PALAM:欲情,5),'(,GETPALAMLV(PALAM:欲情,5) * 3,')
		S = 1
	ENDIF
	;恥じらい
	IF TALENT:羞恥心 > 0
		PRINT  - 
		A -= 2
		PRINT Shy
		PRINTV '(,2,')
		S = 1
	ENDIF
	;快感の否定（淫乱、娼婦で無効）
	IF TALENT:快感応答 < 0 && !TALENT:淫乱
		PRINT  - 
		A -= 5
		PRINT Denies Pleasure
		PRINTV '(,5,')
		S = 1
	ENDIF
	;男嫌い（恋慕、親愛で無効）
	IF OTOKOGIRAI(TARGET)
		PRINT  - 
		A -= 12
		PRINT Man Hater
		PRINTV '(,12,')
		S = 1
	ENDIF
	;淫乱
	IF TALENT:淫乱 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		A += 15
		PRINT Lewd
		PRINTV '(,15,')
		S = 15
	ENDIF
	;娼婦
	IF TALENT:淫乱 > 1 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		A += 5
		PRINT Whore
		PRINTV '(,5,')
		S = 5
	ENDIF


	;A経験
	IF EXP:Ａ経験 < EXPLV:2
		PRINT  - 
		A -= 30
		PRINT Lack of 
		PRINT A Exp
		PRINTV '(,30,')
		S = 1
	ENDIF

	;調教者がふたなり
	IF (TALENT:PLAYER:2 & 3) == 3
		SIF S
			PRINT  + 
		A += 4
		PRINT Futanari
		PRINTV '(,4,')
		S = 1
	ENDIF

	;媚薬
	IF TCVAR:媚薬
		SIF S
			PRINT  + 
		A += 6
		PRINT Aphrodisiac
		PRINTV '(,6,')
		S = 1
	ENDIF

	Y = 0
	;愛液の汚れ
	SIF STAIN:PLAYER:2 & 1
		Y += 1
	;精液の汚れ
	SIF STAIN:PLAYER:2 & 4
		Y += 3
	;アナルの汚れ
	SIF STAIN:PLAYER:2 & 8
		Y += 7
	SIF STAIN:PLAYER:2 & 16
		Y += 15
	Y = Y * (2 - TALENT:汚臭耐性) / 2

	;汚れあり
	IF Y
		PRINT  - 
		A -= Y
		PRINT Dirty
		;汚臭鈍感
		IF TALENT:汚臭耐性 > 0
			PRINT , Smell Resistant
		ELSEIF TALENT:汚臭耐性 < 0
			PRINT , Smell Sensitive
		ENDIF
		PRINTV '(,Y,')
		S = 1
	ENDIF

	;合計を表示(45以上で実行)
	PRINT  = 
	PRINTV A

	V = 45
	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT Required Value 
	PRINTV V

	WAIT
	SKIPDISP 0
	;実行できない
	SIF A < V
		RETURN 0
	;Custom code
	IF !FLAG:STABLEBYPASS
		IF TALENT:妊娠 && (CFLAG:903 <= 30 || CFLAG:903 >= 71)
			PRINTFORMW お腹の子の為にも今は%CALLNAME:ARG%に無理はさせない方がいいだろう
			RETURN 0
		ENDIF
	ENDIF
ENDIF
SKIPDISP 0
RETURN 1


;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE66
;騎乗位アナル実行判定
SIF !TFLAG:100
	RETURN 0
SIF GLOBAL_COMABLE(66)
	RETURN RESULT
;毒人形だと肛門がついてないのでダメ
SIF TALENT:196 == 99
	RETURN 0
;睡眠姦中
SIF CFLAG:イタズラ > 1
	RETURN 0
;調教者がオトコ、ふたなりの場合はある程度勃起してないとダメ
SIF (TALENT:PLAYER:2 & 2) && BASE:PLAYER:勃起 < 1000
	RETURN 0
;アナルバイブ挿入中はダメ
SIF TEQUIP:14
	RETURN 0
;アナルビーズ使用中は不可
SIF TEQUIP:15
	RETURN 0
;普通の浣腸使用中は不可
SIF TEQUIP:21
	RETURN 0
;バルーン使用中は不可
SIF TEQUIP:22
	RETURN 0
;電極使用中は不可
SIF TEQUIP:23
	RETURN 0
;時間停止中はスキップ
IF FLAG:70 != 1
	;一定のＡ経験が必要
	SIF EXP:2 < EXPLV:3
		RETURN 0
	;縄使用中は不可
	SIF TEQUIP:縄
		RETURN 0
ENDIF
;体格差がある場合は主人が禁断の知識持ちじゃないとダメ
SIF 挿入不可(TARGET) == 1 && !TALENT:PLAYER:禁断の知識 && !TALENT:PLAYER:禁断の知識
	RETURN 0
;さらに体格差があると経験が必要
SIF 挿入不可(TARGET) == 2 && (!TALENT:PLAYER:禁断の知識 || EXP:Ａ経験 < 50)
	RETURN 0
SIF 挿入不可(TARGET) == 3
	RETURN 0
;調教者がオトコか半人半妖かふたなりじゃないとダメ
SIF !(TALENT:PLAYER:2 & 2)
	RETURN 0


;下半身着衣
SIF TEQUIP:0 & 4
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
IF TEQUIP:31
	SIF ITEM:20 == 0 && NOITEM == 0
		RETURN 0
ENDIF
;潤滑不足で、従順3以下・レズっ気3以下のASSIの場合はできない（サドならＯＫ）
IF PALAM:潤滑 < PALAMLV:2 && ASSIPLAY
	SIF (ABL:ASSI:従順 <= 3 || ABL:ASSI:技巧 <= 3) && TALENT:ASSI:サド == 0
		RETURN 0
ENDIF
;ベッドルームorバスルームでプレイマットがないとダメ
SIF !BEDROOM(CFLAG:PLAYER:現在位置) && !(BATHROOM() && ITEM:20)
	RETURN 0
RETURN 1

