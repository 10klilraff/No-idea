;----------------------------------------------------------------------------------------------------
;依頼受託処理
;----------------------------------------------------------------------------------------------------
@SET_IRAI_SLOT(CHARA)
#DIM CHARA
#DIM IRAI_ID
#DIM CHK_SLOT
IRAI_ID = CFLAG:CHARA:依頼内容
CHK_SLOT = SEARCH_IRAI_EMPTY()
SIF !CHK_SLOT
	RETURN
IRAI_SLOT:CHK_SLOT     = IRAI_ID
IRAI_DATA:CHK_SLOT     = INT_DATA_IRAI(IRAI_ID, "依頼内容",     CHARA)
IRAI_SUB:CHK_SLOT      = INT_DATA_IRAI(IRAI_ID, "依頼サブ",     CHARA)
IRAI_DEADLINE:CHK_SLOT = INT_DATA_IRAI(IRAI_ID, "依頼期限", CHARA) + DAY
CALL SET_IRAI_PROGRESS(CHARA, "依頼中")



;----------------------------------------------------------------------------------------------------
;依頼終了処理
;----------------------------------------------------------------------------------------------------
@CLEAR_IRAI_SLOT(CHARA, CHK_SLOT, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIM  CHK_SLOT
#DIMS SCENE
ARRAYREMOVE IRAI_SLOT,     CHK_SLOT, 1
ARRAYREMOVE IRAI_DATA,     CHK_SLOT, 1
ARRAYREMOVE IRAI_SUB,      CHK_SLOT, 1
ARRAYREMOVE IRAI_EX,       CHK_SLOT, 1
ARRAYREMOVE IRAI_DEADLINE, CHK_SLOT, 1
CALL SET_IRAI_PROGRESS(CHARA, SCENE)



;----------------------------------------------------------------------------------------------------
;依頼発生処理
;適当に10キャラ見繕って依頼を作るよ
;----------------------------------------------------------------------------------------------------
@IRAI_HAPPEN
#DIM CHARA_ID, 10
[IF_DEBUG]
	;#;PRINTFORMW Processing request creation (Execute for all characters, display logs in debug window)
	;#;DEBUGCLEAR
	FOR LOCAL:0, 1, キャラクタ数上限
		SIF !CAN_MEET(LOCAL:0)
			CONTINUE
		SIF CFLAG:(LOCAL:0):依頼状況
			CONTINUE
		CALL SET_NEW_IRAI(LOCAL:0)
	NEXT
	;#;PRINTFORML Request creation processing complete, deleting debug text
	;#;FORCEWAIT
	;#;DEBUGCLEAR
	RETURN
[ENDIF]
CALL ARRAY_RAND(CHARA_ID, 10, 1, キャラクタ数上限)
FOR LOCAL:0, 0, 10
	SIF !CAN_MEET(CHARA_ID:(LOCAL:0))
		CONTINUE
	SIF CFLAG:(CHARA_ID:(LOCAL:0)):依頼状況
		CONTINUE
	CALL SET_NEW_IRAI(CHARA_ID:(LOCAL:0))
NEXT



;----------------------------------------------------------------------------------------------------
;各キャラの依頼発生処理
;依頼なし/一般依頼/固有依頼
;----------------------------------------------------------------------------------------------------
@SET_NEW_IRAI(CHARA)
#DIM CHARA
SELECTCASE IRAI_HIT(CHARA, "HAPPEN")
CASE IS <= 0
	RETURN
CASE 1
	LOCAL = IRAI_HIT(CHARA, "COMMON")
	SIF !LOCAL
		RETURN
CASE 2
	LOCAL = IRAI_HIT(CHARA, "UNIQUE")
	SIF LOCAL <= 100
		RETURN
ENDSELECT
CALL SET_IRAI_PROGRESS(CHARA, "依頼発生直後")
CFLAG:CHARA:依頼内容 = MAKE_IRAI_ID(CHARA, LOCAL)



;----------------------------------------------------------------------------------------------------
;依頼抽選処理はまとめてここに
;----------------------------------------------------------------------------------------------------
@IRAI_HIT(CHARA, TYPE)
#FUNCTION
#DIM  CHARA
#DIMS TYPE
#DIM CONST LAST_COMMON = 99 ;一般依頼IDの末端
#DIM       LAST_UNIQUE      ;そのキャラに実装されている固有依頼IDの末端
#DIM       HIT_LIST, 100    ;抽選確率配列
#DIM       ID_LIST,  100    ;抽選を対象依頼IDの一覧
#DIM       CNT_LIST         ;抽選対象となる依頼の数
DEBUGPRINTFORML ＞IRAI_HIT{CHARA}　%TYPE%
SELECTCASE TYPE
CASE "HAPPEN"
;依頼発生抽選
	HIT_LIST:0 = GET_INT(0, "キャラデータ", CHARA, "依頼なし発生率")
	HIT_LIST:1 = GET_INT(0, "キャラデータ", CHARA, "一般依頼発生率")
	HIT_LIST:2 = GET_INT(0, "キャラデータ", CHARA, "固有依頼発生率")
	SIF !SUMARRAY(HIT_LIST, 0, 3)
		HIT_LIST = 7, 3, 0
	DEBUGPRINTFORML ＞IRAI_HIT{CHARA}　{HIT_LIST:0}/{HIT_LIST:1}/{HIT_LIST:2}
	RETURNF ARRAY_HIT(HIT_LIST, 3)
CASE "COMMON"
;一般依頼の抽選
	VARSET HIT_LIST
	VARSET ID_LIST
	VARSET CNT_LIST
	FOR LOCAL:0, 1, LAST_COMMON + 1
		SIF !INT_DATA_IRAI(MAKE_IRAI_ID(CHARA, LOCAL:0), "依頼発生条件", CHARA)
			CONTINUE
		ID_LIST:CNT_LIST  = LOCAL:0
		HIT_LIST:CNT_LIST = INT_DATA_IRAI(MAKE_IRAI_ID(CHARA, LOCAL:0), "依頼発生率", CHARA)
		CNT_LIST ++
	NEXT
	SIF !CNT_LIST
		RETURNF 0
	RETURNF ID_LIST:(ARRAY_HIT(HIT_LIST, CNT_LIST))
CASE "UNIQUE"
;固有依頼の抽選
	VARSET HIT_LIST
	VARSET ID_LIST
	VARSET CNT_LIST
	LAST_UNIQUE = GET_INT(0, "キャラデータ", CHARA, "固有依頼最終要素")
	FOR LOCAL:0, 101, LAST_UNIQUE + 1
		SIF !INT_DATA_IRAI(MAKE_IRAI_ID(CHARA, LOCAL:0), "依頼発生条件", CHARA)
			CONTINUE
		ID_LIST:CNT_LIST  = LOCAL:0
		HIT_LIST:CNT_LIST = INT_DATA_IRAI(MAKE_IRAI_ID(CHARA, LOCAL:0), "依頼発生率", CHARA)
		CNT_LIST ++
	NEXT
	SIF !CNT_LIST
		RETURNF 0
	RETURNF ID_LIST:(ARRAY_HIT(HIT_LIST, CNT_LIST))
ENDSELECT



;----------------------------------------------------------------------------------------------------
;依頼消滅処理
;全キャラが対象となり大規模化した
;----------------------------------------------------------------------------------------------------
@IRAI_VANISH
#DIM CHK_SLOT
#DIM CHARA
;#;PRINTFORMW Processing request updates (Execute for all characters, display logs in debug window)
;#;DEBUGCLEAR
;全キャラ対象、受託前の/既に報告の終わった依頼の処理
FOR CHARA, 1, CHARANUM
	SELECTCASE IRAI_CHARA_TO_PROGRESS(CHARA)
	;これらはまだ依頼スロットに入っていない
	CASE "依頼発生直後"
		CALL SET_IRAI_PROGRESS(CHARA, "依頼未受託")
		
	CASE "依頼未受託"
		SIF INT_DATA_IRAI(CFLAG:CHARA:依頼内容, "依頼消滅率", CHARA)
			CALL SET_IRAI_PROGRESS(CHARA, "依頼消滅")
	[SKIPSTART]
	依頼中のケースは依頼スロット見たほうがはやい
	CASE "依頼中"
	
	基本的にこの4段階は未報告状態、依頼主のリアクション待ちです（報告必須）
	CASE "依頼達成"
	CASE "期限切れ"
	CASE "報告忘れ"
	CASE "依頼失敗"
	[SKIPEND]
	CASE "依頼終了（成功）", "依頼終了（期限切れ）", "依頼終了（報告忘れ）", "依頼終了（失敗）"
	;依頼が終了し、依頼人に報告を行なったパターン（スロットに入ってない）
		CALL SET_IRAI_PROGRESS(CHARA, "依頼消滅")
	ENDSELECT
NEXT
;#;PRINTFORML Request progression processing complete, deleting debug text
;#;FORCEWAIT
;#;DEBUGCLEAR

SIF !COUNT_IRAI_SLOT()
	RETURN

;#;PRINTFORMW Processing request deadlines (Execute for all characters, display logs in debug window)
;依頼の期限判定
;スロットは空けないことにした（依頼主への報告を強制させる）
;依頼報告不要時にスロットを空ける処理が必要になった
;スロット1から見ていくと依頼のリセットによってIDズレが発生するので末尾からチェックする
FOR CHK_SLOT, COUNT_IRAI_SLOT(), 0, -1
	CHARA = IRAI_SLOT_TO_CLIENT(CHK_SLOT)
	;期限による依頼の失敗判定
	;期限が残ってるならスルー
	SIF IRAI_DEADLINE:CHK_SLOT && DAY <= IRAI_DEADLINE:CHK_SLOT
		CONTINUE
	SELECTCASE IRAI_CHARA_TO_PROGRESS(CHARA)
	CASE "依頼中"
		CALL SET_IRAI_PROGRESS(CHARA, "期限切れ")
	CASE "依頼達成"
		IF STRLENS(STR_DATA_IRAI(IRAI_SLOT:CHK_SLOT, "依頼報告不要", CHARA))
			CALL IRAI_CARRYOUT(CHARA, IRAI_SLOT:CHK_SLOT, "依頼報告不要")
			CALL SHOW_IRAI_MESSAGE(CHARA, IRAI_SLOT:CHK_SLOT, "依頼報告不要")
			CALL CLEAR_IRAI_SLOT(CHARA, CHK_SLOT, "依頼消滅")
		ELSE
			CALL SET_IRAI_PROGRESS(CHARA, "報告忘れ")
		ENDIF
	ENDSELECT
NEXT
;#;PRINTFORML Request deadline processing complete, deleting debug text
;#;FORCEWAIT
;#;DEBUGCLEAR


;----------------------------------------------------------------------------------------------------
;依頼内容の実行処理
;実行時のSOURCEの変化の他、報酬や失敗による処理も含む
;----------------------------------------------------------------------------------------------------
@IRAI_CARRYOUT(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
RESULT = 0
IF IS_COMMON_IRAI(IRAI_ID)
	TRYCCALLFORM IRAI_一般{IRAI_ID % 1000}(CHARA, IRAI_ID, SCENE)
	CATCH
		PRINTFORML ＊エラー発生＊
		PRINTFORML 　CHARA:{CHARA}　SCENE:%SCENE%　IRAI_ID:{IRAI_ID}
		PRINTFORML 　指定の関数 @IRAI_一般{IRAI_ID % 1000}は存在しません
		PRINTFORML 　エラーログをコピペして報告して下さい
		PRINTFORML 
		PRINTFORMW 　入力待機中
	ENDCATCH
ELSE
	TRYCCALLFORM IRAI_固有{IRAI_ID_TO_CLIENT(IRAI_ID)}(CHARA, IRAI_ID, SCENE)
	CATCH
		PRINTFORML ＊エラー発生＊
		PRINTFORML 　CHARA:{CHARA}　SCENE:%SCENE%　IRAI_ID:{IRAI_ID}
		PRINTFORML 　指定の関数 @IRAI_固有{IRAI_ID_TO_CLIENT(IRAI_ID)}は存在しません
		PRINTFORML 　エラーログをコピペして報告して下さい
		PRINTFORML 
		PRINTFORMW 　入力待機中
	ENDCATCH
ENDIF
RETURN RESULT
;RESULTと依頼実行判定結果の対応はIRAI_CONST_RESULT(TYPE)を参照のこと



;----------------------------------------------------------------------------------------------------
;受託済み依頼情報表示関数
;----------------------------------------------------------------------------------------------------
@SHOW_ALL_IRAI_SLOT
#DIM CHK_SLOT
#DIM IRAI_NUM
IRAI_NUM = COUNT_IRAI_SLOT()
FOR CHK_SLOT, 1, IRAI_NUM + 1
	CALL SHOW_IRAI_DATA(IRAI_SLOT:CHK_SLOT, CHK_SLOT)
	PRINTFORML 
NEXT



;----------------------------------------------------------------------------------------------------
;依頼情報表示関数
;依頼ID単体でも使用できるが受託済みの場合は簡略化のためにSLOT番号を渡す
;フルネーム表記なのはわざとです（そのほうがお硬い文面っぽく見えるので）
;----------------------------------------------------------------------------------------------------
@SHOW_IRAI_DATA(IRAI_ID, CHK_SLOT)
#DIM IRAI_ID   ;対象の依頼ID（依頼人情報含む）
#DIM CHK_SLOT  ;判定対象とする依頼スロット、未受託の状態では0で読んで下さい
#DIM CLIENT_ID ;依頼人ID
#DIM CHK_ID    ;依頼自体のID
#DIM DEAD_LINE ;期限日数
CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
CHK_ID    = IRAI_ID % 1000
DEAD_LINE = INT_DATA_IRAI(IRAI_ID, "依頼期限", CLIENT_ID)
SIF CHK_SLOT
	PRINTFORML Request %TOFULL(TOSTR(CHK_SLOT))%
PRINTFORML 　＜%REQUEST_NAMES_TR(STR_DATA_IRAI(IRAI_ID, "依頼名", CLIENT_ID))%＞
PRINTFORML %"Client:", 20, RIGHT% %NAME:CLIENT_ID%
PRINTFORML %"Objective:", 20, RIGHT% %STR_DATA_IRAI(IRAI_ID, "依頼内容表示", CLIENT_ID)%
PRINTFORML %"Reward:", 20, RIGHT% %STR_DATA_IRAI(IRAI_ID, "依頼報酬表示", CLIENT_ID)%
SIF STRLENS(STR_DATA_IRAI(IRAI_ID, "依頼報告不要", CLIENT_ID))
	PRINTFORML %"Report:", 20, RIGHT% %STR_DATA_IRAI(IRAI_ID, "依頼報告不要", CLIENT_ID)%
SIF STRLENS(STR_DATA_IRAI(IRAI_ID, "依頼備考表示", CLIENT_ID))
	PRINTFORML %"Note:", 20, RIGHT% %STR_DATA_IRAI(IRAI_ID, "依頼備考表示", CLIENT_ID)%
;#;PRINTFORML %"Request ID:", 20, RIGHT% {IRAI_ID}　CLIENT_ID{CLIENT_ID}

;未受託の場合は期限のみ表示しておしまい
IF !CHK_SLOT
	IF !DEAD_LINE
		PRINTFORML %"Deadline:", 20, RIGHT% Today
	ELSE
		PRINTFORML %"Deadline:", 20, RIGHT% Within %PRINT_PLUR("day", DEAD_LINE)%
	ENDIF
	RETURN
ELSE
	;PRINTFORML %"Deadline:", 20, RIGHT% After {IRAI_DEADLINE:CHK_SLOT - DAY} days
ENDIF

;ここから受託済み、業種別の表示
SELECTCASE IRAI_SLOT_TO_TASKNAME(CHK_SLOT)
CASE "調達"
	PRINTFORML %"Required Item:", 20, RIGHT% %ITEMNAME_TR(IRAI_SLOT_TO_ITEM(CHK_SLOT))%×{IRAI_SUB:CHK_SLOT}
	SIF ITEMNAME:IRAI_SLOT_TO_ITEM(CHK_SLOT) == "うまい魚"
		PRINTFORML 　　　　　（Rainbow Trout・Salmon・Wakasagi・Lamprey・Northern Snakehead）
CASE "伝令（人物）"
	PRINTFORML %"Target Character:", 20, RIGHT% %NAME:(IRAI_SLOT_TO_PERSON(CHK_SLOT))%
CASE "伝令（場所）"
	PRINTFORML %"Destination:", 20, RIGHT% %THE_CASE_SHIFT(GET_PLACENAME(IRAI_SLOT_TO_PLACE(CHK_SLOT)))%
;ナズーリンに聞いても忘れるあなた(わたし)へ
CASE "捜索（人物）"
	IF IRAI_EX:CHK_SLOT
		PRINTFORML %"Key Person:", 20, RIGHT% %CALLNAME:(IRAI_SLOT_TO_PERSON(CHK_SLOT))%

	ELSE
		PRINTFORM %"Hint:", 20, RIGHT% 
		CALL SHOW_IRAI_HINT("捜索（人物）", IRAI_SLOT_TO_PERSON(CHK_SLOT), IRAI_SUB:CHK_SLOT)
	ENDIF
CASE "捜索（場所）"
	IF IRAI_EX:CHK_SLOT > 5000
		PRINTFORML %"Estimated Position:", 20, RIGHT% %STR_TR(5000 + GET_MAPID(IRAI_EX:CHK_SLOT-5000))%

	ELSEIF IRAI_EX:CHK_SLOT
		PRINTFORML %"Estimated Position:", 20, RIGHT% %STR_TR(5000 + GET_MAPID(IRAI_EX:CHK_SLOT))% - %GET_PLACENAME(IRAI_EX:CHK_SLOT)%
	ELSE
		PRINTFORM %"Hint:", 20, RIGHT% 
		CALL SHOW_IRAI_HINT("捜索（場所）", IRAI_SLOT_TO_PLACE(CHK_SLOT), IRAI_SUB:CHK_SLOT)
	ENDIF
CASE "討伐"
	PRINTFORML %"Subjugation Target:", 20, RIGHT% %NAME:(IRAI_SLOT_TO_PERSON(CHK_SLOT))%
ENDSELECT
CALL IRAI_CARRYOUT(CLIENT_ID, IRAI_ID, "依頼条件表示")

;確定してる場合はその旨を表示しておしまい
IF GROUPMATCH(IRAI_CHARA_TO_PROGRESS(CLIENT_ID), "期限切れ", "報告忘れ", "依頼失敗")
	PRINTFORML %"Request status:", 20, RIGHT% Failed
	RETURN
ELSEIF GROUPMATCH(IRAI_CHARA_TO_PROGRESS(CLIENT_ID), "依頼達成")
	PRINTFORML %"Request status:", 20, RIGHT% Completed (Not handed in)
	RETURN
;Custom code formatting
ELSE
	IF DAY == IRAI_DEADLINE:CHK_SLOT || IRAI_DEADLINE:CHK_SLOT - DAY <= 0
		PRINTFORML %"Deadline:", 20, RIGHT% Today
	ELSEIF IRAI_DEADLINE:CHK_SLOT > DAY
		PRINTFORML %"Deadline:", 20, RIGHT% %PRINT_PLUR("Day", IRAI_DEADLINE:CHK_SLOT - DAY)%
	ELSE
		PRINTFORML %"Deadline:", 20, RIGHT% Today
	ENDIF
ENDIF

;----------------------------------------------------------------------------------------------------
;依頼情報内のヒント表示
;DYNAMIC部分はSHOW_TASTEを参考,助手効果のデータだけを抽出する
;----------------------------------------------------------------------------------------------------
@SHOW_IRAI_HINT(TASKNAME, ARG, パターン)
#DIMS TASKNAME
#DIM CHARA
#DIM PLACE
#DIM パターン
#DIM 洞察力
#DIMS DYNAMIC LIST, 100 ;リスト
#DIM  DYNAMIC CNT,  100 ;リスト各要素の重複個数
#DIM  DYNAMIC NUM       ;種類の総数
;◆洞察力に関して
;元々はEXP:依頼達成経験とABL:依頼こなしレベルを設定して、依頼をこなすほど洞察力(=依頼レベル)が増えるという風にしようと思っていた
;が、わざわざ作らなくてもプレイヤー側のリアル洞察力の方が必要なので凍結させました。
LOCALS = 
;持ち主を推定する,現状パターンは1～3,パターン0は場所の方へ
IF TASKNAME == "捜索（人物）" && パターン > 0
	CHARA = ARG
	洞察力 = 100
	CALL DISH_TASTE_FROM_CHARA(CHARA, "OUTPUT")
	CALLF CNT_SPLIT(RESULTS, LIST, CNT, NUM)
	FOR LOCAL, 0, NUM
		SELECTCASE LIST:LOCAL
		CASE "神力"
				LOCALS += "Loaded with divine power."
		CASE "妖力"
				LOCALS += "Faintly Youkai-ish."
		CASE "霊障"
				LOCALS += "Filled with spiritual energy."
		CASE "魔力"
				LOCALS += "Loaded with magical power"
		CASE "生命力"
				LOCALS += "Containing the power of nature."
		CASE "仙術", "秘術"
				LOCALS += "Filled with mysterious energy."
		CASE "鬼の魔力", "血の魔力"
				LOCALS += "Filled with violent energy."
		CASE "メイド流"
				LOCALS += "Something a maid would make."
		CASE "天狗流"
				LOCALS += "It has Tengu writing on it."
		CASE "冥界風"
				LOCALS += "It's underworldly."
		CASE "天界風"
				LOCALS += "It's heavenly."
		CASE "地底風"
				LOCALS += "A Subtarranean treat."
		CASE "地獄風"
				LOCALS += "Has some sand from Hell on it."
		CASE "畜生流"
				LOCALS += "As seen in the Animal Realm."
		CASE "月都風"
				LOCALS += "That can only be found in the Lunar Capital."
		CASE "玉兎流"
				LOCALS += "Has a rabbit symbol."
		CASE "魔界風"
				LOCALS += "Made with Makai Stones."
		CASE "夢幻風"
				LOCALS += "Carrying traces of Mugenkan."
		CASE "外界風"
				LOCALS += "From the outside world."
		CASE "化学的"
				LOCALS += "Made with precision."
		CASE "小人族秘伝の味"
				LOCALS += "Of Inchling make."
		CASE "稗田家秘伝の味"
				LOCALS += "Bearing the crest of the Hieda family."
		CASE "おふくろの味"
				LOCALS += "Very homely."
;		CASE "プロの味"
;				LOCALS += ""
		;天候と時間によってきらきらしなくなったりもする
		CASE "きらめき"
				LOCALS += "Glitters quite a bit."
		CASE "純化した味"
				LOCALS += "Free of impurities."
		CASE "薬効"
				LOCALS += "Looks rather medicinal."
		CASE "エンチャント"
				LOCALS += "Strengthened via magic."
		CASE "奇跡", "幸運"
				LOCALS += "Feels auspicious."
		CASE "加護", "豊穣"
				LOCALS += "Carrying Grace."
		CASE "嫉妬"
				LOCALS += "Feels obsessive..."
		CASE "道具の共鳴"
				LOCALS += "Close item of Tsukomogami."
;		CASE "濃い味"
;				LOCALS += ""
;		CASE "薄味"
;				LOCALS += ""
;		CASE "隠し味"
;				LOCALS += ""
		CASE "ドッキリ味"
				LOCALS += "Belies its true nature."
		CASE "未知の味"
				LOCALS += "A strange thing."
		CASE "氷結"
				LOCALS += "Cold."
		CASE "大火力"
				LOCALS += "Warm."
		CASE "彩り"
				LOCALS += "Painted with all seven colors."
		CASE "絢爛"
				LOCALS += "Very flashy."
		CASE "貧乏飯"
				LOCALS += "Very worn-out."
		CASE "切れ味"
				LOCALS += "Looks like a gun."
		CASE "おいしい水"
				LOCALS += "Waterproof."
		CASE "刺激的"
				LOCALS += "Carries a skull symbol..."
		ENDSELECT
	NEXT
	SELECTCASE パターン
	CASE 1
		IF TALENT:CHARA:針さばき
			LOCALS += "Sharp and pointy."
		ELSEIF TALENT:CHARA:日光浴
			LOCALS += "Sun-bleached."
		ELSEIF TALENT:CHARA:月光浴
			LOCALS += "Moon-bleached."
		ELSEIF TALENT:CHARA:幼稚
			LOCALS += "Looks like a toy."
		ENDIF
	CASE 2
		IF ABL:CHARA:音楽技能 >= 2 && ABL:MASTER:音楽技能 >= 1
			LOCALS += "Related to music."
		ELSEIF TALENT:CHARA:釣りLv >= 2 && TALENT:MASTER:釣りLv >= 1
			LOCALS += "Tool for fishing."
		ELSEIF ABL:CHARA:料理技能 >= 1 && ABL:MASTER:料理技能 >= 1
			LOCALS += "Tool for cooking."
		ELSEIF ABL:CHARA:教養 >= 4 && ABL:MASTER:教養 >= 3
			LOCALS += "A difficult book."
		ENDIF
	CASEELSE
		SIF TALENT:CHARA:度胸 < 0
			LOCALS += "An amulet."
		SIF TALENT:CHARA:陽気／陰気 < 0
			LOCALS += "Looks gloomy."
		SIF TALENT:CHARA:陽気／陰気 > 0
			LOCALS += "Looks bright."
		SIF TALENT:CHARA:目立ちたがり
			LOCALS += "Asserts itself."
		SIF TALENT:CHARA:汚臭耐性 < 0
			LOCALS += "Has been polished neatly."
		SIF TALENT:CHARA:汚臭耐性 > 0
			LOCALS += "Kinda dirty."
		SIF TALENT:CHARA:狐 || TALENT:CHARA:妖狐
			LOCALS += "Has fur on it."
		SIF TALENT:CHARA:吸血鬼
			LOCALS += "Has a bat seal on it."
		SIF TALENT:CHARA:水棲
			LOCALS += "It's wet."
	ENDSELECT
	;custom code, add spaces (dirty regex I know, it's for more periods than one)
	SIF STRLENS(LOCALS)
		LOCALS '= REPLACE(REPLACE(LOCALS, "\\.", ". "), "(\\. \\. \\.)", "...")
	;ある程度まで行くと断定できる
	IF ABL:CHARA:親密 >= 20 || TALENT:CHARA:恋人
		LOCALS = Must be %CALLNAME:CHARA%'s.
	ELSEIF ABL:CHARA:親密 >= 10
		LOCALS += (STRLENS(LOCALS) ? " " # "") + "It must be that girl's..."
	ELSEIF ABL:CHARA:親密 >= 5
		LOCALS += (STRLENS(LOCALS) ? " " # "") + "Seen it somewhere before..."
	ENDIF
ELSEIF TASKNAME == "捜索（場所）" || (TASKNAME == "捜索（人物）" && !パターン)
	;落とした場所を推定する,現状パターンは1～2
	IF TASKNAME == "捜索（場所）"
		PLACE = ARG
	;落とし物の対象が人物になっているパターン0
	;一応報酬で判別可能だがいきなり人物を推定するのはオカシイので人物の自宅位置＝落とした場所を推定する
	ELSEIF TASKNAME == "捜索（人物）" && !パターン
		IF CFLAG:ARG:自宅位置
			PLACE = CFLAG:ARG:自宅位置
		ELSE
			PRINTFORML No clue...
			RETURN
		ENDIF
	ENDIF
	洞察力 = 100
	SELECTCASE GET_MAPID(PLACE)
	CASE 0
		IF GET_MAPID(PLACE) == MAIN_MAP
			LOCALS += "Somewhere around the Hakurei Shrine."
;		ELSEIF 洞察力>= 3
;			LOCALS += "博麗神社に行った日に失くしたらしい。"
		ELSEIF 洞察力 >= 1
			IF パターン == 1
				LOCALS += "Lost after visiting a temple."
			ELSE
				LOCALS += "Lost after the banquet."
			ENDIF
		ENDIF
	CASE 1
		IF GET_MAPID(PLACE) == MAIN_MAP
			LOCALS += "Somewhere around Myouren Temple"
;		ELSEIF 洞察力>= 3
;			LOCALS += "命蓮寺に行った日に失くしたらしい。"
		ELSEIF 洞察力 >= 1
			IF パターン == 1
				LOCALS += "Lost after visiting a temple."
			ELSE
				LOCALS += "Lost it in a crowd."
			ENDIF
		ENDIF
	CASE 2
		IF GET_MAPID(PLACE) == MAIN_MAP
			LOCALS += "Somewhere around the Human Village."
;		ELSEIF 洞察力>= 3
;			LOCALS += "人里に行った日に失くしたらしい。"
		ELSEIF 洞察力 >= 1
			IF パターン == 1
				LOCALS += "Lost after the banquet."
			ELSE
				LOCALS += "Lost it in a crowd."
			ENDIF
		ENDIF
	CASE 3
		IF GET_MAPID(PLACE) == MAIN_MAP
			LOCALS += "Somewhere around the Scarlet Devil Mansion."
;		ELSEIF 洞察力>= 4
;			LOCALS += "紅魔館に行った日に失くしたらしい。"
		ELSEIF 洞察力 >= 2
			IF パターン == 1
				LOCALS += "Lost after the banquet."
			ELSE
				LOCALS += "Lost it after fishing."
			ENDIF
		ENDIF
	CASE 4
		IF GET_MAPID(PLACE) == MAIN_MAP
			LOCALS += "Somewhere around Eientei."
;		ELSEIF 洞察力>= 5
;			LOCALS += "永遠亭に行った日に失くしたらしい。"
		ELSEIF 洞察力 >= 3
			IF パターン == 1
				LOCALS += "Lost it in the dark."
			ELSE
				LOCALS += "Were lost when they lost it."
			ENDIF
		ENDIF
	CASE 5
		IF GET_MAPID(PLACE) == MAIN_MAP
			LOCALS += "Somewhere in the Forest of Magic."
;		ELSEIF 洞察力>= 5
;			LOCALS += "魔法の森に行った日に失くしたらしい。"
		ELSEIF 洞察力 >= 3
			IF パターン == 1
				LOCALS += "Lost it in the dark."
			ELSE
				LOCALS += "Were lost when they lost it."
			ENDIF
		ENDIF
	CASE 6
		IF GET_MAPID(PLACE) == MAIN_MAP
			LOCALS += "Somewhere in the Netherworld"
;		ELSEIF 洞察力>= 6
;			LOCALS += "冥界方面に行った日に失くしたらしい。"
		ELSEIF 洞察力 >= 4
			LOCALS += "Suddenly noticed it was gone."
		ENDIF
	CASE 7
		IF GET_MAPID(PLACE) == MAIN_MAP
			LOCALS += "Somewhere at the foot of the mountain."
;		ELSEIF 洞察力>= 4
;			LOCALS += "山麓で失くしたらしい。"
		ELSEIF 洞察力 >= 2
			IF パターン == 1
				LOCALS += "Lost it around the Youkai Mountain."
			ELSE
				LOCALS += "Lost it after fishing."
			ENDIF
		ENDIF
	CASE 8
		IF GET_MAPID(PLACE) == MAIN_MAP
			LOCALS += "Somewhere around the summit of the Youkai Mountain."
;		ELSEIF 洞察力>= 4
;			LOCALS += "山頂付近で失くしたらしい。"
		ELSEIF 洞察力 >= 2
			IF パターン == 1
				LOCALS += "Lost it around the Youkai Mountain."
			ELSE
				LOCALS += "Lost after visiting a temple."
			ENDIF
		ENDIF
	CASE 9
		IF GET_MAPID(PLACE) == MAIN_MAP
			LOCALS += "Somewhere in the underworld."
;		ELSEIF 洞察力>= 6
;			LOCALS += "地底で失くしたらしい。"
		ELSEIF 洞察力 >= 4
			IF パターン == 1
				LOCALS += "Lost it in the dark."
			ELSE
				LOCALS += "Lost it after fishing."
			ENDIF
		ENDIF
	CASE 10
		IF GET_MAPID(PLACE) == MAIN_MAP
			LOCALS += "Somewhere around the capital of the Moon."
;		ELSEIF 洞察力>= 6
;			LOCALS += "月の都に行った日に失くしたらしい。"
		ELSEIF 洞察力 >= 4
			LOCALS += "Suddenly noticed it was gone."
		ENDIF
	ENDSELECT
	SIF 洞察力 >= 2 && TASKNAME == "捜索（人物）" && !パターン
		LOCALS += (STRLENS(LOCALS) ? " " # "") + "As it stands out, it might already have been picked up by someone else."
ENDIF
SIF LOCALS == ""
	LOCALS = No clue...
PRINTFORML %LOCALS%

;----------------------------------------------------------------------------------------------------
;依頼関連の口上呼び出し関数
;----------------------------------------------------------------------------------------------------
@SHOW_IRAI_MESSAGE(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
RESULTS =
IF SCENE == "依頼報告不要"
	CALL KOJO_MESSAGE_SEND("IRAI", CHARA, IRAI_ID, 0, 0, "NO_REPORT", SCENE)
ELSEIF CHARA == IRAI_ID_TO_CLIENT(IRAI_ID)
;対象が依頼者である
	CALL KOJO_MESSAGE_SEND("IRAI", CHARA, IRAI_ID, 0, 0, "CLIENT", SCENE)
ELSE
;この場合はおおむねSCENEは"依頼実行時"になるが一応渡す
	CALL KOJO_MESSAGE_SEND("IRAI", CHARA, IRAI_ID, 0, 0, "TARGET", SCENE)
ENDIF
RESETCOLOR


;----------------------------------------------------------------------------------------------------
;依頼リセット関数
;依頼関連変数の全初期化
;----------------------------------------------------------------------------------------------------
@IRAI_RESET
PRINTFORML All requests will be reset.
PRINTFORML Is this ok?
CALL ASK_YN
IF !RESULT
	CVARSET CFLAG, 403
	CVARSET CFLAG, 404
	VARSET IRAI_SLOT
	VARSET IRAI_DATA
	VARSET IRAI_SUB
	VARSET IRAI_DEADLINE
	VARSET IRAI_CLIENT
	VARSET IRAI_EX
	PRINTFORMW Requests have been reset.
ENDIF

;bug fix custom code
@IRAI_RESET_PROCESS
CVARSET CFLAG, 403
CVARSET CFLAG, 404
VARSET IRAI_SLOT
VARSET IRAI_DATA
VARSET IRAI_SUB
VARSET IRAI_DEADLINE
VARSET IRAI_CLIENT

;----------------------------------------------------------------------------------------------------
;依頼期限通知関数
;毎朝の通知
;----------------------------------------------------------------------------------------------------
@MEMO_IRAI_DEADLINE
#DIM CLIENT
;受託済み依頼の期限の処理
FOR LOCAL,1, 6
	SIF !IRAI_SLOT:LOCAL
		CONTINUE
	CLIENT = IRAI_SLOT_TO_CLIENT(LOCAL)
	SIF GROUPMATCH(IRAI_CHARA_TO_PROGRESS(CLIENT), "依頼中", "依頼達成", "依頼失敗")
		PRINTFORML %CALLNAME:CLIENT%'s request expires in {IRAI_DEADLINE:LOCAL - DAY} days.
NEXT



;----------------------------------------------------------------------------------------------------
;「部屋を訪ねる」での依頼関連のアクション
;----------------------------------------------------------------------------------------------------
@IRAI_VISIT_HOME(CHARA)
#DIM PREV_TARGET     ;TARGET退避
#DIM CHARA           ;対象きゃら
#DIM IRAI_NUM        ;受託依頼数
#DIM CHK_SLOT        ;判定対象スロット
#DIM DYNAMIC CNT_DO  ;実行/報告した依頼数＝戻り値
PREV_TARGET = TARGET
TARGET = CHARA
IRAI_NUM = COUNT_IRAI_SLOT()
FOR CHK_SLOT, 1, IRAI_NUM + 1
	SIF !CAN_IRAI_VISIT_HOME_PERSON(CHARA, CHK_SLOT)
		CONTINUE
	PRINTFORML ＜%REQUEST_NAMES_TR(STR_DATA_IRAI(IRAI_SLOT:CHK_SLOT, "依頼名", CHARA))%＞
	PRINTFORMW 　→[%IRAI_SLOT_TO_ACTNAME(CHK_SLOT)%]
	CALL IRAI_APPLY(CHK_SLOT)
	SIF RESULT > 0
		CNT_DO ++
NEXT

IRAI_NUM = COUNT_IRAI_SLOT()
FOR CHK_SLOT, IRAI_NUM, 0, -1
	SIF !CAN_IRAI_VISIT_HOME_CLIENT(CHARA, CHK_SLOT)
		CONTINUE
	PRINTFORML ＜%REQUEST_NAMES_TR(STR_DATA_IRAI(IRAI_SLOT:CHK_SLOT, "依頼名", CHARA))%＞
	PRINTFORMW 　→ Request Report
	CALL IRAI_REPORT(CHARA)
	SIF RESULT > 0
		CNT_DO ++
NEXT
TARGET = PREV_TARGET
RETURN CNT_DO
