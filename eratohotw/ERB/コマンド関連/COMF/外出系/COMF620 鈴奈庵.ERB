;貸本屋
@COM620
#DIM CHARISMA
#DIM ItemId
;addition custom code
CALL COM620_TR
RETURN RESULT

$C_LOOP
DRAWLINE
IF !CHARISMA
	PRINTFORML Money: %金額表示(MONEY)%
ELSE
	PRINTFORML Charisma: {MONEY:2}
ENDIF
PRINTL 「Borrowing costs \3000」
DRAWLINE
FOR LOCAL,1, BookTitles + 1
	ItemId = LOCAL + 100
	PRINTFORM [{LOCAL}] %ITEMNAME_TR(ItemId)%
	IF ITEM:ItemId
		PRINTFORML (%PARSE("You are")% already borrowing it)
	ELSEIF GETBIT (FLAG:1011,LOCAL)
		PRINTL (Rented out)
	ELSE
		PRINTL 　
	ENDIF
NEXT

IF !CHARISMA
	PRINTFORML [100] Pay with Charisma
ELSE
	PRINTFORML [100] Pay with money
ENDIF
DRAWLINE
PRINTL [0] Cancel

$LOOP
INPUT
FOR LOCAL,1, BookTitles + 1
	ItemId = LOCAL + 100
	IF ITEM:ItemId
		LOCALS = 「You're not allowed to borrow two books at once...」
		IF CFLAG:[[小鈴]]:現在位置 == CFLAG:PLAYER:現在位置
			CALL SPTALK, [[小鈴]], "通常", 0, @"%LOCALS%", 1
			SIF TARGET != [[小鈴]]
				RESETCOLOR
		ELSE
			CALL SPTALK, 0, "通常", 12, @"%LOCALS%", 1
		ENDIF
		RETURN -1
	ENDIF
NEXT

IF RESULT == 100
	;reformatted
	IF !CHARISMA
		LOCALS = 「Pay with Charisma????」/「Uhh... Then, 10 Charisma...? Should be good enough I think...」
	ELSE
		LOCALS = 「Ah, you'll pay with money then?」
	ENDIF
	IF CFLAG:[[小鈴]]:現在位置 == CFLAG:PLAYER:現在位置
		CALL SPTALK, [[小鈴]], "通常", 0, @"%LOCALS%", 1
		SIF TARGET != [[小鈴]]
			RESETCOLOR
	ELSE
		CALL SPTALK, 0, "通常", 12, @"%LOCALS%", 1
	ENDIF
	IF !CHARISMA && MONEY:2 < 10
		PRINTFORMW Not enough Charisma
		GOTO C_LOOP
	ENDIF
	
	PRINTFORML 
	CHARISMA = !CHARISMA
	GOTO C_LOOP
ELSE
	IF !CHARISMA && MONEY < 3000
		PRINTW Not enough money...
		RETURN -1
	ENDIF
	IF INRANGE(RESULT, 1, BookTitles)
		IF GETBIT (FLAG:1011,RESULT)
			LOCALS = 「I'm sorry, but it's rented out.」
			IF CFLAG:[[小鈴]]:現在位置 == CFLAG:PLAYER:現在位置
				CALL SPTALK, [[小鈴]], "通常", 0, @"%LOCALS%", 1
				SIF TARGET != [[小鈴]]
					RESETCOLOR
			ELSE
				CALL SPTALK, 0, "通常", 12, @"%LOCALS%", 1
			ENDIF
		ELSE
			SETBIT FLAG:1011,RESULT
			IF !CHARISMA
				MONEY -= 3000
			ELSE
				MONEY:2 -= 10
			ENDIF
			ITEM:(100 + RESULT) = 1
			FLAG:貸本返却期限 = 8
			TFLAG:193 = 1
			PRINTFORML Borrowed %ITEMNAME_TR(100 + RESULT)%
			LOCALS = 「Thank you very much!」/「The loan period is one week.」
			IF CFLAG:[[小鈴]]:現在位置 == CFLAG:PLAYER:現在位置
				CALL SPTALK, [[小鈴]], "笑顔", 0, @"%LOCALS%", 1
				SIF TARGET != [[小鈴]]
					RESETCOLOR
			ELSE
				CALL SPTALK, 0, "笑顔", 12, @"%LOCALS%", 1
			ENDIF
			RETURN 1
		ENDIF
	ELSEIF RESULT == 0
		RETURN -1
	ELSE
		GOTO LOOP
	ENDIF
ENDIF
TIME += 30

;貸本屋
@COM_ABLE620
;一括管理
SIF GLOBAL_COMABLE(620)
	RETURN RESULT
;貸本屋ではない場所
SIF !BOOK_RENTAL(CFLAG:MASTER:現在位置)
	RETURN 0
SIF MONEY < 3000
	RETURN 0
;睡眠中
SIF CFLAG:MASTER:睡眠 || CFLAG:MASTER:イタズラ || CFLAG:うふふ ;com custom code
	RETURN 0
;時間停止中
SIF FLAG:70
	RETURN 0
RETURN 1

;貸本屋を判定する関数
@BOOK_RENTAL(ARG)
#FUNCTION
;鈴奈庵
SIF ARG == GET_MAP_REPLACEMENT(213)
	RETURNF 1
RETURNF 0

@BOOKLEND
FOR LOCAL,1,BookTitles + 1
	SIF LOCAL == 6	;[紙芝居]をスルー
		CONTINUE
	;addition custom code
	SIF LOCAL == (GETNUM(ITEM,  "妖魔本")-100) ;skip for demon book
		CONTINUE
	;-------------------------------------------------------------
	SIF !ITEM:(100 + LOCAL) && !RAND:3
		INVERTBIT FLAG:1011,LOCAL
NEXT
SIF FLAG:貸本返却期限
	FLAG:貸本返却期限 -= 1
IF FLAG:貸本返却期限 == 1
	;fix
	FLAG:貸本返却期限 = 0
	FOR LOCAL,1,BookTitles + 1
		ITEM:(100 + LOCAL) = 0
	NEXT
	PRINTW The return date has come, so the borrowed book had to be returned.
ENDIF
