;FileName_USERCOM.ERB ------------------------------ Rev1.00
;コマンド判定処理
;CALL		SYSTEM
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@USERCOM
#DIM 能力表示用ダミー
#DIM 装備部位
#DIM パンツ消費
#DIM なだめ判定
$LOOP_USERCOM
;一般コマンド(COMF)
IF RESULT >= 0 && RESULT < 800
	;連れ出す判定
	IF RESULT == 351 && !CHK_DATENOW(CFLAG:MASTER:デート中)
		CALL COM_ABLE351
		SIF !RESULT
			RETURN 0
		PRINTL Lead Around
		STR:0 = %TRAINNAME_TR(351)%
		CALL COM351
		RETURN 0
	ENDIF
	;コマンド登録チェック
	SIF TRAINNAME:RESULT == ""
		RETURN 0
	;SELECTCOMとTFLAG:3(SELECTCOM退避)に入力値セット
	SELECTCOM = RESULT
	TFLAG:3 = SELECTCOM
	;一部コマンドの特殊制限
	;Custom code
	IF !FLAG:STABLEBYPASS
		SELECTCASE SELECTCOM
			CASE 100 TO 107,140 TO 145
				IF TALENT:TARGET:妊娠
				PRINTW Cannot do that to someone pregnant.
					RETURN 0
	;			ELSEIF !GETBIT(CFLAG:TARGET:既成事実, 4)
	;				CALL ＳＭ許可
				ENDIF
			CASE 60 TO 71
				IF TALENT:TARGET:妊娠 && (CFLAG:TARGET:903 <= 30 || CFLAG:TARGET:903 >= 71)
				PRINTFORMW It'd be best to not let %CALLNAME:TARGET% force %HIMSELF_HERSELF(TARGET)%.
					RETURN 0
	;			ELSEIF !GETBIT(CFLAG:TARGET:既成事実, 3)
	;				CALL Ａ許可
				ENDIF
		ENDSELECT
	ENDIF
	SIF GLOBAL_COMABLE2(SELECTCOM)
		RETURN 0
	;コマンド使用可能判定
	TRYCALLFORM COM_ABLE{SELECTCOM}
	SIF RESULT == 0
		RETURN 0
	;実行制限(実行拒否等)チェック
	TRYCALLFORM CAN_COM{SELECTCOM}(1)
	IF RESULT == 0 && !CFLAG:負け犬 && !TCVAR:無理矢理
		;実行値表示のため再度CAN_COM
		TRYCALLFORM CAN_COM{SELECTCOM}(0)
		RETURN 0
	ENDIF
	;ダミーTRAIN
	DOTRAIN 999
;履歴表示（未実装）
ELSEIF RESULT == 800
	CALL SHOW_EQUIP
	TFLAG:Info表示中 = 0
	RETURN 1
;汚れ表示
ELSEIF RESULT == 801
	CALL STAIN_INFO
	TFLAG:Info表示中 = 0
	RETURN 1
;TARGET変更
ELSEIF RESULT == 802
	IF !CFLAG:添い寝中
		CALL TARGET_CHANGE
		TFLAG:Info表示中 = 0
		RETURN 1
	ENDIF
;TARGETの状態表示
ELSEIF RESULT == 803
	IF TARGET == MASTER
		;Custom code
		CALL SINGLE_CHARA_STATE_TR(MASTER, "「調教中」")
	ELSE
		CALL TARGET_LIST_STATE
	ENDIF
	TFLAG:Info表示中 = 0
	RETURN 1
;MASTERの状態表示
ELSEIF RESULT == 804
	;Custom code
	CALL SINGLE_CHARA_STATE_TR(MASTER, "「調教中」")
	TFLAG:Info表示中 = 0
	RETURN 1
ELSEIF RESULT == 805
	CALL SHOW_ALLITEM
	RETURN 1
;コマンドフィルタ
ELSEIF RESULT == 809
	CALL OPTION_COMMAND_FILTER
ELSEIF RESULT == 815 && FLAG:70 && !CFLAG:TARGET:睡眠 && AT_HOME(MASTER)
	IF BATHCHECK(CFLAG:MASTER:現在位置) || IN_TOILET(CFLAG:MASTER:現在位置)
		PRINTFORMW It would be better not to do this here.
	ELSEIF TFLAG:運搬 != TARGET
		PRINTFORMW %PARSE("You start")% carrying %CALLNAME:TARGET%.
		TFLAG:運搬 = TARGET
	ELSE
		PRINTFORMW %PARSE("You put")% %CALLNAME:TARGET% down.
		TFLAG:運搬 = 0
	ENDIF
ELSEIF RESULT == 816
	FLAG:画像表示 = !FLAG:画像表示
;集合
ELSEIF RESULT == 817 && FLAG:70 && TALENT:MASTER:集合 && AT_HOME(MASTER) && !CFLAG:うふふ
	CALL SYUGO()
;Bug-fix
ELSEIF RESULT == 819
	FLAG:挿絵表示 = !FLAG:挿絵表示
ELSEIF RESULT == 818
	CALL SHOW_GATHERING_LIST_SELECT(GATHERING_SPOT(CFLAG:MASTER:現在位置))
;custom code
ELSEIF RESULT == 890 && ABL:MASTER:教養 >= 3
	CALL SHOW_FISHING_LIST_SELECT(FISHING_SPOT(CFLAG:MASTER:現在位置))
;addition custom code from Yuugi's dialogue
ELSEIF RESULT == 891 && CFLAG:64:現在位置 != CFLAG:MASTER:現在位置 && !FIRSTTIME("YuugiBath", 1, 64)
	TRYCALL K64_CALLIN
;服を脱がせる
ELSEIF RESULT == 820 && EQUIP:0 && !(TFLAG:102 == 3 && TFLAG:抱きつきモード)
	CALL 服を脱がせる
;下着をくすねるコマンドの切り離し
ELSEIF RESULT == 821 && EQUIP:0 && EQUIP:TARGET:下半身下着２
	IF !TARGET && !FLAG:70 && !CFLAG:MASTER:イタズラ
		CALL SIMATTYAUOJISAN
	ELSE
		SIF CFLAG:TARGET:291 == 0
			CALL 脱衣処理(TARGET, "くすねる")
	ENDIF
;上半身下着を脱がす
ELSEIF RESULT == 822 && EQUIP:TARGET:0 && EQUIP:TARGET:上半身下着１ != 0
	IF !TARGET
		CALL SIMATTYAUOJISAN
	ELSE
		IF EQUIP:TARGET:上半身上着１ || EQUIP:TARGET:上半身上着２ || EQUIP:TARGET:ワンピース || EQUIP:TARGET:着物 || EQUIP:TARGET:ボディースーツ
			PRINTFORM %CALLNAME:TARGET%の%SHOW_TOP(TARGET,1)%を肌蹴させ、
		ELSE
			PRINTFORM %CALLNAME:TARGET%の
		ENDIF
		PRINTFORMW %CLOTHNAME(7,EQUIP:TARGET:上半身下着１)%を外した
		EQUIP:TARGET:上半身下着１ = 0
		TEQUIP:TARGET:上半身着衣状況 = 1
		TEQUIP:TARGET:上半身はだけ状態 = 1
		EQUIP:MASTER:上半身下着１ = 0
		TEQUIP:MASTER:上半身着衣状況 = 1
		TEQUIP:MASTER:上半身はだけ状態 = 1
		RETURN 0
	ENDIF
;下着姿にする
ELSEIF RESULT == 823 && EQUIP:0 && (EQUIP:TARGET:上半身上着１ || EQUIP:TARGET:上半身上着２ || EQUIP:TARGET:ワンピース || EQUIP:TARGET:着物 || EQUIP:TARGET:外衣)
	IF !TARGET
		CALL SIMATTYAUOJISAN
	ELSE
		CALL 脱衣処理(TARGET, "下着姿にする")
		CALL 脱衣処理(MASTER, "下着姿にする")
	ENDIF
;身体測定する
ELSEIF RESULT == 824 && MEASUREMENTCHECK(CFLAG:TARGET:現在位置)
	CALL ASK_M(@"Measure %CALLNAME:MASTER%",1,@"Measure %CALLNAME:TARGET%",1,"Never mind",1)
	SELECTCASE RESULT
		CASE 0
			CALL SOMATOMETRY(MASTER)
		CASE 1
			CALL SOMATOMETRY(TARGET)
		CASE 2
	ENDSELECT
	RETURN 0
;下着収奪コマンドし
ELSEIF RESULT == 825 && TFLAG:パンツ収奪
	IF !FLAG:70
		CALL SIMATTYAUOJISAN
	ELSE
		FOR LOCAL,1,CHARANUM
			;Bug-fix
			IF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置 && !CFLAG:LOCAL:ノーパン && EQUIP:LOCAL:下半身下着２ && CFLAG:LOCAL:344 == 0
				SIF !PANTS_ALREADY_COLLECTED(LOCAL)
					SETCOLOR C_PINK
				PRINTFORMW %PARSE("You take")% off %CALLNAME:LOCAL%'s %TOLOWER(PANTSNAME(EQUIP:LOCAL:下半身下着２,LOCAL))% and %PARSE("put")% it in %PARSE("your",1)% pocket.
				RESETCOLOR
				CALL PANTS_GET(LOCAL)
			ENDIF
		NEXT
		TFLAG:Info表示中 = 0
		RETURN 0
	ENDIF
;Bug-fix
ELSEIF RESULT == 826 && (FLAG:70 == 1 ||(CFLAG:うふふ && !CFLAG:イタズラ))
	IF TEQUIP:焦らしモード
		TEQUIP:焦らしモード = 0
	ELSE
		IF TCVAR:MASTER:イきそう
			PRINTFORMW Can't handle it anymore!
	 	ELSEIF (EX:20 + EX:21 + EX:22 + EX:23 + EX:24) > 0
	 		PRINTFORMW Can't tease any more than this.
	 	ELSE
	 		TEQUIP:焦らしモード = 1
	 	ENDIF
	 ENDIF
;過激なイタズラ（未実装）
;Bug-fix
ELSEIF RESULT == 830 && CFLAG:イタズラ < 2 && (EXP:TARGET:睡眠姦経験 > 100 || TCVAR:睡眠薬強度 || TCVAR:睡眠深度 > 2000)
	PRINTFORML %PARSE("You")% slowly %PARSE("take")% off %CALLNAME:TARGET%'s clothes.
	EQUIP:0 = 0
	SIF CFLAG:TARGET:睡眠姦 < 2
	CFLAG:TARGET:睡眠姦 = 2
	CALL SET_PRANK(2,TARGET,1)
	CALL 脱衣処理(TARGET, "全裸", "「表示なし」")
	FOR LOCAL,1,CHARANUM
		CALL CLOTHES_SETTING_TRAIN(LOCAL)
	NEXT
	RETURN 1
;test
;ELSEIF RESULT == 99999
;	TIME:5 = 4
;	RETURN 1
;抜け出す
;Bug-fix
ELSEIF RESULT == 850 && CFLAG:うふふ == 2
	SELECTCOM = RESULT
	TFLAG:3 = SELECTCOM
	DOTRAIN 999
;居場所察知=============================================================================================================================
ELSEIF RESULT == 811 && TALENT:MASTER:居場所察知 && !CFLAG:うふふ && !CFLAG:MASTER:イタズラ ;bug fix (includes qol change)
	TFLAG:206 = CFLAG:MASTER:現在位置
	;qol custom code
	CALL HITOSAGASI(, 1)
	INPUT
	IF GROUPMATCH(RESULT, 0, CFLAG:MASTER:現在位置)
		TFLAG:Info表示中 = 0
		RETURN 1
	ENDIF
	
	IF RESULT == -1 ;qol custom code
		CALL BROAD_HITOSAGASI
		RETURN 1
	ENDIF
	;-----------------------------------
	;移動範囲外 bug fix re-added to prevent crashes and walking into void
	IF IN_HOME(RESULT) != 1 || GET_MAPID(CFLAG:MASTER:現在位置) != MAIN_MAP
		TFLAG:マップ切り替え = 0
		PRINTW Outside of movement range.
	;-----------------------------
	ELSEIF MAP_CAN_MOVE(RESULT) == 0
		IF TFLAG:移動不能メッセージ == 999
			PRINTFORMW It's locked.
			CLEARLINE 2
			REUSELASTLINE 
		ELSE
			TRYCALLFORM MAP_CANNOT_WORD_{MAIN_MAP}(RESULT)
		ENDIF
		TFLAG:移動不能メッセージ = 0
		NEXTCOM = -1
		RETURN 0
	ELSEIF IN_HOME(RESULT) == 1
		;NEXTCOM = GETNUM(TRAINNAME, "移動")
		;TFLAG:195 = RESULT
		CALL SKIP_MOVE(CFLAG:MASTER:現在位置, RESULT)
	ENDIF
	TFLAG:Info表示中 = 0
	RETURN 1
;====================================================
ELSEIF RESULT == 812 && TALENT:MASTER:汚部屋察知 && AT_HOME(MASTER) && !CFLAG:うふふ && !CFLAG:MASTER:イタズラ ;bug fix
	TFLAG:206 = CFLAG:MASTER:現在位置
	CALL OHEYA_CHECK
	INPUT
	IF GROUPMATCH(RESULT, 0, CFLAG:MASTER:現在位置)
		TFLAG:Info表示中 = 0
		RETURN 1
	ENDIF
	;移動範囲外 bug fix re-added to prevent crashes and walking into void
	IF IN_HOME(RESULT) != 1 || GET_MAPID(CFLAG:MASTER:現在位置) != MAIN_MAP
		TFLAG:マップ切り替え = 0
		PRINTW Outside of movement range.
	;-----------------------------
	ELSEIF MAP_CAN_MOVE(RESULT) == 0
		IF TFLAG:移動不能メッセージ == 999
			PRINTFORMW It's locked.
			CLEARLINE 2
			REUSELASTLINE 
		ELSE
			TRYCALLFORM MAP_CANNOT_WORD_{MAIN_MAP}(RESULT)
		ENDIF
		NEXTCOM = -1
		RETURN 0
	ELSEIF IN_HOME(RESULT) == 1
		CALL SKIP_MOVE(CFLAG:MASTER:現在位置, RESULT)
	ENDIF
	TFLAG:Info表示中 = 0
	RETURN 1
ELSEIF RESULT == 813 && GETBIT(TALENT:MASTER:劣情察知, 1) && !CFLAG:うふふ && !CFLAG:MASTER:イタズラ ;bug fix
	PRINTFORML Consume some energy and try to find girls with large sexual desire?
	;CALL ASK_M("やめる",1,"発動",1,"触媒を使って発動",CSTR:MASTER:12 != "")
	CALL ASK_M("Cancel",1,"Invoke",1,"Invoke using a catalyst",FLAG:パンツ輸送)
	SELECTCASE RESULT
		CASE 0
			RETURN -1
		CASE 1,2
			PRINTFORMW %PARSE("You")%\@RESULT == 2 ? %" "% %PARSE("grasp")% the freshly stolen panties tightly and # \@ %PARSE("steel")% %PARSE("your",1)% mind!
			SETCOLOR 255,100,255
			FONTBOLD
			FOR LOCAL,1,CHARANUM
				IF CFLAG:LOCAL:溜まってる度 >= 500
					PRINTFORML %CALLNAME:LOCAL% has a large sexual desire...!
					SIF CFLAG:LOCAL:現在位置 == SUKIMA() && RESULT == 2
						PRINTFORML And, %CALLNAME:LOCAL% is at home! ... Maybe.
				ENDIF
			NEXT
			RESETCOLOR
			FONTREGULAR
			IF RESULT == 1
				BASE:MASTER:気力 -= 500
			ELSE
				PRINTFORMW After %PARSE("you finish")% concentrating, the panties in %PARSE("your",1)% hands dissolve into the atmosphere.
				CSTR:MASTER:12 =
				FLAG:パンツ輸送 = 0
				VARSET PANTS_TEMP
				BASE:MASTER:気力 -= 50
			ENDIF
			SIF !FLAG:70
				TIME += 5
			TFLAG:Info表示中 = 0
			RETURN 1
	ENDSELECT

;フィルタ----------------------------
ELSEIF RESULT == 839
	FLAG:家事アンフィルタ = !FLAG:家事アンフィルタ
	RETURN -1
ELSEIF RESULT == 4000
	CFLAG:オート喘ぎ ++
	SIF CFLAG:オート喘ぎ > 2
		CFLAG:オート喘ぎ = 0
	RETURN -1
ELSEIF RESULT >= 831 && RESULT <= 843
	CFLAG:TARGET:(RESULT - 830 + 590) = !CFLAG:TARGET:(RESULT - 830 + 590)
	RETURN -1
;未完成---------------------------- 

;ターゲット変更
ELSEIF RESULT >=1001 && RESULT < (CHARANUM + 1000) && CFLAG:(RESULT - 1000):同室
	;立ち絵種類切り替え
	SIF RESULT - 1000 == TARGET
		FLAG:立ち絵種類 = !FLAG:立ち絵種類
	TFLAG:104 = (RESULT - 1000)
;ストーキング対象切り替え
ELSEIF RESULT >=4001 && RESULT < (CHARANUM + 4000) && CFLAG:(RESULT - 4000):同室
	IF STALKED == TARGET
		STALKED = 0
	ELSE
		STALKED = RESULT - 4000
	ENDIF
ELSEIF RESULT >=5000 && RESULT < (CHARANUM + 5000)
	TCVAR:(RESULT - 5000):射精我慢 ++
	SIF TCVAR:(RESULT - 5000):射精我慢 > 4
		TCVAR:(RESULT - 5000):射精我慢 = 0
;一日を終える
;custom code, only for debug
[IF_DEBUG]
ELSEIF RESULT == 888
	BEGIN AFTERTRAIN
[ENDIF]
;神社に誘う
ELSEIF RESULT == 990 && CAN_DATE(TARGET)
	CALL 外出先から誘えるか(TARGET)
	IF !RESULT
		RETURN 1
	ELSE
		CALL 帰宅の時間経過
		CALL SET_DATE(99,TARGET)
		CFLAG:TARGET:現在位置 = ENTRANCE
		CFLAG:MASTER:現在位置 = ENTRANCE
		CFLAG:TARGET:デート中 = MAIN_MAP
		CFLAG:MASTER:デート中 = MAIN_MAP
		CFLAG:TARGET:同行 = 60
		CFLAG:MASTER:同行 = 60
		IF TARGET == 93 && !GETBIT (FLAG:プール, 0)
			TIME += 30
			BASE:MASTER:体力 = MAX(BASE:MASTER:体力 - 500,500)
		ENDIF
		IF CFLAG:TARGET:負け犬 && (!TALENT:TARGET:恋慕 && !TALENT:TARGET:思慕 && !TALENT:TARGET:愛欲)
			PRINTFORMW %PARSE("You return")% with the beaten %CALLNAME:TARGET%.
		ELSE
			PRINTFORMW Accompanied by %CALLNAME:TARGET%, %PARSE("you return")% back home.
		ENDIF
		RETURN 1
	ENDIF
;自室施錠,とりあえず封印
ELSEIF RESULT == 991
;	FLAG:自室施錠 = !FLAG:自室施錠
	RETURN 0
;デートに誘う
ELSEIF RESULT == 992 && CAN_DATE(TARGET)
	CALL 外出先から誘えるか(TARGET)
	IF !RESULT
		RETURN 1
	ELSE
		CFLAG:MASTER:同行 = 60
		CFLAG:TARGET:同行 = 60
		CFLAG:TARGET:デート中 = CFLAG:MASTER:現在位置 / 100
		FLAG:デート相手 = TARGET
		TFLAG:デート前好感度 = CFLAG:TARGET:好感度
		TIME += 1
		IF CFLAG:負け犬
			PRINTFORMW %PARSE("You bring")% the beaten %CALLNAME:TARGET% with %PARSE("you",5)%.
		ELSE
			PRINTFORMW %PARSE("You decide")% to go on a date with %CALLNAME:TARGET%, who has some spare time.
		ENDIF
	ENDIF
;カレンダー
;Bug-fix
ELSEIF RESULT == 993 & CFLAG:MASTER:現在位置 == CFLAG:MASTER:初期位置
	CALL カレンダー
;Bug-fix
ELSEIF RESULT == 994 && FLAG:パンツ輸送
	CALL SHOW_HAVE_COLLECTION()
ELSEIF RESULT == 995
	IF AT_HOME(MASTER)
		INVERTBIT FLAG:瞬間移動, 1
	ELSE
		INVERTBIT FLAG:瞬間移動, 0
	ENDIF
;自動掃除
ELSEIF RESULT == 996
	IF AT_HOME(MASTER)
		INVERTBIT FLAG:自動掃除, 1
	ELSE
		INVERTBIT FLAG:自動掃除, 0
	ENDIF
;時間停止解除
ELSEIF RESULT == 999 && FLAG:70 == 1 && !CFLAG:うふふ
	CALL 時間停止解除
	;はだけフラグをオフ
	TEQUIP:2 = 0
	TEQUIP:上半身はだけ状態 = 0
	;キャラ全員の服再セッティング
	FOR LOCAL,0,CHARANUM
		CALL CLOTHES_SETTING_TRAIN(LOCAL)
		;上着脱衣済みフラグをオフ
		TEQUIP:LOCAL:5 = 0
		;身体測定終了
		SIF CFLAG:LOCAL:身体測定 == 2
			CFLAG:LOCAL:身体測定 = 1
	NEXT
;逢瀬終了
ELSEIF RESULT == 999 && CFLAG:うふふ
	SIF CFLAG:MASTER:イタズラ == 2
		CALL SET_PRANK(0,TARGET,2)
	IF CFLAG:うふふ == 2
			SELECTCOM = RESULT
			TFLAG:3 = SELECTCOM
			DOTRAIN 999
	ELSE
		IF FLAG:70 == 1
			FOR LOCAL,0,CHARANUM
				SIF CFLAG:うふふ == 2
					CALL SET_DERAY(5,LOCAL)
				SIF CFLAG:LOCAL:うふふ == 1 && CFLAG:LOCAL:時間停止口上有 ;custom code, re-added
					CALL KOJO_MESSAGE_SEND("EVENT",15,LOCAL,2,0)
				CALL ENDUFUFU(LOCAL)
			NEXT
			TFLAG:106 = 0
		ELSE
			CALL AFTER_AFFAIR
			FOR LOCAL,1,CHARANUM
				;うふふ終了口上呼び出し
				SIF CFLAG:LOCAL:うふふ == 1 && (CFLAG:LOCAL:イタズラ < 1 || CFLAG:LOCAL:眠姦口上有)
					CALL KOJO_MESSAGE_SEND("EVENT",15,LOCAL,2,0)
				CALL ENDUFUFU(LOCAL)
			NEXT
			TFLAG:106 = 0
		ENDIF
		;MASTERとの接触位置をリセット
		CALL TOUCH_SET(0,0,0,1)
		;はだけフラグをオフ
		TEQUIP:2 = 0
		TEQUIP:上半身はだけ状態 = 0
		;キャラ全員の服再セッティング
		FOR LOCAL,0,CHARANUM
			CALL CLOTHES_SETTING_TRAIN(LOCAL)
			;上着脱衣済みフラグをオフ
			TEQUIP:LOCAL:5 = 0
		NEXT
		SIF TFLAG:泡風呂
			CALL 泡風呂終了判定("コマンドから終了")
		RETURN 1
	ENDIF
;イタズラ終了
ELSEIF RESULT == 999 && CFLAG:イタズラ && !CFLAG:うふふ
	FOR LOCAL,0,CHARANUM
		CALL SET_PRANK(0,LOCAL,1)
	NEXT
	;MASTERとの接触位置をリセット
	CALL TOUCH_SET(0,0,0,1)
	;はだけフラグをオフ
	TEQUIP:2 = 0
	TEQUIP:上半身はだけ状態 = 0
	;キャラ全員の服再セッティング
	FOR LOCAL,0,CHARANUM
		CALL CLOTHES_SETTING_TRAIN(LOCAL)
		;上着脱衣済みフラグをオフ
		TEQUIP:LOCAL:5 = 0
	NEXT
	RETURN 1
;添い寝終了
ELSEIF RESULT == 999 && CFLAG:添い寝中
	FOR LOCAL,0,CHARANUM
		CALL SET_TOGETHER(0,LOCAL,1)
	NEXT
	;MASTERとの接触位置をリセット
	CALL TOUCH_SET(0,0,0,1)
	CALL KOJO_MESSAGE_SEND("EVENT",16,TARGET,2,0)
	;衰弱なら寝る
	SIF CFLAG:TARGET:衰弱 && !TCVAR:TARGET:休憩中
		CALL CHARA_SLEEP, TARGET,0

	;はだけフラグをオフ
	TEQUIP:2 = 0
	TEQUIP:上半身はだけ状態 = 0
	;キャラ全員の服再セッティング
	FOR LOCAL,0,CHARANUM
		CALL CLOTHES_SETTING_TRAIN(LOCAL)
		;上着脱衣済みフラグをオフ
		TEQUIP:LOCAL:5 = 0
	NEXT
	RETURN 1
;デート終了
ELSEIF RESULT == 999 && CHK_DATENOW(CFLAG:MASTER:デート中) && CFLAG:MASTER:現在位置 != OMANEKIBEYA()
	CALL 外出先から帰宅
	RETURN 1
ELSEIF RESULT == 3000
	TFLAG:PALAM非表示 = !TFLAG:PALAM非表示
ELSEIF RESULT == 3001
	TFLAG:Status非表示 = !TFLAG:Status非表示
ELSEIF RESULT == 3002
	TFLAG:USERCOM非表示 = !TFLAG:USERCOM非表示
ELSEIF RESULT == 3003
	TFLAG:LOOK非表示 = !TFLAG:LOOK非表示
	
;リソース表示関連
ELSEIF RESULT == 3100
	;表示設定の切り替え
	FLAG:画像表示設定 = !FLAG:画像表示設定
ELSEIF RESULT == 3101
	;画像表示人数 -
	FLAG:画像表示 = MAX(FLAG:画像表示-1,1)
	
ELSEIF RESULT == 3102
	;画像表示人数 +
	FLAG:画像表示 = MIN(FLAG:画像表示+1,10)
ELSEIF RESULT == 3103
	;表示位置切り替え
	FLAG:画像表示位置 = !FLAG:画像表示位置

ELSEIF RESULT == 3104
	;枠表示 ON/OFF
	FLAG:画像枠表示 = !FLAG:画像枠表示
ELSEIF RESULT == 3105
	FLAG:立ち絵種類 = !FLAG:立ち絵種類
ELSEIF RESULT == 3106
	isModifier = MAX(isModifier - 10, -50)
ELSEIF RESULT == 3107
	isModifier = MIN(isModifier + 10, 50)
ELSEIF RESULT == 3108
	FLAG:巨乳差分表示 = !FLAG:巨乳差分表示
ELSEIF RESULT == 3109
	FLAG:追加エフェクト = !FLAG:追加エフェクト
ELSEIF RESULT == 3110
	CFLAG:TARGET:差し替え適用 = !CFLAG:TARGET:差し替え適用
ELSEIF RESULT == 3111
	IF FLAG:マルチサイズ
		FLAG:マルチサイズ = !FLAG:マルチサイズ
		デフォルトキャラ画像横幅 = 180
	ELSEIF デフォルトキャラ画像横幅 == 180
		デフォルトキャラ画像横幅 = 270
	ELSEIF デフォルトキャラ画像横幅 == 270
		デフォルトキャラ画像横幅 = 360
	ELSEIF デフォルトキャラ画像横幅 == 360
		デフォルトキャラ画像横幅 = 540
	ELSEIF デフォルトキャラ画像横幅 == 540
		デフォルトキャラ画像横幅 = 720
	ELSE
		FLAG:マルチサイズ = !FLAG:マルチサイズ
	ENDIF
;addition custom code
ELSEIF RESULT == 3199
	nCloseAndPersonal = !nCloseAndPersonal
;--------------------
;custom code, only for debug
[IF_DEBUG]
ELSEIF RESULT == 9999
	FOR LOCAL,1,CHARANUM
		PRINTFORMLC %CALLNAME:LOCAL% Current location: {CFLAG:LOCAL:現在位置}
	NEXT
[ENDIF]
;Custom Code
ELSEIF RESULT == 2000 && FLAG:CHEATMENU
	CALL IG_CHEAT_MENU
ENDIF
TFLAG:Info表示中 = 0
RETURN 0

;FileName_USERCOM.ERB ------------------------------ Rev1.00
;TARGET変更処理
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@TARGET_CHANGE
PRINTFORML Current target is %CALLNAME:TARGET%.
PRINTL [0] - Return
FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置
		PRINTFORML [{LOCAL}] - %CALLNAME:LOCAL% (Favorability: {CFLAG:LOCAL:2})
NEXT
INPUT
IF RESULT <= 0 || RESULT >= CHARANUM
	RETURN 0
ELSEIF CFLAG:RESULT:現在位置 == CFLAG:MASTER:現在位置
	TFLAG:104 = RESULT
ENDIF

;----------------------------------------------------------
;FileName_USERCOM.ERB ------------------------------ Rev1.00
;履歴表示処理（未実装）
;CALL		SYSTEM
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@SHOW_EQUIP
WAIT

;FileName_USERCOM.ERB ------------------------------ Rev1.00
;日常コマンドの成否判定
;CALL					USER
;ARG:0（条件最大値		コマンド事の成否条件の最大値
;ARG:1（成功値			コマンド事の成功値
;ARG:2（大成功補正値	コマンド事の大成功に対する補正値
;RETURN		VOID
;COMMENT	各コマンドに散らばっている成否判定を一括
;-------------------------------------------------
@SUC_FAIL(条件最大値,成功値,大成功補正値)
#DIM 条件最大値
#DIM 条件値
#DIM 成功値
#DIM 大成功補正値

SIF !条件最大値
	条件最大値 = 100

条件値 = RAND:条件最大値

SIF !成功値
	成功値 = MIN(90 + GET_SUCCESS_RATE() / 5, 99)

SELECTCASE TFLAG:192
	;通常
	CASE 0
		;大成功
		IF 条件値 <= 9 + 大成功補正値
			TFLAG:193 = 1
		;失敗
		ELSEIF 条件値 >= 成功値
			TFLAG:193 = -1
		;成功
		ELSE
			TFLAG:193 = 0
		ENDIF
	;強制成功or大成功(強制成功時は大成功しやすいように
	CASE 1
		;大成功
		IF 条件値 <= 19 + 大成功補正値 / 2
			TFLAG:193 = 1
		;成功
		ELSE
			TFLAG:193 = 0
		ENDIF
	;強制失敗orコマンド終了
	CASE -1,-2
		TFLAG:193 = -1
ENDSELECT
条件最大値 = 0
条件値 = 0
成功値 = 0
大成功補正値 = 0

@SOMATOMETRY(ARG)
PRINTFORML Starting measurement of %CALLNAME:ARG%.
CALL MAKE_SIZE(ARG)
CFLAG:ARG:身体測定 = 1
CALL SHOW_SIZE(ARG)
CALL ASK_YN("Remeasure","This is fine")
IF !RESULT
	CFLAG:ARG:身体測定 = 2
	RESTART
ENDIF

