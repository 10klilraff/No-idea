;--------------------------------------------
@BATTLE_SYSTEM(ARG)
;--------------------------------------------
#DIM 基礎攻撃力
#DIM 前回行動
#DIM ウェイト
#DIMS バー色
ENEMY_NUM = ARG
CALL SET_ENEMY
基礎攻撃力 = ABL:MASTER:戦闘能力 + 1

$TURN_START
ウェイト = 0
LOCAL = 100 * BASE:MASTER:体力 / MAXBASE:MASTER:体力

LOCALS = STA(%CALLNAME:MASTER%)
PRINTFORM %LOCALS,20,LEFT%
SELECTCASE LOCAL
	CASE IS > 50
		CALL PRINT_COLORBAR, BASE:MASTER:0,MAXBASE:MASTER:0,18, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("青")
	CASE 50 TO 20
		CALL PRINT_COLORBAR, BASE:MASTER:0,MAXBASE:MASTER:0,18, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("黄")
	CASEELSE
		CALL PRINT_COLORBAR, BASE:MASTER:0,MAXBASE:MASTER:0,18, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("赤")
ENDSELECT

PRINTFORM  ({BASE:MASTER:体力,5}/{MAXBASE:MASTER:体力,5})
PRINTL
LOCAL = 100 * ENEMY_HP / ENEMY_MAXHP

LOCALS = STA(%ENEMY_NAME%)
PRINTFORM %LOCALS,20,LEFT%
SELECTCASE LOCAL
	CASE IS > 50
CALL PRINT_COLORBAR, ENEMY_HP, ENEMY_MAXHP, 18, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("青")
	CASE 50 TO 20
CALL PRINT_COLORBAR, ENEMY_HP, ENEMY_MAXHP, 18, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("黄")
	CASEELSE
CALL PRINT_COLORBAR, ENEMY_HP, ENEMY_MAXHP, 18, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("赤")
ENDSELECT

PRINTFORM  ({ENEMY_HP,5}/{ENEMY_MAXHP,5}) 

PRINTFORML 
DRAWLINE
PRINTFORML %PARSE("Your")% turn!
CALL ASK_M("Weak attack",1,"Strong attack",1,"Time stop attack",BASE:MASTER:TSP >= 500)
前回行動 = RESULT
SELECTCASE RESULT
	CASE 0
		CALL BATTLE_ATK(2500,基礎攻撃力 * 100)
	CASE 1
		CALL BATTLE_ATK(1500,基礎攻撃力 * 200)
		ウェイト = 500
	CASE 2
		PRINTFORML %PARSE("You do")% a time stop attack!
		ENEMY_HP -= 基礎攻撃力 * 200
		PRINTFORMW %ENEMY_NAME% takes {基礎攻撃力 * 200} damage!
ENDSELECT

SIF ENEMY_HP <= 0
	RETURN 1
CALL BATTLE_DEF(ウェイト)
IF BASE:MASTER:体力 <= 500
	RETURN -1
ELSE
	GOTO TURN_START
ENDIF

;--------------------------------------------
@BATTLE_ATK(制限時間,DAMAGE)
;--------------------------------------------
#DIM 制限時間
#DIM 正解
#DIM DAMAGE
#DIM ANSWER,2
正解 = RAND:4
LOCALS:0 = Up
LOCALS:1 = Right
LOCALS:2 = Left
LOCALS:3 = Down
PRINTFORM Aim 
SETCOLOR C_YELLOW
PRINTFORML %LOCALS:正解%
RESETCOLOR
PRINTFORML !
GETMILLISECOND
ANSWER = RESULT
制限時間 += ENEMY_SKILL_WAIT
CALL ASK_BATTLE(制限時間)

IF RESULT == 正解
	GETMILLISECOND
	ANSWER:1 = 制限時間 - (RESULT - ANSWER)
	PRINTFORMW {ANSWER:1} time remaining.
	IF ANSWER:1 >= 800
		CALL COLORMESSAGE("CRITICAL HIT!",C_AQUA,2,1)
		DAMAGE = DAMAGE * 3 / 2
	ENDIF
	ENEMY_HP -= DAMAGE
	PRINTFORMW %ENEMY_NAME% takes {DAMAGE} damage!
ELSEIF RESULT == -1
	PRINTFORMW Miss!
ELSE
	PRINTFORML It was blocked!
	ENEMY_HP -= DAMAGE / 5
	PRINTFORMW %ENEMY_NAME% takes {DAMAGE / 5} damage!
ENDIF

;--------------------------------------------
@BATTLE_DEF(ウェイト)
;--------------------------------------------

#DIM 前回行動
#DIM ウェイト
#DIM 正解
#DIM 制限時間
#DIM DAMAGE
#DIM ANSWER,2
CALL ENEMY_SKILL_DECIDE
DAMAGE = ENEMY_ATK * ENEMY_SKILL_ATK
正解 = ENEMY_ATK_DIRECTION
LOCALS:0 = Up
LOCALS:1 = Right
LOCALS:2 = Left
LOCALS:3 = Down

PRINTFORM %ENEMY_NAME% aims 
SETCOLOR C_YELLOW
PRINTFORM %LOCALS:正解% 
RESETCOLOR
PRINTFORML with %ENEMY_SKILL_NAME%!
GETMILLISECOND
ANSWER = RESULT
制限時間 = ENEMY_SKILL_SPD - ウェイト
CALL ASK_BATTLE(制限時間)

IF RESULT == 正解
	GETMILLISECOND
	ANSWER:1 = 制限時間 - (RESULT - ANSWER)
	PRINTFORMW {ANSWER:1} time remaining.
	IF ANSWER:1 >= 800
		CALL COLORMESSAGE("JUST GUARD!",C_AQUA,2,1)
		DAMAGE /= 4
	ELSE
		CALL COLORMESSAGE("GUARD!",C_YELLOW,2,1)
		DAMAGE /= 2
	ENDIF
ELSEIF RESULT == 3 - 正解
	GETMILLISECOND
	ANSWER:1 = 制限時間 - (RESULT - ANSWER)
	PRINTFORMW {ANSWER:1} time remaining.
	IF ANSWER:1 >= 800
		CALL COLORMESSAGE("AVOID!",C_AQUA,2,1)
		DAMAGE = 0
	ENDIF
ELSEIF RESULT == -1
	PRINTFORMW CRITICAL HIT!
	DAMAGE = DAMAGE * 3 / 2
ENDIF
BASE:MASTER:体力 -= DAMAGE
PRINTFORMW %PARSE("You take")% {DAMAGE} damage!　（{BASE:MASTER:体力} HP remaining）
;--------------------------------------------
@SET_ENEMY
;--------------------------------------------
SELECTCASE ENEMY_NUM
CASE 1
	ENEMY_NAME = Sandbag
	ENEMY_ATK = 1
	ENEMY_HP = 1000
CASE 2
	ENEMY_NAME = Small fry
	ENEMY_ATK = 2
	ENEMY_HP = 2000
ENDSELECT
ENEMY_MAXHP = ENEMY_HP
;--------------------------------------------
@ENEMY_SKILL_DECIDE
;--------------------------------------------
SELECTCASE ENEMY_NUM
CASE 1
	LOCAL = 1
CASE 2
	IF !RAND:4
		LOCAL = 3
	ELSEIF !RAND:2
		LOCAL = 4
	ELSE
		LOCAL = 2
	ENDIF
ENDSELECT
CALL ENEMY_SKILL(LOCAL)
;--------------------------------------------
@ENEMY_SKILL(ARG)
;--------------------------------------------
ENEMY_SKILL_WAIT = 0
SELECTCASE ARG
CASE 1
	ENEMY_SKILL_NAME = Shabby attack
	ENEMY_SKILL_ATK = 50
	ENEMY_SKILL_SPD = 4000
	ENEMY_ATK_DIRECTION = RAND:2 + 1
CASE 2
	ENEMY_SKILL_NAME = Regular attack
	ENEMY_SKILL_ATK = 150
	ENEMY_SKILL_SPD = 2000
	ENEMY_ATK_DIRECTION = RAND:4
CASE 3
	ENEMY_SKILL_WAIT = 500
	ENEMY_SKILL_NAME = Smash
	ENEMY_SKILL_ATK = 300
	ENEMY_SKILL_SPD = 2000
	ENEMY_ATK_DIRECTION = 1
CASE 4
	ENEMY_SKILL_WAIT = -500
	ENEMY_SKILL_NAME = Jab
	ENEMY_SKILL_ATK = 100
	ENEMY_SKILL_SPD = 1500
	ENEMY_ATK_DIRECTION = 2
ENDSELECT
;--------------------------------------------
@ASK_BATTLE(制限時間)
;--------------------------------------------
#DIM 制限時間
[SKIPSTART]
PRINTFORML 
PRINTL       [0]↑        
PRINTL   [2]←     [1]→
PRINTL       [3]↓
PRINTFORML
[SKIPEND]
PRINTFORML            w
PRINT          
PRINTBUTTON "[↑] ", "w"
PRINTFORML
PRINT  　a 
PRINTBUTTON "[←] ", "a"
PRINTFORM    
PRINTBUTTON "[→] ", "d"
PRINTFORML d
PRINT          
PRINTBUTTON "[↓] ", "s"
PRINTFORML
PRINTFORML            s
TONEINPUTS 制限時間, "p", 1
SELECTCASE RESULTS
	CASE "w"
		RETURN 0
	CASE "a"
		RETURN 2
	CASE "d"
		RETURN 1
	CASE "s"
		RETURN 3
	CASEELSE
		RETURN -1
ENDSELECT
